#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу доступности сотрудников
//
Функция ПолучитьТаблицуДоступности(МассивСотрудников) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаДоступности = Новый ТаблицаЗначений;
	ТаблицаДоступности.Колонки.Добавить("Сотрудник");
	ТаблицаДоступности.Колонки.Добавить("ДеньНедели");
	ТаблицаДоступности.Колонки.Добавить("ВремяНачала");
	ТаблицаДоступности.Колонки.Добавить("ВремяОкончания");
	ТаблицаДоступности.Колонки.Добавить("Занят");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоступноеВремяСотрудников.Сотрудник,
		|	ДоступноеВремяСотрудников.ДеньНедели,
		|	ДоступноеВремяСотрудников.ВремяНачала,
		|	ДоступноеВремяСотрудников.ВремяОкончания,
		|	ЛОЖЬ КАК Занят
		|ИЗ
		|	РегистрСведений.ДоступноеВремяСотрудников КАК ДоступноеВремяСотрудников
		|ГДЕ
		|	ДоступноеВремяСотрудников.Сотрудник В (&МассивСотрудников)";
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	ТаблицаДоступноеВремяСотрудников = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиРабочегоКалендаря.Сотрудник КАК Сотрудник,
		|	НастройкиРабочегоКалендаря.Значение КАК Доступность
		|ИЗ
		|	РегистрСведений.ЦБР_НастройкиРабочегоКалендаря КАК НастройкиРабочегоКалендаря
		|ГДЕ
		|	НастройкиРабочегоКалендаря.Сотрудник В(&МассивСотрудников)
		|	И НастройкиРабочегоКалендаря.Настройка = ЗНАЧЕНИЕ(Перечисление.ЦБР_НастройкиРабочегоКалендаря.Доступность)";
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	ТаблицаНастройкиРабочегоКалендаря = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Сотрудник Из МассивСотрудников Цикл
		
		ДоступностьСотрудника = Неопределено;
		
		НастройкаРабочегоКалендаряСотрудника =
			ТаблицаНастройкиРабочегоКалендаря.Найти(Сотрудник, "Сотрудник");
		Если НастройкаРабочегоКалендаряСотрудника <> Неопределено Тогда
			ДоступностьСотрудника = НастройкаРабочегоКалендаряСотрудника.Доступность;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДоступностьСотрудника) Тогда
			ДоступностьСотрудника = Перечисления.ЦБР_ДоступноеВремяСотрудников.ДоступенВсегда;
		КонецЕсли;
		
		Если ДоступностьСотрудника = Перечисления.ЦБР_ДоступноеВремяСотрудников.ДоступенВсегда Тогда
			
			ЗаполнитьТаблицуДоступностиПоУмолчаниюДляСотрудника(
				ТаблицаДоступности, Сотрудник, Перечисления.ЦБР_СостоянияЗанятости.Доступен);
			
		ИначеЕсли ДоступностьСотрудника = Перечисления.ЦБР_ДоступноеВремяСотрудников.ЗанятВсегда Тогда
			
			ЗаполнитьТаблицуДоступностиПоУмолчаниюДляСотрудника(
				ТаблицаДоступности, Сотрудник, Перечисления.ЦБР_СостоянияЗанятости.Занят);
			
		ИначеЕсли ДоступностьСотрудника = Перечисления.ЦБР_ДоступноеВремяСотрудников.ДоступенПоРасписанию Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Сотрудник", Сотрудник);
			НайденныеСтроки = ТаблицаДоступноеВремяСотрудников.НайтиСтроки(ПараметрыОтбора);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				НачалоОтсчета = НачалоДня(ТекущаяДатаСеанса());
				ВремяНачала = НачалоОтсчета
					+ (НайденнаяСтрока.ВремяНачала - НачалоДня(НайденнаяСтрока.ВремяНачала));
				ВремяОкончания = НачалоОтсчета
					+ (НайденнаяСтрока.ВремяОкончания - НачалоДня(НайденнаяСтрока.ВремяОкончания));
				
				ИсходныйДеньНачала = День(ВремяНачала);
				ИсходныйДеньОкончания = День(ВремяОкончания);
				
				ВремяНачала = МестноеВремя(ВремяНачала, ЧасовойПоясСеанса());
				ВремяОкончания = МестноеВремя(ВремяОкончания, ЧасовойПоясСеанса()); 

				ВремяНачала =  МестноеВремя(ВремяНачала, ЧасовойПоясСеанса());
			
				СмещенныйДеньНачала = День(ВремяНачала);
				СмещенныйДеньОкончания = День(ВремяОкончания);
				
				Если ИсходныйДеньНачала <> СмещенныйДеньНачала Тогда
					
					НоваяСтрока = ТаблицаДоступности.Добавить();
					НоваяСтрока.Сотрудник = НайденнаяСтрока.Сотрудник;
					Если НайденнаяСтрока.ДеньНедели = 1 Тогда
						НоваяСтрока.ДеньНедели = 7
					Иначе
						НоваяСтрока.ДеньНедели = НайденнаяСтрока.ДеньНедели - 1;
					КонецЕсли;
					НоваяСтрока.ВремяНачала = Дата(1, 1, 1)
						+ (ВремяНачала - НачалоДня(ВремяНачала));
					НоваяСтрока.ВремяОкончания = Дата(1, 1, 1)
						+ (КонецДня(ВремяНачала) - НачалоДня(ВремяНачала));
					
					Если НайденнаяСтрока.Занят Тогда
						НоваяСтрока.Занят = Перечисления.ЦБР_СостоянияЗанятости.Занят;
					Иначе
						НоваяСтрока.Занят = Перечисления.ЦБР_СостоянияЗанятости.Доступен;
					КонецЕсли;
					
					ВремяНачала = НачалоДня(ВремяНачала);
					
				КонецЕсли;
				
				Если ИсходныйДеньОкончания <> СмещенныйДеньОкончания Тогда
					
					НоваяСтрока = ТаблицаДоступности.Добавить();
					НоваяСтрока.Сотрудник = НайденнаяСтрока.Сотрудник;
					Если НайденнаяСтрока.ДеньНедели = 7 Тогда
						НоваяСтрока.ДеньНедели = 1
					Иначе
						НоваяСтрока.ДеньНедели = НайденнаяСтрока.ДеньНедели + 1;
					КонецЕсли;
					НоваяСтрока.ВремяНачала = Дата(1, 1, 1)
						+ (НачалоДня(ВремяОкончания) - НачалоДня(ВремяОкончания));
					НоваяСтрока.ВремяОкончания = Дата(1, 1, 1)
						+ (ВремяОкончания - НачалоДня(ВремяОкончания));
					
					Если НайденнаяСтрока.Занят Тогда
						НоваяСтрока.Занят = Перечисления.ЦБР_СостоянияЗанятости.Занят;
					Иначе
						НоваяСтрока.Занят = Перечисления.ЦБР_СостоянияЗанятости.Доступен;
					КонецЕсли;
					
					ВремяОкончания = НачалоДня(ВремяОкончания);
					
				КонецЕсли;
				
				ВремяНачала = Дата(1, 1, 1)
					+ (ВремяНачала - НачалоДня(ВремяНачала));
				ВремяОкончания = Дата(1, 1, 1)
					+ (ВремяОкончания - НачалоДня(ВремяОкончания));
				
				НоваяСтрока = ТаблицаДоступности.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
				
				НоваяСтрока.ВремяНачала = ВремяНачала;
				НоваяСтрока.ВремяОкончания = ВремяОкончания;
				
				Если НайденнаяСтрока.Занят Тогда
					НоваяСтрока.Занят = Перечисления.ЦБР_СостоянияЗанятости.Занят;
				Иначе
					НоваяСтрока.Занят = Перечисления.ЦБР_СостоянияЗанятости.Доступен;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаДоступности;
	
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТаблицуДоступностиПоУмолчаниюДляСотрудника(
	ТаблицаДоступности, Сотрудник, Занят)
	
	ВремяНачала = Дата(1, 1, 1);
	ВремяОкончания = КонецДня(ВремяНачала) + 1;
	
	Для ДеньНедели = 1 По 7 Цикл
		
		НоваяСтрока = ТаблицаДоступности.Добавить();
		НоваяСтрока.Сотрудник = Сотрудник;
		НоваяСтрока.ДеньНедели = ДеньНедели;
		НоваяСтрока.ВремяНачала = ВремяНачала;
		НоваяСтрока.ВремяОкончания = ВремяОкончания;
		НоваяСтрока.Занят = Занят;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
