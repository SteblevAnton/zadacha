#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Задание", 	Задание);
	Параметры.Свойство("Заявитель", Заявитель);
	Параметры.Свойство("Ответчик", 	Ответчик);	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если СокрЛП(ТекстЗапроса) = "" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан текст запроса.");
		Возврат;
	КонецЕсли;
	Отправлено = СохранитьНаСервере();  
	Если Отправлено Тогда
		СообщениеВОбсуждение();
		ЭтаФорма.Закрыть(Истина);  
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНаСервере()    
	
	ВТранзакции = ПроверитьНеобходимостьТранзакции();
	
	Если ВТранзакции Тогда //Если нужно что-то менять в объектах - делаем все в транзакции 
		НачатьТранзакцию();
	КонецЕсли;
	
	Запись = РегистрыСведений.ЦБР_УточненияПоЗадачамЗаданиям.СоздатьМенеджерЗаписи(); 
	Запись.Период 		= ТекущаяДатаСеанса();	
	Запись.Задание 		= Задание;
	Запись.Заявитель 	= Заявитель;
	Запись.Ответчик 	= Ответчик;
	Запись.ТекстЗапроса = ТекстЗапроса;
	Запись.Записать();
	
	Если ВТранзакции Тогда   
		
		ЗаданиеОбъект = Задание.ПолучитьОбъект(); 
		
		Если ТипЗнч(Задание) = Тип("ДокументСсылка.ЦБР_Задание") Тогда
			ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Задание);
			Если ПоследнееДействие <> Неопределено Тогда
				Если Не ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
					
					МенеджерЗаписи = РегистрыСведений.ЦБР_ДействияПоЗаданиям.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(
					Задание));
					МенеджерЗаписи.Прочитать();
					МенеджерЗаписи.ДатаОкончаниеДействия = ТекущаяДатаСеанса();
					МенеджерЗаписи.Статус                = Перечисления.ЦБР_Состояние.Приостановлен;
					МенеджерЗаписи.Комментарий           = "Уточнение информации";
					
					Действие = МенеджерЗаписи.ЗаписьКалендаря.ПолучитьОбъект();
					Действие.ДатаОкончания = ТекущаяДатаСеанса();
					Действие.Записать();
					
					РаботаСРабочимКалендаремСервер.УстановитьОтработанаЗаписьКалендаря(Действие.Ссылка);
					
					СтатусЗадачи = Перечисления.ЦБР_Состояние.ЗапросИнформации;
					
					СтруктураДат = Новый Структура;
					СтруктураДат.Вставить("ДатаНачала", НачалоДня(МенеджерЗаписи.ДатаНачалаДействия));
					СтруктураДат.Вставить("ДатаОкончания", НачалоДня(МенеджерЗаписи.ДатаОкончаниеДействия));
					СтруктураДат.Вставить("ВремяНачала", ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьВремя(
					МенеджерЗаписи.ДатаНачалаДействия));
					СтруктураДат.Вставить("ВремяОкончания", ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьВремя(
					МенеджерЗаписи.ДатаОкончаниеДействия));
					ЦБР_ЗадачиЗаданияВызовСервера.РасчитатьДлительностьФакт(СтруктураДат, ЗаданиеОбъект.ДлительностьФакт);
					
					МенеджерЗаписи.Записать(Истина);       
					
					Для каждого Исполнители Из ЗаданиеОбъект.СсылкаНаЗадачу.Команда Цикл
						ПоследнийСтатус = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадачиПользователя(ЗаданиеОбъект.СсылкаНаЗадачу, Исполнители.Исполнитель);
						Если СтатусЗадачи <> ПоследнийСтатус Тогда
							МенеджерЗаписи = РегистрыСведений.ЦБР_СтатусыЗадач.СоздатьМенеджерЗаписи();
							МенеджерЗаписи.Период = ТекущаяДатаСеанса();
							МенеджерЗаписи.Задача = ЗаданиеОбъект.СсылкаНаЗадачу;
							МенеджерЗаписи.Пользователь = Исполнители.Исполнитель; 
							МенеджерЗаписи.СостояниеЗадачи = СтатусЗадачи;
							МенеджерЗаписи.Записать();
						КонецЕсли;
					КонецЦикла;
					
					ЗаданиеОбъект.ДатаОкончаниеФакт = ТекущаяДатаСеанса();
				КонецЕсли;
				
			КонецЕсли;
			
		   	ЗаданиеОбъект.Состояние = Перечисления.ЦБР_Состояние.ЗапросИнформации;  
			ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(ЗаданиеОбъект.Ссылка, Перечисления.ЦБР_Состояние.ЗапросИнформации);  
			
		Иначе //Задача
			
			СтатусЗадачи = Перечисления.ЦБР_Состояние.ЗапросИнформации;
			Для каждого Исполнители Из ЗаданиеОбъект.Команда Цикл
				ПоследнийСтатус = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадачиПользователя(Задание, Исполнители.Исполнитель);
				Если СтатусЗадачи <> ПоследнийСтатус Тогда
					МенеджерЗаписи = РегистрыСведений.ЦБР_СтатусыЗадач.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.Период = ТекущаяДатаСеанса();
					МенеджерЗаписи.Задача = ЗаданиеОбъект.Ссылка;
					МенеджерЗаписи.Пользователь = Исполнители.Исполнитель; 
					МенеджерЗаписи.СостояниеЗадачи = СтатусЗадачи;
					МенеджерЗаписи.Записать();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
				
		Если ТипЗнч(ЗаданиеОбъект) = Тип("СправочникОбъект.ЦБР_Задача") Тогда
			ЗаданиеОбъект.Записать();
		Иначе
			ЗаданиеОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьНеобходимостьТранзакции()

	Если ТипЗнч(Задание) = Тип("ДокументСсылка.ЦБР_Задание") Тогда
		Если Задание.Состояние = Перечисления.ЦБР_Состояние.ВРаботе Или Задание.Состояние = Перечисления.ЦБР_Состояние.Согласован Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		СостояниеЗадачи = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадачиПользователя(Задание, Пользователи.ТекущийПользователь());
		
		Если СостояниеЗадачи <> Перечисления.ЦБР_Состояние.ЗапросИнформации Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

КонецФункции

&НаСервере
Процедура СообщениеВОбсуждение()
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Получатели = Новый Массив;
	Получатели.Добавить(Ответчик);
	ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Задание, "ЗапросИнформации", Получатели, ТекстЗапроса);	
		
КонецПроцедуры

#КонецОбласти

