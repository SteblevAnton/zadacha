#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значение настройки для Сотрудника
// 
// Параметры:
//  Сотрудник - СправочникСсылка.Сотрудники
//  Настройка - ПеречислениеСсылка.НастройкиРабочегоКалендаря
//  
// Возвращаемое значение:
//  ПеречислениеСсылка.ДоступностьСотрудника, Неопределено
//
Функция ПолучитьНастройку(Сотрудник, Настройка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НастройкиРабочегоКалендаря.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ЦБР_НастройкиРабочегоКалендаря КАК НастройкиРабочегоКалендаря
		|ГДЕ
		|	НастройкиРабочегоКалендаря.Сотрудник = &Сотрудник
		|	И НастройкиРабочегоКалендаря.Настройка = &Настройка");
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Настройка", Настройка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Значение;
	
КонецФункции

// Устанавливает настройку для Сотрудника
//
// Параметры:
//  Сотрудник - СправочникСсылка.Сотрудники
//  Настройка - ПеречислениеСсылка.НастройкиРабочегоКалендаря
//  Значение - ПеречислениеСсылка.ДоступностьСотрудника

Функция УстановитьНастройку(Сотрудник, Настройка, Значение) Экспорт
	
	ИзмененоЗначение = Ложь;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиРабочегоКалендаря.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Сотрудник = Сотрудник;
	МенеджерЗаписи.Настройка = Настройка;
	
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Значение <> Значение Тогда
		
		ИзмененоЗначение = Истина;
		
		МенеджерЗаписи.Сотрудник = Сотрудник;
		МенеджерЗаписи.Настройка = Настройка;
		МенеджерЗаписи.Значение = Значение;
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
	Возврат ИзмененоЗначение;
	
КонецФункции

#КонецОбласти

#КонецЕсли