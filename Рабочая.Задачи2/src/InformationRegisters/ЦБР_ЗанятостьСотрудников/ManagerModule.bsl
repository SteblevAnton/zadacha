#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Изменяет доступность времени
Функция ИзменитьДоступностьВремени(Сотрудник, ДатаНачала, ДатаОкончания, Занят) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(ДатаОкончания) 
		ИЛИ ДатаОкончания <= ДатаНачала Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СводнаяТаблицаЗанятости = РаботаСРабочимКалендаремСервер.ПолучитьТаблицуЗанятости(
		Сотрудник, ДатаНачала, ДатаОкончания);
	
	Если Занят = Неопределено Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Сотрудник", Сотрудник);
		ПараметрыОтбора.Вставить("Занят", Перечисления.ЦБР_СостоянияЗанятости.Доступен);
		СтрокиСвободногоВремени = СводнаяТаблицаЗанятости.НайтиСтроки(ПараметрыОтбора);
		Если СтрокиСвободногоВремени.Количество() <> 0 Тогда
			Занят = Перечисления.ЦБР_СостоянияЗанятости.Занят;
		Иначе
			Занят = Перечисления.ЦБР_СостоянияЗанятости.Доступен;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		
		НачатьТранзакцию();
		
		ТаблицаЗанятости = ПолучитьТаблицуЗанятости(Сотрудник, ДатаНачала, ДатаОкончания);
		Для Каждого Занятость Из ТаблицаЗанятости Цикл
			
			Если Занятость.ДатаНачала < ДатаОкончания
				И Занятость.ДатаОкончания > ДатаНачала Тогда
				
				Если Занятость.ДатаНачала < ДатаНачала Тогда
					МенеджерЗаписи = РегистрыСведений.ЦБР_ЗанятостьСотрудников.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Занятость);
					МенеджерЗаписи.ДатаОкончания = ДатаНачала;
					МенеджерЗаписи.Записать();
				КонецЕсли;
				
				Если Занятость.ДатаОкончания > ДатаОкончания Тогда
					МенеджерЗаписи = РегистрыСведений.ЦБР_ЗанятостьСотрудников.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Занятость);
					МенеджерЗаписи.ДатаНачала = ДатаОкончания;
					МенеджерЗаписи.Записать();
				КонецЕсли;
				
				МенеджерЗаписи = РегистрыСведений.ЦБР_ЗанятостьСотрудников.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Занятость);
				МенеджерЗаписи.Удалить();
				
			КонецЕсли;
			
		КонецЦикла;
		
		МенеджерЗаписи = РегистрыСведений.ЦБР_ЗанятостьСотрудников.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Сотрудник = Сотрудник;
		МенеджерЗаписи.ДатаНачала = ДатаНачала;
		МенеджерЗаписи.ДатаОкончания = ДатаОкончания;
		МенеджерЗаписи.Занят = Занят;
		МенеджерЗаписи.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат Занят;
	
КонецФункции

// Возвращает таблицу занятости сотрудников.
//
Функция ПолучитьТаблицуЗанятости(Знач МассивСотрудников, ДатаНачала, ДатаОкончания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗанятостьСотрудников.Сотрудник КАК Сотрудник,
		|	ЗанятостьСотрудников.ДатаНачала КАК ДатаНачала,
		|	ЗанятостьСотрудников.ДатаОкончания КАК ДатаОкончания,
		|	ЗанятостьСотрудников.Занят КАК Занят
		|ИЗ
		|	РегистрСведений.ЦБР_ЗанятостьСотрудников КАК ЗанятостьСотрудников
		|ГДЕ
		|	ЗанятостьСотрудников.Сотрудник В(&МассивСотрудников)
		|	И ЗанятостьСотрудников.ДатаНачала < &ДатаОкончания
		|	И ЗанятостьСотрудников.ДатаОкончания > &ДатаНачала";
	
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	ТаблицаЗанятости = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаЗанятости Из ТаблицаЗанятости Цикл
		
		СтрокаЗанятости.ДатаНачала =  МестноеВремя(СтрокаЗанятости.ДатаНачала, ЧасовойПоясСеанса());
		СтрокаЗанятости.ДатаОкончания =СтрокаЗанятости.ДатаОкончания =  МестноеВремя(СтрокаЗанятости.ДатаНачала, ЧасовойПоясСеанса());
		
	КонецЦикла;
	
	Возврат ТаблицаЗанятости;
	
КонецФункции

#КонецОбласти

#КонецЕсли