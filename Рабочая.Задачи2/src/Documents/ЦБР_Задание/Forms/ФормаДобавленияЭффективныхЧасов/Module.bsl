
#Область ОбработчикиФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ТекущаяДатаСеанса();
		
	Если Параметры.Свойство("Комментарий") Тогда 
		Комментарий = Параметры.Комментарий;
	КонецЕсли;
	
	Если Параметры.Свойство("Задание") Тогда 
		
		Задание = Параметры.Задание; 
		
		РеквизитыЗадания = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(Задание, "СсылкаНаЗадачу, ТрудозатратыПлан, ТрудозатратыФакт, ТипОценкиЗадачи, ОплатаПоФакту, УдалосьРешитьПриПодключении, ОплатаПоФакту, Исполнитель"); 
		Задача          = РеквизитыЗадания.СсылкаНаЗадачу;
		ТипОценкиЗадачи = РеквизитыЗадания.ТипОценкиЗадачи;
		ПоФакту         = РеквизитыЗадания.ОплатаПоФакту;
		Исполнитель     = РеквизитыЗадания.Исполнитель;
		Если ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.Предсогласованные") Тогда
			ЧасовДоступно = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьОстатокПредсогласованныхЧасов(Задача);  
		Иначе
			ЧасовДоступно = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьОстатокЧасовПоЗаданию(Задание);  
			КоличествоЗаданийВЗадаче = ЦБР_ЗадачиЗаданияВызовСервера.КоличествоЗаданийВЗадаче(Задача);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РешеноПриПервомПодключении", "Видимость", КоличествоЗаданийВЗадаче = 1 И Не РеквизитыЗадания.УдалосьРешитьПриПодключении);	
		КонецЕсли;
		ЧасовДоступноПредставление = ЧасовДоступно;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Исполнитель) И Не ЦБР_ЗадачиЗаданияВызовСервера.ЕстьПравоСписанияЧасовПоЗаданию(Исполнитель, Задача) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЧасовДоступно", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДобавитьВсеДоступныеЧасы", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЭффективныеЧасы", "Видимость", Ложь);
	ИначеЕсли ЧасовДоступно > 0 Тогда
        ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЧасовДоступно", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДобавитьВсеДоступныеЧасы", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЭффективныеЧасы", "Видимость", Истина);
	ИначеЕсли ПоФакту Тогда
        ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЧасовДоступно", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДобавитьВсеДоступныеЧасы", "Видимость", Ложь);		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЭффективныеЧасы", "Видимость", Истина);
		ЧасовДоступноПредставление = "по факту";
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЧасовДоступно", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДобавитьВсеДоступныеЧасы", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЭффективныеЧасы", "Видимость", Ложь);
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаОкончания") Тогда 
		ДатаОкончания = Параметры.ДатаОкончания;
	КонецЕсли;

	Если Параметры.Свойство("ПоПлану") Тогда
		ПоПлану = Параметры.ПоПлану;
	КонецЕсли;  
	
	Если Параметры.Свойство("Описание") Тогда
		Описание = Параметры.Описание;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаписьОС") Тогда
		ЗаписьОС = Параметры.ЗаписьОС;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикКомандФормы

&НаКлиенте
Процедура Добавить(Команда)	

	Если ОписаниеВыполненнойРаботы.Элементы.Количество() = 0 Тогда  
		ОбщегоНазначенияКлиент.СообщитьПользователю("Поле ""Описание выполненной работы"" не заполненно!",,"ОписаниеВыполненнойРаботы","ОписаниеВыполненнойРаботы");
		Возврат;	
	КонецЕсли;
	
	Если ЭффективныеЧасы > ЧасовДоступно И Не ПоФакту Тогда		 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Количество эффективных часов превышает, количество доступных!");
		Возврат;	
	КонецЕсли;
	
	Если РешеноПриПервомПодключении Тогда 
		ЭффективныеЧасы = 0.5;	   
	КонецЕсли;
	
	ВыполнитьПроцедурыЗакрытияФормы();
	
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВсеДоступныеЧасы(Команда)
	ЭффективныеЧасы = ЧасовДоступно;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РешеноПриПервомПодключенииПриИзменении(Элемент)
	
	Если ОписаниеВыполненнойРаботы.Элементы.Количество() = 0 Тогда   
		ОписаниеВыполненнойРаботы.Добавить("Решено при первом подключении.", ТипЭлементаФорматированногоДокумента.Текст);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьПроцедурыЗакрытияФормы()
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ЗаписьОС", ЗаписьОС);
	ДанныеЗаполнения.Вставить("ЗаданиеСсылка", Задание);  
	ДанныеЗаполнения.Вставить("ЗадачаСсылка", Задача);  
	ДанныеЗаполнения.Вставить("ЭффективныеЧасы", ЭффективныеЧасы); 
	ДанныеЗаполнения.Вставить("Дата", Дата);
	ДанныеЗаполнения.Вставить("ЗакрытьЗадание", ЗакрытьЗадание);
	ДанныеЗаполнения.Вставить("РешеноПриПервомПодключении", РешеноПриПервомПодключении);
	ДанныеЗаполнения.Вставить("ОписаниеВыполненнойРаботы", ОписаниеВыполненнойРаботы);
	ДанныеЗаполнения.Вставить("ПоПлану", ПоПлану);
	
	ЦБР_ЗадачиЗаданияВызовСервера.ЗаписатьЧасыВЗадачу(ДанныеЗаполнения);
	ЦБР_ЗадачиЗаданияВызовСервера.УстановитьОтработана(ДанныеЗаполнения);
	
	УведомлениеВОбсуждение();
	
КонецПроцедуры

&НаСервере
Процедура СогласоватьПолЧасаЗаРешение()	  
	
	ПредметОбъект = Задание.ПолучитьОбъект();	
	ПредметОбъект.ТрудозатратыПлан = 0.5;
	ПредметОбъект.УдалосьРешитьПриПодключении = Истина;
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(ПредметОбъект, Перечисления.ЦБР_Состояние.Выполнен);
    ПредметОбъект.Записать(); 
	
КонецПроцедуры

&НаСервере
Процедура УведомлениеВОбсуждение()
	
	Получатели = Новый Массив;     		
	Получатели.Добавить(ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Задача, "Постановщик"));
	ТекстСообщения = СтрШаблон("Действие: %1%2Результат: %3", Описание, Символы.ПС, ОписаниеВыполненнойРаботы.ПолучитьТекст());
	ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Задача, "РезультатОС", Получатели, ТекстСообщения); 
	
КонецПроцедуры

#КонецОбласти
