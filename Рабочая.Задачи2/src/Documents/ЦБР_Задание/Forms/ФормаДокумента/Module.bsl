
#Область ОбработчикиФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Параметры.Свойство("Ссылка") Тогда
		Объект.СсылкаНаЗадачу = Параметры.Ссылка;
		
		Если Параметры.Свойство("Задание") Тогда
			Если Параметры.Задание.ЭтоГруппа Тогда
				Объект.Родитель = Параметры.Задание;
			Иначе
				Объект.Родитель = Параметры.Задание.Родитель;
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.Свойство("Подзадача") Тогда
			Объект.ПредыдущееЗадание = Параметры.Подзадача;
		КонецЕсли;
		
		Если Параметры.Свойство("Исполнитель") Тогда
			Объект.Исполнитель = Параметры.Исполнитель;
		КонецЕсли;

	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ЗначениеЗаполнено(Объект.Исполнитель) Тогда
			Объект.Исполнитель = Пользователи.ТекущийПользователь();  
		КонецЕсли; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СсылкаНаЗадачу) Тогда
		
		ЗначенияРеквизитовЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СсылкаНаЗадачу, "ТипОценкиЗадачи, ТипЗадачи");   
		Если ЗначенияРеквизитовЗадачи.ТипОценкиЗадачи <> Объект.ТипОценкиЗадачи Тогда
			Объект.ТипОценкиЗадачи = ЗначенияРеквизитовЗадачи.ТипОценкиЗадачи; 
		КонецЕсли;	
		Если Не ЗначениеЗаполнено(Объект.ВидЧаса) Тогда
			Если ЗначенияРеквизитовЗадачи.ТипЗадачи = Перечисления.ЦБР_ТипЗадачи.Демо Тогда
				Объект.ВидЧаса = Перечисления.ЦБР_ВидыЧасовПоЗадачам.Демо;
			Иначе
				Объект.ВидЧаса = Перечисления.ЦБР_ВидыЧасовПоЗадачам.Платные;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
	Если Объект.ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.ПоФакту") Тогда
		Объект.ОплатаПоФакту = Истина;
	КонецЕсли;	
	
	НастройкиОтображения = РаботаСРабочимКалендаремСервер.ПолучитьНастройкиОтображения();
	
	ПользователиКалендаря.Добавить(Объект.Исполнитель);
	ПользовательКалендаря = Объект.Исполнитель;
	ОтображаемаяДата = НачалоДня(ТекущаяДатаСеанса());
	
	УстановитьВидимостьЭлементовЗапросаИнформации(); 
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)   
	
	УстановитьВидимостьКомандДействий();
	УстановитьДоступностьЭлементов();
	НайтиОтменитьЗапросИнформции();         
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗакрытиеЧата" Тогда
		УстановитьВидимостьКомандДействий();
		НайтиОтменитьЗапросИнформции(); 
	ИначеЕсли ИмяСобытия = "Изменение_ЭффективныеЧасы" Тогда
		ЭтаФорма.Прочитать();  
	ИначеЕсли ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда	
		ОбновитьДанныеКалендаря("ПериодОтображения");
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	ОписаниеЗадания = ТекущийОбъект.Описание.Получить();
	Если Параметры.Свойство("СтруктураСДанными") Тогда 
		ДанныеИзЗадачи                  = Параметры.СтруктураСДанными;
		Объект.ЕстьТестирование         = ДанныеИзЗадачи.ЕстьТестирование;
		Объект.ЭтоТестирование          = ДанныеИзЗадачи.ЭтоТестирование;
		Объект.СсылкаНаЭтапТестирования = ДанныеИзЗадачи.СсылкаНаЭтапТестирования;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Описание = Новый ХранилищеЗначения(ОписаниеЗадания);
КонецПроцедуры                                                      

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("НужноОбновлениеАРМ");  
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Стартовать(Команда)
	
	Действие = НайтиЗаписьКалендаря();
	Если Действие <> Неопределено Тогда
		Оповещение = Новый ОписаниеОповещения("ОтветНаВопрос", ЭтотОбъект, Действие);
		
		ПоказатьВопрос(Оповещение, "Уже есть запланирование действие по задаче! Начать его выполнение?",
		РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		СтартоватьНаСервере();
		//Прочитать();
	КонецЕсли;
	//Если Не ЕстьНезавершенноеДействие() Тогда
	//	
	//	Действие = НайтиЗаписьКалендаря();
	//	Если Действие <> Неопределено Тогда
	//		Оповещение = Новый ОписаниеОповещения("ОтветНаВопрос", ЭтотОбъект, Действие);
	//		
	//		ПоказатьВопрос(Оповещение, "На сегодня есть запланирование действие по задаче! Начать его выполнение?",
	//		РежимДиалогаВопрос.ДаНетОтмена);
	//	Иначе
	//		СтартоватьНаСервере();
	//		//Прочитать();
	//	КонецЕсли;
	//Иначе
	//	СтартоватьНаСервере();     
	//	//Прочитать();
	//КонецЕсли;

	ПараметрыЗаписи = Новый Структура("РежимЗаписи");
	ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьКомандДействий();
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьНезавершенноеДействие()
	ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.Ссылка);
	Если ПоследнееДействие = Неопределено Или ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ОтветНаВопрос(Результат, ЗаписьКалендаря) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Действие = СоздатьЗаписьВКалендаре();
	Иначе
		Действие = СоздатьЗаписьВКалендаре(ЗаписьКалендаря);
		//Объект.Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе");
	КонецЕсли;
	
	Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
	Структура.Задание = Объект.Ссылка;
	Структура.ДатаНачалаДействия =  ТекущаяДата();
	Структура.Статус = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе");
	Объект.ДатаНачалоФакт = ТекущаяДата();
	//Объект.Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе");
	Структура.ЗаписьКалендаря = Действие;
	ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
	
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе"));

	ПараметрыЗаписи = Новый Структура("РежимЗаписи");
	ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьКомандДействий();
	
КонецПроцедуры

&НаСервере
Процедура СтартоватьНаСервере()
	
	Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
	Структура.Задание = Объект.Ссылка;
	Структура.ДатаНачалаДействия =  ТекущаяДатаСеанса();
	Структура.Статус = Перечисления.ЦБР_Состояние.ВРаботе;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачалоФакт) Тогда
		Объект.ДатаНачалоФакт = ТекущаяДатаСеанса();		
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.ВРаботе);
		//Объект.Состояние = Перечисления.ЦБР_Состояние.ВРаботе;
		Действие = СоздатьЗаписьВКалендаре();
		Структура.ЗаписьКалендаря = Действие;
		ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
	Иначе
		//ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.Ссылка);
		//Если ПоследнееДействие = Неопределено Или ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
			ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.ВРаботе);
			//Объект.Состояние = Перечисления.ЦБР_Состояние.ВРаботе;
			//Записать();
			Действие  = СоздатьЗаписьВКалендаре();
			Структура.ЗаписьКалендаря = Действие;
			ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
		//Иначе
		//	ОбщегоНазначения.СообщитьПользователю("Есть не заврешенное действие!");
		//КонецЕсли;
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Функция НайтиЗаписьКалендаря()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка = &ЗаданиеСсылка      
	|	И ДЕНЬ(ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала) = ДЕНЬ(&Дата)
	|	И ЦБР_ЗаписиРабочегоКалендаря.Статус = &Статус
	|	И НЕ ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ЗаданиеСсылка", Объект.Ссылка); 
	Запрос.УстановитьПараметр("Статус", Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);  
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СоздатьЗаписьВКалендаре(ЗаписьКалендаря = Неопределено)
	
	Если ЗаписьКалендаря = Неопределено Тогда
		Действие = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
	Иначе
		Действие = ЗаписьКалендаря.ПолучитьОбъект();
	КонецЕсли;
	
	Длительность = ?(Объект.ТрудозатратыПлан - Объект.ТрудозатратыФакт > 0, Объект.ТрудозатратыПлан - Объект.ТрудозатратыФакт, 1);
	
	Действие.Автор = Пользователи.ТекущийПользователь();
	
	Действие.ДатаНачала = ТекущаяДатаСеанса();
	Действие.ВидРаботы = Справочники.ЦБР_ВидыРаботВКалендаре.Эффективный;
	Действие.ДатаОкончания = ЦБР_ЗадачиЗаданияВызовСервера.РассчитатьОкончаниеПериода(Действие.ДатаНачала, ?(Длительность = 0, 1, Длительность));
	Если НЕ ЗначениеЗаполнено(Действие.Описание) Тогда
		Действие.Описание = "" + Объект.Ссылка + "";
	КонецЕсли;
	Действие.Пользователь = Объект.Исполнитель;
	Действие.ЗаданиеСсылка = Объект.Ссылка;
	Действие.ЭтоСтартИзЗадания = Истина;
	
	Действие.Записать();
	
	РаботаСРабочимКалендаремСервер.УстановитьПриступилЗаписьКалендаря(Действие.Ссылка, Истина);
//	РаботаСРабочимКалендаремСервер.УстановитьОтработанаЗаписьКалендаря(Действие.Ссылка, Ложь);

	Возврат Действие.Ссылка;
	
КонецФункции

&НаКлиенте
Процедура Пауза(Команда)
	
	Если Не ЗначениеЗаполнено(ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.Ссылка).ДатаОкончаниеДействия) Тогда
		ПричинаПереноса = "";
		Подсказка = "Причина приостановки:";
		Оповещение  = Новый ОписаниеОповещения("ПослеВводаОписаниеПричиныПаузы", ЭтотОбъект);
		ПоказатьВводСтроки(Оповещение, ПричинаПереноса, Подсказка, 0, Истина);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нет действия в работе!");
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура("РежимЗаписи");
	ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ ЦБР_ЗадачиЗаданияВызовСервера.ПроверитьВозможностьЗакрытия(Объект.Ссылка) Тогда      
		Возврат;
	КонецЕсли;
	
	ЦБР_ЗадачиЗаданияВызовСервера.ПроверкаЗаполнениеПодчиненныхЭтапов(Объект.Ссылка);
	
	ЦБР_ЗадачиЗаданияВызовСервера.ЗавершитьЗадание(Объект.Ссылка);  
	
	УстановитьВидимостьКомандДействий();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗаписьОС(Статус)
	
	МенеджерЗаписи = РегистрыСведений.ЦБР_ДействияПоЗаданиям.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.Ссылка));
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.ДатаОкончаниеДействия = ТекущаяДатаСеанса();
	МенеджерЗаписи.Статус = Статус;
	
	Действие = МенеджерЗаписи.ЗаписьКалендаря.ПолучитьОбъект();
	Действие.ДатаОкончания = ТекущаяДатаСеанса();
	Действие.Записать();
	
	РаботаСРабочимКалендаремСервер.УстановитьОтработанаЗаписьКалендаря(Действие.Ссылка, Истина);
	Действие.Ссылка.Цвет = Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет;
	
	СтруктураДат = Новый Структура;
	СтруктураДат.Вставить("ДатаНачала",НачалоДня(МенеджерЗаписи.ДатаНачалаДействия));
	СтруктураДат.Вставить("ДатаОкончания",НачалоДня(МенеджерЗаписи.ДатаОкончаниеДействия));
	СтруктураДат.Вставить("ВремяНачала", ПолучитьВремя(МенеджерЗаписи.ДатаНачалаДействия));
	СтруктураДат.Вставить("ВремяОкончания", ПолучитьВремя(МенеджерЗаписи.ДатаОкончаниеДействия));
	РасчитатьДлительностьФакт(СтруктураДат);
	
	МенеджерЗаписи.Записать(Истина);
	
	Если Объект.ТрудозатратыПлан - Объект.ТрудозатратыФакт = 0 ИЛИ Объект.ОплатаПоФакту Тогда
		//Объект.Состояние = Перечисления.ЦБР_Состояние.Выполнен;
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.Выполнен);
	Иначе
		//Объект.Состояние = Перечисления.ЦБР_Состояние.ВРаботе;
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.Выполнен);
		ОбщегоНазначения.СообщитьПользователю("Чтобы закрыть задание, необходимо выработать все часы. Остаток часов: " + Строка(Объект.ТрудозатратыПлан - Объект.ТрудозатратыФакт));
	КонецЕсли;
	
	Объект.ДатаОкончаниеФакт = ТекущаяДатаСеанса(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭффективныеЧасы(Команда) 
	
	//Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораЗавершение", ЭтотОбъект);
	//ПараметрыФормы = Новый Структура;
	//ПараметрыФормы.Вставить("ЧасыДоступны", Объект.ТрудозатратыПлан - Объект.ТрудозатратыФакт);
	//ПараметрыФормы.Вставить("ПоФакту", Объект.ОплатаПоФакту); 
	//ПараметрыФормы.Вставить("Задание", Объект.Ссылка);
	//ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтаФорма, , , ,
	//					Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура Уточнение(Команда)
	
	Если Элементы.ФормаУточнение.Заголовок = "Запрос информации" Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Задание", Объект.Ссылка);
		СтруктураПараметров.Вставить("Заявитель", Объект.Исполнитель);
		СтруктураПараметров.Вставить("Ответчик", Объект.Аналитик);
		
		Оповещение = Новый ОписаниеОповещения("УточнениеЗавершение", ЭтаФорма);
		ОткрытьФорму("РегистрСведений.ЦБР_УточненияПоЗадачамЗаданиям.Форма.ФормаУточнения", СтруктураПараметров, ЭтаФорма, , , , Оповещение);
	Иначе     
		Оповещение = Новый ОписаниеОповещения("ОтменитьЗапросИнформацииПродолжить", ЭтотОбъект);
		ТекстВопроса = "Отменить запрос информации?";
		Кнопки = РежимДиалогаВопрос.ДаНет;
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗапросИнформацииПродолжить(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НайтиОтменитьЗапросИнформции(Истина);  
		УстановитьВидимостьКомандДействий(); 
		УстановитьВидимостьЭлементовЗапросаИнформации();
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.ВРаботе"));  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеЗавершение(Результат, ДопПараметры) Экспорт 
	
	Если Результат Тогда
		УстановитьВидимостьКомандДействий();
		НайтиОтменитьЗапросИнформции();
		УстановитьВидимостьЭлементовЗапросаИнформации();
		ЭтаФорма.Прочитать();
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура РазвыполнитьЭтап(Команда)

		Оповещение = Новый ОписаниеОповещения("РазвыполнитьЭтапПродолжить", ЭтотОбъект);
		ТекстВопроса = "Отменить выполнение всей задачи?";
		Кнопки = РежимДиалогаВопрос.ДаНет;
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьИсполнителя(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьИсполнителяЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ЦБР_ФормаПодбораИсполнителей", , ЭтотОбъект, , , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаСогласование(Команда)

	ЕстьСтартованныеДействия = ЦБР_СтатусыОбъектовПолныеПрава.ЕстьСтартованныеДействия(Объект.Ссылка);
	Если ЕстьСтартованныеДействия Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Чтобы согласовать задание, необходимо закрыть все ОС");
		Возврат;
	КонецЕсли;
	
	Если Объект.РамочнаяОценка Тогда
		Если Объект.ТрудозатратыПланДо <= Объект.ТрудозатратыПланОт Тогда
			ТекстСообщения = "Верхний уровень оценки должен быть выше нижнего!";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		ИначеЕсли Объект.ТрудозатратыПланОт = 0 Тогда
			ТекстСообщения = "Нижний порог не может быть 0!";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли; 
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ТрудозатратыПлан) И Не Объект.ОплатаПоФакту Тогда
		ТекстСообщения = "Должна быть точная оценка или оценка ""По факту""!";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОтправитьНаСогласованиеНаСервере();
	
	УстановитьВидимостьКомандДействий();
	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЧат(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЧатПослеЗакрытия", ЭтотОбъект);  
	ПараметрыФормы = Новый Структура("Задание", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.ЦБР_УточненияПоЗадачамЗаданиям.Форма.ФормаЧата",ПараметрыФормы , ЭтотОбъект, , , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура Переоценить(Команда)     
	
	Если НЕ Объект.ЧасыСогласованы Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Задача ранее не была согласована");
		Возврат;
	КонецЕсли;

    Оповещение = Новый ОписаниеОповещения("ПослеВводаНовойОценки", 
      ЭтотОбъект);	
 
    ПоказатьВводЗначения(
        Оповещение,
        , 
        "Введите новую оценку",
        "Число"
    );

КонецПроцедуры  

&НаКлиенте
Процедура ОтклонитьЭтап(Команда)
	
	ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаОтклоненияЭтапа",, ЭтотОбъект, , , ,
		Новый ОписаниеОповещения("ПослеВводаСообщениеОтклонитьЭтап", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
   
КонецПроцедуры

&НаКлиенте
Процедура ПринятьЭтап(Команда)
	
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.Новый"));	
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиЗадачу(Команда)
	ПараметрыФормы = Новый Структура("Куратор", Объект.Исполнитель);
	ОткрытьФорму("Справочник.ЦБР_Задача.Форма.ФормаПеревода", Параметрыформы, ЭтотОбъект, , , ,
		Новый ОписаниеОповещения("ПеревестиЗадачуЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ЧасыИРеализации(Команда)
	
	ПараметрыФормы = Новый Структура("СсылкаНаЗадание, СсылкаНаЗадачу", Объект.Ссылка, Объект.СсылкаНаЗадачу);  
	
	ОткрытьФорму("Справочник.ЦБР_Задача.Форма.ФормаУчетаЧасовИРеализаций", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОценкиПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура РамочнаяОценкаПриИзменении(Элемент)
			
	//ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаРамочнаяОценка", "Видимость", Объект.РамочнаяОценка);
	//ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Видимость", НЕ Объект.РамочнаяОценка);  
	
	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ПредыдущееЗаданиеПриИзменении(Элемент) 
	
	УстановитьДоступностьЭлементов();  
	
КонецПроцедуры  

&НаКлиенте
Процедура ЭффективныеЧасыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыПланДоПриИзменении(Элемент)     
	
	Объект.ТрудозатратыПлан  =  Объект.ТрудозатратыПланДо;   
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НайтиОтменитьЗапросИнформции(Отменить=Ложь)	
	ЦБР_ЗадачиЗаданияВызовСервера.НайтиОтменитьЗапросИнформции(Элементы, Объект, Отменить);	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаОписаниеПричиныПаузы(Ответ, Параметры) Экспорт
	Если НЕ Ответ = Неопределено И ЗначениеЗаполнено(Ответ)Тогда 
		
		ПричинаПереноса = Ответ;	
		
		ИзменитьЗаписьОС(ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.Приостановлен"));
		УстановитьВидимостьКомандДействий();
		
		ПараметрыЗаписи = Новый Структура("РежимЗаписи");
		ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
		
		Если Не Записать(ПараметрыЗаписи) Тогда
			Возврат;
		КонецЕсли;	
		
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана причина приостановки!");	
	КонецЕсли;   
	
КонецПроцедуры

&НаСервере
Процедура РасчитатьДлительностьФакт(СтруктураДат)
	
	Если НЕ ЗначениеЗаполнено(СтруктураДат.ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(СтруктураДат.ДатаОкончания) 
		ИЛИ НЕ ЗначениеЗаполнено(СтруктураДат.ВремяНачала) ИЛИ НЕ ЗначениеЗаполнено(СтруктураДат.ВремяОкончания) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеПроизводственногоКалендаря.Дата КАК ДатаПредпразничный
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|ГДЕ
	|	ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И (ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|			ИЛИ ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПроизводственногоКалендаря.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПредпразничный";
	
	Запрос.УстановитьПараметр("ДатаНачала", СтруктураДат.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", СтруктураДат.ДатаОкончания);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	КоличествоДней = Результат.Количество();
	
	Если КоличествоДней > 1 Тогда
		Если СтруктураДат.ВремяНачала = Дата(01,01,01,09,00,00) И СтруктураДат.ВремяОкончания = Дата(01,01,01,18,00,00) Тогда
			
			Длительность = КоличествоДней * 8;
			
		ИначеЕсли СтруктураДат.ВремяНачала = Дата(01,01,01,18,00,00) И СтруктураДат.ВремяОкончания = Дата(01,01,01,18,00,00) Тогда
			
			Длительность = (КоличествоДней - 1) * 8;
			
		Иначе
			
			ПолныхДней = КоличествоДней - 2;
			
			ЧасыВНачале = (17 - Час(СтруктураДат.ВремяНачала));
			МинутыВНачале = (60 - Минута(СтруктураДат.ВремяНачала)) / 60;
			
			ПервыйДень = ?(МинутыВНачале = 1, ЧасыВНачале, ЧасыВНачале - 1 + МинутыВНачале);
			
			Если СтруктураДат.ВремяОкончания <= Дата(1,1,1,12,00,00) Тогда
				ЧасыВКонце = (Час(СтруктураДат.ВремяОкончания) - 9);
			Иначе
				ЧасыВКонце = (Час(СтруктураДат.ВремяОкончания) - 10);
			КонецЕсли;
			
			МинутыВКонце = (60 - Минута(СтруктураДат.ВремяОкончания)) / 60;
			
			ПоследнийДень = ?(МинутыВКонце = 1, ЧасыВКонце, ЧасыВКонце + МинутыВКонце);
			
			Длительность = ПолныхДней * 8 + ПервыйДень + ПоследнийДень;
			
		КонецЕсли;
		
	Иначе
		
		Часы = (Час(СтруктураДат.ВремяОкончания) - Час(СтруктураДат.ВремяНачала));
		Минуты = (Минута(СтруктураДат.ВремяОкончания) - Минута(СтруктураДат.ВремяНачала)) / 60;
		
		Длительность = ?(Минуты = 0, Часы - 1, Часы - 1 + Минуты);
		
	КонецЕсли;
	
	Объект.ДлительностьФакт = Объект.ДлительностьФакт + Длительность;
	
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВремя(Дата)
	
	Возврат Дата(1, 1, 1, Час(Дата), Минута(Дата), Секунда(Дата));
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовЗапросаИнформации()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.Период КАК Период,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.Заявитель КАК Заявитель,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.Ответчик КАК Ответчик,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.ТекстЗапроса КАК ТекстЗапроса,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.ТекстОтвета КАК ТекстОтвета,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.Отменено КАК Отменено,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.ДатаОтветаНаЗапрос КАК ДатаОтветаНаЗапрос,
	               |	ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних.ДатаОтветаНаЗапросФакт КАК ДатаОтветаНаЗапросФакт
	               |ИЗ
	               |	РегистрСведений.ЦБР_УточненияПоЗадачамЗаданиям.СрезПоследних(
	               |			,
	               |			Задание = &Задание
	               |				И НЕ Отменено) КАК ЦБР_УточненияПоЗадачамЗаданиямСрезПоследних
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период";

	Запрос.УстановитьПараметр("Задание", Объект.Ссылка);

	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОткрытьЧат", "Видимость", Истина);

		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если ЗначениеЗаполнено(Выборка.ДатаОтветаНаЗапрос) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаОтветаНаЗапрос", "Видимость", Истина);  
			ДатаОтветаНаЗапрос = Формат(Выборка.ДатаОтветаНаЗапрос, "ДЛФ=DD;"); 
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаОтветаНаЗапрос", "Видимость", Ложь);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОткрытьЧат", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКомандДействий()
	
	ТекущийПользователь = ПользователиКлиент.ТекущийПользователь(); 
	
	Если ЗначениеЗаполнено(Объект.Исполнитель) И Не ЦБР_ЗадачиЗаданияВызовСервера.ЕстьПравоСписанияЧасовПоЗаданию(Объект.Исполнитель, Объект.СсылкаНаЗадачу) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтправитьНаСогласование", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Переоценить", "Видимость", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЧасы", "Доступность", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВидЧаса", "Доступность", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредыдущееЗадание", "Доступность", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаИсполнительПодразделение", "Доступность", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		
		Возврат;
	КонецЕсли;	
	
	Состояние = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(Объект.Ссылка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Переоценить", "Видимость", Объект.ЧасыСогласованы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтправитьНаСогласование", "Видимость", Не Объект.ЧасыСогласованы);
	
	Если Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", ЗначениеЗаполнено(Объект.Аналитик) И ТекущийПользователь = Объект.Исполнитель);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Истина); 
		
	ИначеЕсли Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.Выполнен") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", Ложь );
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтправитьНаСогласование", "Видимость", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Переоценить", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		
	ИначеЕсли Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.Закрыт") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", Ложь );
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Переоценить", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтправитьНаСогласование", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		
	ИначеЕсли Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.Согласован") Тогда 
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", ЗначениеЗаполнено(Объект.Аналитик) И ТекущийПользователь = Объект.Исполнитель);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Истина); 
		
	ИначеЕсли Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.НаСогласовании") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Переоценить", "Видимость", Истина);
	    ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтправитьНаСогласование", "Видимость", Ложь);  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		
	ИначеЕсли Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ЗапросИнформации") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", ТекущийПользователь = Объект.Исполнитель);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСтартовать", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДобавитьСобытие", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		
	КонецЕсли;
	
	Если ПользователиКлиент.ТекущийПользователь() = Объект.Исполнитель Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаГруппаКомандыШапки", "Видимость", Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаГруппаКомандыШапки", "Видимость", Ложь);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаПеренестиЧасы", "Видимость", Не Объект.Разделена);
	
	Если Объект.ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.ПоФакту") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтправитьНаСогласование", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Переоценить", "Видимость", Ложь);
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	
	Состояние = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(Объект.Ссылка);

	Если ЗначениеЗаполнено(Объект.Аналитик) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Аналитик", "Видимость",Истина); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКомандыАналитика", "Видимость",Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НуженАналитик", "Видимость",Ложь);   
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Аналитик", "Видимость",Ложь);    
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НуженАналитик", "Видимость",Истина); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКомандыАналитика", "Видимость",Ложь);
	КонецЕсли;
	
	Если ПользователиКлиент.ТекущийПользователь() = Объект.Аналитик ИЛИ ЕстьПолныеПрава() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Принято", "Доступность",Истина);
	КонецЕсли;

	Сообщение = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееСообщениеПоЗаданию(Объект.Ссылка, Истина);
	
	Для Каждого ЕстьХотьОдно Из Сообщение Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаРегистрСведенийЦБР_УточненияПоЗадачамЗаданиямОткрытьЧат","Видимость", Истина);
		Прервать;		
	КонецЦикла;
	
	Если Объект.ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.Предсогласованные") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаТипОценки", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаРамочнаяОценка", "Видимость", Ложь);  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Заголовок", "Доп. пресогласованные часы"); 		
	ИначеЕсли Объект.ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.ПоФакту") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаТипОценки", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РамочнаяОценка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаРамочнаяОценка", "Видимость", Ложь);  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Видимость", Ложь); 
	ИначеЕсли Объект.ОплатаПоФакту Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РамочнаяОценка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаРамочнаяОценка", "Видимость", Ложь);  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Видимость", Ложь); 
	Иначе 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РамочнаяОценка", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаРамочнаяОценка", "Видимость", Объект.РамочнаяОценка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Видимость", НЕ Объект.РамочнаяОценка);  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланОценка", "Заголовок", "Трудозатраты, ч"); 		
	КонецЕсли;
	
	Если Состояние = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.НеСогласован")  
		Или Состояние = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.Новый")  
		Или Состояние = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.Запланировано")  
		Или Объект.ТрудозатратыПлан = 0
		Тогда      
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОценкаЗадания", "ТолькоПросмотр", Ложь);	  
    	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаименованиеЗадания", "Доступность", Истина); 
	Иначе    
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОценкаЗадания", "ТолькоПросмотр", Истина);   
    	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаименованиеЗадания", "Доступность", Ложь); 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПредыдущееЗадание) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПринятьЭтап", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтклонитьЭтап", "Видимость", Истина);
	Иначе	
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПринятьЭтап", "Видимость", Ложь);     
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтклонитьЭтап", "Видимость", Ложь);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если НЕ Объект.ОплатаПоФакту И Объект.ЭффективныеЧасы.Итог("ЭффективныеЧасы") + Результат.ЭффективныеЧасы > Объект.ТрудозатратыПлан Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю("Количество эффективных часов превышает, количество доступных!",Объект.Ссылка);
		Иначе
			Объект.ТрудозатратыФакт = Объект.ТрудозатратыФакт + Результат.ЭффективныеЧасы;
			НоваяСтрока = Объект.ЭффективныеЧасы.Добавить();
			НоваяСтрока.Исполнитель = Объект.Исполнитель;
			НоваяСтрока.ЭффективныеЧасы = Результат.ЭффективныеЧасы;
			НоваяСтрока.Дата = Результат.Дата; 
			НоваяСтрока.Комментарий = Результат.ОписаниеВыполненнойРаботы.ПолучитьТекст();
			
			ПараметрыЗаписи = Новый Структура("РежимЗаписи");
			ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Если Не Записать(ПараметрыЗаписи) Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеРеквизита(Ссылка, Реквизит)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,Реквизит);
КонецФункции

&НаСервере
Функция ЕстьПолныеПрава()

	Возврат РольДоступна("ПолныеПрава");
	
КонецФункции

&НаКлиенте
Процедура РазвыполнитьЭтапПродолжить(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		 ОчиститьЭфективныеЧасы();
	Иначе
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура("РежимЗаписи");
	ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьЭфективныеЧасы()
	
	СтрокиНаУдаление = Объект.ЭффективныеЧасы.НайтиСтроки(Новый Структура("Реализация", Документы.РеализацияТоваровУслуг.ПустаяСсылка()));
	Для Каждого Стр Из СтрокиНаУдаление Цикл
		Объект.ЭффективныеЧасы.Удалить(Объект.ЭффективныеЧасы.Индекс(Стр));	
	КонецЦикла;
		
	Объект.ТрудозатратыФакт = Объект.ЭффективныеЧасы.Итог("ЭффективныеЧасы");
	//Объект.Состояние        = Перечисления.ЦБР_Состояние.Согласован;   
	
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.Согласован);	

КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЭффективныеЧасыЭффективныеЧасы" Тогда
		ТекущиеДанные = Элементы.ЭффективныеЧасы.ТекущиеДанные;
		ПоказатьЗначение(,ТекущиеДанные.ДействиеКалендаря); 
	ИначеЕсли Поле.Имя = "ЭффективныеЧасыДействиеКалендаря" Тогда 
		ТекущиеДанные = Элементы.ЭффективныеЧасы.ТекущиеДанные;
		ПоказатьЗначение(,ТекущиеДанные.ДействиеКалендаря); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭффективныеЧасыОкончание(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Строка = Объект.ЭффективныеЧасы.НайтиПоИдентификатору(ДопПараметры.ТекущаяСтрока);
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Строка.ЭффективныеЧасы = Результат.ЭффективныеЧасы;

	ПараметрыЗаписи = Новый Структура("РежимЗаписи");
	ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИсполнителяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда   
		
		Объект.Исполнитель = Результат.МассивИсполнителей[0].Пользователь;

		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		
		МассивРолей = Новый Массив;
	    МассивРолей.Добавить(ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_РольВЗадаче.Исполнитель"));	
		
		ЦБР_ЗадачиЗаданияВызовСервера.ДобавитьПользователяВКомандуЗадачи(Объект.СсылкаНаЗадачу, Объект.Исполнитель, МассивРолей);
		ЦБР_ЗадачиЗаданияВызовСервера.УстановитьАналитикаВПодчиненныхЗаданиях(Объект.Ссылка, Объект.Исполнитель);
		
		Оповестить("ЗакрытиеДокЗадания");   
		Закрыть(); 
		
	КонецЕсли;  
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСогласованиеНаСервере(НоваяОценка = Неопределено)
	
	Если НоваяОценка <> Неопределено Тогда
		СтатусЗадания = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(Объект.Ссылка);	
		Если СтатусЗадания <> Перечисления.ЦБР_Состояние.НаСогласовании Тогда
			Объект.ТрудозатратыПланСтарый = Объект.ТрудозатратыПлан; 
		КонецЕсли;
		Объект.ТрудозатратыПлан       = НоваяОценка;
	КонецЕсли;
	
	РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СсылкаНаЗадачу, "Постановщик"); 
		
	Получатели = Новый Массив;
	Получатели.Добавить(РеквизитыЗадачи.Постановщик);
	
	ОтправленныеЭтапы = Новый Массив;
	ОтправленныеЭтапы.Добавить(Объект.Наименование);
	
	Если Объект.ТипОценкиЗадачи = Перечисления.ЦБР_ТипОценкиЗадачи.ПоФакту И Объект.ОплатаПоФакту Тогда
		Объект.ЧасыСогласованы = Истина;
	    ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.Согласован);	
		ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.СсылкаНаЗадачу, "СогласоватьОценку", Получатели, ОтправленныеЭтапы);
	Иначе 
	    ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.НаСогласовании);	
		ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.СсылкаНаЗадачу, "СогласованиеОценки", Получатели, ОтправленныеЭтапы);
		
		Если Объект.ТрудозатратыПланСтарый > 0 Тогда 
	         ЦБР_ОбсужденияСервер.ОтправитьСообщениеВОбщееОбсуждение(Объект.СсылкаНаЗадачу, СтрШаблон("Прежняя оценка: %1 ч", Объект.ТрудозатратыПланСтарый), Получатели);     
		КонецЕсли;	
		
		ЦБР_ОбсужденияСервер.ОтправитьСообщениеВОбщееОбсуждение(Объект.СсылкаНаЗадачу, СтрШаблон("Новая оценка для согласования: %1 ч", Объект.ТрудозатратыПлан), Получатели);     
		
		Если Объект.ОплатаПоФакту Тогда 
			ЦБР_ОбсужденияСервер.ОтправитьСообщениеВОбщееОбсуждение(Объект.СсылкаНаЗадачу, "Согласование по факту", Получатели);     
		КонецЕсли;	
	КонецЕсли;
	
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));   
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		Оповестить("ЗакрытиеДокЗадания");   
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура ПериодОтображенияПриИзменении(Элемент)
	
	УстановитьОтображаемуюДату(ОтображаемаяДата);

	ОбновитьДанныеКалендаря("ПериодОтображения");

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтображаемуюДату(НоваяОтображаемаяДата)

	Элементы.ОтображаемаяДата.ВыделенныеДаты.Очистить();
	ОтображаемаяДата = НоваяОтображаемаяДата;
	Элементы.ОтображаемаяДата.ВыделенныеДаты.Добавить(ОтображаемаяДата);
	ВыделенныеДаты.ЗагрузитьЗначения(Элементы.ОтображаемаяДата.ВыделенныеДаты);

	ОбновитьНастройкиОтображения();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеКалендаря(ИзмененнаяНастройка = Неопределено)
	
	РаботаСРабочимКалендаремКлиент.ОбновитьОтображениеПланировщика(
			Планировщик, НастройкиОтображения, ИзмененнаяНастройка);

КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеСервер(Параметр = Неопределено, ИзмененнаяНастройка = Неопределено,
	ОбновитьНастройки = Ложь)

	РаботаСРабочимКалендаремСервер.СохранитьНастройку(НастройкиОтображения, ИзмененнаяНастройка);

	НастройкиОтображения = РаботаСРабочимКалендаремСервер.ПолучитьНастройкиОтображения();
	НастройкиОтображения.Пользователи.Очистить();
	Для Каждого ОчереднойПользователь Из ПользователиКалендаря Цикл
		НастройкиОтображения.Пользователи.Добавить(ОчереднойПользователь.Значение);
	КонецЦикла;
	Если ПользователиКалендаря.Количество() = 1 Тогда
		ПользовательКалендаря = ПользователиКалендаря[0].Значение;
	Иначе
		ПользовательКалендаря = Неопределено;
	КонецЕсли;
	Если ПользователиКалендаря.Количество() > 1 Тогда
		НастройкиОтображения.ОтображатьЗанятость = Ложь;
	КонецЕсли;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты = ВыделенныеДаты.ВыгрузитьЗначения();
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;


	РаботаСРабочимКалендаремСервер.ОтобразитьКалендарь(Планировщик, НастройкиОтображения);
	
	//ИспользоватьБыстроеРедактирование = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
	//"ИспользоватьБыстроеРедактирование");


	Если ОбновитьНастройки Тогда
		УстановитьАвтообновление();
		//ОтображатьЛегенду = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьЛегенду");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьАвтообновление()

	НастройкиАвтообновления = Автообновление.ПолучитьНастройкиАвтообновленияФормы(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОтображения()

	//НастройкиОтображения.Пользователи.Очистить();
	//Для Каждого ОчереднойПользователь Из ПользователиКалендаря Цикл
	//	НастройкиОтображения.Пользователи.Добавить(ОчереднойПользователь.Значение);
	//КонецЦикла;
	//Если ПользователиКалендаря.Количество() = 1 Тогда
	//	ПользовательКалендаря = ПользователиКалендаря[0].Значение;
	//Иначе
	//	ПользовательКалендаря = Неопределено;
	//КонецЕсли;
	//Если ПользователиКалендаря.Количество() > 1 Тогда
	//	НастройкиОтображения.ОтображатьЗанятость = Ложь;
	//КонецЕсли;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты = ВыделенныеДаты.ВыгрузитьЗначения();
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередСозданием(
		Элемент,
		Объект.Ссылка,
		Объект.СсылкаНаЗадачу,
		Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка, Ложь, ПользовательКалендаря,
		НастройкиОтображения, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораЭлемента(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаОкончанияРедактированияЭлемента(
		Элемент, НовыйЭлемент, ОтменаРедактирования, Планировщик, НастройкиОтображения, УникальныйИдентификатор,
		ПользовательКалендаря);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломРедактированиемЭлемента(Элемент, НовыйЭлемент,
		СтандартнаяОбработка, ПользовательКалендаря);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемЭлемента(Элемент, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка);


КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНовойОценки(Результат, Параметры) Экспорт	
 
    Если Не Результат = Неопределено Тогда
		
		Если Результат < Объект.ТрудозатратыФакт И Объект.ТипОценкиЗадачи <> ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.Предсогласованные") Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Новая оценка не может быть меньше фактически выполненных часов. Выполнено: " + Строка(Объект.ТрудозатратыФакт) + ".");	
			Возврат;
		КонецЕсли;
		ОтправитьНаСогласованиеНаСервере(Результат);
		Закрыть();
		
    КонецЕсли;
 
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.Неделя");
	ОбновитьДанныеКалендаря("ПериодОтображения"); 

КонецПроцедуры

&НаКлиенте
Процедура ЧатПослеЗакрытия(РезультатЗакрытия, ДопПараметры) Экспорт
	Оповестить("ЗакрытиеЧата");
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)
	
	Если Элемент.ВыделенныеЭлементы = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РаботаСРабочимКалендаремКлиент.ОбработкаПриАктивизации(Элемент, СтандартнаяОбработка);

	ЕстьВыделенныеЭлементы = Элемент.ВыделенныеЭлементы.Количество() <> 0;
	
	Если ЕстьВыделенныеЭлементы Тогда 
		ЗаписьВКалендаре = Элемент.ВыделенныеЭлементы[0].Значение.Ссылка;
		
		СтатусНовый = ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый"));
		СтатусНачал = ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_Приступить", "Видимость", СтатусНовый);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_Закончить", "Видимость", СтатусНачал);   
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_ПоПлану", "Видимость", СтатусНовый);   
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_Отменить", "Видимость", СтатусНовый);   
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаСообщениеОтклонитьЭтап(РезультатЗакрытия, ДопПараметры) Экспорт	
	
	Если Не РезультатЗакрытия = Неопределено Тогда
		ОтклонитьЭтапЗавершение(РезультатЗакрытия.ТекстСообщения); 
		Оповестить("ЗакрытиеДокЗадания");   
		Закрыть(); 
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ОтклонитьЭтапЗавершение(Результат)	
	
	Получатели = Новый Массив;
	Получатели.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ПредыдущееЗадание, "Исполнитель"));
	                                                      
	ЦБР_ОбсужденияСервер.ОтправитьСообщениеВОбщееОбсуждение(Объект.СсылкаНаЗадачу, Результат, Получатели); 
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.Ссылка, Перечисления.ЦБР_Состояние.Запланировано);	
	
	СтатусПредыдущегоЭтапа = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(Объект.ПредыдущееЗадание);
	Если СтатусПредыдущегоЭтапа = Перечисления.ЦБР_Состояние.Выполнен Или СтатусПредыдущегоЭтапа = Перечисления.ЦБР_Состояние.Закрыт Тогда 
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.ПредыдущееЗадание, Перечисления.ЦБР_Состояние.ВРаботе);	
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПеревестиЗадачуЗавершение(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		
		СостояниеЗадачи = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадачиПользователя(Объект.Ссылка, ПользователиКлиент.АвторизованныйПользователь());

		НовыйИсполнитель = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(РезультатЗакрытия, "Руководитель");

		Объект.Исполнитель = НовыйИсполнитель;  
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		
		МассивРолей = Новый Массив;
	    МассивРолей.Добавить(ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_РольВЗадаче.Исполнитель"));	
	    МассивРолей.Добавить(ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_РольВЗадаче.КураторИсполнителя"));	
		
		ЦБР_ЗадачиЗаданияВызовСервера.ДобавитьПользователяВКомандуЗадачи(Объект.СсылкаНаЗадачу, НовыйИсполнитель, МассивРолей);
		
		Оповестить("ЗакрытиеДокЗадания");   
		Закрыть(); 
		
	КонецЕсли;     
	
КонецПроцедуры

#КонецОбласти

#Область ОС_Календаря 

&НаКлиенте
Процедура ОС_Закончить(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	Если Не ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал")) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Команда доступна только для записей календаря в статусе ""Начал""");
		Возврат;
	КонецЕсли;
	
	РеквизитыЗаписи = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ЗаписьВКалендаре, "ЗаданиеСсылка,ЗадачаСсылка,Описание");     
	
	Если ЗначениеЗаполнено(РеквизитыЗаписи.ЗаданиеСсылка) Тогда
		
		РеквизитыЗадания = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(РеквизитыЗаписи.ЗаданиеСсылка, "ТрудозатратыПлан, ТрудозатратыФакт, ТипОценкиЗадачи, ОплатаПоФакту");     
		
		ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(РеквизитыЗаписи.ЗаданиеСсылка);
		Если Не ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
			
			Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект);
			
			ПараметрыФормы = Новый Структура;       
			ПараметрыФормы.Вставить("Задание", РеквизитыЗаписи.ЗаданиеСсылка);
			ПараметрыФормы.Вставить("Описание", РеквизитыЗаписи.Описание);  				
			ПараметрыФормы.Вставить("ЗаписьОС", ЗаписьВКалендаре); 
			
			ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
		КонецЕсли;	
		
	Иначе
		ЦБР_ЗадачиЗаданияВызовСервера.УстановитьОтработана(Новый Структура("ЗаписьОС,ПоПлану,РешеноПриПервомПодключении,ОписаниеВыполненнойРаботы", ЗаписьВКалендаре, Ложь, Ложь, Неопределено));	
	    Оповестить("Запись_ЗаписьКалендаря");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОС_Приступить(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	Если Не ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый")) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Команда доступна только для записей календаря в статусе ""Новый""");
		Возврат;
	КонецЕсли;	
	
	ЦБР_ЗадачиЗаданияВызовСервера.УстановитьСтарт(ЗаписьВКалендаре);

	Оповестить("Запись_ЗаписьКалендаря");

КонецПроцедуры

&НаКлиенте
Процедура ОС_Отменить(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	ПараметрыОповещения = Новый Структура("ЗаписьВКалендаре", ЗаписьВКалендаре);
	
	Оповещение = Новый ОписаниеОповещения("ОС_ОтменитьОкончание", ЭтотОбъект, ПараметрыОповещения);
	ПоказатьВводСтроки(Оповещение,,"Укажите причину отмены ОС.", 0, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОС_ПоПлану(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	Если Не ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый")) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Команда доступна только для записей календаря в статусе ""Новый""");
		Возврат;
	КонецЕсли;
	
	РеквизитыЗаписи = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ЗаписьВКалендаре, "ЗаданиеСсылка,ЗадачаСсылка,Описание,ДатаНачала,ДатаОкончания");     
			
	Если НачалоДня(РеквизитыЗаписи.ДатаНачала) < НачалоДня(ТекущаяДата() - 7 * 24 * 3600) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нельзя создать ОС по плану позже " + Формат(НачалоДня(ТекущаяДата() - 7 * 24 * 3600),"ДФ=dd.MM.yyyy"));  
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитыЗаписи.Описание) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполните описание!");  
		Возврат;
	КонецЕсли;	
		
	КоличествоДней = (НачалоДня(РеквизитыЗаписи.ДатаОкончания) - НачалоДня(РеквизитыЗаписи.ДатаНачала)) / (60 * 60 * 24) + 1;
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;   	
	ПараметрыФормы.Вставить("ЗаписьОС", ЗаписьВКалендаре);
	ПараметрыФормы.Вставить("Задание", РеквизитыЗаписи.ЗаданиеСсылка); 
	ПараметрыФормы.Вставить("Описание", РеквизитыЗаписи.Описание);  	
	ПараметрыФормы.Вставить("ПоказатьЗакрытьЗадание", Ложь);      
	ПараметрыФормы.Вставить("ПоПлану", Истина);       
	ПараметрыФормы.Вставить("КоличествоДней", КоличествоДней);
	Если КоличествоДней > 1 Тогда
		ПараметрыФормы.Вставить("ДатыПоДням", РазбитьЗаписиНаНесколькоДней(РеквизитыЗаписи.ДатаНачала, РеквизитыЗаписи.ДатаОкончания, КоличествоДней));
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры 

&НаКлиенте
Процедура ДобавитьЭффективныеЧасыОкончание(Результат, ДопПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		Оповестить("Запись_ЗаписьКалендаря");
	КонецЕсли;
		
КонецПроцедуры  

&НаКлиенте
Процедура ОС_ОтменитьОкончание(Результат, Параметры) Экспорт
 
	Если Не Результат = Неопределено И ЗначениеЗаполнено(Результат) Тогда
		
		ЗаписьВКалендаре = Параметры.ЗаписьВКалендаре;
		
		ЦБР_ЗадачиЗаданияВызовСервера.УстановитьОтменена(ЗаписьВКалендаре, Результат);
			
		УстановленаПометкаУдаления = РаботаСРабочимКалендаремСервер.УстановитьПометкуУдаления(
		ЗаписьВКалендаре,
		Истина,
		Результат);
		
		Если УстановленаПометкаУдаления Тогда
			Оповестить("Запись_ЗаписьКалендаря", ЗаписьВКалендаре);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана причина отмены!");
	КонецЕсли;
 
КонецПроцедуры 

&НаСервереБезКонтекста
Функция РазбитьЗаписиНаНесколькоДней(Начало, Конец, КоличествоДней)
	
	ОсПоДням = Новый Массив;
	День = 1;
	ДатаОкончания = Конец;
	Пока День <= КоличествоДней Цикл
		Если ДеньНедели(Начало) = 6 Или ДеньНедели(Начало) = 7 Тогда
			День = День + 1;
			Начало = НачалоДня(НачалоДня(Начало) + 25 * 60 * 60) + 9 * 60 * 60;
			Продолжить;
		КонецЕсли;
		СтруктураДат = Новый Структура("Начало, Конец, Часов");
		Если День = КоличествоДней Тогда
			Конец = ДатаОкончания;
		Иначе  
			Конец = НачалоДня(Начало) + 64800;
		КонецЕсли;
		СтруктураДат.Начало = Начало;
		СтруктураДат.Конец = Конец; 
		
		КолВоЧасов = (Конец-Начало)/3600;
		
		СтруктураДат.Часов = ЦенообразованиеКлиентСервер.ОкруглитьЦену(КолВоЧасов, 1, Перечисления.ВариантыОкругления.ВсегдаВПользуПредприятия);    
		ОсПоДням.Добавить(СтруктураДат);
		День = День + 1;
		Начало = НачалоДня(НачалоДня(Начало) + 25 * 60 * 60) + 9 * 60 * 60;
	КонецЦикла;   
	
	Возврат ОсПоДням; 
	
КонецФункции

#КонецОбласти
