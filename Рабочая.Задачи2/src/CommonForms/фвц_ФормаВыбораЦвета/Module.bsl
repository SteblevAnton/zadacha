&НаКлиенте
Перем ЛексическоеОкруженииеПоляHTML;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолеВыбораЦветаHTML = фвц_ВыборЦветаHTMLПовтИсп.ТекстОбщегоМакета("фвц_СтраницаВыбораЦветаHTML");	 
	ПолеВыбораЦветаHTML = СтрЗаменить(ПолеВыбораЦветаHTML, "initColorPickers();", ""); 
	
	Параметры.Свойство("Цвет", Цвет);
	
КонецПроцедуры

#КонецОбласти 


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(
		Новый Структура("ЦветHEX, ЦветRGB, ЦветТекстаRGB", 
			Цвет, 
			ЦветRGB(), 
			ЦветТекстаRGB()
		)
	);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолеВыбораЦветаHTMLДокументСформирован(Элемент)
	
	ЛексическоеОкруженииеПоляHTML = Элементы.ПолеВыбораЦветаHTML.Документ.defaultView;
	ЛексическоеОкруженииеПоляHTML.initColorPickers(Цвет); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВыбораЦветаHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Цвет 	= ЛексическоеОкруженииеПоляHTML.currentColorHEX;
	Красный = ЛексическоеОкруженииеПоляHTML.currentColorRGB.R;
	Синий 	= ЛексическоеОкруженииеПоляHTML.currentColorRGB.B;
	Зеленый = ЛексическоеОкруженииеПоляHTML.currentColorRGB.G;    
	
	ЭтоКорректныйЦвет 				= ЛексическоеОкруженииеПоляHTML.isCorrectColor; 
	Элементы.ФормаОК.Доступность 	= ЭтоКорректныйЦвет; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ЦветRGB()

	Возврат Новый Структура("R, G, B", Красный, Зеленый, Синий);

КонецФункции

&НаКлиенте
Функция ЦветТекстаRGB()

	ЦветТекстаRGB = Новый Структура("R, G, B", 0, 0, 0);
	
	Попытка
		СтрокаRGB = СтрЗаменить(СтрЗаменить(ЛексическоеОкруженииеПоляHTML.color_input.style.color, "rgb(", ""), ")", "");  
		СтрокаRGB = СтрЗаменить(СтрокаRGB, " ", "");
		
		Если НРег(СтрокаRGB) = "white" Тогда  
			СтрокаRGB = "255,255,255";	
		ИначеЕсли НРег(СтрокаRGB) = "black" Тогда
			СтрокаRGB = "0,0,0";
		КонецЕсли;  
		
		МассивСтрокиRGB = СтрРазделить(СтрокаRGB, ",");    
		
		ЦветТекстаRGB.Вставить("R", Число(МассивСтрокиRGB[0]));      
		ЦветТекстаRGB.Вставить("G", Число(МассивСтрокиRGB[1]));
		ЦветТекстаRGB.Вставить("B", Число(МассивСтрокиRGB[2]));
	Исключение
	КонецПопытки; 
	
	Возврат ЦветТекстаRGB;

КонецФункции

#КонецОбласти