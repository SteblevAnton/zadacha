#Область ОбработчикиСобытийФормы
	 
&НаКлиенте
Процедура ПриОткрытии(Отказ)  
	
	ПодключитьОбработчикОжидания("АвтообновлениеАРМ", 60);
	УстановитьВидимостьЭлементов();
	ЗаполнитьСписокНаправленийДляОтбора();
	
	Элементы.АРМЗадачи.Пометка = Истина; 
	Элементы.АРМЗадачи.ЦветФона = Новый Цвет(30,144,255); 
	
КонецПроцедуры  

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОпределитьТипСотрудника();
	ЗаполнитьТаблицуТребуетВнимания();  
	Если ЭтоРуководитель Тогда
		ЗаполнитьТаблицуТребуетВниманияРуководителя(); 
	КонецЕсли;
	ИнициализироватьАРМПользователя();  
	
	СогласованиеСроков = Ложь;
	СогласованиеОценки = Истина;

	СписокЗадачМенеджера.Параметры.УстановитьЗначениеПараметра("Постановщик", Пользователи.ТекущийПользователь());
	СписокЗадачМенеджера.Параметры.УстановитьЗначениеПараметра("Состояние", Перечисления.ЦБР_Состояние.НаСогласовании);

	УстановитьВидимостьЭлементов();
	
КонецПроцедуры 

 &НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	АвтообновлениеАРМНаСервере();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Новая(Команда)
	ОткрытьФорму("Справочник.ЦБР_Задача.ФормаОбъекта");
КонецПроцедуры
 
&НаКлиенте
Процедура МоиНастройки(Команда)
	
	НастройкиКанбанаТекущегоПользователя = ОпределитьНастройкиТекущегоПользователя();
	
	Если  НастройкиКанбанаТекущегоПользователя  = ПредопределенноеЗначение("Справочник.ЦБР_ШаблоныАРМ.ПустаяСсылка") Тогда
		СоздатьНастрокиДляТекущегоПользователя(); 
		НастройкиКанбанаТекущегоПользователя = ОпределитьНастройкиТекущегоПользователя();
	КонецЕсли;  

	
    ПараметрыФормы = Новый Структура("Ключ",НастройкиКанбанаТекущегоПользователя);
    ОткрытьФорму("Справочник.ЦБР_ШаблоныАРМ.ФормаОбъекта",ПараметрыФормы,ЭтаФорма,,,,Новый ОписаниеОповещения("ВыборЭлементаЗавершение", ЭтотОбъект),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры            

&НаКлиенте
Процедура АРМЗадача(Команда)
	
	РежимАРМ = "Задачи";
	УстановитьОформлениеРежимаАРМ(РежимАРМ);
	
	ОпределитьТипСотрудника();
	ЗаполнитьТаблицуТребуетВнимания();  
	Если ЭтоРуководитель Тогда
		ЗаполнитьТаблицуТребуетВниманияРуководителя(); 
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьЗадачи(Команда)
	АвтообновлениеАРМНаСервере();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы  


#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменениеКолонкиЗавершение(Результат,ДополнительныеПараметры) Экспорт
	
	НастройкиТекущегоПользователя = ОпределитьНастройкиТекущегоПользователя();
	
	УдалитьЭлементыАРМ(НастройкиТекущегоПользователя);
	ДобавитьКолонкиАРМ(НастройкиТекущегоПользователя);
   	УстановитьОтборВАРМНаСервере();    
	
КонецПроцедуры

&НаКлиенте
Процедура АвтообновлениеАРМ()
	АвтообновлениеАРМНаСервере(); 
КонецПроцедуры
 
&НаСервере
Процедура ОпределитьТипСотрудника() 
	
    ЭтоРуководитель = РольДоступна("ЦБР_РуководительКО");
	ЭтоПолныеПрава  = РольДоступна("ПолныеПрава");
	
КонецПроцедуры

&НаСервере
Процедура АвтообновлениеАРМНаСервере()
	
	Если ЭтоРуководитель Тогда
		ЗаполнитьТаблицуТребуетВниманияРуководителя();   
	Иначе
		ЗаполнитьТаблицуТребуетВнимания();     
	КонецЕсли;
	
	ЗаполнитьТаблицыНаСервере(ОпределитьНастройкиТекущегоПользователя());   
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокНаправленийДляОтбора()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЦБР_ГруппыСотрудников.Руководитель КАК Руководитель,
	|	ЦБР_ГруппыСотрудников.Наименование КАК Наименование,
	|	ЦБР_ГруппыСотрудников.Руководитель.Наименование КАК РуководительНаименование
	|ИЗ
	|	Справочник.ЦБР_ГруппыСотрудников КАК ЦБР_ГруппыСотрудников
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";

	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Элементы.отборНаправление.СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Руководитель, ВыборкаДетальныеЗаписи.Наименование
			+ " (" + ВыборкаДетальныеЗаписи.РуководительНаименование + ")");
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов() 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "отборИсполнитель", "Видимость", ЭтоРуководитель); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "отборНаправление", "Видимость", ЭтоПолныеПрава); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапросИнформации", "Видимость", Не ЭтоРуководитель); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЗапросИнформации", "Видимость", Не ЭтоРуководитель); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапросИнформацииСотрудников", "Видимость", ЭтоРуководитель); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапросИнформацииРуководителя", "Видимость", ЭтоРуководитель);  

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТребуетВнимания()

	ТаблицаЗапросИнформации.Очистить();

	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ
	|ЦБР_СостоянияЗадач.Задача КАК Задача,
	|ЦБР_СостоянияЗадач.ПользовательАРМ КАК Исполнитель,
	|ЦБР_СостоянияЗадач.ТрудозатратыПлан КАК Трудозатраты,
	|ЦБР_СостоянияЗадач.Клиент КАК Клиент,
	|ЦБР_СостоянияЗадач.Менеджер КАК Постановщик,
	|ЦБР_СостоянияЗадач.Состояние КАК Состояние,
	|ЦБР_СостоянияЗадач.Задача.Наименование КАК Наименование,
	|ЦБР_СостоянияЗадач.Задача.Куратор КАК Направление
	|ИЗ
	|	РегистрСведений.ЦБР_СостоянияЗадач КАК ЦБР_СостоянияЗадач
	|ГДЕ
	|	ЦБР_СостоянияЗадач.ПользовательАРМ = &ПользовательАРМ
	|	И ЦБР_СостоянияЗадач.Состояние = &Состояние";

	Запрос.УстановитьПараметр("ПользовательАРМ", Пользователи.АвторизованныйПользователь()); 
	Запрос.УстановитьПараметр("Состояние", Перечисления.ЦБР_Состояние.ЗапросИнформации); 

	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗапросИнформации.Загрузить(РезультатЗапроса.Выгрузить());
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТребуетВниманияРуководителя()

	ЗапросИнформацииРуководителя.Очистить();

	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ
	|ЦБР_СостоянияЗадач.Задача КАК Задача,
	|ЦБР_СостоянияЗадач.ПользовательАРМ КАК Исполнитель,
	|ЦБР_СостоянияЗадач.ТрудозатратыПлан КАК Трудозатраты,
	|ЦБР_СостоянияЗадач.Клиент КАК Клиент,
	|ЦБР_СостоянияЗадач.Менеджер КАК Постановщик,
	|ЦБР_СостоянияЗадач.Состояние КАК Состояние,
	|ЦБР_СостоянияЗадач.Задача.Наименование КАК Наименование,
	|ЦБР_СостоянияЗадач.Задача.Куратор КАК Направление
	|ИЗ
	|	РегистрСведений.ЦБР_СостоянияЗадач КАК ЦБР_СостоянияЗадач
	|ГДЕ
	|	ЦБР_СостоянияЗадач.ПользовательАРМ = &ПользовательАРМ
	|	И ЦБР_СостоянияЗадач.Состояние = &Состояние";

	Запрос.УстановитьПараметр("ПользовательАРМ", Пользователи.АвторизованныйПользователь()); 
	Запрос.УстановитьПараметр("Состояние", Перечисления.ЦБР_Состояние.ЗапросИнформации); 

	РезультатЗапроса = Запрос.Выполнить();
	ЗапросИнформацииРуководителя.Загрузить(РезультатЗапроса.Выгрузить());
		
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьАРМПользователя() 
	
	НастройкиКанбанаТекущегоПользователя = ОпределитьНастройкиТекущегоПользователя();
	
	Если НастройкиКанбанаТекущегоПользователя  = Справочники.ЦБР_ШаблоныАРМ.ПустаяСсылка() Тогда
		НастройкиКанбанаТекущегоПользователя = Справочники.ЦБР_ШаблоныАРМ.Менеджеры;
	КонецЕсли;
	
	ДобавитьКолонкиАРМ(НастройкиКанбанаТекущегоПользователя);
	
	ЗаполнитьТаблицыНаСервере(НастройкиКанбанаТекущегоПользователя);
	
КонецПроцедуры 

#КонецОбласти   

&НаСервере
Процедура ДобавитьКолонкиАРМ(НастрокиТекущегоПользователя) 
	
	Для Каждого СтрокаШаблона Из НастрокиТекущегоПользователя.СоставКолонок Цикл  
		СоздатьКолонкуАРМ(СтрокаШаблона.КолонкаАРМ);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СоздатьКолонкуАРМ(КолонкаАРМ)  
	
	Наименование 	= ПочиститьНаименование(КолонкаАРМ.Наименование);
	Заголовок    	= КолонкаАРМ.Наименование;
	Цвет 			= фвц_ВыборЦветаHTMLПовтИсп.ЦветПоСтрокеRGB(КолонкаАРМ.ЦветRGB);
	ЦветТекста      = фвц_ВыборЦветаHTMLПовтИсп.ЦветПоСтрокеRGB(КолонкаАРМ.ЦветТекстаRGB); 
	
	ЦБР_СозданиеОбъектовФормы.СздГруппаОбычнаяБезОтображения(ЭтотОбъект, "Список"+Наименование, Элементы.КанбанСписки, 1);

	СтруктураСвойств = Новый Структура();
	СтруктураСвойств.Вставить("ЦветФона", Цвет);												
	ЦБР_СозданиеОбъектовФормы.СздГруппаОбычнаяБезОтображения(ЭтотОбъект, "СписокГоризонтальнаяНаименование"+Наименование, "Список"+Наименование,2,,,СтруктураСвойств);
	
	СтруктураСвойств = Новый Структура();		
	СтруктураСвойств.Вставить("Заголовок", Заголовок);	 
	СтруктураСвойств.Вставить("Шрифт", Новый Шрифт(,11));
	СтруктураСвойств.Вставить("ГоризонтальноеПоложение", ГоризонтальноеПоложениеЭлемента.Центр);	
	СтруктураСвойств.Вставить("Вид", ВидДекорацииФормы.Надпись);
	СтруктураСвойств.Вставить("РастягиватьПоГоризонтали", Истина);
	СтруктураСвойств.Вставить("АвтоМаксимальнаяШирина", Ложь);
	СтруктураСвойств.Вставить("ЦветТекста", ЦветТекста);	
	ЦБР_СозданиеОбъектовФормы.СздДекорация(ЭтотОбъект, "ЗаголовокЗадачи"+Наименование, Элементы["СписокГоризонтальнаяНаименование"+Наименование], СтруктураСвойств);														
	ЦБР_СозданиеОбъектовФормы.СоздатьКоманду(ЭтотОбъект, "КомандаКолонки"+Наименование,"КомандаКолонки"+Наименование,"ПриИзмененииКолонки");
	
	СтруктураСвойств = Новый Структура();		
	СтруктураСвойств.Вставить("Картинка", БиблиотекаКартинок.CRM_Изменить);	
	СтруктураСвойств.Вставить("Отображение", ОтображениеКнопки.Картинка);	
	ЦБР_СозданиеОбъектовФормы.СздКнопка(ЭтотОбъект, "КомандаКолонки"+Наименование, Элементы["СписокГоризонтальнаяНаименование"+Наименование], "КомандаКолонки"+Наименование, 
											"КомандаКолонки"+Наименование, 2, СтруктураСвойств); 
	
	ЦБР_СозданиеОбъектовФормы.СоздатьРеквизит(ЭтотОбъект, "Таблица"+Наименование, Новый ОписаниеТипов("ТаблицаЗначений"));	
	
	структураРеквизитов = Новый Структура;		
	структураРеквизитов.Вставить("Наименование", 	Новый ОписаниеТипов("Строка"));	
	структураРеквизитов.Вставить("Клиент", 			Новый ОписаниеТипов("СправочникСсылка.Партнеры"));		
	структураРеквизитов.Вставить("Исполнитель", 	Новый ОписаниеТипов("СправочникСсылка.Пользователи"));		
	структураРеквизитов.Вставить("Трудозатраты", 	Новый ОписаниеТипов("Строка"));		
	структураРеквизитов.Вставить("Отступ", 			Новый ОписаниеТипов("Строка"));		
	структураРеквизитов.Вставить("Состояние", 		Новый ОписаниеТипов("ПеречислениеСсылка.ЦБР_Состояние"));	
	структураРеквизитов.Вставить("КартинкаКлиент", 	Новый ОписаниеТипов("Число"));
	структураРеквизитов.Вставить("Задача", 			Новый ОписаниеТипов("СправочникСсылка.ЦБР_Задача"));
	структураРеквизитов.Вставить("Постановщик", 	Новый ОписаниеТипов("СправочникСсылка.Пользователи"));		
	структураРеквизитов.Вставить("КартинкаИсполнитель", 	Новый ОписаниеТипов("Число"));

	ЦБР_СозданиеОбъектовФормы.СоздатьРеквизитыТаблицы(ЭтотОбъект, "Таблица"+Наименование, структураРеквизитов);
	
	СтруктураКолонок = Новый Структура;		
	СтруктураКолонок.Вставить("Наименование");	
	СтруктураКолонок.Вставить("Клиент");		
	СтруктураКолонок.Вставить("Исполнитель");		
	СтруктураКолонок.Вставить("Трудозатраты");		
	СтруктураКолонок.Вставить("Отступ");		
	СтруктураКолонок.Вставить("Состояние");	
	СтруктураКолонок.Вставить("Постановщик");		
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет);
	СтруктураСвойств.Вставить("ПоложениеКоманднойПанели", ПоложениеКоманднойПанелиЭлементаФормы.Нет);
	СтруктураСвойств.Вставить("Отображение", ОтображениеТаблицы.Список);
	СтруктураСвойств.Вставить("РежимВыделения", РежимВыделенияТаблицы.Одиночный);
	СтруктураСвойств.Вставить("РежимВыделенияСтроки", РежимВыделенияСтрокиТаблицы.Строка);
	СтруктураСвойств.Вставить("Шапка", Ложь);
	СтруктураСвойств.Вставить("Подвал", Ложь);
	СтруктураСвойств.Вставить("ЦветРамки", Цвет);
	СтруктураСвойств.Вставить("Ширина", 25);
	СтруктураСвойств.Вставить("ГоризонтальныеЛинии", Ложь);
	СтруктураСвойств.Вставить("Шрифт", ОбщегоНазначенияВызовСервера.ШрифтСтиля("ШрифтМикротекста"));

	СтруктураСобытий	= Новый Структура;	
	СтруктураСобытий.Вставить("Выбор", "СписокВыбор");	
	ЦБР_СозданиеОбъектовФормы.СздТаблицаФормы(ЭтотОбъект, "Таблица"+Наименование, Элементы["Список"+Наименование], 
										"Таблица"+Наименование, , СтруктураСвойств, СтруктураСобытий);

	ЦБР_СозданиеОбъектовФормы.СздГруппаКолонокТаблицыФормы(ЭтотОбъект, "ГруппаКарточка"+Наименование, Элементы["Таблица"+Наименование], 2);												

	СтруктураСвойств = Новый Структура();	
	ЦБР_СозданиеОбъектовФормы.СздГруппаКолонокТаблицыФормы(ЭтотОбъект, "ГруппаГоризонтальнаяНаименование"+Наименование, Элементы["ГруппаКарточка"+Наименование], 1);												

	СтруктураСвойств = Новый Структура();
	СтруктураСвойств.Вставить("Шрифт", Новый Шрифт(,8,Истина));
	СтруктураСвойств.Вставить("ЦветФона", Новый Цвет(77, 77, 77));  
	СтруктураСвойств.Вставить("ЦветТекста",ЦветТекста); 
	СтруктураСвойств.Вставить("РастягиватьПоГоризонтали", Истина);
	СтруктураСвойств.Вставить("АвтоМаксимальнаяШирина", Ложь);
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "СписокНаименование"+Наименование, Элементы["ГруппаГоризонтальнаяНаименование"+Наименование], 1, , "Таблица"+Наименование+".Наименование", , , СтруктураСвойств);

	СтруктураСвойств = Новый Структура();
	СтруктураСвойств.Вставить("Шрифт", Новый Шрифт(,8,Истина));
	СтруктураСвойств.Вставить("ЦветФона", Новый Цвет(77, 77, 77));  
	СтруктураСвойств.Вставить("ЦветТекста",ЦветТекста);   
	СтруктураСвойств.Вставить("РастягиватьПоГоризонтали", Ложь);  
	СтруктураСвойств.Вставить("АвтоМаксимальнаяШирина", Истина); 
	СтруктураСвойств.Вставить("Ширина", 3);
	
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "Трудозатраты"+Наименование, Элементы["ГруппаГоризонтальнаяНаименование"+Наименование], 1, , "Таблица"+Наименование+".Трудозатраты", , , СтруктураСвойств);
	ЦБР_СозданиеОбъектовФормы.СздГруппаКолонокТаблицыФормы(ЭтотОбъект, "ГруппаГоризонтальнаяИсполнитель"+Наименование, Элементы["ГруппаКарточка"+Наименование], 1);												
	
	СтруктураСвойств = Новый Структура();	
	СтруктураСвойств.Вставить("КартинкаЗначений", БиблиотекаКартинок.ИндивидуальноеСоглашениеСКлиентом);		
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "КартинкаИсполнитель"+Наименование, Элементы["ГруппаГоризонтальнаяИсполнитель"+Наименование], 3, , "Таблица"+Наименование+".КартинкаИсполнитель", , , СтруктураСвойств);

	СтруктураСвойств = Новый Структура();		
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "Исполнитель"+Наименование, Элементы["ГруппаГоризонтальнаяИсполнитель"+Наименование], 1, , "Таблица"+Наименование+".Исполнитель", , , СтруктураСвойств);
	ЦБР_СозданиеОбъектовФормы.СздГруппаКолонокТаблицыФормы(ЭтотОбъект, "ГруппаГоризонтальнаяКлиент"+Наименование, Элементы["ГруппаКарточка"+Наименование], 1);												
	
	СтруктураСвойств = Новый Структура();	
	СтруктураСвойств.Вставить("КартинкаЗначений", БиблиотекаКартинок.ОткрытьКарточкуПартнера);		
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "КартинкаКлиент"+Наименование, Элементы["ГруппаГоризонтальнаяКлиент"+Наименование], 3, , "Таблица"+Наименование+".КартинкаКлиент", , , СтруктураСвойств);

	СтруктураСвойств = Новый Структура();		
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "Клиент"+Наименование, Элементы["ГруппаГоризонтальнаяКлиент"+Наименование], 1, , "Таблица"+Наименование+".Клиент", , , СтруктураСвойств);
	
	СтруктураСвойств = Новый Структура();
	СтруктураСвойств.Вставить("ГоризонтальноеПоложение", ГоризонтальноеПоложениеЭлемента.Право);	
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "Постановщик"+Наименование, Элементы["ГруппаКарточка"+Наименование], 1, , "Таблица"+Наименование+".Постановщик", , , СтруктураСвойств);

	СтруктураСвойств = Новый Структура();		
	СтруктураСвойств.Вставить("Шрифт", Новый Шрифт(,1));
	ЦБР_СозданиеОбъектовФормы.СздКолонкаТаблицыФормы(ЭтотОбъект, "СписокОтступ"+Наименование, Элементы["ГруппаКарточка"+Наименование], 1, , "Таблица"+Наименование+".Отступ", , , СтруктураСвойств);

КонецПроцедуры   

&НаСервере
Процедура ЗаполнитьТаблицыНаСервере(НастройкиКанбанаТекущегоПользователя, ДополнительныйОтбор = Неопределено)
	
	Если ЭтоРуководитель Тогда
		СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетРуководитель"); 
		НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;  
		ПараметрДанных = СхемаКомпоновкиДанных.Параметры.Найти("Подразделение");
		ПараметрДанных.Значение = Пользователи.АвторизованныйПользователь().Подразделение;
		
		ПараметрДанных = СхемаКомпоновкиДанных.Параметры.Найти("ПользовательАРМ");
		ПараметрДанных.Значение = Пользователи.АвторизованныйПользователь();
		
	Иначе
		СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");  
		НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;  
		ПараметрДанных = СхемаКомпоновкиДанных.Параметры.Найти("ПользовательАРМ");
		ПараметрДанных.Значение = Пользователи.АвторизованныйПользователь();
	КонецЕсли;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;    
	
	Для Каждого СтрокаШаблона Из НастройкиКанбанаТекущегоПользователя.СоставКолонок Цикл  
		УсловияОтбора 		= СтрокаШаблона.КолонкаАРМ.УсловияОтбора.Получить(); 
		
		НастройкиКомпоновкиДанных.Отбор.Элементы.Очистить();
	
		Для Каждого Отбор ИЗ УсловияОтбора.Отбор.Элементы Цикл
			НовыйОтбор = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
            ЗаполнитьЗначенияСвойств(НовыйОтбор,Отбор);
		КонецЦикла;  
		
		Если ДополнительныйОтбор <> Неопределено Тогда   
			Для Каждого Строка  Из  ДополнительныйОтбор Цикл  
				Если Строка = "Клиент" Тогда
					НовыйОтбор = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					НовыйОтбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Клиент");                       
					НовыйОтбор.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
					НовыйОтбор.ПравоеЗначение 	= отборКлиент;
					НовыйОтбор.Использование 	= Истина;
				КонецЕсли; 
				Если Строка = "Исполнитель" Тогда
					НовыйОтбор = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					НовыйОтбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Исполнитель");                       
					НовыйОтбор.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
					НовыйОтбор.ПравоеЗначение 	= отборИсполнитель;
					НовыйОтбор.Использование 	= Истина;
				КонецЕсли;
				Если Строка = "Постановщик" Тогда
					НовыйОтбор = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					НовыйОтбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Постановщик");                       
					НовыйОтбор.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
					НовыйОтбор.ПравоеЗначение 	= отборПостановщик;
					НовыйОтбор.Использование 	= Истина;
				КонецЕсли;        
				Если Строка = "Направление" Тогда
					НовыйОтбор = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					НовыйОтбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Направление");                       
					НовыйОтбор.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
					НовыйОтбор.ПравоеЗначение 	= отборНаправление;
					НовыйОтбор.Использование 	= Истина;
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
		
		МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		
		ТЗ = Новый ТаблицаЗначений;
		ПроцессорВывода.УстановитьОбъект(ТЗ);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
		Наименование 	= ПочиститьНаименование(СтрокаШаблона.КолонкаАРМ.Наименование);
		ЭтотОбъект["Таблица"+Наименование].Загрузить(ТЗ)		
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ВыборЭлементаЗавершение(Результат,ДополнительныеПараметры) Экспорт
	
	НастройкиТекущегоПользователя = ОпределитьНастройкиТекущегоПользователя();
	
	УдалитьЭлементыАРМ(НастройкиТекущегоПользователя);
	ДобавитьКолонкиАРМ(НастройкиТекущегоПользователя);
   	УстановитьОтборВАРМНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыАРМ(НастрокиТекущегоПользователя) 
	
	МассивИмен =  Новый Массив;
	Для каждого Колонка из Элементы.КанбанСписки.ПодчиненныеЭлементы Цикл  
		Если Колонка.Имя = "ГруппаЗапросИнформации" Тогда
			Продолжить;
		КонецЕсли;
		МассивИмен.Добавить(СтрЗаменить(Колонка.Имя,"Список",""));	  
	КонецЦикла;
	
	СписокУдаляемыхЭлементов = Новый Массив; 
	Для каждого Имя из МассивИмен Цикл 
		СписокУдаляемыхЭлементов.Добавить("Таблица"+Имя);
		СписокУдаляемыхЭлементов.Добавить("ЗаголовокЗадачи"+Имя);
		СписокУдаляемыхЭлементов.Добавить("Список"+Имя);    
		СписокУдаляемыхЭлементов.Добавить("КомандаКолонки"+Имя);  
		СписокУдаляемыхЭлементов.Добавить("СписокГоризонтальнаяНаименование"+Имя);  
	КонецЦикла;
	
	ЦБР_СозданиеОбъектовФормы.УдлЭлементы(ЭтотОбъект,СписокУдаляемыхЭлементов); 
	
	УдаляемыеРеквизиты = Новый Массив;
	Для каждого Имя из МассивИмен Цикл 
		УдаляемыеРеквизиты.Добавить("Таблица"+Имя); 
    КонецЦикла;    
    ИзменитьРеквизиты(,УдаляемыеРеквизиты); 
	
	УдаляемыеКоманды = Новый Массив;
	Для Каждого Команда из ЭтотОбъект.Команды Цикл   
		Если СтрНайти(Команда.Имя, "КомандаКолонки") > 0 Тогда 
			УдаляемыеКоманды.Добавить(Команда); 
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Команда Из УдаляемыеКоманды Цикл  
		ЭтотОбъект.Команды.Удалить(Команда);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОпределитьНастройкиТекущегоПользователя()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_НастройкиАРМДляПользователей.ШаблонАРМ КАК ШаблонАРМ
		|ИЗ
		|	РегистрСведений.ЦБР_НастройкиАРМДляПользователей КАК ЦБР_НастройкиАРМДляПользователей
		|ГДЕ
		|	ЦБР_НастройкиАРМДляПользователей.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.ЦБР_ШаблоныАРМ.ПустаяСсылка();
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
        ВыборкаДетальныеЗаписи.Следующий();
		Возврат ВыборкаДетальныеЗаписи.ШаблонАРМ;  
	КонецЕсли;

КонецФункции

&НаСервере
Процедура СоздатьНастрокиДляТекущегоПользователя()
	
	НовыйЭлемент = Справочники.ЦБР_ШаблоныАРМ.СоздатьЭлемент();
	НовыйЭлемент.Наименование = Пользователи.АвторизованныйПользователь().Наименование;
	НовыйЭлемент.Записать();
	
	МенеджерЗаписи = РегистрыСведений.ЦБР_НастройкиАРМДляПользователей.СоздатьМенеджерЗаписи(); 
	МенеджерЗаписи.Пользователь = Пользователи.АвторизованныйПользователь().Ссылка; 
	МенеджерЗаписи.ШаблонАРМ = НовыйЭлемент.Ссылка; 
	МенеджерЗаписи.Записать(); 
	
КонецПроцедуры  

&НаКлиенте
Процедура ТаблицаТребуютВниманиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	Если Не ТекСтрока = Неопределено Тогда
		СтруктураОткрытия = Новый Структура;
		СтруктураОткрытия.Вставить("Ключ", ТекСтрока.Задача);
		ОткрытьФорму("Справочник.ЦБР_Задача.ФормаОбъекта", СтруктураОткрытия);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент)

	ТекСтрока = Элемент.ТекущиеДанные;
	Если Не ТекСтрока = Неопределено Тогда
		СтруктураОткрытия = Новый Структура;
		СтруктураОткрытия.Вставить("Ключ", ТекСтрока.Задача);
		ОткрытьФорму("Справочник.ЦБР_Задача.ФормаОбъекта", СтруктураОткрытия);
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборВАРМ(Элемент)  
	 УстановитьОтборВАРМНаСервере();
КонецПроцедуры    

 &НаКлиенте
 Процедура УстановитьОтборВАРМНаСервере()  
	 
	МассивОтборов = Новый Массив;
	Если ЗначениеЗаполнено(отборКлиент) Тогда  
		МассивОтборов.Добавить("Клиент");
	КонецЕсли;     
	Если ЗначениеЗаполнено(отборИсполнитель) Тогда  
		МассивОтборов.Добавить("Исполнитель");
	КонецЕсли;     
	Если ЗначениеЗаполнено(отборПостановщик) Тогда  
		МассивОтборов.Добавить("Постановщик");
	КонецЕсли;     
	Если ЗначениеЗаполнено(отборНаправление) Тогда  
		МассивОтборов.Добавить("Направление");
	КонецЕсли;     
	
	ЗаполнитьТаблицыНаСервере(ОпределитьНастройкиТекущегоПользователя(), МассивОтборов);    
	
КонецПроцедуры      

&НаКлиенте
Процедура отборКлиентОчистка(Элемент, СтандартнаяОбработка)
	УстановитьОтборВАРМНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура отборИсполнительОчистка(Элемент, СтандартнаяОбработка)
	УстановитьОтборВАРМНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура отборПостановщикОчистка(Элемент, СтандартнаяОбработка)
	УстановитьОтборВАРМНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура отборНаправлениеОчистка(Элемент, СтандартнаяОбработка)
	УстановитьОтборВАРМНаСервере();
КонецПроцедуры

&НаСервере
Функция ПочиститьНаименование(Наименование)  
	
	НовНаименование 	= СтрЗаменить(Наименование," ","_");  
	НовНаименование 	= СтрЗаменить(НовНаименование,".","_");
	НовНаименование 	= СтрЗаменить(НовНаименование,",","_"); 
	НовНаименование 	= СтрЗаменить(НовНаименование,"-","_");
	НовНаименование 	= СтрЗаменить(НовНаименование,"=","_");
	НовНаименование 	= СтрЗаменить(НовНаименование,"\","_");
	НовНаименование 	= СтрЗаменить(НовНаименование,"/","_");
	
	Возврат НовНаименование;
	
КонецФункции

&НаКлиенте
Процедура ТребуютВниманияРуководителяВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	Если Не ТекСтрока = Неопределено Тогда
		СтруктураОткрытия = Новый Структура;
		СтруктураОткрытия.Вставить("Ключ", ТекСтрока.Задача);
		ОткрытьФорму("Справочник.ЦБР_Задача.ФормаОбъекта", СтруктураОткрытия);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКолонки(Элемент)
	
	Наименование = СтрЗаменить(Элемент.Имя,"КомандаКолонки","");  
	Наименование = СтрЗаменить(Наименование,"_"," ");  
	ИзменяемаяКолонкаПользователя = ОпределитьИзменяемуюКолонкуНаСервере(Наименование);
	
	Если ИзменяемаяКолонкаПользователя <> ПредопределенноеЗначение("Справочник.ЦБР_КолонкиАРМ.ПустаяСсылка") Тогда 
		ПараметрыФормы = Новый Структура("Ключ",ИзменяемаяКолонкаПользователя);
		ОткрытьФорму("Справочник.ЦБР_КолонкиАРМ.ФормаОбъекта",ПараметрыФормы,ЭтаФорма,,,,Новый ОписаниеОповещения("ИзменениеКолонкиЗавершение", ЭтотОбъект),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры     

&НаСервере
Функция ОпределитьИзменяемуюКолонкуНаСервере(Наименование)  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_НастройкиАРМДляПользователей.ШаблонАРМ КАК ШаблонАРМ
		|ПОМЕСТИТЬ ШаблонАРМКлиента
		|ИЗ
		|	РегистрСведений.ЦБР_НастройкиАРМДляПользователей КАК ЦБР_НастройкиАРМДляПользователей
		|ГДЕ
		|	ЦБР_НастройкиАРМДляПользователей.Пользователь = &Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦБР_КолонкиАРМ.Ссылка КАК Ссылка
		|ИЗ
		|	ШаблонАРМКлиента КАК ШаблонАРМКлиента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЦБР_КолонкиАРМ КАК ЦБР_КолонкиАРМ
		|		ПО ШаблонАРМКлиента.ШаблонАРМ = ЦБР_КолонкиАРМ.Владелец
		|ГДЕ
		|	ЦБР_КолонкиАРМ.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.ЦБР_КолонкиАРМ.ПустаяСсылка();
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
        ВыборкаДетальныеЗаписи.Следующий();
		Возврат ВыборкаДетальныеЗаписи.Ссылка;  
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОформлениеРежимаАРМ(АктивныйРежим)

	Если АктивныйРежим = "Согласование" Тогда
		УстановитьОформлениеНеактивногоТипаЗадачи(Элементы.АРМЗадания);
		УстановитьОформлениеНеактивногоТипаЗадачи(Элементы.АРМЗадачи);
		УстановитьОформлениеАктивногоТипаЗадачи(Элементы.АРМСогласование);
	ИначеЕсли АктивныйРежим = "Задачи" Тогда
		УстановитьОформлениеАктивногоТипаЗадачи(Элементы.АРМЗадачи);
		УстановитьОформлениеНеактивногоТипаЗадачи(Элементы.АРМЗадания);
		УстановитьОформлениеНеактивногоТипаЗадачи(Элементы.АРМСогласование);
	ИначеЕсли АктивныйРежим = "Задания" Тогда
		УстановитьОформлениеАктивногоТипаЗадачи(Элементы.АРМЗадания);
		УстановитьОформлениеНеактивногоТипаЗадачи(Элементы.АРМЗадачи);
		УстановитьОформлениеНеактивногоТипаЗадачи(Элементы.АРМСогласование);
	КонецЕсли;
	
	УстановитьВидимостьГрупп(АктивныйРежим);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьГрупп(Группа)

	Если ТипЗнч(Группа) <> Тип("Строка") Тогда
		ТекстСообщения = НСтр("ru = 'Неверный тип параметра'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	Если Группа = "Согласование" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "ГруппаСогласования", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "КанбанЗадачи", "Видимость", Ложь);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "ГруппаСогласования", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "КанбанЗадачи", "Видимость", Истина);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОформлениеНеактивногоТипаЗадачи(Элемент)
	Элемент.Пометка 	= Ложь; 
	Элемент.ЦветФона 	= Новый Цвет(163, 173, 187);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОформлениеАктивногоТипаЗадачи(Элемент)
	Элемент.Пометка 	= Истина;
	Элемент.ЦветФона 	= Новый Цвет(30,144,255);
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачМенеджераПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокЗадачМенеджера.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Предмет = ТекущиеДанные.СсылкаНаЗадачу;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачМенеджераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПараметрыФормы = Новый Структура("Ключ", ВыбраннаяСтрока);
	ОткрытьФорму("Справочник.ЦБР_Задача.Форма.ФормаЭлемента", ПараметрыФормы);
КонецПроцедуры
  