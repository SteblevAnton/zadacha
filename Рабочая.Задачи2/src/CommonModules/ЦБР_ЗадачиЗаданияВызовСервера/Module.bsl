#Область ПрограммныйИнтерфейс

Функция СоздатьНовоеЗадание(Этап, ДанныеЗаполнения = Неопределено) Экспорт
	
	Если ТипЗнч(Этап) = Тип("СправочникСсылка.ЦБР_СтруктураЗадач") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_СтруктураЗадач.Аналитик КАК Аналитик,
		|	ЦБР_СтруктураЗадач.ВидЧаса КАК ВидЧаса,
		|	ЦБР_СтруктураЗадач.ВремяНачалаПлан КАК ВремяНачалаПлан,
		|	ЦБР_СтруктураЗадач.ВремяНачалаФакт КАК ВремяНачалаФакт,
		|	ЦБР_СтруктураЗадач.ВремяОкончанияПлан КАК ВремяОкончанияПлан,
		|	ЦБР_СтруктураЗадач.ВремяОкончанияФакт КАК ВремяОкончанияФакт,
		|	ЦБР_СтруктураЗадач.ДатаНачалоПлан КАК ДатаНачалоПлан,
		|	ЦБР_СтруктураЗадач.ДатаНачалоФакт КАК ДатаНачалоФакт,
		|	ЦБР_СтруктураЗадач.ДатаОкончаниеПлан КАК ДатаОкончаниеПлан,
		|	ЦБР_СтруктураЗадач.ДатаОкончаниеФакт КАК ДатаОкончаниеФакт,
		|	ЦБР_СтруктураЗадач.ДлительностьПлан КАК ДлительностьПлан,
		|	ЦБР_СтруктураЗадач.ДлительностьФакт КАК ДлительностьФакт,
		|	ЦБР_СтруктураЗадач.ДополнительноеОписание КАК ДополнительноеОписание,
		|	ЦБР_СтруктураЗадач.ЕстьАналитик КАК ЕстьАналитик,
		|	ЦБР_СтруктураЗадач.ЕстьПереписка КАК ЕстьПереписка,
		|	ЦБР_СтруктураЗадач.ЕстьПрикрепленныйФайл КАК ЕстьПрикрепленныйФайл,
		|	ЦБР_СтруктураЗадач.Исполнитель КАК Исполнитель,
		|	ЦБР_СтруктураЗадач.КодСДР КАК КодСДР,
		|	ЦБР_СтруктураЗадач.КоментарийКСогласованию КАК КоментарийКСогласованию,
		|	ЦБР_СтруктураЗадач.НетГарантии КАК НетГарантии,
		|	ЦБР_СтруктураЗадач.НомерЗаданияВУровне КАК НомерЗаданияВУровне,
		|	ЦБР_СтруктураЗадач.ОплатаПоФакту КАК ОплатаПоФакту,
		|	ЦБР_СтруктураЗадач.ОценкаСогласована КАК ОценкаСогласована,
		|	ЦБР_СтруктураЗадач.ПредыдущееЗадание КАК СсылкаНаЭтапТестирования,
		|	ЦБР_СтруктураЗадач.ПротестированоИсполнителем КАК ПротестированоИсполнителем,
		|	ЦБР_СтруктураЗадач.Состояние КАК Состояние,
		|	ЦБР_СтруктураЗадач.СрокиСогласованы КАК СрокиСогласованы,
		|	ЦБР_СтруктураЗадач.ТипЗадания КАК ТипЗадания,
		|	ЦБР_СтруктураЗадач.ТрудозатратыПлан КАК ТрудозатратыПлан,
		|	ЦБР_СтруктураЗадач.ТрудозатратыПланДо КАК ТрудозатратыПланДо,
		|	ЦБР_СтруктураЗадач.ТрудозатратыПланКомментарий КАК ТрудозатратыПланКомментарий,
		|	ЦБР_СтруктураЗадач.ТрудозатратыПланОт КАК ТрудозатратыПланОт,
		|	ЦБР_СтруктураЗадач.ТрудозатратыПланПредставление КАК ТрудозатратыПланПредставление,
		|	ЦБР_СтруктураЗадач.ТрудозатратыФакт КАК ТрудозатратыФакт,
		|	ЦБР_СтруктураЗадач.Владелец КАК СсылкаНаЗадачу,
		|	ЦБР_СтруктураЗадач.Ссылка КАК СсылкаНаСтруктуру,
		|	ЦБР_СтруктураЗадач.Владелец.Постановщик КАК Постановщик,
		|	ЦБР_СтруктураЗадач.Владелец.ТипОценкиЗадачи КАК ТипОценкиЗадачи,
		|	ЦБР_СтруктураЗадач.Исполнитель.Подразделение КАК Подразделение,
		|	ЦБР_СтруктураЗадач.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_СтруктураЗадач
		|ГДЕ
		|	ЦБР_СтруктураЗадач.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Этап);
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			НовоеЗадание = Документы.ЦБР_Задание.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(НовоеЗадание, ВыборкаДетальныеЗаписи); 
			НовоеЗадание.Дата                           = ТекущаяДатаСеанса();
		    НовоеЗадание.УникальныйИдентификаторЗадания = Строка(Новый УникальныйИдентификатор);
			Если ДанныеЗаполнения <> Неопределено Тогда
				Для Каждого Строка Из ДанныеЗаполнения Цикл   
					НовоеЗадание[Строка.Ключ] = Строка.Значение;	
				КонецЦикла; 
			КонецЕсли;
			НовоеЗадание.Записать(РежимЗаписиДокумента.Проведение);
			
			СтруктураЗадачи = Этап.ПолучитьОбъект();
			СтруктураЗадачи.СсылкаНаЗадание = НовоеЗадание.Ссылка;
			СтруктураЗадачи.Записать();  
			
			Возврат НовоеЗадание.Ссылка;

		КонецЕсли;
		
	КонецЕсли;

КонецФункции

Процедура НайтиОтменитьЗапросИнформции(Элементы, Объект, Отменить = Ложь) Экспорт

	ЗаголовокЗИ = "Запрос информации";
	КартинкаЗИ = БиблиотекаКартинок.СправкаФормы;
	Сообщение = ПолучитьПоследнееСообщениеПоЗаданию(Объект.Ссылка);
	Если Сообщение.Свойство("Период") Тогда
		Если Отменить И Сообщение.Отменено Тогда
			ОбщегоНазначения.СообщитьПользователю("Запрос уже был отменен другим пользователем.");
		ИначеЕсли Отменить Тогда
			Если ЗначениеЗаполнено(Сообщение.ДатаОтветаНаЗапросФакт) Тогда
				ОбщегоНазначения.СообщитьПользователю("Ответ уже был получен. Отменить запрос нельзя.");
			Иначе
				НачатьТранзакцию();
				Попытка
					Запись = РегистрыСведений.ЦБР_УточненияПоЗадачамЗаданиям.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(Запись, Сообщение);
					Запись.Отменено = Истина;
					Запись.Записать();
					//Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЦБР_Задача") Тогда
					//	ПоменятьСтатусЗадачи(Объект.Ссылка);
					//КонецЕсли;
					Получатели = Новый Массив();
					Получатели.Добавить(Сообщение.Ответчик);
					ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.Ссылка,"ОтменаЗапросаИнформации", Получатели, Сообщение.ТекстЗапроса);	
				Исключение
					ЗаписьЖурналаРегистрации("Отмена запроса информации", УровеньЖурналаРегистрации.Ошибка, ?(ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЦБР_Задание"),Метаданные.Документы.ЦБР_Задание,Метаданные.Документы.ЦБР_Задача), Объект.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ОтменитьТранзакцию();
					ВызватьИсключение;
				КонецПопытки;
				ЗафиксироватьТранзакцию();
			КонецЕсли;
		ИначеЕсли Сообщение.Ответчик <> Пользователи.ТекущийПользователь() И Не Сообщение.Отменено И Не ЗначениеЗаполнено(Сообщение.ДатаОтветаНаЗапросФакт)Тогда
			ЗаголовокЗИ = "Отменить запрос";
			КартинкаЗИ = БиблиотекаКартинок.УстановитьСтатусЗаявкиКВозврату;
		КонецЕсли;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Заголовок", ЗаголовокЗИ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаУточнение", "Картинка", КартинкаЗИ);

КонецПроцедуры

Функция ПолучитьПоследнееСообщениеПоЗаданию(Задание, НеОтменено=Ложь) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МАКСИМУМ(ЦБР_УточненияПоЗаданиямСрезПоследних.Период) КАК Период,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.Задание КАК Задание
	|ПОМЕСТИТЬ ВТ_Последние
	|ИЗ
	|	РегистрСведений.ЦБР_УточненияПоЗадачамЗаданиям.СрезПоследних(, Задание = &Задание) КАК
	|		ЦБР_УточненияПоЗаданиямСрезПоследних
	|СГРУППИРОВАТЬ ПО
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.Задание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.Период КАК Период,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.Задание КАК Задание,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.Заявитель КАК Заявитель,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.Ответчик КАК Ответчик,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.ТекстЗапроса КАК ТекстЗапроса,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.ТекстОтвета КАК ТекстОтвета,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.ДатаОтветаНаЗапрос КАК ДатаОтветаНаЗапрос,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.ДатаОтветаНаЗапросФакт КАК ДатаОтветаНаЗапросФакт,
	|	ЦБР_УточненияПоЗаданиямСрезПоследних.КомментарийДатаОтветаНаЗапрос КАК КомментарийДатаОтветаНаЗапрос,
	|	ЕСТЬNULL(ЦБР_УточненияПоЗаданиямСрезПоследних.Отменено, ЛОЖЬ) КАК Отменено
	|ИЗ
	|	ВТ_Последние КАК ВТ_Последние
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦБР_УточненияПоЗадачамЗаданиям.СрезПоследних КАК
	|			ЦБР_УточненияПоЗаданиямСрезПоследних
	|		ПО ВТ_Последние.Период = ЦБР_УточненияПоЗаданиямСрезПоследних.Период
	|		И ВТ_Последние.Задание = ЦБР_УточненияПоЗаданиямСрезПоследних.Задание";
	
	Запрос.УстановитьПараметр("Задание", Задание);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Структура = Новый Структура; 
	
	Если Выборка.Следующий() Тогда
		Если НеОтменено И Выборка.Отменено Тогда
			Возврат Структура;
		КонецЕсли;
		Для Каждого Колонка Из Результат.Колонки Цикл
			Структура.Вставить(Колонка.Имя);
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(Структура, Выборка);
	КонецЕсли;

	Возврат Структура;  
	
КонецФункции

//Создает, заполняет и  записывает новую запись в регистре Действия по заданиям (ЦБР)
Процедура СоздатьЗаписьДействияПоЗадаче(ДанныеЗаполнения) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ЦБР_ДействияПоЗаданиям.СоздатьМенеджерЗаписи();
	
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаполнения);
	
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

Функция РасчитатьДлительностьФакт(СтруктураДат, ДлительностьФакт) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураДат.ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(СтруктураДат.ДатаОкончания) 
		ИЛИ НЕ ЗначениеЗаполнено(СтруктураДат.ВремяНачала) ИЛИ НЕ ЗначениеЗаполнено(СтруктураДат.ВремяОкончания) Тогда
		
		Возврат ДлительностьФакт;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеПроизводственногоКалендаря.Дата КАК ДатаПредпразничный
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|ГДЕ
	|	ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И (ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|			ИЛИ ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПроизводственногоКалендаря.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПредпразничный";
	
	Запрос.УстановитьПараметр("ДатаНачала", СтруктураДат.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", СтруктураДат.ДатаОкончания);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	КоличествоДней = Результат.Количество();
	
	Если КоличествоДней > 1 Тогда
		Если СтруктураДат.ВремяНачала = Дата(01,01,01,09,00,00) И СтруктураДат.ВремяОкончания = Дата(01,01,01,18,00,00) Тогда
			
			Длительность = КоличествоДней * 8;
			
		ИначеЕсли СтруктураДат.ВремяНачала = Дата(01,01,01,18,00,00) И СтруктураДат.ВремяОкончания = Дата(01,01,01,18,00,00) Тогда
			
			Длительность = (КоличествоДней - 1) * 8;
			
		Иначе
			
			ПолныхДней = КоличествоДней - 2;
			
			ЧасыВНачале = (17 - Час(СтруктураДат.ВремяНачала));
			МинутыВНачале = (60 - Минута(СтруктураДат.ВремяНачала)) / 60;
			
			ПервыйДень = ?(МинутыВНачале = 1, ЧасыВНачале, ЧасыВНачале - 1 + МинутыВНачале);
			
			Если СтруктураДат.ВремяОкончания <= Дата(1,1,1,12,00,00) Тогда
				ЧасыВКонце = (Час(СтруктураДат.ВремяОкончания) - 9);
			Иначе
				ЧасыВКонце = (Час(СтруктураДат.ВремяОкончания) - 10);
			КонецЕсли;
			
			МинутыВКонце = (60 - Минута(СтруктураДат.ВремяОкончания)) / 60;
			
			ПоследнийДень = ?(МинутыВКонце = 1, ЧасыВКонце, ЧасыВКонце + МинутыВКонце);
			
			Длительность = ПолныхДней * 8 + ПервыйДень + ПоследнийДень;
			
		КонецЕсли;
		
	Иначе
		
		Часы = (Час(СтруктураДат.ВремяОкончания) - Час(СтруктураДат.ВремяНачала));
		Минуты = (Минута(СтруктураДат.ВремяОкончания) - Минута(СтруктураДат.ВремяНачала)) / 60;
		
		Длительность = ?(Минуты = 0, Часы - 1, Часы - 1 + Минуты);
		
	КонецЕсли;
	
	Если Длительность < 0 Тогда
		Длительность = 0;
	КонецЕсли;

	ДлительностьФакт = ДлительностьФакт + Длительность;

	Возврат ДлительностьФакт;
		
КонецФункции

Процедура ЗаписатьИзмененияЗаписиКалендаря(Данные) Экспорт

	МенеджерЗаписи = РегистрыСведений.ЦБР_ИзмененияЗаписейВКалендаре.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Данные);
	МенеджерЗаписи.Записать(Ложь);

КонецПроцедуры

Функция ПолучитьВремя(Дата) Экспорт
	
	Возврат Дата(1, 1, 1, Час(Дата), Минута(Дата), Секунда(Дата));
	
КонецФункции    

//Находит данные последнего действия по заданию и возвращает его структуру или неопределено если данных не найдено
Функция ПолучитьПоследнееДействие(Задание) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЦБР_ДействияПоЗаданиям.Задание КАК Задание,
	|	ЦБР_ДействияПоЗаданиям.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	ЦБР_ДействияПоЗаданиям.ДатаОкончаниеДействия КАК ДатаОкончаниеДействия,
	|	ЦБР_ДействияПоЗаданиям.Статус КАК Статус,
	|	ЦБР_ДействияПоЗаданиям.Комментарий КАК Комментарий,
	|	ЦБР_ДействияПоЗаданиям.ЗаписьКалендаря КАК ЗаписьКалендаря
	|ИЗ
	|	РегистрСведений.ЦБР_ДействияПоЗаданиям КАК ЦБР_ДействияПоЗаданиям
	|ГДЕ
	|	ЦБР_ДействияПоЗаданиям.Задание = &Задание
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЦБР_ДействияПоЗаданиям.ДатаНачалаДействия УБЫВ";
	
	Запрос.УстановитьПараметр("Задание", Задание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Структура = ПолучитьСтруктуруПоследнегоДействия();
		ЗаполнитьЗначенияСвойств(Структура, Выборка);
		Возврат Структура;
	КонецЕсли;	
	
	Возврат Неопределено;	
	
КонецФункции

//Возвращает пустую структуру последнего действия по заданию
Функция ПолучитьСтруктуруПоследнегоДействия() Экспорт
	
	Структура = Новый Структура();
	
	Структура.Вставить("Задание");
	Структура.Вставить("ДатаНачалаДействия");
	Структура.Вставить("ДатаОкончаниеДействия");
	Структура.Вставить("Статус");
	Структура.Вставить("Комментарий");
	Структура.Вставить("ЗаписьКалендаря");
	
	Возврат Структура;
	
КонецФункции

Функция ПолучитьДатуОкончанияСек(ГрафикРаботы, Знач ДатаНачала, ДлительностьСек = 0) Экспорт
	
	ДатаОкончания = ДатаНачала;
	
	Если ДлительностьСек > 0 Тогда
		Длительность = ДлительностьСек;
		
		НачалоПериода = ДатаНачала;
		КонецПериода = НачалоПериода + 10 * 24 * 3600; // десять дней
		
		ТаблицаРабочегоВремени = СформироватьТаблицуРабочегоВремени(ГрафикРаботы, НачалоПериода, КонецПериода);
		Пока ТаблицаРабочегоВремени.Количество() < 2 Цикл
			КонецПериода = КонецПериода + 10 * 24 * 3600; // десять дней
			ТаблицаРабочегоВремени = СформироватьТаблицуРабочегоВремени(ГрафикРаботы, НачалоПериода, КонецПериода);
		КонецЦикла;
		
		ДатаОкончания = ТаблицаРабочегоВремени[0].ДатаНачала;
		Пока Длительность > 0 Цикл
			Для Инд = 0 По ТаблицаРабочегоВремени.Количество() - 2 Цикл
				Строка = ТаблицаРабочегоВремени[Инд];
				Если Строка.Длительность = Длительность Тогда
					ДатаОкончания = Строка.ДатаОкончания;
					Длительность = 0;
					Прервать;
				ИначеЕсли Строка.Длительность < Длительность Тогда
					ДатаОкончания = ТаблицаРабочегоВремени[Инд + 1].ДатаНачала;
					Длительность = Длительность - Строка.Длительность;
				Иначе
					ДатаОкончания = ДатаОкончания + Длительность;
					Длительность = 0;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Длительность > 0 Тогда
				НачалоПериода = ДатаОкончания;
				КонецПериода = НачалоПериода + 10 * 24 * 3600; // десять дней
				
				ТаблицаРабочегоВремени = СформироватьТаблицуРабочегоВремени(ГрафикРаботы, НачалоПериода, КонецПериода);
				Пока ТаблицаРабочегоВремени.Количество() < 2 Цикл
					КонецПериода = КонецПериода + 10 * 24 * 3600; // десять дней
					ТаблицаРабочегоВремени = СформироватьТаблицуРабочегоВремени(ГрафикРаботы, НачалоПериода,
					КонецПериода);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДатаОкончания;
	
КонецФункции

Функция СформироватьТаблицуРабочегоВремени(ГрафикРаботы, ДатаНачала, ДатаОкончания) Экспорт 
	
	// рабочие дни   
	РабочиеДни = ПрочитатьДанныеГрафикаИзРегистра(ГрафикРаботы, Год(ДатаНачала));
	Если РабочиеДни.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
		НСтр("ru = 'Не заполнен рабочий календарь %1 на %2 год!'"), Строка(ГрафикРаботы), Формат(Год(ДатаНачала),
		"ЧГ="));
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если Год(ДатаОкончания) > Год(ДатаНачала) Тогда
		ВрРабочиеДни = ПрочитатьДанныеГрафикаИзРегистра(ГрафикРаботы, Год(ДатаОкончания));
		Если ВрРабочиеДни.Количество() = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
			НСтр("ru = 'Не заполнен рабочий календарь %1 на %2 год'"), Строка(ГрафикРаботы), Формат(Год(
			ДатаОкончания), "ЧГ="));
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		Для Каждого Элемент Из ВрРабочиеДни Цикл
			РабочиеДни.Добавить(Элемент);
		КонецЦикла;
	КонецЕсли;	
	
	
	// общее рабочее время
	РабочееВремя = Новый ТаблицаЗначений;
	РабочееВремя.Колонки.Добавить("ВремяНачала");
	РабочееВремя.Колонки.Добавить("ВремяОкончания");
	РабочееВремя.Колонки.Добавить("Рабочее");
	
	РабочееВремяГрафика = ГрафикРаботы.РасписаниеРаботы.Выгрузить();
	РабочееВремяГрафика.Сортировать("ВремяНачала");
	ВГраница = РабочееВремяГрафика.Количество() - 1;
	
	Если РабочееВремяГрафика[0].ВремяНачала > '00010101' Тогда
		НоваяСтрока = РабочееВремя.Добавить();
		НоваяСтрока.ВремяНачала = '00010101';
		НоваяСтрока.ВремяОкончания = РабочееВремяГрафика[0].ВремяНачала - 1;
		НоваяСтрока.Рабочее = Ложь;
	КонецЕсли;
	
	Для Инд = 0 По ВГраница Цикл
		НоваяСтрока = РабочееВремя.Добавить();
		НоваяСтрока.ВремяНачала = РабочееВремяГрафика[Инд].ВремяНачала;
		НоваяСтрока.ВремяОкончания = РабочееВремяГрафика[Инд].ВремяОкончания;
		НоваяСтрока.Рабочее = Истина;
		
		Если Инд < ВГраница Тогда
			НоваяСтрока = РабочееВремя.Добавить();
			НоваяСтрока.ВремяНачала = РабочееВремяГрафика[Инд].ВремяОкончания + 1;
			НоваяСтрока.ВремяОкончания = РабочееВремяГрафика[Инд + 1].ВремяНачала - 1;
			НоваяСтрока.Рабочее = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если РабочееВремяГрафика[ВГраница].ВремяОкончания < '00010101235959' Тогда
		НоваяСтрока = РабочееВремя.Добавить();
		НоваяСтрока.ВремяНачала = РабочееВремяГрафика[ВГраница].ВремяОкончания + 1;
		НоваяСтрока.ВремяОкончания = '00010101235959';
		НоваяСтрока.Рабочее = Ложь;
	КонецЕсли;
	
	// таблица рабочего времени
	ТаблицаРабочегоВремени = Новый ТаблицаЗначений;
	ТаблицаРабочегоВремени.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ТаблицаРабочегоВремени.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	ТаблицаРабочегоВремени.Колонки.Добавить("Длительность", Новый ОписаниеТипов("Число"));
	
	ТекущаяДата = ДатаНачала;
	Пока ТекущаяДата < ДатаОкончания Цикл
		
		Если РабочиеДни.Найти(НачалоДня(ТекущаяДата)) = Неопределено Тогда // нерабочий день
			ТекущаяДата = КонецДня(ТекущаяДата) + 1;
			Продолжить;
		КонецЕсли;
		ТекущееВремя = ПолучитьВремя(ТекущаяДата);
		
		врРабочееВремя = РабочееВремя;
		
		Для Каждого Строка Из ВрРабочееВремя Цикл
			Если (Строка.ВремяНачала <= ТекущееВремя) И (ТекущееВремя <= Строка.ВремяОкончания) Тогда
				
				Если Строка.Рабочее Тогда
					НоваяСтрока = ТаблицаРабочегоВремени.Добавить();
					НоваяСтрока.ДатаНачала = ТекущаяДата;
					НоваяСтрока.ДатаОкончания = ДобавитьВремя(ТекущаяДата, Строка.ВремяОкончания);
					
					Если НоваяСтрока.ДатаОкончания > ДатаОкончания Тогда
						НоваяСтрока.ДатаОкончания = ДатаОкончания;
					КонецЕсли;
					
					НоваяСтрока.Длительность = НоваяСтрока.ДатаОкончания - НоваяСтрока.ДатаНачала;
					Если НоваяСтрока.ДатаОкончания = КонецДня(НоваяСтрока.ДатаОкончания) Тогда
						НоваяСтрока.Длительность = НоваяСтрока.Длительность + 1;
					КонецЕсли;
				КонецЕсли;
				
				ТекущаяДата = ДобавитьВремя(ТекущаяДата, Строка.ВремяОкончания) + 1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаРабочегоВремени;
	
КонецФункции

// Возвращаемое значение
//	СписокЗначений		- список значений, в котором хранятся даты, входящие в календарь
//
Функция ПрочитатьДанныеГрафикаИзРегистра(Календарь, НомерГода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КалендарныеГрафики.ДатаГрафика КАК ДатаКалендаря
	|ИЗ
	|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	|ГДЕ
	|	КалендарныеГрафики.Календарь = &Календарь
	|	И КалендарныеГрафики.Год = &ТекущийГод
	|	И КалендарныеГрафики.ДеньВключенВГрафик";
	
	Запрос.УстановитьПараметр("Календарь", Календарь);
	Запрос.УстановитьПараметр("ТекущийГод", НомерГода);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДатаКалендаря");
	
КонецФункции

// Возвращаемое значение
//	Ссылка на справочник- Календарь
//
Функция ПолучитьГрафикРаботы() Экспорт
	
	Возврат Константы.ОсновнойКалендарьПредприятия.Получить();
	
КонецФункции

// Возвращаемое значение
//	Дата		- Дата периода
//
Функция РассчитатьОкончаниеПериода(ДатаНачала, Длительность) Экспорт
	
	ГрафикРаботы = ПолучитьГрафикРаботы();
	ДатаОкончания = ПолучитьДатуОкончанияСек(ГрафикРаботы, ДатаНачала, Длительность * 3600);
	Возврат ДатаОкончания;
	
КонецФункции

Функция ПолучитьАктуальныйСтатусЗадания(СсылкаНаЗадание, Пользователь) Экспорт

	//Запрос = Новый Запрос;
	//Запрос.Текст = 
	//	"ВЫБРАТЬ
	//	|	ЦБР_СостоянияЗаданий.Состояние КАК Состояние,
	//	|	ЦБР_СостоянияЗаданий.Задание КАК Задание,
	//	|	ЦБР_СостоянияЗаданий.ПользовательАРМ КАК ПользовательАРМ
	//	|ИЗ
	//	|	РегистрСведений.ЦБР_СостоянияЗаданий КАК ЦБР_СостоянияЗаданий
	//	|ГДЕ
	//	|	ЦБР_СостоянияЗаданий.Задание = &Задание
	//	|	И ЦБР_СостоянияЗаданий.ПользовательАРМ = &ПользовательАРМ";
	//
	//Запрос.УстановитьПараметр("Задание", СсылкаНаЗадание);
	//Запрос.УстановитьПараметр("ПользовательАРМ", Пользователь);
	//
	//РезультатЗапроса = Запрос.Выполнить();
	//
	//ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	//
	//Если ВыборкаДетальныеЗаписи.Следующий() Тогда
	//	ПоследнийСтатус = ВыборкаДетальныеЗаписи.Состояние; 
	//Иначе 
	//	ПоследнийСтатус = Перечисления.ЦБР_Состояние.Черновик;
	//КонецЕсли;
	
	//Возврат ПоследнийСтатус;   
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаЗадание, "Состояние");   
	
КонецФункции

Функция ПолучитьРольВЗадаче(Задача, Пользователь) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Роль КАК Роль
	|ИЗ
	|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|ГДЕ
	|	ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
	|	И ЦБР_ЗадачаКоманда.Исполнитель = &Исполнитель";
	
	Запрос.УстановитьПараметр("Исполнитель", Пользователь);
	Запрос.УстановитьПараметр("Ссылка", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Возврат ВыборкаДетальныеЗаписи.Роль;
	Иначе
		Возврат Перечисления.ЦБР_РольВЗадаче.Наблюдатель;
	КонецЕсли;

КонецФункции

Функция ПолучитьПредсогласованныеЧасы(Задача) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ЦБР_ПредсогласованныеЧасыПоЗадачам.Часы) КАК Часы,
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача КАК Задача
		|ИЗ
		|	РегистрСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам КАК ЦБР_ПредсогласованныеЧасыПоЗадачам
		|ГДЕ
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача = &Задача
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Часы;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
КонецФункции

Функция ПолучитьОстатокПредсогласованныхЧасов(Задача) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ЦБР_ПредсогласованныеЧасыПоЗадачам.Часы) КАК Часы,
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача КАК Задача
		|ПОМЕСТИТЬ ВТ_Итоговая
		|ИЗ
		|	РегистрСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам КАК ЦБР_ПредсогласованныеЧасыПоЗадачам
		|ГДЕ
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача = &Задача
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	-ЦБР_СогласованоЗакрытоЧасовОбороты.СогласованоОборот,
		|	ЦБР_СогласованоЗакрытоЧасовОбороты.Задача
		|ИЗ
		|	РегистрНакопления.ЦБР_СогласованоЗакрытоЧасов.Обороты(, , , Задача = &Задача) КАК ЦБР_СогласованоЗакрытоЧасовОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ВТ_Итоговая.Часы) КАК Часы,
		|	ВТ_Итоговая.Задача КАК Задача
		|ИЗ
		|	ВТ_Итоговая КАК ВТ_Итоговая
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Итоговая.Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Часы;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
КонецФункции

Функция ПолучитьОстатокЧасовПоЗаданию(Задание) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦБР_Задание.ТрудозатратыПлан - ЦБР_Задание.ТрудозатратыФакт КАК Часы,
		|	ЦБР_Задание.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЦБР_Задание КАК ЦБР_Задание
		|ГДЕ
		|	ЦБР_Задание.Ссылка = &Задание
		|	И ЦБР_Задание.ЧасыСогласованы
		|	И ЦБР_Задание.Проведен";
	
	Запрос.УстановитьПараметр("Задание", Задание);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Часы;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
КонецФункции

Процедура ДобавитьПредсогласованныеЧасы(ОбъектРегистратор, Задача) Экспорт
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам");
		ЭлементБлокировкиДанных.УстановитьЗначение("Задача", Задача);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Задача.Установить(Задача);
		НаборЗаписей.Отбор.ОбъектРегистратор.Установить(ОбъектРегистратор.Ссылка);
		Если ОбъектРегистратор.ПометкаУдаления Тогда
			Часы = 0;
		ИначеЕсли ТипЗнч(ОбъектРегистратор) = Тип("СправочникОбъект.ЦБР_Задача") Тогда
			Часы = ОбъектРегистратор.ПредСогласованныеЧасы;
		ИначеЕсли ТипЗнч(ОбъектРегистратор) = Тип("ДокументОбъект.ЦБР_Задание") И ОбъектРегистратор.ЧасыСогласованы 
			И ОбъектРегистратор.ТипОценкиЗадачи = Перечисления.ЦБР_ТипОценкиЗадачи.Предсогласованные Тогда
			Часы = ОбъектРегистратор.ТрудозатратыПлан;
		Иначе	
			Часы = 0;
		КонецЕсли;	
		Если Часы <> 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Задача            = Задача;     
			НоваяЗапись.ОбъектРегистратор = ОбъектРегистратор.Ссылка;     
			НоваяЗапись.Часы              = Часы;
		КонецЕсли;
		НаборЗаписей.Записать(Истина);
		ЗафиксироватьТранзакцию(); 
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись в регистр ЦБР_ПредсогласованныеЧасыПоЗадачам'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьЧасыПоЭтапу(ОбъектРегистратор) Экспорт
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЦБР_Часы");
		ЭлементБлокировкиДанных.УстановитьЗначение("ОбъектРегистратор", ОбъектРегистратор);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЦБР_Часы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектРегистратор.Установить(ОбъектРегистратор); 
		
		Запрос = Новый Запрос(); 
		Запрос.Текст = ТекстЗапроса_ЦБР_Часы();
		Запрос.Параметры.Вставить("Ссылка", ОбъектРегистратор);  
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаРезультатаЗапроса = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаРезультатаЗапроса.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаРезультатаЗапроса); 
		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);
		ЗафиксироватьТранзакцию(); 
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись в регистр ЦБР_Часы'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры  

Функция ТекстЗапроса_ЦБР_Часы()
	
	ТекстЗапроса =	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              	|	ЦБР_Задание.Ссылка КАК ОбъектРегистратор,
	              	|	ЦБР_Задание.Постановщик КАК Постановщик,
	              	|	ЦБР_Задание.Исполнитель КАК Исполнитель,
	              	|	ЦБР_Задание.ЧасыСогласованы КАК ЧасыСогласованы,
	              	|	ЦБР_Задание.Подразделение КАК ПодразделениеИсполнителя,
	              	|	ЦБР_Задание.Постановщик.Подразделение КАК ПодразделениеПостановщика,
	              	|	ЦБР_Задание.СсылкаНаЗадачу КАК Задача,
	              	|	ЦБР_Задание.Дата КАК Дата,
	              	|	ЦБР_СостоянияЗаданий.Состояние КАК Состояние,
	              	|	ЦБР_Задание.ТипОценкиЗадачи КАК ТипОценкиЗадачи,
	              	|	ЦБР_Задание.ТрудозатратыПлан КАК ТрудозатратыПлан,
	              	|	ЦБР_Задание.ТрудозатратыФакт КАК ТрудозатратыФакт,
	              	|	ЦБР_Задание.ТрудозатратыПланСтарый КАК ТрудозатратыПланСтарый,
	              	|	ЦБР_Задание.ВидЧаса КАК ВидЧаса
	              	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	              	|ИЗ
	              	|	РегистрСведений.ЦБР_СостоянияЗаданий КАК ЦБР_СостоянияЗаданий
	              	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЦБР_Задание КАК ЦБР_Задание
	              	|		ПО ЦБР_СостоянияЗаданий.Задание = ЦБР_Задание.Ссылка
	              	|			И ЦБР_СостоянияЗаданий.ПользовательАРМ = ЦБР_Задание.Исполнитель
	              	|ГДЕ
	              	|	ЦБР_Задание.Ссылка = &Ссылка
	              	|;
	              	|
	              	|////////////////////////////////////////////////////////////////////////////////
	              	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              	|	ВТ_ДанныеДокумента.ПодразделениеИсполнителя КАК ПодразделениеИсполнителя,
	              	|	ВТ_ДанныеДокумента.ПодразделениеПостановщика КАК ПодразделениеПостановщика,
	              	|	ВТ_ДанныеДокумента.Задача КАК Задача,
	              	|	ВТ_ДанныеДокумента.Исполнитель КАК Исполнитель,
	              	|	ВТ_ДанныеДокумента.Постановщик КАК Постановщик,
	              	|	ВТ_ДанныеДокумента.ВидЧаса КАК ВидЧаса,
	              	|	ВТ_ДанныеДокумента.ОбъектРегистратор КАК ОбъектРегистратор,
	              	|	0 КАК ВРаботе,
	              	|	ВЫБОР
	              	|		КОГДА ВТ_ДанныеДокумента.ЧасыСогласованы
	              	|			ТОГДА ВТ_ДанныеДокумента.ТрудозатратыПлан - ВТ_ДанныеДокумента.ТрудозатратыПланСтарый
	              	|		ИНАЧЕ ВТ_ДанныеДокумента.ТрудозатратыПлан
	              	|	КОНЕЦ КАК НаСогласовании
	              	|ПОМЕСТИТЬ ВТ_Итоговая
	              	|ИЗ
	              	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	              	|ГДЕ
	              	|	ВТ_ДанныеДокумента.ТипОценкиЗадачи = ЗНАЧЕНИЕ(Перечисление.ЦБР_ТипОценкиЗадачи.Стандартная)
	              	|	И ВТ_ДанныеДокумента.Состояние = ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.НаСогласовании)
	              	|	И ВЫБОР
	              	|			КОГДА ВТ_ДанныеДокумента.ЧасыСогласованы
	              	|				ТОГДА ВТ_ДанныеДокумента.ТрудозатратыПлан - ВТ_ДанныеДокумента.ТрудозатратыПланСтарый
	              	|			ИНАЧЕ ВТ_ДанныеДокумента.ТрудозатратыПлан
	              	|		КОНЕЦ <> 0
	              	|
	              	|ОБЪЕДИНИТЬ ВСЕ
	              	|
	              	|ВЫБРАТЬ
	              	|	ВТ_ДанныеДокумента.ПодразделениеИсполнителя,
	              	|	ВТ_ДанныеДокумента.ПодразделениеПостановщика,
	              	|	ВТ_ДанныеДокумента.Задача,
	              	|	ВТ_ДанныеДокумента.Исполнитель,
	              	|	ВТ_ДанныеДокумента.Постановщик,
	              	|	ВТ_ДанныеДокумента.ВидЧаса,
	              	|	ВТ_ДанныеДокумента.ОбъектРегистратор,
	              	|	ВТ_ДанныеДокумента.ТрудозатратыПлан - ВТ_ДанныеДокумента.ТрудозатратыФакт,
	              	|	0
	              	|ИЗ
	              	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	              	|ГДЕ
	              	|	ВТ_ДанныеДокумента.ТипОценкиЗадачи = ЗНАЧЕНИЕ(Перечисление.ЦБР_ТипОценкиЗадачи.Стандартная)
	              	|	И ВТ_ДанныеДокумента.Состояние В (ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.НаСогласовании), ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.ВРаботе), ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.Новый), ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.Запланировано), ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.Согласован))
	              	|	И ВТ_ДанныеДокумента.ЧасыСогласованы
	              	|	И ВТ_ДанныеДокумента.ТрудозатратыПлан - ВТ_ДанныеДокумента.ТрудозатратыФакт <> 0
	              	|;
	              	|
	              	|////////////////////////////////////////////////////////////////////////////////
	              	|ВЫБРАТЬ
	              	|	ВТ_Итоговая.ПодразделениеИсполнителя КАК ПодразделениеИсполнителя,
	              	|	ВТ_Итоговая.ПодразделениеПостановщика КАК ПодразделениеПостановщика,
	              	|	ВТ_Итоговая.Задача КАК Задача,
	              	|	ВТ_Итоговая.Исполнитель КАК Исполнитель,
	              	|	ВТ_Итоговая.Постановщик КАК Постановщик,
	              	|	ВТ_Итоговая.ВидЧаса КАК ВидЧаса,
	              	|	ВТ_Итоговая.ОбъектРегистратор КАК ОбъектРегистратор,
	              	|	СУММА(ВТ_Итоговая.ВРаботе) КАК ВРаботе,
	              	|	СУММА(ВТ_Итоговая.НаСогласовании) КАК НаСогласовании
	              	|ИЗ
	              	|	ВТ_Итоговая КАК ВТ_Итоговая
	              	|
	              	|СГРУППИРОВАТЬ ПО
	              	|	ВТ_Итоговая.ПодразделениеИсполнителя,
	              	|	ВТ_Итоговая.ПодразделениеПостановщика,
	              	|	ВТ_Итоговая.Задача,
	              	|	ВТ_Итоговая.Исполнитель,
	              	|	ВТ_Итоговая.Постановщик,
	              	|	ВТ_Итоговая.ВидЧаса,
	              	|	ВТ_Итоговая.ОбъектРегистратор";
	
	Возврат ТекстЗапроса;
	
КонецФункции


Процедура СписатьПредсогласованныеЧасы(Задача, СписатьЧасы, Регистратор) Экспорт
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам");
		ЭлементБлокировкиДанных.УстановитьЗначение("Задача", Задача);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		ОшибкаДанных = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ЦБР_ПредсогласованныеЧасыПоЗадачам.Часы) КАК Часы
		|ИЗ
		|	РегистрСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам КАК ЦБР_ПредсогласованныеЧасыПоЗадачам
		|ГДЕ
		|	ЦБР_ПредсогласованныеЧасыПоЗадачам.Задача = &Задача
		|	И ЦБР_ПредсогласованныеЧасыПоЗадачам.ОбъектРегистратор <> &ОбъектРегистратор";
		
		Запрос.УстановитьПараметр("Задача", Задача);
		Запрос.УстановитьПараметр("ОбъектРегистратор", Регистратор);
		
		Результат = Запрос.Выполнить().Выбрать();;
		
		КоличествоПредсогласованныхЧасов = 0;
		Пока Результат.Следующий() Цикл
			КоличествоПредсогласованныхЧасов = Результат.Часы - СписатьЧасы;
			Если КоличествоПредсогласованныхЧасов < 0 Тогда 
				ОбщегоНазначения.СообщитьПользователю("Списание не возможно. Доступно для списания: " + Результат.Часы + ". Вы хотите списать: "+ СписатьЧасы); 
				ОшибкаДанных = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ОшибкаДанных = Ложь Тогда
			НаборЗаписей = РегистрыСведений.ЦБР_ПредсогласованныеЧасыПоЗадачам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Задача.Установить(Задача);
			НаборЗаписей.Отбор.ОбъектРегистратор.Установить(Регистратор);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Задача            = Задача;     
			НоваяЗапись.ОбъектРегистратор = Регистратор;     
			НоваяЗапись.Часы              = -СписатьЧасы;
			НаборЗаписей.Записать(Истина);
			ЗафиксироватьТранзакцию(); 
		Иначе
			ОтменитьТранзакцию();  
		КонецЕсли;
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись в регистр ЦБР_ПредсогласованныеЧасыПоЗадачам'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры 

Функция КоличествоЗаданийВЗадаче(Задача) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЦБР_Задание.Ссылка) КАК Количество,
		|	ЦБР_Задание.СсылкаНаЗадачу КАК СсылкаНаЗадачу
		|ИЗ
		|	Документ.ЦБР_Задание КАК ЦБР_Задание
		|ГДЕ
		|	ЦБР_Задание.Проведен
		|	И ЦБР_Задание.СсылкаНаЗадачу = &СсылкаНаЗадачу
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦБР_Задание.СсылкаНаЗадачу";
	
	Запрос.УстановитьПараметр("СсылкаНаЗадачу", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Количество;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции 

Процедура ЗакрытьЗадачу(Задача) Экспорт 	

	Если ВсеЗаданияВыполнены(Задача) Тогда
		Получатели = Новый Массив;
		Для каждого Исполнители Из Задача.Команда Цикл
			МенеджерЗаписи = РегистрыСведений.ЦБР_СтатусыЗадач.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДатаСеанса();
			МенеджерЗаписи.Задача = Задача;
			МенеджерЗаписи.Пользователь = Исполнители.Исполнитель; 
			МенеджерЗаписи.СостояниеЗадачи = Перечисления.ЦБР_Состояние.Выполнен;
			МенеджерЗаписи.Записать(); 			
			Получатели.Добавить(Исполнители.Исполнитель);
		КонецЦикла;
		
		НаборЗаписей = РегистрыСведений.ЦБР_СостоянияЗадач.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Задача.Установить(Задача);
		НаборЗаписей.Записать();
		НаборЗаписей = РегистрыСведений.ЦБР_СостоянияЗадач.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Задача.Установить(Задача);
		
		ТЧкоманда = Задача.Команда.Выгрузить();
		ТЧкоманда.Свернуть("Исполнитель");
		
		Длительность = ПолучитьСуммуОценокПоЗадаче(Задача);
		Для каждого Исполнители Из ТЧкоманда Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Задача = Задача;
			НоваяЗапись.ПользовательАРМ = Исполнители.Исполнитель;
			НоваяЗапись.ТрудозатратыПлан = Длительность;
			НоваяЗапись.Клиент = Задача.Заказчик;            
			НоваяЗапись.Менеджер = Задача.Постановщик;
			НоваяЗапись.Состояние = Перечисления.ЦБР_Состояние.Выполнен;  
			НоваяЗапись.Роль = Исполнители.Роль;

		КонецЦикла;
		НаборЗаписей.Записать();

		
		ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Задача, "ЗакрытиеЗадачи", Получатели);
	Иначе
		ОбщегоНазначения.СообщитьПользователю("Невозможно закрыть задачу! Не все этапы выполнены или нет прикрепленных реализаций!");
	КонецЕсли;    
	
КонецПроцедуры

Процедура РеанимироватьЗадачу(Задача) Экспорт 

	Получатели = Новый Массив;
	Для каждого Исполнители Из Задача.Команда Цикл
		//МенеджерЗаписи = РегистрыСведений.ЦБР_СтатусыЗадач.СоздатьМенеджерЗаписи();
		//МенеджерЗаписи.Период = ТекущаяДатаСеанса();
		//МенеджерЗаписи.Задача = Задача;
		//МенеджерЗаписи.Пользователь = Исполнители.Исполнитель; 
		//МенеджерЗаписи.СостояниеЗадачи = Перечисления.ЦБР_Состояние.Новый;
		//МенеджерЗаписи.Записать();
		Получатели.Добавить(Исполнители.Исполнитель);
	КонецЦикла;
		
	Описание = "Задача реанимирована";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_СостоянияЗаданий.Задание КАК Задание,
		|	ЦБР_СостоянияЗаданий.Задание.Наименование КАК ЗаданиеНаименование,
		|	ЦБР_СостоянияЗаданий.Задание.Описание КАК ЗаданиеОписание
		|ИЗ
		|	РегистрСведений.ЦБР_СостоянияЗаданий КАК ЦБР_СостоянияЗаданий
		|ГДЕ
		|	ЦБР_СостоянияЗаданий.Задание.СсылкаНаЗадачу = &Владелец
		|	И ЦБР_СостоянияЗаданий.Состояние = &Состояние";
	
	Запрос.УстановитьПараметр("Владелец", Задача);
	Запрос.УстановитьПараметр("Состояние", Перечисления.ЦБР_Состояние.Новый);
	
	РезультатЗапроса       = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		ОписаниеЗадания = ВернутьТекстФорматированногоДокумента(ВыборкаДетальныеЗаписи.ЗаданиеОписание);
		Описание = Описание + ". " + ВыборкаДетальныеЗаписи.ЗаданиеНаименование + ". " + ОписаниеЗадания;
	КонецЦикла;
	
	//НаборЗаписей = РегистрыСведений.ЦБР_СостоянияЗадач.СоздатьНаборЗаписей();
	//НаборЗаписей.Отбор.Задача.Установить(Задача);
	//НаборЗаписей.Записать();

	//ТЧкоманда = Задача.Команда.Выгрузить();
	//ТЧкоманда.Свернуть("Исполнитель, Роль");
	//
	//Длительность = ПолучитьСуммуОценокПоЗадаче(Задача);
	//Для каждого Исполнители Из ТЧкоманда Цикл 
	//	ПоследнийСтатус = "";
	//	//ПоследнийСтатус = ПолучитьАктуальныйСтатусЗадачи(Задача, Исполнители.Исполнитель);
	//	
	//	НоваяЗапись = НаборЗаписей.Добавить();
	//	НоваяЗапись.Задача = Задача;
	//	НоваяЗапись.ПользовательАРМ = Исполнители.Исполнитель;
	//	НоваяЗапись.ТрудозатратыПлан = Длительность;
	//	НоваяЗапись.Клиент = Задача.Заказчик;            
	//	НоваяЗапись.Менеджер = Задача.Постановщик;
	//	НоваяЗапись.Состояние = ПоследнийСтатус;   
	//	НоваяЗапись.Роль = Исполнители.Роль;
	//КонецЦикла;

	//НаборЗаписей.Записать();
	ЦБР_ОбсужденияСервер.ОтправитьСообщениеВОбщееОбсуждение(Задача, Описание, Получатели);     
	
КонецПроцедуры

Функция ПолучитьСуммуОценокПоЗадаче(СсылкаНаЗадачу) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦБР_ЗаданиеЭффективныеЧасы.Ссылка КАК Ссылка,
	|	СУММА(ЦБР_ЗаданиеЭффективныеЧасы.ЭффективныеЧасы) КАК ЭффективныеЧасы
	|ПОМЕСТИТЬ ВТ_Факт
	|ИЗ
	|	Документ.ЦБР_Задание.ЭффективныеЧасы КАК ЦБР_ЗаданиеЭффективныеЧасы
	|ГДЕ
	|	ЦБР_ЗаданиеЭффективныеЧасы.Ссылка.СсылкаНаЗадачу = &СсылкаНаЗадачу
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦБР_ЗаданиеЭффективныеЧасы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ЦБР_Задание.РамочнаяОценка
	|				ТОГДА ВЫБОР
	|						КОГДА ЦБР_Задание.ТрудозатратыПлан = 0
	|							ТОГДА ЕСТЬNULL(ВТ_Факт.ЭффективныеЧасы, 0)
	|						ИНАЧЕ ЦБР_Задание.ТрудозатратыПлан
	|					КОНЕЦ
	|			ИНАЧЕ ЦБР_Задание.ТрудозатратыПланДо
	|		КОНЕЦ) КАК ТрудозатратыПлан,
	|	ЦБР_Задание.СсылкаНаЗадачу КАК СсылкаНаЗадачу
	|ИЗ
	|	Документ.ЦБР_Задание КАК ЦБР_Задание
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Факт КАК ВТ_Факт
	|		ПО ВТ_Факт.Ссылка = ЦБР_Задание.Ссылка
	|ГДЕ
	|	ЦБР_Задание.СсылкаНаЗадачу = &СсылкаНаЗадачу
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦБР_Задание.СсылкаНаЗадачу";
	
	Запрос.УстановитьПараметр("СсылкаНаЗадачу", СсылкаНаЗадачу);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда 
		Возврат Результат.ТрудозатратыПлан;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Процедура РазбитьЗадание(СсылкаНаЗадание) Экспорт
	
	Объект = СсылкаНаЗадание.ПолучитьОбъект();
	
	Если Объект.Разделена Тогда
		Возврат;
	КонецЕсли;
	
	ОстатокЧасов = Объект.ТрудозатратыПлан - Объект.ТрудозатратыФакт;
	
	Если ОстатокЧасов <= 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Нельзя разделить задачу с нулевым остатком!");
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		НоваяСтруктура = Справочники.ЦБР_СтруктураЗадач.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НоваяСтруктура, Объект.СсылкаНаСтруктуру,,"Владелец");
		НоваяСтруктура.Состояние = Перечисления.ЦБР_Состояние.Согласован;
		Если ОстатокЧасов > 0 Тогда
			НоваяСтруктура.ТрудозатратыПлан = ОстатокЧасов;
			НоваяСтруктура.ТрудозатратыПланПредставление = ОстатокЧасов; 
		Иначе 
			НоваяСтруктура.ТрудозатратыПлан = 0;
			НоваяСтруктура.ТрудозатратыПланПредставление = "По факту";
		КонецЕсли;
		НоваяСтруктура.ДлительностьФакт = 0;
		НоваяСтруктура.СсылкаНаЗадание = Документы.ЦБР_Задание.ПустаяСсылка();
		НоваяСтруктура.НомерЗаданияВУровне = "";
		НоваяСтруктура.КодСДР = 0;
		НоваяСтруктура.Наименование = "Продолжение задания: " + Объект.Наименование;
		НоваяСтруктура.Код = "";
		НоваяСтруктура.ТрудозатратыФакт = 0;
		НоваяСтруктура.Записать();
		
		Этап = НоваяСтруктура.Ссылка;
		Если ЗначениеЗаполнено(Этап) Тогда       
			Задание = СоздатьНовоеЗадание(Этап);
		КонецЕсли;
		
		Объект.ТрудозатратыПлан = Объект.ТрудозатратыФакт;
		Объект.Разделена = Истина;
		Объект.Записать(РежимЗаписиДокумента.Проведение);
		ЗафиксироватьТранзакцию();
		
		Получатели = Новый Массив;
		Получатели.Добавить(Объект.Постановщик);
		ДопИнформация = "Для задания " + Объект.Ссылка + " создано задание-продолжение с количеством часов: " + ОстатокЧасов;
		ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.Задача, "РазделениеЗадания", Получатели, ДопИнформация);
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Разделение задачи'"),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение; // есть вложенные транзакции
	КонецПопытки;

КонецПроцедуры 

Функция ПолучитьЗаданияПоЗадаче(СсылкаНаЗадачу) Экспорт
	МассивЗаданий = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_Задание.Ссылка КАК Ссылка,
		|	ЦБР_Задание.Состояние КАК Состояние,
		|	ЦБР_Задание.Исполнитель КАК Исполнитель
		|ПОМЕСТИТЬ ВТ_Задания
		|ИЗ
		|	Документ.ЦБР_Задание КАК ЦБР_Задание
		|ГДЕ
		|	ЦБР_Задание.СсылкаНаЗадачу = &Задача
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЦБР_ЗаписиРабочегоКалендаря.Ссылка) КАК КоличествоЗапланированныхДействий,
		|	ВТ_Задания.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_КоличествоСобытий
		|ИЗ
		|	ВТ_Задания КАК ВТ_Задания,
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Статус В(&Статус)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Задания.Ссылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Задания.Ссылка КАК Ссылка,
		|	ВТ_Задания.Состояние КАК Состояние,
		|	ВТ_Задания.Исполнитель КАК Исполнитель,
		|	ЕСТЬNULL(ВТ_КоличествоСобытий.КоличествоЗапланированныхДействий, 0) КАК КоличествоЗапланированныхДействий
		|ИЗ
		|	ВТ_Задания КАК ВТ_Задания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоСобытий КАК ВТ_КоличествоСобытий
		|		ПО ВТ_Задания.Ссылка = ВТ_КоличествоСобытий.Ссылка";
	
	Запрос.УстановитьПараметр("Задача", СсылкаНаЗадачу);
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);
	Запрос.УстановитьПараметр("Статус", МассивСтатусов);

	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеЗаполнения = Новый Структура();
		ДанныеЗаполнения.Вставить("Ссылка",Выборка.Ссылка);
		ДанныеЗаполнения.Вставить("Состояние",Выборка.Состояние);
		ДанныеЗаполнения.Вставить("Исполнитель",Выборка.Исполнитель);
		ДанныеЗаполнения.Вставить("КоличествоЗапланированныхДействий",Выборка.КоличествоЗапланированныхДействий);
		
		МассивЗаданий.Добавить(ДанныеЗаполнения);
	КонецЦикла;
	
	Возврат МассивЗаданий;
	
КонецФункции

Процедура ЗаменитьОсПоПересогласованию(Пересогласование, Задание) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.ЗадачаСсылка = &ЗадачаСсылка";
	
	Запрос.УстановитьПараметр("ЗадачаСсылка", Пересогласование);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОС = Выборка.Ссылка.ПолучитьОбъект();
		ОС.ЗадачаСсылка = Задание;
		ОС.Записать();
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьФактическиеЧасыПоЗадаче(Задача) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_КоличествоЧасовСотрудниковПоЗадачамОбороты.ЧасыОборот КАК ЧасыОборот
	|ИЗ
	|	РегистрНакопления.ЦБР_КоличествоЧасовСотрудниковПоЗадачам.Обороты(, , , Задача = &Задача) КАК ЦБР_КоличествоЧасовСотрудниковПоЗадачамОбороты";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.ЧасыОборот;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИсполнителей(Задача) Экспорт    
	
	МассивИсполнителей = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_Задание.Исполнитель КАК Исполнитель
		|ИЗ
		|	Документ.ЦБР_Задание КАК ЦБР_Задание
		|ГДЕ
		|	ЦБР_Задание.Проведен
		|	И ЦБР_Задание.СсылкаНаЗадачу = &СсылкаНаЗадачу
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦБР_Задание.Исполнитель";
	
	Запрос.УстановитьПараметр("СсылкаНаЗадачу", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивИсполнителей.Добавить(ВыборкаДетальныеЗаписи.Исполнитель);
	КонецЦикла;   
	
	Возврат МассивИсполнителей;
	
КонецФункции

Функция ПолучитьПоследнегоИсполнителяЗадачи(Задача) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ЦБР_СостоянияЗаданий.Задание.ДатаОкончаниеФакт КАК ДатаОкончаниеФакт,
		|	ЦБР_СостоянияЗаданий.Задание.Исполнитель КАК Исполнитель
		|ИЗ
		|	РегистрСведений.ЦБР_СостоянияЗаданий КАК ЦБР_СостоянияЗаданий
		|ГДЕ
		|	ЦБР_СостоянияЗаданий.Состояние = &Состояние
		|	И ЦБР_СостоянияЗаданий.Задание.СсылкаНаЗадачу = &Владелец
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОкончаниеФакт УБЫВ";
	
	Запрос.УстановитьПараметр("Владелец", Задача);
	Запрос.УстановитьПараметр("Состояние", Перечисления.ЦБР_Состояние.Выполнен);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		 Возврат ВыборкаДетальныеЗаписи.Исполнитель;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции	

Процедура ЗаписатьЧасыВЗадачу(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.ЭффективныеЧасы = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения.ЗаданиеСсылка) Тогда
		Возврат;
	КонецЕсли;	
	
	СменаСтатуса = Ложь;
	
	ЗаданиеОбъект = ДанныеЗаполнения.ЗаданиеСсылка.ПолучитьОбъект(); 
	
	Если ДанныеЗаполнения.РешеноПриПервомПодключении Тогда     
		ЗаданиеОбъект.ТрудозатратыПлан            = 0.5;
	    ЗаданиеОбъект.УдалосьРешитьПриПодключении = Истина;
	    ЗаданиеОбъект.ЧасыСогласованы             = Истина;
	КонецЕсли;
	
	НайденныеСтроки = ЗаданиеОбъект.ЭффективныеЧасы.НайтиСтроки(Новый Структура("ДействиеКалендаря", ДанныеЗаполнения.ЗаписьОС));
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		ДобавитьЧасыЗП(ЗаданиеОбъект, НачалоМесяца(ДанныеЗаполнения.Дата), ДанныеЗаполнения.ЭффективныеЧасы - НайденныеСтроки[0].ЭффективныеЧасы);
			
		НайденныеСтроки[0].ЭффективныеЧасы = ДанныеЗаполнения.ЭффективныеЧасы; 
	Иначе
		НоваяСтрока                   = ЗаданиеОбъект.ЭффективныеЧасы.Добавить();
		НоваяСтрока.Исполнитель       = ЗаданиеОбъект.Исполнитель;
		НоваяСтрока.ВидЧаса           = ЗаданиеОбъект.ВидЧаса; 
		НоваяСтрока.ЭффективныеЧасы   = ДанныеЗаполнения.ЭффективныеЧасы;
		НоваяСтрока.Дата              = ДанныеЗаполнения.Дата; 
		НоваяСтрока.ДействиеКалендаря = ДанныеЗаполнения.ЗаписьОС;
		НоваяСтрока.Комментарий       = Новый ХранилищеЗначения(ДанныеЗаполнения.ОписаниеВыполненнойРаботы);
		
		ДобавитьЧасыЗП(ЗаданиеОбъект, НачалоМесяца(ДанныеЗаполнения.Дата), ДанныеЗаполнения.ЭффективныеЧасы);
		
	КонецЕсли;
	
	ЗаданиеОбъект.ТрудозатратыФакт = ЗаданиеОбъект.ЭффективныеЧасы.Итог("ЭффективныеЧасы");
	
	РасчетДлительностиЗадания(ЗаданиеОбъект, ДанныеЗаполнения.ОписаниеВыполненнойРаботы.ПолучитьТекст(), ДанныеЗаполнения.ЗакрытьЗадание, СменаСтатуса);
	
	ЗаданиеОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Если СменаСтатуса Тогда
		Если ДанныеЗаполнения.ЗакрытьЗадание Или ЗаданиеОбъект.УдалосьРешитьПриПодключении Тогда 
			ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(ЗаданиеОбъект.Ссылка,Перечисления.ЦБР_Состояние.Выполнен);
		Иначе    
			ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(ЗаданиеОбъект.Ссылка,Перечисления.ЦБР_Состояние.ВРаботе);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЧасыЗП(ЗаданиеОбъект, ПериодЗП = Неопределено, ЧасыЗП) Экспорт
	
	СтруктураПоискаЧасыЗП = Новый Структура;
	СтруктураПоискаЧасыЗП.Вставить("Исполнитель", ЗаданиеОбъект.Исполнитель);
	СтруктураПоискаЧасыЗП.Вставить("ВидЧаса", ЗаданиеОбъект.ВидЧаса); 
	СтруктураПоискаЧасыЗП.Вставить("Реализация", Документы.РеализацияТоваровУслуг.ПустаяСсылка());
	СтруктураПоискаЧасыЗП.Вставить("ЗаСчетКомпании", Ложь);    
	СтруктураПоискаЧасыЗП.Вставить("Зачтено", Ложь);    
	
	Если ПериодЗП <> Неопределено Тогда 
		СтруктураПоискаЧасыЗП.Вставить("ПериодЗП", ПериодЗП);  
	Иначе
		ПериодЗП = НачалоМесяца(ТекущаяДата());
	КонецЕсли;  
	
	СтрокиЧасыЗП = ЗаданиеОбъект.ЧасыЗП.НайтиСтроки(СтруктураПоискаЧасыЗП);
	Если СтрокиЧасыЗП.Количество() > 0 Тогда
		СтрокиЧасыЗП[СтрокиЧасыЗП.Количество()-1].ЭффективныеЧасы = СтрокиЧасыЗП[СтрокиЧасыЗП.Количество()-1].ЭффективныеЧасы + ЧасыЗП;  
	Иначе
		НоваяСтрока                   = ЗаданиеОбъект.ЧасыЗП.Добавить();
		НоваяСтрока.Исполнитель       = ЗаданиеОбъект.Исполнитель;
		НоваяСтрока.ВидЧаса           = ЗаданиеОбъект.ВидЧаса; 
		НоваяСтрока.ЭффективныеЧасы   = ЧасыЗП;
		НоваяСтрока.ПериодЗП          = ПериодЗП; 
	КонецЕсли;
		
КонецПроцедуры 

Процедура РасчетДлительностиЗадания(ЗаданиеОбъект, ПричинаПереноса, ЗакрытьЗадание = Ложь, СменаСтатуса) Экспорт
	
	ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(ЗаданиеОбъект.Ссылка);
	
	Если ПоследнееДействие <> Неопределено Тогда
		
		МенеджерЗаписи = РегистрыСведений.ЦБР_ДействияПоЗаданиям.СоздатьМенеджерЗаписи();	
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПоследнееДействие);
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.ДатаОкончаниеДействия = ТекущаяДатаСеанса();  
		МенеджерЗаписи.Комментарий           = ПричинаПереноса;
		Если ЗакрытьЗадание Или ЗаданиеОбъект.УдалосьРешитьПриПодключении Тогда
			МенеджерЗаписи.Статус = Перечисления.ЦБР_Состояние.Выполнен;  
		Иначе 
			МенеджерЗаписи.Статус = Перечисления.ЦБР_Состояние.Приостановлен;
		КонецЕсли;
		МенеджерЗаписи.Записать(Истина);   
		
		СтруктураДат = Новый Структура;
		СтруктураДат.Вставить("ДатаНачала", НачалоДня(МенеджерЗаписи.ДатаНачалаДействия));
		СтруктураДат.Вставить("ДатаОкончания", НачалоДня(МенеджерЗаписи.ДатаОкончаниеДействия));
		СтруктураДат.Вставить("ВремяНачала", ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьВремя(МенеджерЗаписи.ДатаНачалаДействия));
		СтруктураДат.Вставить("ВремяОкончания", ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьВремя(МенеджерЗаписи.ДатаОкончаниеДействия));
		
		ЗаданиеОбъект.ДлительностьФакт = ЦБР_ЗадачиЗаданияВызовСервера.РасчитатьДлительностьФакт(СтруктураДат, ЗаданиеОбъект.ДлительностьФакт);
		
		СменаСтатуса = Истина;
		
	КонецЕсли;    
	
КонецПроцедуры

Функция ВернутьРуководителя(Пользователь) Экспорт
	
	РуководительПользователя = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтруктураПредприятия.ТекущийРуководитель КАК ФизическоеЛицо,
		|	СтруктураПредприятия.Ссылка КАК Ссылка,
		|	Пользователи.Ссылка КАК ТекущийРуководитель
		|ПОМЕСТИТЬ вт_Руководители
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|		ПО Пользователи.ФизическоеЛицо = СтруктураПредприятия.ТекущийРуководитель
		|ГДЕ
		|	НЕ СтруктураПредприятия.ПометкаУдаления
		|	И СтруктураПредприятия.ТекущийРуководитель <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Руководители.ТекущийРуководитель КАК ТекущийРуководитель
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Руководители КАК вт_Руководители
		|		ПО (Пользователи.Подразделение = вт_Руководители.Ссылка)
		|		И (Пользователи.Ссылка = &Пользователь)";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РуководительПользователя = ВыборкаДетальныеЗаписи.ТекущийРуководитель;
	КонецЦикла;
	
	Возврат РуководительПользователя;
	
КонецФункции

Функция ЕстьПравоСписанияЧасовПоЗаданию(Пользователь, Задача) Экспорт
	
	ТекПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Если ТекПользователь = Пользователь Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Если ЯКуратор(Задача) Тогда
		Возврат Истина;
	КонецЕсли;	
	
	РуководительПользователя = ВернутьРуководителя(Пользователь); 
	
	Если ТекПользователь = РуководительПользователя Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьДанныеПоКонфигурациям(Объект) Экспорт

	Объект.Конфигурации.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	_центрКонфигурацииКлиентов.Клиент КАК Клиент,
	|	_центрКонфигурацииКлиентов.Конфигурация КАК Конфигурация,
	|	_центрКонфигурацииКлиентов.РегистрационныйНомер КАК РегистрационныйНомер,
	|	_центрКонфигурацииКлиентов.Релиз КАК Релиз,
	|	_центрКонфигурацииКлиентов.Версия КАК Версия,
	|	_центрКонфигурацииКлиентов.СостояниеКонфигурации КАК СостояниеКонфигурации,
	|	_центрКонфигурацииКлиентов.ЕстьРасширения КАК ЕстьРасширения,
	|	_центрКонфигурацииКлиентов.КонфигурацияКлиентаОписание КАК КонфигурацияКлиентаОписание,
	|	_центрКонфигурацииКлиентов.Проблемная КАК Проблемная
	|ИЗ
	|	РегистрСведений._центрКонфигурацииКлиентов КАК _центрКонфигурацииКлиентов
	|ГДЕ
	|	_центрКонфигурацииКлиентов.Клиент = &Клиент";

	Запрос.УстановитьПараметр("Клиент", Объект.Заказчик);

	Объект.Конфигурации.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры  

Процедура ЗаполнитьДанныеПоКоманде(Объект, Исполнитель) Экспорт

	Объект.Команда.Очистить();
			
	НоваяСтрока             = Объект.Команда.Добавить();
	НоваяСтрока.Исполнитель = Объект.Постановщик;
	НоваяСтрока.Роль        = Перечисления.ЦБР_РольВЗадаче.Постановщик;
	
	НоваяСтрока             = Объект.Команда.Добавить();
	НоваяСтрока.Исполнитель = Исполнитель;
	НоваяСтрока.Роль        = Перечисления.ЦБР_РольВЗадаче.ИсполнительРазработчик;
	
	НоваяСтрока             = Объект.Команда.Добавить();
	НоваяСтрока.Исполнитель = Объект.РуководительЗадачи;
	НоваяСтрока.Роль        = Перечисления.ЦБР_РольВЗадаче.ВладелецЗадачи;
	
	НоваяСтрока             = Объект.Команда.Добавить();
	НоваяСтрока.Исполнитель = Объект.РуководительЗадачи;
	НоваяСтрока.Роль        = Перечисления.ЦБР_РольВЗадаче.КураторИсполнителя;
	
	РуководительПользователя = ЦБР_ЗадачиЗаданияВызовСервера.ВернутьРуководителя(Объект.Постановщик);
	
	Если РуководительПользователя <> Неопределено Тогда
		НоваяСтрока = Объект.Команда.Добавить();
		НоваяСтрока.Исполнитель = РуководительПользователя;
		НоваяСтрока.Роль = Перечисления.ЦБР_РольВЗадаче.КураторПостановщика;
	КонецЕсли;

КонецПроцедуры

Функция ВернутьКонтактноеЛицо(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактныеЛицаПартнеров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Владелец = &Владелец
	|	И НЕ КонтактныеЛицаПартнеров.ПометкаУдаления
	|	И КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КонтактныеЛицаПартнеров.CRM_Ключевой УБЫВ";
	
	Запрос.УстановитьПараметр("Владелец", Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
	
КонецФункции	

Процедура ЗавершитьЗадание(СсылкаНаЗадание) Экспорт 
	
	ТекущееСостояниеЗадания = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(СсылкаНаЗадание);
	
	Если ТекущееСостояниеЗадания = Перечисления.ЦБР_Состояние.ВРаботе Или ТекущееСостояниеЗадания = Перечисления.ЦБР_Состояние.Согласован Тогда 
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(СсылкаНаЗадание, Перечисления.ЦБР_Состояние.Выполнен);	
	КонецЕсли;   
	
КонецПроцедуры

Функция ПроверитьВозможностьЗакрытия(СсылкаНаЗадание) Экспорт
	
	БезОшибок = Истина;
	
	Если СсылкаНаЗадание.ТипОценкиЗадачи <> Перечисления.ЦБР_ТипОценкиЗадачи.Предсогласованные
		И СсылкаНаЗадание.ТрудозатратыПлан - СсылкаНаЗадание.ТрудозатратыФакт <> 0 И Не СсылкаНаЗадание.ОплатаПоФакту Тогда
		ОбщегоНазначения.СообщитьПользователю("Чтобы закрыть задание, необходимо выработать все часы. Остаток часов: " + Строка(СсылкаНаЗадание.ТрудозатратыПлан - СсылкаНаЗадание.ТрудозатратыФакт)+". Можно пересогласовать.");
		БезОшибок = Ложь;
	КонецЕсли;
	
	ЕстьСтартованныеДействия = ЦБР_СтатусыОбъектовПолныеПрава.ЕстьСтартованныеДействия(СсылкаНаЗадание);
	Если ЕстьСтартованныеДействия Тогда
		ОбщегоНазначения.СообщитьПользователю("Чтобы закрыть задание, необходимо закрыть все ОС");
		БезОшибок = Ложь;
	КонецЕсли; 
	
	ТекущееСостояниеЗадания = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(СсылкаНаЗадание);
	
	Если ТекущееСостояниеЗадания = Перечисления.ЦБР_Состояние.НаСогласовании Тогда 
		ОбщегоНазначения.СообщитьПользователю("Чтобы закрыть задание, снимите его с согласования");
		БезОшибок = Ложь;
	КонецЕсли;   
	
	Возврат БезОшибок; 
	
КонецФункции

Процедура ПроверкаЗаполнениеПодчиненныхЭтапов(ТекущееЗадание) Экспорт
	  	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦБР_Задание.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЦБР_Задание КАК ЦБР_Задание
	|ГДЕ
	|	ЦБР_Задание.Исполнитель = &Исполнитель
	|	И ЦБР_Задание.ПредыдущееЗадание = &ТекущееЗадание";
	
	Запрос.УстановитьПараметр("Исполнитель", Справочники.Пользователи.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТекущееЗадание", ТекущееЗадание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		
		ИсполнительПодчиненныхЭтапов = Неопределено;	
		
		ПолучитьИсполнителяПодчиненныхЭтапов(ТекущееЗадание, ИсполнительПодчиненныхЭтапов);
				
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			 ДокОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			 ДокОбъект.Исполнитель = ИсполнительПодчиненныхЭтапов;
			 ДокОбъект.Записать();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры  

Процедура ПолучитьИсполнителяПодчиненныхЭтапов(ТекущееЗадание, ИсполнительПодчиненныхЭтапов) Экспорт
	
	РеквизитыПредыдущегоЭтапа    = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущееЗадание, "ПредыдущееЗадание, Исполнитель");
	ИсполнительПодчиненныхЭтапов = РеквизитыПредыдущегоЭтапа.Исполнитель;
	
	Если ЗначениеЗаполнено(РеквизитыПредыдущегоЭтапа.ПредыдущееЗадание) Тогда
		ПолучитьИсполнителяПодчиненныхЭтапов(РеквизитыПредыдущегоЭтапа.ПредыдущееЗадание, ИсполнительПодчиненныхЭтапов);
	КонецЕсли;	
	
КонецПроцедуры	

Функция ЯМенеджер(СсылкаНаЗадачу) Экспорт 
	
	Роли = Новый Массив;
	Роли.Добавить(Перечисления.ЦБР_РольВЗадаче.Постановщик);
	Роли.Добавить(Перечисления.ЦБР_РольВЗадаче.КураторПостановщика);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|ГДЕ
	|	ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
	|	И ЦБР_ЗадачаКоманда.Роль В(&Роли)
	|	И ЦБР_ЗадачаКоманда.Исполнитель = &Исполнитель";
	
	Запрос.УстановитьПараметр("Исполнитель", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Роли", Роли);
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаЗадачу);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции 

Функция ЯКуратор(СсылкаНаЗадачу) Экспорт 
	
	Роли = Новый Массив;
	Роли.Добавить(Перечисления.ЦБР_РольВЗадаче.КураторИсполнителя);
	Роли.Добавить(Перечисления.ЦБР_РольВЗадаче.ВладелецЗадачи);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|ГДЕ
	|	ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
	|	И ЦБР_ЗадачаКоманда.Роль В(&Роли)
	|	И ЦБР_ЗадачаКоманда.Исполнитель = &Исполнитель";
	
	Запрос.УстановитьПараметр("Исполнитель", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Роли", Роли);
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаЗадачу);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ДобавитьПользователяВКомандуЗадачи(СсылкаНаЗадачу, Исполнитель, МассивРолей) Экспорт

	НеобходимаЗапись = Ложь;
	
	ЗадачаОбъект = СсылкаНаЗадачу.ПолучитьОбъект(); 
	
	Для Каждого Роль Из МассивРолей Цикл 
		
		СтрокиКоманды = ЗадачаОбъект.Команда.НайтиСтроки(Новый Структура("Исполнитель, Роль", Исполнитель, Роль));
		
		Если СтрокиКоманды.Количество() = 0 Тогда
			
			НоваяСтрока             = ЗадачаОбъект.Команда.Добавить();
			НоваяСтрока.Исполнитель = Исполнитель;
			НоваяСтрока.Роль        = Роль;   
			
			НеобходимаЗапись        = Истина;
			
		КонецЕсли; 
	КонецЦикла;
	
	Если НеобходимаЗапись Тогда
		ЗадачаОбъект.Записать();
	КонецЕсли; 
		
	Получатели = Новый Массив;
	Получатели.Добавить(Исполнитель);
	ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(СсылкаНаЗадачу, "НазначениеИсполнителя", Получатели);
	
КонецПроцедуры 

Процедура УстановитьАналитикаВПодчиненныхЗаданиях(СсылкаНаЗадание, Исполнитель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦБР_Задание.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЦБР_Задание КАК ЦБР_Задание
	|ГДЕ
	|	ЦБР_Задание.ПредыдущееЗадание = &ПредыдущееЗадание";
	
	Запрос.УстановитьПараметр("ПредыдущееЗадание", СсылкаНаЗадание);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаданиеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЗаданиеОбъект.Аналитик = Исполнитель;
		ЗаданиеОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Получатели = Новый Массив;
		Получатели.Добавить(Исполнитель);
		ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Выборка.Ссылка, "НазначениеКуратора", Получатели);
		
	КонецЦикла;
		
КонецПроцедуры 

#Область ОС_Календаря

Процедура УстановитьСтарт(Запись_ОС) Экспорт
	
	ЗаданиеСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись_ОС, "ЗаданиеСсылка");
	
	Если ЗначениеЗаполнено(ЗаданиеСсылка) Тогда 		
		
		Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
		Структура.Задание            = ЗаданиеСсылка;
		Структура.ДатаНачалаДействия = ТекущаяДата();
		Структура.Статус             = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.ВРаботе");
		
		ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Запись_ОС);
		Если ПоследнееДействие = Неопределено Или ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
			Структура.ЗаписьКалендаря = Запись_ОС;
			ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
		Иначе
			ОбщегоНазначения.СообщитьПользователю("Есть незавершенное действие по этой задаче!");
			Возврат;
		КонецЕсли;
		
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(ЗаданиеСсылка, ПредопределенноеЗначение(
		"Перечисление.ЦБР_Состояние.ВРаботе"));
		
	КонецЕсли;

	РаботаСРабочимКалендаремСервер.УстановитьПриступилЗаписьКалендаря(Запись_ОС);	
	
КонецПроцедуры 

Процедура УстановитьОтработана(ДанныеЗаполнения) Экспорт   
	
	ЗаписьКалендаря                                    = ДанныеЗаполнения.ЗаписьОС.ПолучитьОбъект();
	ЗаписьКалендаря.УдалосьРешитьПриПодключенииПометка = ДанныеЗаполнения.РешеноПриПервомПодключении;
	ЗаписьКалендаря.Статус                             = Перечисления.ЦБР_СтатусЗаписиКалендаря.Завершен;
	ЗаписьКалендаря.Цвет                               = Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет;  
	Если Не ДанныеЗаполнения.ПоПлану Тогда
		ЗаписьКалендаря.ДатаОкончания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ДанныеЗаполнения.ОписаниеВыполненнойРаботы <> Неопределено Тогда 
		ЗаписьКалендаря.Результат = Новый ХранилищеЗначения(ДанныеЗаполнения.ОписаниеВыполненнойРаботы);
	КонецЕсли;
	
	ЗаписьКалендаря.Записать(); 
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
	ЗаписьКалендаря.ДатаНачала, ЗаписьКалендаря.ДатаОкончания, ЗаписьКалендаря.ВесьДень);

КонецПроцедуры
 
Процедура УстановитьОтменена(Запись_ОС, ПричинаОтмены) Экспорт
 
	ДокОбъект = Запись_ОС.ПолучитьОбъект(); 
	
	Если Не ЗначениеЗаполнено(ДокОбъект.ДатаНачала) Тогда
		ДокОбъект.ДатаНачала = ТекущаяДата();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокОбъект.ДатаОкончания) Тогда
		ДокОбъект.ДатаОкончания = ТекущаяДата();
	КонецЕсли;
	
	ДокОбъект.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Отмена");
	ДокОбъект.Цвет   = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий");
	ДокОбъект.Записать(); 
	
	Если ЗначениеЗаполнено(ДокОбъект.ЗаданиеСсылка) И ЗначениеЗаполнено(ПричинаОтмены) Тогда
		ТекстУведомления = "Задание: " + ДокОбъект.ЗаданиеСсылка + " Действие: " + ДокОбъект.Ссылка + " Причина отмены:" + ПричинаОтмены + "";
		ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(ДокОбъект.ЗадачаСсылка, "ПричинаИзмененияОС",ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ДокОбъект.ЗадачаСсылка, "Постановщик"), ТекстУведомления);
	КонецЕсли;
	
КонецПроцедуры 

Функция ОС_ЕстьСтатус(ЗаписьВКалендаре, Статус) Экспорт
	
	СтатусЗаписиВКалендаре = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьВКалендаре, "Статус"); 
	
	Возврат СтатусЗаписиВКалендаре = Статус;
	
КонецФункции

#КонецОбласти 

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ДобавитьВремя(Дата, Время)	
	Возврат НачалоДня(Дата) + Час(Время) * 3600 + Минута(Время) * 60 + Секунда(Время);	
КонецФункции

Функция ПолучитьСотрудниковКуратора(Руководитель)
	Подчиненные = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ГруппыСотрудниковСостав.Пользователь КАК Пользователь
	|ИЗ
	|	Справочник.ЦБР_ГруппыСотрудников.Состав КАК ЦБР_ГруппыСотрудниковСостав
	|ГДЕ
	|	ЦБР_ГруппыСотрудниковСостав.Ссылка.Руководитель = &Руководитель
	|	И ЦБР_ГруппыСотрудниковСостав.Недействителен = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Руководитель", Руководитель);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Подчиненные.Добавить(Выборка.Пользователь);
	КонецЦикла;
	
	Возврат Подчиненные;
КонецФункции

Функция ВсеЗаданияВыполнены(Задача)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_СтруктураЗадач.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_СтруктураЗадач
	|ГДЕ
	|	ЦБР_СтруктураЗадач.Владелец = &Ссылка
	|	И ЦБР_СтруктураЗадач.Состояние <> ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.Выполнен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЦБР_СтруктураЗадач.Ссылка
	|ИЗ
	|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_СтруктураЗадач
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЦБР_Задание.ЭффективныеЧасы КАК ЦБР_ЗаданиеРеализации
	|		ПО ЦБР_СтруктураЗадач.СсылкаНаЗадание = ЦБР_ЗаданиеРеализации.Ссылка
	|ГДЕ
	|	ЦБР_СтруктураЗадач.СсылкаНаЗадачу = &Ссылка
	|	И ЦБР_СтруктураЗадач.Состояние = ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.Выполнен)
	|	И ЦБР_ЗаданиеРеализации.Реализация = ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Задача);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Истина;	
	Иначе
		Возврат Ложь;	
	КонецЕсли;
КонецФункции

Функция ВернутьТекстФорматированногоДокумента(ХЗ)
	
	ТекстФД = "";
	
	ФД = ХЗ.Получить(); 
	
	Для Каждого Стр Из ФД.Элементы Цикл
		Для Каждого Стр2 Из Стр.Элементы Цикл
			Если ТипЗнч(Стр2) = Тип("ТекстФорматированногоДокумента") Тогда
				Если ТекстФД = "" Тогда 
					ТекстФД = ТекстФД +Стр2.Текст;
				Иначе
					ТекстФД = ТекстФД + Символы.ПС + Стр2.Текст;
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
	Возврат ТекстФД; 
	        
КонецФункции	

#КонецОбласти



//Процедура ПоменятьСтатусЗадачи(Задача) Экспорт
//		
//	Для каждого Исполнители Из Задача.Команда Цикл
//		СтатусЗадачи = ЦБР_ЗадачиЗаданияВызовСервера.ВычислитьСтатусЗадачи(Задача.Ссылка, Исполнители.Исполнитель, Исполнители.Роль);	
//		ПоследнийСтатус = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьАктуальныйСтатусЗадачи(Задача.Ссылка, Исполнители.Исполнитель);
//		
//		Если СтатусЗадачи <> ПоследнийСтатус  Тогда
//			МенеджерЗаписи = РегистрыСведений.ЦБР_СтатусыЗадач.СоздатьМенеджерЗаписи();
//			МенеджерЗаписи.Период = ТекущаяДатаСеанса();
//			МенеджерЗаписи.Задача = Задача.Ссылка;
//			МенеджерЗаписи.СостояниеЗадачи = СтатусЗадачи;
//			МенеджерЗаписи.Пользователь = Исполнители.Исполнитель; 
//			МенеджерЗаписи.Записать();
//		КонецЕсли;
//	КонецЦикла;
//		
//КонецПроцедуры
