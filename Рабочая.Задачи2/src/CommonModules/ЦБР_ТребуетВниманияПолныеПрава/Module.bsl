#Область СлужебныеПроцедурыИФункции

Процедура ЦБР_ЗаписьКалендаряТребуетВнимание(Источник, Отказ) Экспорт 
	ПроверкаЗаписейКалендаря();	
КонецПроцедуры
	
Процедура ЦБР_ЗаписьОбъектаТребуетВнимание(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.ЦБР_Задача") Тогда
		ЗадачаСсылка 	= Источник.Ссылка;  
		ЗаданиеСсылка   = Документы.ЦБР_Задание.ПустаяСсылка();  
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ЦБР_Задача") Тогда
		ЗадачаСсылка 	= Источник;  
		ЗаданиеСсылка   = Документы.ЦБР_Задание.ПустаяСсылка();  
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ЦБР_СтруктураЗадач") Тогда
		ЗадачаСсылка 	= Источник.Владелец;  
		ЗаданиеСсылка   = Источник.СсылкаНаЗадание;	
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ЦБР_ЗаписиРабочегоКалендаря") Тогда
		ЗадачаСсылка 	= Источник.ЗадачаСсылка;  
		ЗаданиеСсылка   = Источник.ЗаданиеСсылка;	
	Иначе
		ЗадачаСсылка 	= Источник.СсылкаНаЗадачу;  
		ЗаданиеССылка   = Источник.Ссылка;
	КонецЕсли;     
	
	СостояниеЗадачи = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадачиПользователя(ЗадачаСсылка, Пользователи.АвторизованныйПользователь());

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦБР_ТребуютВнимания.Задача КАК Задача,
	|	ЦБР_ТребуютВнимания.Задание КАК Задание,
	|	ЦБР_ТребуютВнимания.Ответственный КАК Ответственный,
	|	ЦБР_ТребуютВнимания.Основание КАК Основание,
	|	ЦБР_ТребуютВнимания.ДатаДобавления КАК ДатаДобавления,
	|	ЦБР_ТребуютВнимания.НаименованиеДокументаЭтапа КАК НаименованиеДокументаЭтапа,
	|	ЦБР_ТребуютВнимания.ТрудозатратыПлан КАК ТрудозатратыПлан
	|ИЗ
	|	РегистрСведений.ЦБР_ТребуютВнимания КАК ЦБР_ТребуютВнимания
	|ГДЕ
	|	ЦБР_ТребуютВнимания.Задача = &СсылкаНаЗадачу";
	Запрос.УстановитьПараметр("СсылкаНаЗадачу", ЗадачаСсылка);	
	
	Если Запрос.Выполнить().Пустой() Тогда
		СписокТребуетВнимания_Старый = Новый ТаблицаЗначений;    
		СписокТребуетВнимания_Старый.Колонки.Добавить("Задача", 					Новый ОписаниеТипов("СправочникСсылка.ЦБР_Задача"));
		СписокТребуетВнимания_Старый.Колонки.Добавить("Задание", 					Новый ОписаниеТипов("ДокументСсылка.ЦБР_Задание"));
		СписокТребуетВнимания_Старый.Колонки.Добавить("Ответственный", 				Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
		СписокТребуетВнимания_Старый.Колонки.Добавить("Основание", 					Новый ОписаниеТипов("ПеречислениеСсылка.ЦБР_ПричиныВнимания"));
		СписокТребуетВнимания_Старый.Колонки.Добавить("ДатаДобавления", 			Новый ОписаниеТипов("Дата"));
		СписокТребуетВнимания_Старый.Колонки.Добавить("НаименованиеДокументаЭтапа", Новый ОписаниеТипов("Строка")); 
		СписокТребуетВнимания_Старый.Колонки.Добавить("ТрудозатратыПлан", 			Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Любой)));
	Иначе
		СписокТребуетВнимания_Старый = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	СписокТребуетВнимания = СписокТребуетВнимания_Старый.Скопировать();
	СписокТребуетВнимания.Очистить();

#Область ОчисткаРС
	НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Задача.Установить(ЗадачаСсылка);	 

	МассивОснований = Новый Массив;
	МассивОснований.Добавить(Перечисления.ЦБР_ПричиныВнимания.ЭтоНоваяЗадача); 
	МассивОснований.Добавить(Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий);

	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЦБР_Задание") Тогда    
		НаборЗаписей.Отбор.Задание.Установить(Источник.Ссылка);			
	КонецЕсли;  
	
	Для Каждого Строка ИЗ МассивОснований Цикл 
		НаборЗаписей.Отбор.Основание.Установить(Строка);	
		НаборЗаписей.Записать();
	КонецЦикла;
#КонецОбласти

	Если СостояниеЗадачи = Перечисления.ЦБР_Состояние.Черновик  И 
		НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗадачаСсылка, "ВзялиВРаботу") Тогда
		Возврат;
	КонецЕсли;	
	
	
	// 1. Добавим Исполнителей, у которых есть этапы на этапах черновик и оценка не согласована
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦБР_Задание.Исполнитель КАК Ответственный
	|ИЗ
	|	РегистрСведений.ЦБР_СостоянияЗаданий КАК ЦБР_СостоянияЗаданий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЦБР_Задание КАК ЦБР_Задание
	|		ПО ЦБР_СостоянияЗаданий.Задание = ЦБР_Задание.Ссылка
	|			И ЦБР_СостоянияЗаданий.ПользовательАРМ = ЦБР_Задание.Исполнитель
	|ГДЕ
	|	ЦБР_СостоянияЗаданий.Состояние В(&МассивСостоянийЭтаповЗадачи)
	|	И ЦБР_Задание.СсылкаНаЗадачу = &СсылкаНаЗадачу
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦБР_Задание.Исполнитель";
	
	Запрос.УстановитьПараметр("СсылкаНаЗадачу", ЗадачаСсылка);
	
	МассивСостоянийЭтаповЗадачи = Новый Массив;
	МассивСостоянийЭтаповЗадачи.Добавить(Перечисления.ЦБР_Состояние.Черновик);
	МассивСостоянийЭтаповЗадачи.Добавить(Перечисления.ЦБР_Состояние.НеСогласован);
	Запрос.УстановитьПараметр("МассивСостоянийЭтаповЗадачи", МассивСостоянийЭтаповЗадачи);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаОтветственных = РезультатЗапроса.Выгрузить(); 
	Если ТаблицаОтветственных.Количество() > 0 Тогда
		СписокТребуетВнимания = ДобавитьТребуетВниманиеВОбщийСписок(ЗадачаСсылка, ЗаданиеССылка, ТаблицаОтветственных, 
						Перечисления.ЦБР_ПричиныВнимания.ЭтоНоваяЗадача, ТекущаяДатаСеанса(), СписокТребуетВнимания);
	КонецЕсли; 
	
	// 2. Добавим Исполнителей, которые в Задаче, но у них нет этапов	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Исполнитель КАК Ответственный,
	|	ЦБР_Задание.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЦБР_Задание КАК ЦБР_Задание
	|		ПО ЦБР_ЗадачаКоманда.Исполнитель = ЦБР_Задание.Исполнитель
	|ГДЕ
	|	ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
	|	И ЦБР_ЗадачаКоманда.Роль В(&МассивРолей)
	|	И ЦБР_Задание.Ссылка ЕСТЬ NULL";
	
	МассивРолей = Новый Массив; 
	МассивРолей.Добавить(Перечисления.ЦБР_РольВЗадаче.Исполнитель);
	МассивРолей.Добавить(Перечисления.ЦБР_РольВЗадаче.ИсполнительРазработчик);
	МассивРолей.Добавить(Перечисления.ЦБР_РольВЗадаче.ИсполнительАналитик);
	
	Запрос.УстановитьПараметр("МассивРолей", МассивРолей);
	Запрос.УстановитьПараметр("Ссылка", ЗадачаСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОтветственных = РезультатЗапроса.Выгрузить();
	Если ТаблицаОтветственных.Количество() > 0 Тогда
		СписокТребуетВнимания = ДобавитьТребуетВниманиеВОбщийСписок(ЗадачаСсылка, ЗаданиеССылка, ТаблицаОтветственных, 
						Перечисления.ЦБР_ПричиныВнимания.ЭтоНоваяЗадача, ТекущаяДатаСеанса(), СписокТребуетВнимания);
	КонецЕсли; 
	
	//3. Добавим Руководителей Исполнителей, если на 1,2 этапах никого нет
	Если СписокТребуетВнимания.Количество() = 0 Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_СтруктураЗадач.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_СтруктураЗадач
		|ГДЕ
		|	ЦБР_СтруктураЗадач.Владелец = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец", ЗадачаСсылка);
		
		Если Запрос.Выполнить().Пустой() Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ЦБР_ЗадачаКоманда.Исполнитель КАК Ответственный
			|ИЗ
			|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
			|ГДЕ
			|	ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
			|	И ЦБР_ЗадачаКоманда.Роль В (&МассивРолей)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЦБР_ЗадачаКоманда.Исполнитель";
			
			МассивРолей = Новый Массив; 
			МассивРолей.Добавить(Перечисления.ЦБР_РольВЗадаче.КураторИсполнителя);
			МассивРолей.Добавить(Перечисления.ЦБР_РольВЗадаче.ВладелецЗадачи);
			
			Запрос.УстановитьПараметр("МассивРолей", МассивРолей);
			Запрос.УстановитьПараметр("Ссылка", ЗадачаСсылка);
			
			ТаблицаОтветственных = Запрос.Выполнить().Выгрузить(); 
			СписокТребуетВнимания = ДобавитьТребуетВниманиеВОбщийСписок(ЗадачаСсылка, ЗаданиеССылка, ТаблицаОтветственных, 
			Перечисления.ЦБР_ПричиныВнимания.ЭтоНоваяЗадача, ТекущаяДатаСеанса(), СписокТребуетВнимания);
			
		КонецЕсли; 	
	КонецЕсли; 	
	
	// 4. Пройдемся по Заданиям без ОС или просроченным
	МассивСостояний = Новый Массив;
	МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.ВРаботе);
	//МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.Запланировано);
	МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.Согласован);
	МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.Новый);
	//МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.ОценкаНеСогласована);     
	//МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.ОценкаСогласована);
	//МассивСостояний.Добавить(Перечисления.ЦБР_Состояние.Приостановлен);

	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка КАК СобытиеЗаданиеСсылка
	               |ПОМЕСТИТЬ ВТ_ЗаданияСОС
	               |ИЗ
	               |	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	               |ГДЕ
	               |	ЦБР_ЗаписиРабочегоКалендаря.Статус В(&Статус)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЦБР_Задание.Исполнитель КАК Ответственный
	               |ИЗ
	               |	Документ.ЦБР_Задание КАК ЦБР_Задание
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦБР_СостоянияЗаданий КАК ЦБР_СостоянияЗаданий
	               |		ПО ЦБР_Задание.Ссылка = ЦБР_СостоянияЗаданий.Задание
	               |ГДЕ
	               |	ЦБР_Задание.СсылкаНаЗадачу = &Задача
	               |	И НЕ ЦБР_Задание.Ссылка В
	               |				(ВЫБРАТЬ
	               |					ВТ_ЗаданияСОС.СобытиеЗаданиеСсылка
	               |				ИЗ
	               |					ВТ_ЗаданияСОС КАК ВТ_ЗаданияСОС)
	               |	И ЦБР_СостоянияЗаданий.Состояние В(&МассивСостояний)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЦБР_Задание.Исполнитель";
	
	
	Запрос.УстановитьПараметр("Задача", ЗадачаСсылка);
	Запрос.УстановитьПараметр("МассивСостояний", МассивСостояний);
	Запрос.УстановитьПараметр("Статус", МассивСтатусов);
	
	ТаблицаОтветственных = Запрос.Выполнить().Выгрузить(); 
	СписокТребуетВнимания = ДобавитьТребуетВниманиеВОбщийСписок(ЗадачаСсылка, ЗаданиеССылка, ТаблицаОтветственных, 
	Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий, ТекущаяДатаСеанса(), СписокТребуетВнимания);
					
	//Почистим старые записи, по которым нет необходимости напоминать
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокТребуетВнимания.Ответственный КАК Ответственный,
	|	СписокТребуетВнимания.Основание КАК Основание
	|ПОМЕСТИТЬ вт_Список
	|ИЗ
	|	&СписокТребуетВнимания КАК СписокТребуетВнимания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокТребуетВнимания_Старый.Ответственный КАК Ответственный,
	|	СписокТребуетВнимания_Старый.Основание КАК Основание
	|ПОМЕСТИТЬ вт_Старый
	|ИЗ
	|	&СписокТребуетВнимания_Старый КАК СписокТребуетВнимания_Старый
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Старый.Ответственный КАК Ответственный,
	|	вт_Старый.Основание КАК Основание
	|ИЗ
	|	вт_Старый КАК вт_Старый
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Список КАК вт_Список
	|		ПО вт_Старый.Ответственный = вт_Список.Ответственный
	|			И вт_Старый.Основание = вт_Список.Основание
	|ГДЕ
	|	вт_Список.Ответственный ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("СписокТребуетВнимания", 			СписокТребуетВнимания); 
	Запрос.УстановитьПараметр("СписокТребуетВнимания_Старый", 	СписокТребуетВнимания_Старый); 
	СписокТребуетВниманияУдалить = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокТребуетВнимания.Ответственный КАК Ответственный,
	|	СписокТребуетВнимания.Основание КАК Основание,
	|	СписокТребуетВнимания.ДатаДобавления КАК ДатаДобавления
	|ПОМЕСТИТЬ вт_список
	|ИЗ
	|	&СписокТребуетВнимания КАК СписокТребуетВнимания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Старый.Ответственный КАК Ответственный,
	|	Старый.Основание КАК Основание,
	|	Старый.ДатаДобавления КАК ДатаДобавления
	|ПОМЕСТИТЬ вт_старый
	|ИЗ
	|	&СписокТребуетВнимания_Старый КАК Старый
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_список.Ответственный КАК Ответственный,
	|	вт_список.Основание КАК Основание,
	|	вт_список.ДатаДобавления КАК ДатаДобавления
	|ПОМЕСТИТЬ вт_общая
	|ИЗ
	|	вт_список КАК вт_список
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	вт_старый.Ответственный,
	|	вт_старый.Основание,
	|	вт_старый.ДатаДобавления
	|ИЗ
	|	вт_старый КАК вт_старый
	|ГДЕ
	|	НЕ вт_старый.Ответственный В (&СписокДляУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_общая.Ответственный КАК Ответственный,
	|	вт_общая.Основание КАК Основание,
	|	МИНИМУМ(вт_общая.ДатаДобавления) КАК ДатаДобавления
	|ИЗ
	|	вт_общая КАК вт_общая
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_общая.Ответственный,
	|	вт_общая.Основание";
	Запрос.УстановитьПараметр("СписокТребуетВнимания_Старый", 	СписокТребуетВнимания_Старый);
	Запрос.УстановитьПараметр("СписокТребуетВнимания", 			СписокТребуетВнимания);
	Запрос.УстановитьПараметр("СписокДляУдаления", 				СписокТребуетВниманияУдалить);
	
	//Общий список Требует внимание записываем в РС
	ДобавитьЗаписиВРС_ТребуетВнимание(Запрос.Выполнить().Выгрузить(), ЗадачаСсылка, ЗаданиеСсылка); 
	
КонецПроцедуры

Процедура ПроверкаЗаписейКалендаря() Экспорт   
	
	
	#Область ОчисткаРС
	НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
	
	МассивОснований = Новый Массив;
	МассивОснований.Добавить(Перечисления.ЦБР_ПричиныВнимания.ПросроченоОкончаниеОС); 
	МассивОснований.Добавить(Перечисления.ЦБР_ПричиныВнимания.ПросроченСтартОС);
	
	Для Каждого Строка ИЗ МассивОснований Цикл 
		НаборЗаписей.Отбор.Основание.Установить(Строка);	
		НаборЗаписей.Записать();
	КонецЦикла;
	#КонецОбласти     

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Событие,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Ответственный,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗадачаСсылка КАК ЗадачаСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка КАК ЗаданиеСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания КАК ДатаДобавления,
		|	ЦБР_ЗаписиРабочегоКалендаря.Статус КАК Статус
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Статус = &СтатусНовый
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала < &ДатаСобытия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗадачаСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.Статус
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Статус = &СтатусНачал
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания < &ДатаСобытия";
	
	Запрос.УстановитьПараметр("ДатаСобытия", ТекущаяДатаСеанса());
	
	//МассивСтатусов = Новый Массив;
	//МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);
	//МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);
	//Запрос.УстановитьПараметр("Статус", МассивСтатусов);
	Запрос.УстановитьПараметр("СтатусНовый", Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);
	Запрос.УстановитьПараметр("СтатусНачал", Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
			ЗадачаСсылка = ВыборкаДетальныеЗаписи.ЗадачаСсылка; 
			ЗаданиеСсылка = ВыборкаДетальныеЗаписи.ЗаданиеСсылка; 
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей(); 
			НаборЗаписей.Отбор.Задача.Установить(ЗадачаСсылка);
			НаборЗаписей.Отбор.Задание.Установить(ЗаданиеСсылка);
			
			ОбъемЧасов = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСуммуОценокПоЗадаче(ЗадачаСсылка);
			НоваяЗапись = НаборЗаписей.Добавить(); 
			НоваяЗапись.Задача				= ЗадачаСсылка;         
			НоваяЗапись.Задание				= ЗаданиеСсылка; 
			НоваяЗапись.Ответственный 		= ВыборкаДетальныеЗаписи.Ответственный; 
			Если ВыборкаДетальныеЗаписи.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал Тогда
				НоваяЗапись.Основание 			= Перечисления.ЦБР_ПричиныВнимания.ПросроченоОкончаниеОС;
			Иначе
				НоваяЗапись.Основание 			= Перечисления.ЦБР_ПричиныВнимания.ПросроченСтартОС;
			КонецЕсли;
			НоваяЗапись.ДатаДобавления 		= ВыборкаДетальныеЗаписи.ДатаДобавления; 
			НоваяЗапись.ТрудозатратыПлан 	= ОбъемЧасов;
			НаборЗаписей.Записать();   
		КонецЦикла;    
	КонецЕсли; 
	
КонецПроцедуры

Функция ДобавитьТребуетВниманиеВОбщийСписок(ЗадачаСсылка, ЗаданиеСсылка, ТаблицаОтветственных, ПричинаВнимания, ДатаДобавления, СписокТребуетВнимания)
	
	Для Каждого СтрокаТаблицы Из ТаблицаОтветственных Цикл 
		НС = СписокТребуетВнимания.Добавить();
		НС.Ответственный 	= СтрокаТаблицы.Ответственный;
		НС.Задача          	= ЗадачаСсылка;
		НС.Задание          = ЗаданиеСсылка;
		НС.Основание 		= ПричинаВнимания; 
		НС.ДатаДобавления 	= ДатаДобавления; 	
	КонецЦикла; 
	
	Возврат СписокТребуетВнимания;
КонецФункции

Процедура ДобавитьЗаписиВРС_ТребуетВнимание(СписокТребуетВнимания, ЗадачаСсылка, ЗаданиеСсылка)  
	
	//Если СписокТребуетВнимания.Количество() > 0 Тогда
		ОбъемЧасов = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСуммуОценокПоЗадаче(ЗадачаСсылка);
		
		НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.Задача.Установить(ЗадачаСсылка); 
		Для Каждого СтрокаТаблицы Из СписокТребуетВнимания Цикл 
			НоваяЗапись = НаборЗаписей.Добавить(); 
			НоваяЗапись.Задача				= ЗадачаСсылка;
			НоваяЗапись.Задание				= ЗаданиеСсылка;
			НоваяЗапись.Ответственный 		= СтрокаТаблицы.Ответственный; 
			НоваяЗапись.Основание 			= СтрокаТаблицы.Основание; 
			НоваяЗапись.ДатаДобавления 		= СтрокаТаблицы.ДатаДобавления; 
			НоваяЗапись.ТрудозатратыПлан 	= ОбъемЧасов; 
		КонецЦикла; 
		НаборЗаписей.Записать(); 
	//КонецЕсли;

КонецПроцедуры

Функция ПолучитьКоличествоЗапланированныхДействийИсполнителя(Исполнитель, ЗаданиеССылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка,
	|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка = &ЗаданиеСсылка
	|	И ЦБР_ЗаписиРабочегоКалендаря.Статус В (&Статус)
	|	И НЕ ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления";

	Запрос.УстановитьПараметр("Пользователь", Исполнитель);
	Запрос.УстановитьПараметр("ЗаданиеСсылка", ЗаданиеСсылка); 
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);
	Запрос.УстановитьПараметр("Статус", МассивСтатусов);  

	Возврат Запрос.Выполнить().Выбрать().Количество();
	
КонецФункции

#КонецОбласти
