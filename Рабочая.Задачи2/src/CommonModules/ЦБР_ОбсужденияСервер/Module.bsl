#Область ПрограммныйИнтерфейс

// Последний отправитель по получателю.
// 
// Параметры:
//  Задание - ДокуменСсылка.ЦБР_Задание - Задача или задание
//  Получатель - СправочникСсылка.Пользователи - Получатель для которого необходимо найти последнего отправителя
// 
// Возвращаемое значение:
//  Неопределено, СправочникСсылка.Пользователи - Последний отправитель по получателю
Функция ПоследнийОтправительПоПолучателю(Задание, Получатель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦБР_УточненияПоЗаданиямСрезПоследних.Отправитель КАК Отправитель
	               |ИЗ
	               |	РегистрСведений.ЦБР_УточненияПоЗадачамЗаданиям.СрезПоследних(
	               |			,
	               |			Задание = &Задание
	               |				И Получатель = &Получатель) КАК ЦБР_УточненияПоЗаданиямСрезПоследних";
	
	Запрос.УстановитьПараметр("Задание", Задание);
	Запрос.УстановитьПараметр("Получатель", Получатель);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	    Возврат Выборка.Отправитель;
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

Процедура ПроверитьСоздатьОбщееОбсуждение(Ссылка, Статус=Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	МассивОбсуждений = ПолучитьОбсужденияПоСсылке(Ссылка);		
	Если МассивОбсуждений <> Неопределено Тогда
		Если Не МассивОбсуждений.Количество() Тогда
			КлючОбъекта = ПолучитьНавигационнуюСсылку(Ссылка);
			Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
			Обсуждение.Заголовок 			= "Общее обсуждение по "+Ссылка;
			Обсуждение.Ключ 				= "Общее" + Строка(КлючОбъекта);
			Обсуждение.Отображаемое 		= Истина;
			Обсуждение.КонтекстОбсуждения 	= Новый КонтекстОбсужденияСистемыВзаимодействия(КлючОбъекта);
			Обсуждение.Записать();
			//Если это задача со статусом "новая" - надо уведомить куратора
			Если Статус = Перечисления.ЦБР_Состояние.Новый Тогда
				Получатели = Новый Массив;
				Получатели.Добавить(Ссылка.Куратор);
				ТекстОбсуждения = Новый ФорматированнаяСтрока("Новая задача!", ШрифтыСтиля.ШрифтВажнойНадписиБИП, ЦветаСтиля.ЦветТекстаУспех);
				ОтправитьСообщениеВОбщееОбсуждение(Ссылка, ТекстОбсуждения, Получатели);
			КонецЕсли;
		ИначеЕсли Статус = Перечисления.ЦБР_Состояние.Новый Тогда 
			Обсуждение = МассивОбсуждений[0];
			Отбор = Новый ОтборСообщенийСистемыВзаимодействия;
			Отбор.Количество = 1;
			Отбор.Обсуждение = Обсуждение.Идентификатор;
			МассивСообщений = СистемаВзаимодействия.ПолучитьСообщения(Отбор);
			Если Не МассивСообщений.Количество() Тогда
				Получатели = Новый Массив;
				Получатели.Добавить(Ссылка.Куратор);
				ТекстОбсуждения = Новый ФорматированнаяСтрока("Новая задача!", ШрифтыСтиля.ШрифтВажнойНадписиБИП, ЦветаСтиля.ЦветТекстаУспех);
				ОтправитьСообщениеВОбщееОбсуждение(Ссылка, ТекстОбсуждения, Получатели);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры //ПроверитьНаличиеОбщегоОбсуждения()

Процедура ОтправитьСообщениеВОбщееОбсуждение(Ссылка, ТекстСообщения, Получатели, Автор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		МассивОбсуждений = ПолучитьОбсужденияПоСсылке(Ссылка);
		Если МассивОбсуждений.Количество() Тогда
			Если Не Автор = Неопределено Тогда
				АвторИдентификатор = ПолучитьИдентификаторПользователяСистемыВзаимодействий(Автор);
				Если АвторИдентификатор = Неопределено Тогда
					АвторИдентификатор = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
				КонецЕсли;
			Иначе
				АвторИдентификатор = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
			КонецЕсли;
			Обсуждение = МассивОбсуждений[0];
			СообщениеОбсуждения = СистемаВзаимодействия.СоздатьСообщение(Обсуждение.Идентификатор);
			СообщениеОбсуждения.Автор 	= АвторИдентификатор;
			СообщениеОбсуждения.Дата 	= ТекущаяДатаСеанса();
			СообщениеОбсуждения.Текст 	= ТекстСообщения;
			ИдентификаторыПолучателей 	= ПолучитьИдентификаторыПользователейСистемыВзаимодействий(Получатели);
			Для Каждого Получатель Из ИдентификаторыПолучателей Цикл
				СообщениеОбсуждения.Получатели.Добавить(Получатель);
			КонецЦикла;
			СообщениеОбсуждения.Записать();
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры //СформироватьСообщениеДляОбщегоОбсуждения()

Процедура УведомлениеВОбсуждение(Ссылка, Уведомление, Получатели, ДопИнформация = "") Экспорт	 
	
	Соответствие = ЦБР_КэшированиеВызовСервера.СоответствиеУведомленийСобытиям();
	СтруктураУведомления = Соответствие.Получить(Уведомление);
	Если СтруктураУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекстОбсуждения = Новый Массив;
	ТекстОбсуждения.Добавить(Новый ФорматированнаяСтрока(СтруктураУведомления.Заголовок,ШрифтыСтиля.ШрифтОсновногоТекстаОбозначений,СтруктураУведомления.ЦветЗаголовка));
	Если СтруктураУведомления.ЕстьДетализация Тогда
		Если ТипЗнч(ДопИнформация) = Тип("Строка") Тогда
			ТекстОбсуждения.Добавить(Символы.ПС);
			ТекстОбсуждения.Добавить(Новый ФорматированнаяСтрока(СтруктураУведомления.ПередДопИнформацией + ДопИнформация, ШрифтыСтиля.ШрифтМикротекста, ЦветаСтиля.CRM_ЦветДополнительногоТекста));
		Иначе
			ТекстОбсуждения.Добавить(Новый ФорматированнаяСтрока(СтруктураУведомления.ПередДопИнформацией,
				ШрифтыСтиля.ШрифтМикротекста, ЦветаСтиля.CRM_ЦветДополнительногоТекста));
			Для Каждого Элемент Из ДопИнформация Цикл
				ТекстОбсуждения.Добавить(Символы.ПС);
				ТекстОбсуждения.Добавить(Новый ФорматированнаяСтрока(Элемент, ШрифтыСтиля.ШрифтМикротекста,ЦветаСтиля.CRM_ЦветДополнительногоТекста));
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	Сообщение = Новый ФорматированнаяСтрока(ТекстОбсуждения);
	ОтправитьСообщениеВОбщееОбсуждение(Ссылка, Сообщение, Получатели);  
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

Функция ПолучитьИдентификаторПользователяСистемыВзаимодействий(Пользователь)
	
	Если ТипЗнч(Пользователь) <> Тип("СправочникСсылка.Пользователи") Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Запрос = Новый Запрос;	
	Запрос.Текст = ("ВЫБРАТЬ
	                |	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	                |ИЗ
	                |	Справочник.Пользователи КАК Пользователи
	                |ГДЕ
	                |	Пользователи.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Пользователь);	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда		
		Если Не ЗначениеЗаполнено(Выборка.ИдентификаторПользователяИБ) Тогда
			Возврат Неопределено;
		КонецЕсли;
	
		Попытка			
			ПользовательИдентификатор = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(Выборка.ИдентификаторПользователяИБ);			
		Исключение			
			ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Выборка.ИдентификаторПользователяИБ));
			ПользовательСВ.Записать();
			ПользовательИдентификатор = ПользовательСВ.Идентификатор;			
		КонецПопытки;		
	Иначе		
		Возврат Неопределено;		
	КонецЕсли;
	
	Возврат ПользовательИдентификатор;
	
КонецФункции //ПолучитьИдентификаторПользователяСистемыОбсуждений()

Функция ПолучитьИдентификаторыПользователейСистемыВзаимодействий(Пользователи)

	Идентификаторы = Новый Массив;
	Если ТипЗнч(Пользователи) <> Тип("Массив") Тогда
		Возврат Идентификаторы;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = ("ВЫБРАТЬ
					|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
					|ИЗ
					|	Справочник.Пользователи КАК Пользователи
					|ГДЕ
					|	Пользователи.Ссылка В (&Массив)");

	Запрос.УстановитьПараметр("Массив", Пользователи);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.ИдентификаторПользователяИБ) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			Идентификатор = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(Выборка.ИдентификаторПользователяИБ);
		Исключение
			ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(
				ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Выборка.ИдентификаторПользователяИБ));
			ПользовательСВ.Записать();
			Идентификатор = ПользовательСВ.Идентификатор;
		КонецПопытки;
		Идентификаторы.Добавить(Идентификатор);
	КонецЦикла;

	Возврат Идентификаторы;

КонецФункции //ПолучитьИдентификаторПользователяСистемыОбсуждений()

Функция ПолучитьОбсужденияПоСсылке(Ссылка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка		
		КлючОбъекта = ПолучитьНавигационнуюСсылку(Ссылка);
		Отбор = Новый ОтборОбсужденийСистемыВзаимодействия;
		Отбор.КонтекстноеОбсуждение = Истина;	
		Отбор.КонтекстОбсуждения 	= Новый КонтекстОбсужденияСистемыВзаимодействия(КлючОбъекта);
		Отбор.Ключ 					= "Общее" + Строка(КлючОбъекта);				
		ОбсужденияМассив = СистемаВзаимодействия.ПолучитьОбсуждения(Отбор);	
		Если НЕ ОбсужденияМассив.Количество() Тогда
			КлючОбъекта = ПолучитьНавигационнуюСсылку(Ссылка);
			Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
			Обсуждение.Заголовок 			= "Общее обсуждение по "+Ссылка;
			Обсуждение.Ключ 				= "Общее" + Строка(КлючОбъекта);
			Обсуждение.Отображаемое 		= Истина;
			Обсуждение.КонтекстОбсуждения 	= Новый КонтекстОбсужденияСистемыВзаимодействия(КлючОбъекта);
			Обсуждение.Записать();
			ОбсужденияМассив.Добавить(Обсуждение);
		КонецЕсли;		
	Исключение		
		ЗаписьЖурналаРегистрации("Создание служебного обсуждения Системы взаимодействия", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю("Система взаимодействий не зарегистрирована! Обратитесь к администратору!");
		ОбсужденияМассив = Неопределено;		
	КонецПопытки;

	УстановитьПривилегированныйРежим(Ложь);		
	
	Возврат ОбсужденияМассив;
	
КонецФункции //ПолучитьОбсужденияПоЗадаче()

#КонецОбласти