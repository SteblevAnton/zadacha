////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с рабочим календарем.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет количество предстоящих записей календаря.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пльзователи.
// 
// Возвращаемое значение:
//  Число - Количество предстоящих записей календаря.
//
Функция ВсегоПредстоящихЗаписейКалендаря(Пользователь) Экспорт
	
	ТаблицаСобытий = РаботаСРабочимКалендаремСервер.ПолучитьСобытияФизЛицаСотрудника(
		ТекущаяДатаСеанса(),
		ДобавитьМесяц(ТекущаяДатаСеанса(), 1),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ФизЛицо"),
		Ложь);
	
	Возврат ТаблицаСобытий.Количество();
	
КонецФункции

// Формирует структуру данных строки списка виджета.
// 
// Возвращаемое значение:
//  Структура - Данные строки списка виджета.
//   * ИндексКартинки - Число.
//   * Значение - Строка.
//   * Ссылка - Строка.
//   * Комментарий - Строка.
//
Функция ДанныеСтрокиСписка() Экспорт
	
	ДанныеСтрокиСписка = Новый Структура("ИндексКартинки, Значение, Ссылка, Комментарий");
	ДанныеСтрокиСписка.ИндексКартинки = -1;
	ДанныеСтрокиСписка.Значение = "";
	ДанныеСтрокиСписка.Ссылка = "";
	ДанныеСтрокиСписка.Комментарий = "";
	
	Возврат ДанныеСтрокиСписка;
	
КонецФункции

// Формирует представление даты виджета "Список".
//
// Параметры:
//  Дата - Дата - Дата.
// 
// Возвращаемое значение:
//  Строка - Представление даты виджета "Список".
//
Функция ПредставлениеДатыСписка(Дата) Экспорт
	
	ФорматнаяСтрока = "";
	Если Дата = НачалоДня(ТекущаяДатаСеанса()) Или Дата = КонецДня(ТекущаяДатаСеанса())Тогда
		ФорматнаяСтрока = "ДФ=dd.MM";
	ИначеЕсли НачалоДня(Дата) = НачалоДня(ТекущаяДатаСеанса()) Тогда
		ФорматнаяСтрока = "ДФ=HH:mm";
	ИначеЕсли НачалоГода(Дата) = НачалоГода(ТекущаяДатаСеанса()) Тогда
		ФорматнаяСтрока = "ДФ=dd.MM";
	Иначе
		ФорматнаяСтрока = "ДФ=yyyy";
	КонецЕсли;
	
	Возврат Формат(Дата, ФорматнаяСтрока);
	
КонецФункции

// Определяет ближайшие записи календаря.
// 
// Возвращаемое значение:
//  Массив из Структура - Данные ближайших записей календаря. См. РаботаСВиджетами.ДанныеСтрокиСписка().
//
Функция БлижайшиеЗаписиКалендаря() Экспорт
	
	МаксимальноеКоличествоСтрок = 5;
	
	ТаблицаСобытий = РаботаСРабочимКалендаремСервер.ПолучитьСобытияФизЛицаСотрудника(
		ТекущаяДатаСеанса(),
		ДобавитьМесяц(ТекущаяДатаСеанса(), 1),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователи.АвторизованныйПользователь(), "ФизЛицо"),
		Ложь);
	
	КоличествоСтрок = 0;
	Для Каждого СтрокаСобытия Из ТаблицаСобытий Цикл
		
		Если СтрокаСобытия.Цвет <> Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоСтрок = КоличествоСтрок + 1;
		Если КоличествоСтрок = МаксимальноеКоличествоСтрок Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоСтрок = 0;
	БлижайшиеЗаписиКалендаря = Новый Массив;
	Для Каждого СтрокаСобытия Из ТаблицаСобытий Цикл
		
		ДанныеСтрокиСписка = ДанныеСтрокиСписка();
		
		ДанныеСтрокиСписка.Значение = СтрПолучитьСтроку(СтрокаСобытия.Наименование, 1);
			
		ДанныеСтрокиСписка.Ссылка = ПолучитьНавигационнуюСсылку(СтрокаСобытия.Ссылка);
		
		ДанныеСтрокиСписка.ИндексКартинки = РаботаСРабочимКалендаремКлиентСервер.ПолучитьИндексЦвета(
			СтрокаСобытия.Цвет,
			СтрокаСобытия.Отработана);
		Если ДанныеСтрокиСписка.ИндексКартинки = 0 Тогда
			ДанныеСтрокиСписка.ИндексКартинки = 7;
		КонецЕсли;
		
		ДанныеСтрокиСписка.Комментарий = ПредставлениеДатыСписка(
			СтрокаСобытия.ДатаНачала);
		
		БлижайшиеЗаписиКалендаря.Добавить(ДанныеСтрокиСписка);
		
		КоличествоСтрок = КоличествоСтрок + 1;
		Если КоличествоСтрок = МаксимальноеКоличествоСтрок Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат БлижайшиеЗаписиКалендаря;
	
КонецФункции

// Сохраняет переданный массив записей календаря
Функция СохранитьЗаписиКалендаря(Знач ЗаписиКалендаря, Знач НастройкиОтображения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СсылкиИзмененныхЗаписейКалендаря", Новый Массив);
	Результат.Вставить("НовыеИсключенияПовторения", Новый Массив);
	Результат.Вставить("ОбновитьПринудительно", Ложь);
	Результат.Вставить("ДанныеПланировщика", Неопределено);
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ЗаписьКалендаря Из ЗаписиКалендаря Цикл
			
			Если ЗаписьКалендаря.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
				
				ЗаписьКалендаряОбъект = СоздатьИсключениеПовторяющейсяЗаписиКалендаря(ЗаписьКалендаря);
				ЗаписьКалендаряОбъект.Записать();
				
				ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаря.Ссылка);
				ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаряОбъект);
				
				Цвет = РаботаСРабочимКалендаремСервер.ПолучитьЦветСобытияКалендаря(ЗаписьКалендаря.Ссылка);
				УстановитьЦветЗаписиКалендаря(ЗаписьКалендаряОбъект.Ссылка, Цвет);
				
				Результат.СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				Результат.СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);
				
				ИсключениеПовторения = Новый Структура("НоваяСсылка, СтараяСсылка, ДатаНачалаИсходная");
				ИсключениеПовторения.НоваяСсылка = ЗаписьКалендаряОбъект.Ссылка;
				ИсключениеПовторения.СтараяСсылка = ЗаписьКалендаря.Ссылка;
				ИсключениеПовторения.ДатаНачалаИсходная = ЗаписьКалендаря.ДатаНачалаИсходная;
				Результат.НовыеИсключенияПовторения.Добавить(ИсключениеПовторения);
				
			Иначе
				
				ЗаписьКалендаряОбъект = ЗаписьКалендаря.Ссылка.ПолучитьОбъект();
				ЗаполнитьЗначенияСвойств(ЗаписьКалендаряОбъект, ЗаписьКалендаря, "ДатаНачала, ДатаОкончания, ВесьДень");
				ЗаписьКалендаряОбъект.Записать();
				
				ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаряОбъект);
				
				Результат.СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Результат.ОбновитьПринудительно = УстановленоНапоминание(Результат.СсылкиИзмененныхЗаписейКалендаря);
	Результат.ДанныеПланировщика = ОбновитьДанныеПланировщика(НастройкиОтображения);
	
	Возврат Результат;
	
КонецФункции

// Создает запись календаря
Функция СоздатьЗаписьКалендаря(Знач ЗаписьКалендаря, Знач ЗначениеКопирования, Знач НастройкиОтображения) Экспорт
	
	Если ЗначениеКопирования <> Неопределено Тогда
		ОбъектКопирования = ЗначениеКопирования.ПолучитьОбъект();
		ЗаписьКалендаряОбъект = ОбъектКопирования.Скопировать();
		ЗаписьКалендаряОбъект.ДатаНачала = ЗаписьКалендаря.ДатаНачала;
		ЗаписьКалендаряОбъект.ДатаОкончания = ЗаписьКалендаря.ДатаОкончания;
		ЗаписьКалендаряОбъект.Пользователь = ЗаписьКалендаря.Пользователь;
	Иначе
		ЗаписьКалендаряОбъект = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
		ЗаписьКалендаряОбъект.Заполнить(ЗаписьКалендаря);
	КонецЕсли;
	ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("Цвет", ЗаписьКалендаря.Цвет);
	ЗаписьКалендаряОбъект.Записать();
	
	ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаряОбъект);
	
	
	Результат = Новый Структура("НовыйЭлемент, Напоминание, ДанныеПланировщика");
	Результат.Напоминание = ПодключитьНапоминаниеАвтоматически(ЗаписьКалендаряОбъект.Ссылка);
	Результат.НовыйЭлемент = ПолучитьСтруктуруЭлементаПланировщика(ЗаписьКалендаряОбъект);
	Результат.ДанныеПланировщика = ОбновитьДанныеПланировщика(НастройкиОтображения);
	
	Возврат Результат;
	
КонецФункции

// Возвращает по цвету стиля цвет календаря.
//
// Параметры:
//  ЦветСтиля - Цвет - Цвет стиля.
//
// Возвращаемое значение:
//  Перечисление.ЦветаРабочегоКалендаря - Цвет календаря.
//
Функция ПолучитьЦветКалендаря(ЦветСтиля) Экспорт
	
	Если ЦветСтиля = ЦветаСтиля.ЦветКалендаряКрасный Тогда
		ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Красный");
	ИначеЕсли ЦветСтиля = ЦветаСтиля.ЦветКалендаряСиний Тогда
		ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий");
	ИначеЕсли ЦветСтиля = ЦветаСтиля.ЦветКалендаряЖелтый Тогда
		ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Желтый");
	ИначеЕсли ЦветСтиля = ЦветаСтиля.ЦветКалендаряЗеленый Тогда
		ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Зеленый");
	ИначеЕсли ЦветСтиля = ЦветаСтиля.ЦветКалендаряОранжевый  Тогда
		ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Оранжевый");
	Иначе
		ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Произвольный");
	КонецЕсли;
	
	Возврат ЦветКалендаря;
	
КонецФункции

// Формирует структуру элемент планировщика по событию.
//
// Параметры:
//  НастройкиОтображения - Структура - Структура настроек отображения календаря.
//  ЗаписиКалендаря - Массив - Записи календаря.
// 
// Возвращаемое значение:
//  Массив - Структуры элементов планировщика.
//
Функция ПолучитьСтруктурыЭлементовПланировщика(НастройкиОтображения, ЗаписиКалендаря = Неопределено) Экспорт
	
	ПериодОтображения = РаботаСРабочимКалендаремКлиентСервер.ПолучитьПериодОтображения(НастройкиОтображения);
	ВыделенныеДаты = РаботаСРабочимКалендаремКлиентСервер.ПолучитьВыделенныеДаты(НастройкиОтображения);
	
	// Получение отображаемых данных
	ЗаписиКалендаряСНапоминаниями = ЗаписиКалендаряСНапоминаниямиТекущегоПользователя();
	СобытияПоЧасам = СформироватьТаблицуСобытий();
	Для Каждого ВыделеннаяДата Из ВыделенныеДаты Цикл
		
		ОтображаемаяДатаНачала =
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(
				ПериодОтображения, ВыделеннаяДата);
		ОтображаемаяДатаОкончания =
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(
				ПериодОтображения, ВыделеннаяДата);
		
		СобытияНаДату = ПолучитьТаблицуСобытий(
			ОтображаемаяДатаНачала,
			ОтображаемаяДатаОкончания,
			НастройкиОтображения.ОтображатьПомеченныеНаУдаление,
			НастройкиОтображения.Пользователи,
			НастройкиОтображения.ОтображатьОтклоненные,
			ЗаписиКалендаряСНапоминаниями,
			ЗаписиКалендаря);
		
		Для Каждого Событие Из СобытияНаДату Цикл
			НоваяСтрока = СобытияПоЧасам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Событие);
		КонецЦикла;
		
	КонецЦикла;
	
	СтруктурыЭлементов = Новый Массив;
	
	Для Каждого ЗаписьКалендаря Из СобытияПоЧасам Цикл
		СтруктураЭлемента = ПолучитьСтруктуруЭлементаПланировщика(
			ЗаписьКалендаря,
			НастройкиОтображения.ИспользоватьНапоминания,
			НастройкиОтображения.ОтключеноОтображениеВремени);
		СтруктурыЭлементов.Добавить(СтруктураЭлемента);
	КонецЦикла;
	
	Возврат СтруктурыЭлементов;
	
КонецФункции

// Формирует структуры интервалов фона планировщика.
//
// Параметры:
//  НастройкиОтображения - Структура - Структура настроек отображения календаря.
//
// Возвращаемое значение:
//  Массив - Структуры интервалов планировщика.
//
Функция ПолучитьСтруктурыИнтерваловФонаПланировщика(НастройкиОтображения) Экспорт
	
	ИнтервалыФонаПланировщика = Новый Массив;
		
	Возврат ИнтервалыФонаПланировщика;
	
КонецФункции

// Формирует структуру элемент планировщика по событию.
//
Функция ПолучитьСтруктуруЭлементаПланировщика(ЗаписьКалендаря,
	ИспользоватьНапоминания = Неопределено, ОтключеноОтображениеВремени = Неопределено) Экспорт
	
	Если ИспользоватьНапоминания = Неопределено Тогда
		ИспользоватьНапоминания = ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя");
	КонецЕсли;
	
	НастройкаТекста = НастройкаОтображенияТекста();
	
	Если ОтключеноОтображениеВремени = Неопределено Тогда
		НастройкаОтображениеВремениЭлементов = ПолучитьНастройкуОтображениеВремениЭлементов();
		ОтключеноОтображениеВремени =
			НастройкаОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.НеОтображать;
	КонецЕсли;
	
	//Если ТипЗнч(ЗаписьКалендаря) = Тип("СтрокаТаблицыЗначений") Тогда
	//	УстановленоНапоминание = ЗаписьКалендаря.УстановленоНапоминание;
	//	Цвет = ЗаписьКалендаря.Цвет;
	//	Отработана = ЗаписьКалендаря.Отработана;
	//	Приступил = ЗаписьКалендаряПриступил(ЗаписьКалендаря.Ссылка);
	//ИначеЕсли ТипЗнч(ЗаписьКалендаря) = Тип("СправочникОбъект.ЦБР_ЗаписиРабочегоКалендаря") Тогда
		УстановленоНапоминание = УстановленоНапоминание(ЗаписьКалендаря.Ссылка);
		Цвет = ПолучитьЦветСобытияКалендаря(ЗаписьКалендаря.Ссылка);
		Отработана = ЗаписьКалендаряОтработана(ЗаписьКалендаря.Ссылка);
		Приступил = ЗаписьКалендаряПриступил(ЗаписьКалендаря.Ссылка);
	//Иначе
	//	УстановленоНапоминание = Ложь;
	//	Цвет = Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет;
	//	Отработана = Ложь;
	//	Приступил = Ложь;
	//КонецЕсли;
	
	ЭлементПланировщика = Новый ЭлементПланировщика;
	
	ЭлементПланировщика.Начало = ЗаписьКалендаря.ДатаНачала;
	ЭлементПланировщика.Конец = ЗаписьКалендаря.ДатаОкончания;
	УстановитьЗначениеИзмерения(ЭлементПланировщика, "Сотрудник", ЗаписьКалендаря.Пользователь);
	
	ЭлементПланировщика.Значение = РаботаСРабочимКалендаремКлиентСервер.СформироватьЭлементЗаписиКалендаря(
		ЗаписьКалендаря);
	Если Цвет = Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет И Отработана Тогда
		ЭлементПланировщика.ЦветФона = ЦветаСтиля.ЦветКалендаряОтработано;
	Иначе

		Если Не ЗаписьКалендаряПриступил(ЭлементПланировщика.Значение.Ссылка)
			Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементПланировщика.Значение.Ссылка.ВидРаботы, "ПоПлану") Тогда
			ХранилищеЦвет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементПланировщика.Значение.Ссылка.ВидРаботы,
				"ЦветЗаписи");

			Если ХранилищеЦвет <> Неопределено Тогда
				ЦветФонаПланировщика = ХранилищеЦвет.Получить();
				Если ЦветФонаПланировщика <> Неопределено Тогда
					ЭлементПланировщика.ЦветФона = ЦветФонаПланировщика;
				Иначе
					ЭлементПланировщика.ЦветФона = ПолучитьЦветТабличногоДокумента(Цвет);
				КонецЕсли;
			Иначе
				ЭлементПланировщика.ЦветФона = ПолучитьЦветТабличногоДокумента(Цвет);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если ЭлементПланировщика.Конец < ТекущаяДатаСеанса() И Не Отработана Тогда
		Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Красный");
		ЭлементПланировщика.ЦветФона = ПолучитьЦветТабличногоДокумента(Цвет);
		ЭлементПланировщика.Конец = ТекущаяДатаСеанса() - 1;
	ИначеЕсли ЭлементПланировщика.Начало < ТекущаяДатаСеанса() И Не Приступил И Не Отработана Тогда
		Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Красный");
		ЭлементПланировщика.ЦветФона = ПолучитьЦветТабличногоДокумента(Цвет);
	ИначеЕсли Отработана Тогда
		Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий");
		ЭлементПланировщика.ЦветФона = ПолучитьЦветТабличногоДокумента(Цвет);
	КонецЕсли; 
	
	Если ЗаписьКалендаря.ПометкаУдаления Тогда
		ЭлементПланировщика.Картинка = БиблиотекаКартинок.ЗаписьКалендаряУдалена;
	ИначеЕсли ЗаписьКалендаря.Состояние = Перечисления.СостоянияЗаписейРабочегоКалендаря.ПодВопросом Тогда
		ЭлементПланировщика.Картинка = БиблиотекаКартинок.ЗаписьКалендаряПодВопросом;
	ИначеЕсли ЗаписьКалендаря.Состояние = Перечисления.СостоянияЗаписейРабочегоКалендаря.Отклонено Тогда
		ЭлементПланировщика.Картинка = БиблиотекаКартинок.ЗаписьКалендаряОтклонена;
	КонецЕсли;
	ЭлементПланировщика.ЦветТекста = ЦветаСтиля.ЦветКалендаряПодпись;
	ЭлементПланировщика.ЦветРамки = ПолучитьЦветРамкиЭлементаПланировщика(ЭлементПланировщика.ЦветФона);
	
	Если ЗначениеЗаполнено(ЗаписьКалендаря.ЗаданиеСсылка) Тогда
		ЭлементПланировщика.Текст = " ";
		Если НастройкаТекста.ОтображениеОписания Тогда
			ЭлементПланировщика.Текст = ЭлементПланировщика.Текст + ЗаписьКалендаря.Наименование + " ";
		КонецЕсли;
		Если НастройкаТекста.ОтображениеПредмета Тогда
			ЭлементПланировщика.Текст = ЭлементПланировщика.Текст + ЗаписьКалендаря.ЗаданиеСсылка + " ";
		КонецЕсли;
		Постановщик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьКалендаря.ЗаданиеСсылка, "Постановщик");
		Если НастройкаТекста.ОтображениеМенеджера Тогда
			ЭлементПланировщика.Текст = ЭлементПланировщика.Текст + Постановщик + " ";
		КонецЕсли;
		Если НастройкаТекста.ОтображениеКонтрагента Тогда
			Если ТипЗнч(ЗаписьКалендаря.ЗаданиеСсылка) = Тип("ДокументСсылка.ЦБР_Задание") Тогда
				Задача = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьКалендаря.ЗаданиеСсылка, "СсылкаНаЗадачу");
				Заказчик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Заказчик");
				
			Иначе
				Задача = ЗаписьКалендаря.ЗаданиеСсылка;
				Заказчик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Заказчик");
			КонецЕсли;

			ЭлементПланировщика.Текст = ЭлементПланировщика.Текст + Заказчик + " ";
		КонецЕсли;
		Если ЭлементПланировщика.Текст = "" Тогда
			ЭлементПланировщика.Текст = ЗаписьКалендаря.Наименование;
		КонецЕсли;
	Иначе
		ЭлементПланировщика.Текст = ЗаписьКалендаря.Наименование;
	КонецЕсли;
		
	Если ИспользоватьНапоминания И ОтключеноОтображениеВремени И УстановленоНапоминание Тогда
		ДатаНачалаСобытияСтрокой = Формат(ЗаписьКалендаря.ДатаНачала, "ДФ=HH:mm");
		ЭлементПланировщика.Текст = ДатаНачалаСобытияСтрокой + " " + ЭлементПланировщика.Текст;
	КонецЕсли;
	
	Возврат ЭлементПланировщика.Выгрузить();
	
КонецФункции

Функция НастройкаОтображенияТекста()
	СтруктураНастроек = Новый Структура("ОтображениеОписания, ОтображениеПредмета, ОтображениеМенеджера, ОтображениеКонтрагента");
	
	СтруктураНастроек.ОтображениеОписания = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьОписание");
	СтруктураНастроек.ОтображениеМенеджера = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьМенеджера");
	СтруктураНастроек.ОтображениеКонтрагента = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьКонтрагента");
	СтруктураНастроек.ОтображениеПредмета = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьПредмет");
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Формирует структуру элемент планировщика по занятости.
//
Функция ПолучитьСтруктуруЭлементаПланировщикаЗанятость(Занятость, Сотрудник = Неопределено) Экспорт
	
	ЭлементПланировщика = Новый ЭлементПланировщика;
	
	Если Сотрудник = Неопределено Тогда
		Сотрудник = Занятость.Сотрудник;
	КонецЕсли;
	УстановитьЗначениеИзмерения(ЭлементПланировщика, "Сотрудник", Сотрудник);
	
	ЭлементПланировщика.Начало = Занятость.ДатаНачала;
	ЭлементПланировщика.Конец = Занятость.ДатаОкончания;
	ЭлементПланировщика.ЦветТекста = ЦветаСтиля.ЦветКалендаряПодпись;
	ЭлементПланировщика.Текст = Строка(Занятость.Занят);
	ЭлементПланировщика.ЦветФона = ПолучитьЦветЗанятости(Занятость);
	
	Возврат ЭлементПланировщика.Выгрузить();
	
КонецФункции

// Формирует структуру настроек отображения планировщика.
//
Функция ПолучитьНастройкиОтображения() Экспорт
	
	НастройкиОтображения = Новый Структура;
	
	// Период отображения
	НастройкиОтображения.Вставить(
		"ПериодОтображения",
		ПолучитьПерсональнуюНастройку("ПериодОтображенияРабочегоКалендаря"));
	
	// Пользователи
	МассивПользователей = Новый Массив;
	МассивПользователей.Добавить(ПользователиКлиентСервер.ТекущийПользователь());
	НастройкиОтображения.Вставить(
		"Пользователи",
		МассивПользователей);
	
	// Отображаемая дата
	ОтображаемаяДата = НачалоДня(ТекущаяДата());
	НастройкиОтображения.Вставить(
		"ОтображаемаяДата",
		ОтображаемаяДата);
	
	// Выделенные даты
	ВыделенныеДаты = Новый Массив;
	ВыделенныеДаты.Добавить(ОтображаемаяДата);
	НастройкиОтображения.Вставить(
		"ВыделенныеДаты",
		ВыделенныеДаты);
	
	// Отображать события
	НастройкиОтображения.Вставить(
		"ОтображатьСобытия",
		Истина);
	
	// Отображать занятость
	НастройкиОтображения.Вставить(
		"ОтображатьЗанятость",
		ПолучитьПерсональнуюНастройку("ОтображатьЗанятость"));
	
	// Отображать общую занятость
	НастройкиОтображения.Вставить(
		"ОтображатьОбщуюЗанятость",
		Ложь);
	
	// Исключения занятости
	ИсключенияЗанятости = Новый Массив;
	НастройкиОтображения.Вставить(
		"ИсключенияЗанятости",
		ИсключенияЗанятости);
	
	// Использовать напоминания
	НастройкиОтображения.Вставить(
		"ИспользоватьНапоминания",
		ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя"));
	
	// Настройка отображения времени элементов и 
	НастройкаОтображениеВремениЭлементов = ПолучитьНастройкуОтображениеВремениЭлементов();
	НастройкиОтображения.Вставить(
		"НастройкаОтображениеВремениЭлементов",
		НастройкаОтображениеВремениЭлементов);
	
	// Отключено отображение времени
	НастройкиОтображения.Вставить(
		"ОтключеноОтображениеВремени",
		НастройкаОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.НеОтображать);
	
	// Отображать помеченные на удаление
	НастройкиОтображения.Вставить(
		"ОтображатьПомеченныеНаУдаление",
		ПолучитьПерсональнуюНастройку("ОтображатьПомеченныеНаУдаление"));
	
	// Отображать отклоненные
	НастройкиОтображения.Вставить(
		"ОтображатьОтклоненные",
		ПолучитьПерсональнуюНастройку("ОтображатьОтклоненные"));
	
	// Цвет календаря подпись
	НастройкиОтображения.Вставить(
		"ЦветКалендаряПодпись",
		ЦветаСтиля.ЦветКалендаряПодпись);
	
	// Цвет календаря фон шапки
	НастройкиОтображения.Вставить(
		"ЦветКалендаряФонШапки",
		ЦветаСтиля.ЦветКалендаряФонШапки);
	
	// Отображать время с
	НастройкиОтображения.Вставить(
		"ОтображатьВремяС",
		ПолучитьПерсональнуюНастройку("ОтображатьВремяС"));
	
	// Отображать время по
	НастройкиОтображения.Вставить(
		"ОтображатьВремяПо",
		ПолучитьПерсональнуюНастройку("ОтображатьВремяПо"));
	
	// Отображать скрытые элементы
	НастройкиОтображения.Вставить(
		"ОтображатьСкрытыеЭлементы",
		Истина);
	
	// Размер ячейки времени
	НастройкиОтображения.Вставить(
		"РазмерЯчейкиВремени",
		ПолучитьПерсональнуюНастройку("РазмерЯчейкиВремени"));
	
	// Цвет фона текущего дня.
	НастройкиОтображения.Вставить(
		"ЦветФонаТекущегоДня",
		ЦветаСтиля.ЦветКалендаряФонТекущегоДня);
	
	// Цвет пустого фона.
	НастройкиОтображения.Вставить(
		"ЦветКалендаряДоступноеВремя",
		ЦветаСтиля.ЦветКалендаряДоступноеВремя);
	
	// Цвет текущей записи.
	НастройкиОтображения.Вставить(
		"ЦветКалендаряТекущаяЗапись",
		ЦветаСтиля.ЦветКалендаряТекущаяЗапись);
	
	// Цвет текущей записи.
	НастройкиОтображения.Вставить(
		"ЦветКалендаряТекущаяЗапись",
		ЦветаСтиля.ЦветКалендаряТекущаяЗапись);
	
	// Цвет рамки текущей записи.
	НастройкиОтображения.Вставить(
		"ЦветРамкиТекущаяЗапись",
		ПолучитьЦветРамкиЭлементаПланировщика(ЦветаСтиля.ЦветКалендаряТекущаяЗапись));
	
	// Отобразить текущую запись.
	НастройкиОтображения.Вставить(
		"ОтобразитьТекущуюЗапись",
		Ложь);
	
	// Ссылка текущей записи.
	НастройкиОтображения.Вставить(
		"ТекущаяЗаписьСсылка",
		Справочники.ЦБР_ЗаписиРабочегоКалендаря.ПустаяСсылка());
	
	// Дата начала текущей записи.
	НастройкиОтображения.Вставить(
		"ТекущаяЗаписьДатаНачала",
		Дата(1,1,1));
	
	// Дата окончания текущей записи.
	НастройкиОтображения.Вставить(
		"ТекущаяЗаписьДатаОкончания",
		Дата(1,1,1));
	
	// Текущая запись на весь день.
	НастройкиОтображения.Вставить(
		"ТекущаяЗаписьВесьДень",
		Ложь);
	
	// Запрет создания.
	НастройкиОтображения.Вставить(
		"ЗапретСоздания",
		Ложь);
	
	// Запрет изменения.
	НастройкиОтображения.Вставить(
		"ЗапретИзменения",
		Ложь);
	
	// Скрыть пустые дни в плане дня.
	НастройкиОтображения.Вставить(
		"ПланДняСкрытьПустые",
		Ложь);
		
	//Отображать количество дней в неделе
	НастройкиОтображения.Вставить(
		"ОтображатьКоличествоДнейВНеделе",
		 ПолучитьПерсональнуюНастройку("ОтображатьКоличествоДнейВНеделе"));
	
	Возврат НастройкиОтображения;
	
КонецФункции

// Возвращает цвет, которым отображается событие в календаре пользователя
//
// Параметры:
//	Событие - Событие, которому соответствует цвет
//	Пользователь - Пользователь, которому соответствует цвет
//
//	Возвращает ЦветаРабочегоКалендаря
Функция ПолучитьЦветСобытияКалендаря(Событие, Пользователь = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Событие) Тогда
		Возврат Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет;
	КонецЕсли;
	
	Цвет = Событие.Цвет;
	
	Если Цвет = Неопределено  Тогда
		Цвет = Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет;
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

// Устанавливает цвет, которым отображается событие в календаре пользователя.
//
Процедура УстановитьЦветЗаписиКалендаря(ЗаписьКалендаря, Знач Цвет, Пользователь = Неопределено, ФормироватьОписаниеРезультата = Ложь) Экспорт
	
	Если ТипЗнч(Цвет) = Тип("Цвет") Тогда
		Цвет = ПолучитьЦветКалендаря(Цвет);
	КонецЕсли;
	
	ЗаписьКалендаря_Объект = ЗаписьКалендаря.ПолучитьОбъект();
	ЗаписьКалендаря_Объект.Цвет =  Цвет;
	ЗаписьКалендаря_Объект.Записать(); 
	
КонецПроцедуры

// Устанавливает цвет, которым отображается события в календаре пользователя.
//
Функция УстановитьЦветЗаписейКалендаря(Знач ЗаписиКалендаря, Знач Цвет, Знач СоздаватьИсключенияПовторения) Экспорт
	
	СсылкиИзмененныхЗаписейКалендаря = Новый Массив;
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ЗаписьКалендаря Из ЗаписиКалендаря Цикл
			
			Если СоздаватьИсключенияПовторения И ЭтоПовторяющеесяСобытие(ЗаписьКалендаря) Тогда
				
				ЗаписьКалендаряОбъект = СоздатьИсключениеПовторяющейсяЗаписиКалендаря(ЗаписьКалендаря);
				ЗаписьКалендаряОбъект.Записать();
				
				УстановитьЦветЗаписиКалендаря(ЗаписьКалендаряОбъект.Ссылка, Цвет);
				
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);
				
			Иначе
				
				УстановитьЦветЗаписиКалендаря(ЗаписьКалендаря.Ссылка, Цвет);
				
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат СсылкиИзмененныхЗаписейКалендаря;
	
КонецФункции

// Процедура отображает события пользователя в календарь на указанный период
Процедура ОтобразитьКалендарь(Планировщик, НастройкиОтображения) Экспорт
	
	ЗагрузитьДанныеПланировщика(Планировщик, НастройкиОтображения);
	Если НастройкиОтображения.ОтобразитьТекущуюЗапись Тогда
		РаботаСРабочимКалендаремКлиентСервер.ОтобразитьТекущуюЗапись(
			Планировщик,
			НастройкиОтображения);
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиентСервер.ПрименитьНастройкиОтображения(Планировщик, НастройкиОтображения);
	
КонецПроцедуры

// Процедура заполняет таблицу плана дня
//
// Параметры:
//  ДеревоПланДня - ДеревоЗначений - План дня.
//  НастройкиОтображения - Структура - Структура настроек отображения календаря.
//
Процедура ОтобразитьПланДня(ДеревоПланДня, НастройкиОтображения) Экспорт
	
	ДеревоПланДня.Строки.Очистить();
	
	ПериодОтображения =
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьПериодОтображения(НастройкиОтображения);
	ВыделенныеДаты =
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьВыделенныеДаты(НастройкиОтображения);
	ОтображатьПользователя = НастройкиОтображения.Пользователи.Количество() <> 1;
	
	// Получение отображаемых данных.
	ЗаписиКалендаряСНапоминаниями = ЗаписиКалендаряСНапоминаниямиТекущегоПользователя();
	СобытияПоЧасам = СформироватьТаблицуСобытий();
	Для Каждого ВыделеннаяДата Из ВыделенныеДаты Цикл
		ОтображаемаяДатаНачала =
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(
				ПериодОтображения, ВыделеннаяДата);
		ОтображаемаяДатаОкончания =
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(
				ПериодОтображения, ВыделеннаяДата);
		СобытияНаДату = ПолучитьТаблицуСобытий(
			ОтображаемаяДатаНачала,
			ОтображаемаяДатаОкончания,
			НастройкиОтображения.ОтображатьПомеченныеНаУдаление,
			НастройкиОтображения.Пользователи,
			НастройкиОтображения.ОтображатьОтклоненные,
			ЗаписиКалендаряСНапоминаниями);
		Для Каждого Событие Из СобытияНаДату Цикл
			НоваяСтрока = СобытияПоЧасам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Событие);
		КонецЦикла;
	КонецЦикла;
	
	// Отображение плана дня.
	СобытияДняПоЧасам = СобытияПоЧасам.СкопироватьКолонки();
	Для Каждого ВыделеннаяДата Из ВыделенныеДаты Цикл
		ОтображаемаяДатаНачала =
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(
				ПериодОтображения, ВыделеннаяДата);
		ОтображаемаяДатаОкончания =
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(
				ПериодОтображения, ВыделеннаяДата);
		
		НачалоТекущегоДняНедели = ОтображаемаяДатаНачала;
		Пока НачалоТекущегоДняНедели < ОтображаемаяДатаОкончания Цикл
			
			// Отображение строки дня.
			СобытияДняПоЧасам.Очистить();
			НачалоТекущегоДняНедели = НачалоДня(НачалоТекущегоДняНедели);
			КонецТекущегоДняНедели = КонецДня(НачалоТекущегоДняНедели) + 1;
			
			Для Каждого Событие Из СобытияПоЧасам Цикл
				
				Если НастройкиОтображения.ОтобразитьТекущуюЗапись
					И Событие.Ссылка = НастройкиОтображения.ТекущаяЗаписьСсылка Тогда
					Продолжить;
				КонецЕсли;
				
				Если Событие.ДатаНачала < КонецТекущегоДняНедели
					И Событие.ДатаОкончания > НачалоТекущегоДняНедели Тогда
					ЗаполнитьЗначенияСвойств(СобытияДняПоЧасам.Добавить(), Событие);
				КонецЕсли;
				
			КонецЦикла;
			
			Если НастройкиОтображения.ПланДняСкрытьПустые Тогда
				Если СобытияДняПоЧасам.Количество() = 0 Тогда
					НачалоТекущегоДняНедели = НачалоТекущегоДняНедели + 86400; // 86400 - число секунд в сутках
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СобытияДняПоЧасам.Сортировать("ДатаНачала, Наименование");
			
			КоличествоСобытий = СобытияДняПоЧасам.Количество();
			ДополнительноеПредставлениеДня = "";
			Если НачалоТекущегоДняНедели = НачалоДня(ТекущаяДатаСеанса()) Тогда
				ДополнительноеПредставлениеДня = НСтр("ru = 'Сегодня'");
			ИначеЕсли НачалоТекущегоДняНедели = (НачалоДня(ТекущаяДатаСеанса()) + 86400) Тогда // 86400 - число секунд в сутках
				ДополнительноеПредставлениеДня = НСтр("ru = 'Завтра'");
			ИначеЕсли НачалоТекущегоДняНедели = (НачалоДня(ТекущаяДатаСеанса()) - 86400) Тогда // 86400 - число секунд в сутках
				ДополнительноеПредставлениеДня = НСтр("ru = 'Вчера'");
			ИначеЕсли НачалоТекущегоДняНедели = (НачалоДня(ТекущаяДатаСеанса()) - 2 * 86400) Тогда // 86400 - число секунд в сутках
				ДополнительноеПредставлениеДня = НСтр("ru = 'Позавчера'");
			КонецЕсли;
			
			СтрокаТекущегоДня = ДеревоПланДня.Строки.Добавить();
			СтрокаТекущегоДня.ЭтоГруппа = Истина;
			СтрокаТекущегоДня.ДатаДень = НачалоТекущегоДняНедели;
			СтрокаТекущегоДня.ИндексЦвета = -1;
			
			ПредставлениеДня = Формат(НачалоТекущегоДняНедели, "ДФ='дддд'");
			ПредставлениеДаты = Формат(НачалоТекущегоДняНедели, "ДФ='д ММММ гггг'");
			Если ЗначениеЗаполнено(ДополнительноеПредставлениеДня) Тогда
				СтрокаТекущегоДня.ОписаниеКраткое = СтрШаблон("%1, %2, %3",
					ДополнительноеПредставлениеДня,
					ПредставлениеДня,
					ПредставлениеДаты);
			Иначе
				СтрокаТекущегоДня.ОписаниеКраткое = СтрШаблон("%1, %2",
					ТРег(ПредставлениеДня),
					ПредставлениеДаты);
			КонецЕсли;
			Если ЗначениеЗаполнено(КоличествоСобытий) Тогда
				СтрокаТекущегоДня.ОписаниеКраткое = СтрШаблон("%1 (%2)",
					СтрокаТекущегоДня.ОписаниеКраткое,
					КоличествоСобытий);
			КонецЕсли;
			
			// Отображений событий в дне.
			Для Каждого СтрокаСобытие Из СобытияДняПоЧасам Цикл
				СтрокаСобытиеТекущегоДня = СтрокаТекущегоДня.Строки.Добавить();
				ЭлементЗаписиКалендаря =
					РаботаСРабочимКалендаремКлиентСервер.СформироватьЭлементЗаписиКалендаря(СтрокаСобытие);
				ЗаполнитьЗначенияСвойств(СтрокаСобытиеТекущегоДня, ЭлементЗаписиКалендаря);
				СтрокаСобытиеТекущегоДня.ДатаДень = НачалоТекущегоДняНедели;
				СтрокаСобытиеТекущегоДня.ОписаниеКраткое =
					СформироватьТекстовоеОписаниеСобытия(СтрокаСобытие, Не СтрокаСобытие.ВесьДень, ОтображатьПользователя);
				Если СтрокаСобытие.Цвет = Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет И СтрокаСобытие.Отработана Тогда
					СтрокаСобытиеТекущегоДня.ИндексЦвета = 6;
				Иначе
					СтрокаСобытиеТекущегоДня.ИндексЦвета =
						РаботаСРабочимКалендаремКлиентСервер.ПолучитьИндексЦвета(СтрокаСобытие.Цвет);
				КонецЕсли;
				СтрокаСобытиеТекущегоДня.Цвет = СтрокаСобытие.Цвет;
			КонецЦикла;
			
			НачалоТекущегоДняНедели = НачалоТекущегоДняНедели + 86400; // 86400 - число секунд в сутках
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает персональную настройку рабочего календаря текущего пользователя по ключу.
Функция ПолучитьПерсональнуюНастройку(Настройка) Экспорт
	
	Если Настройка = "ПериодОтображенияРабочегоКалендаря" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ПериодОтображенияРабочегоКалендаря",
			Перечисления.ПериодОтображенияРабочегоКалендаря.Неделя);
		
	ИначеЕсли Настройка = "ОтображатьПомеченныеНаУдаление" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьПомеченныеНаУдаление",
			Ложь);
		
	ИначеЕсли Настройка = "ОтображатьОтклоненные" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьОтклоненные",
			Ложь);
			
		
	ИначеЕсли Настройка = "ОтображатьНавигационныйКалендарь" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьНавигационныйКалендарь",
			Ложь);
		
	ИначеЕсли Настройка = "СвернутьСобытияВесьДеньВКалендаре" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"СвернутьСобытияВесьДеньВКалендаре",
			Ложь);
		
	ИначеЕсли Настройка = "ВремяНачалаОтображения" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ВремяНачалаОтображения",
			Дата("00010101080000"));
		
	ИначеЕсли Настройка = "ВремяОкончанияОтображения" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ВремяОкончанияОтображения",
			Дата("00010101190000"));
		
	ИначеЕсли Настройка = "МасштабСтандартный" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"МасштабСтандартный",
			100);
		
	ИначеЕсли Настройка = "МасштабСПанельюИнформации" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"МасштабСПанельюИнформации",
			70);
		
	ИначеЕсли Настройка = "ОтображатьЛегенду" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьЛегенду",
			Истина);
		
	ИначеЕсли Настройка = "ОтображатьВремяС" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьВремяС",
			9);
		
	ИначеЕсли Настройка = "ОтображатьВремяПо" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьВремяПо",
			6);
		
	ИначеЕсли Настройка = "ОтображениеВремениЭлементов" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображениеВремениЭлементов",
			Перечисления.ЦБР_ОтображениеВремениЭлементов.ВремяНачалаИКонца);
		
	ИначеЕсли Настройка = "ОтображатьЗанятость" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьЗанятость",
			Истина);
		
	ИначеЕсли Настройка = "ИспользоватьБыстроеРедактирование" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь", "ИспользоватьБыстроеРедактирование", Ложь);

		Значение = Ложь;
		
	ИначеЕсли Настройка = "РазмерЯчейкиВремени" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"РазмерЯчейкиВремени",
			15);
		
	ИначеЕсли Настройка = "ТекущиеКалендариВсеКалендари" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ТекущиеКалендариВсеКалендари",
			Новый Массив);
		
	ИначеЕсли Настройка = "ОтображатьПолеПользователь" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьПолеПользователь",
			Ложь);
		
	ИначеЕсли Настройка = "ОтображатьКалендарьВЗаписиКалендаря" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"ОтображатьКалендарьВЗаписиКалендаря",
			Истина);
		
	ИначеЕсли Настройка = "СкрытьОтсутствияБудуРазбиратьЗадачи" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			"СкрытьОтсутствияБудуРазбиратьЗадачи",
			Истина);
			
	ИначеЕсли Настройка = "ОтображатьКоличествоДнейВНеделе" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь", 
			"ОтображатьКоличествоДнейВНеделе", 
			Перечисления.ЦБР_КоличествоДнейВКалендаре.Пятидневка);
	
	ИначеЕсли Настройка = "ОтображатьПредмет" Тогда

		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			 "ОтображатьПредмет",
			  Ложь);
			  
	ИначеЕсли Настройка = "ОтображатьКонтрагента" Тогда

		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			 "ОтображатьКонтрагента",
			  Ложь);
			  
	ИначеЕсли Настройка = "ОтображатьМенеджера" Тогда

		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			 "ОтображатьМенеджера",
			  Ложь);
			  
	ИначеЕсли Настройка = "ОтображатьОписание" Тогда

		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РабочийКалендарь",
			 "ОтображатьОписание",
			  Истина);
	//+++	
	ИначеЕсли Настройка = "ПодразделениеПользователь" Тогда

		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ПодразделениеПользователь",
			 "ОтображатьОписание",
			  Истина);
		  
			  
			  
	Иначе
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Некорректная настройка рабочего календаря: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Сохраняет персональную настройку рабочего календаря текущего пользователя.
Процедура УстановитьПерсональнуюНастройку(Настройка, Значение) Экспорт
	
	Если Настройка = "ПериодОтображенияРабочегоКалендаря" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ПериодОтображенияРабочегоКалендаря") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ПериодОтображенияРабочегоКалендаря",
			Значение);
			
	ИначеЕсли Настройка = "ОтображатьПомеченныеНаУдаление" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьПомеченныеНаУдаление",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьОтклоненные" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьОтклоненные",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьНавигационныйКалендарь" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьНавигационныйКалендарь",
			Значение);
		
	ИначеЕсли Настройка = "СвернутьСобытияВесьДеньВКалендаре" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"СвернутьСобытияВесьДеньВКалендаре",
			Значение);
		
	ИначеЕсли Настройка = "ВремяНачалаОтображения" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Дата") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ВремяНачалаОтображения",
			Значение);
		
	ИначеЕсли Настройка = "ВремяОкончанияОтображения" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Дата") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ВремяОкончанияОтображения",
			Значение);
		
	ИначеЕсли Настройка = "МасштабСтандартный" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"МасштабСтандартный",
			Значение);
		
	ИначеЕсли Настройка = "МасштабСПанельюИнформации" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"МасштабСПанельюИнформации",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьЛегенду" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьЛегенду",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьВремяС" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьВремяС",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьВремяПо" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьВремяПо",
			Значение);
		
	ИначеЕсли Настройка = "ОтображениеВремениЭлементов" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ЦБР_ОтображениеВремениЭлементов") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображениеВремениЭлементов",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьЗанятость" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьЗанятость",
			Значение);
		
	ИначеЕсли Настройка = "ИспользоватьБыстроеРедактирование" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ИспользоватьБыстроеРедактирование",
			Значение);
		
	ИначеЕсли Настройка = "РазмерЯчейкиВремени" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"РазмерЯчейкиВремени",
			Значение);
		
	ИначеЕсли Настройка = "ТекущиеКалендариВсеКалендари" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Массив") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ТекущиеКалендариВсеКалендари",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьПолеПользователь" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьПолеПользователь",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьКалендарьВЗаписиКалендаря" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьКалендарьВЗаписиКалендаря",
			Значение);
		
	ИначеЕсли Настройка = "СкрытьОтсутствияБудуРазбиратьЗадачи" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"СкрытьОтсутствияБудуРазбиратьЗадачи",
			Значение);
			
	ИначеЕсли Настройка = "ОтображатьКоличествоДнейВНеделе" Тогда

		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ЦБР_КоличествоДнейВКалендаре") Тогда

			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"), Настройка);

		КонецЕсли;

		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь", "ОтображатьКоличествоДнейВНеделе", Значение);
			
	ИначеЕсли Настройка = "ОтображатьПредмет" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьПредмет",
			Значение);
			
	ИначеЕсли Настройка = "ОтображатьКонтрагента" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьКонтрагента",
			Значение);
			
	ИначеЕсли Настройка = "ОтображатьМенеджера" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьМенеджера",
			Значение);
			
	ИначеЕсли Настройка = "ОтображатьОписание" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Некорректное значение настройки рабочего календаря: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ОтображатьОписание",
			Значение);
			
	//+++		
	ИначеЕсли Настройка = "ПодразделениеПользователь" Тогда
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РабочийКалендарь",
			"ПодразделениеПользователь",
			Значение);
				

	Иначе
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Некорректная настройка рабочего календаря: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает пометку удаления записи и оповещает другие формы.
Функция УстановитьПометкуУдаления(Запись, ПометкаУдаления, Причина="") Экспорт
	
	УстановленаПометкаУдаления = Ложь;
	
	ЗаписьОбъект = Запись.ПолучитьОбъект();
	
	Если ЗаписьОбъект.ПометкаУдаления <> ПометкаУдаления Тогда	
		ЗаписьОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
		УстановленаПометкаУдаления = Истина;
		ЗаписьОбъект.ПричинаОтмены = Причина;
		ДобавитьВИсториюРаботыПользователя(ЗаписьОбъект);
	КонецЕсли;

	Приступил = ЗаписьКалендаряПриступил(Запись);
	Если Приступил Тогда
		ОтработатьЗаписьКалендаря(Запись,Новый Массив);
	КонецЕсли;

	Возврат УстановленаПометкаУдаления;
	
КонецФункции

// Устанавливает пометки удаления записей и возвращает факт изменения пометки удаления.
Функция УстановитьПометкиУдаления(Записи, ПовторяющиесяСобытия, ПометкаУдаления) Экспорт
	
	ОднаИзЗаписей = Неопределено;
	УстановленаПометкаУдаления = Ложь;
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого Запись Из Записи Цикл
			
			ЗаписьОбъект = Запись.ПолучитьОбъект();
			Если ЗаписьОбъект.ПометкаУдаления <> ПометкаУдаления Тогда
				
				ЗаписьОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
				УстановленаПометкаУдаления = Истина;
				
				ДобавитьВИсториюРаботыПользователя(ЗаписьОбъект);
				
				ОднаИзЗаписей = ЗаписьОбъект.Ссылка;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПометкаУдаления и ПовторяющиесяСобытия.Количество() > 0 Тогда
			
			Для Каждого ПовторяющеесяСобытие Из ПовторяющиесяСобытия Цикл
				
				ПовторяющеесяСобытиеОбъект = ПовторяющеесяСобытие.ЗаписьКалендаря.ПолучитьОбъект();
				ПовторяющеесяСобытиеОбъект.Заблокировать();
				ПовторяющеесяСобытиеОбъект.ДобавитьИсключениеПовторения(ПовторяющеесяСобытие.ДатаИсключения);
				ПовторяющеесяСобытиеОбъект.Записать();
				УстановленаПометкаУдаления = Истина;
				
				ДобавитьВИсториюРаботыПользователя(ПовторяющеесяСобытиеОбъект);
				
				ОднаИзЗаписей = ПовторяющеесяСобытиеОбъект.Ссылка;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат УстановленаПометкаУдаления;
	
КонецФункции

// Устанавливает повторение события в соответствии с настройками повторения.
Процедура УстановитьПовторение(ЗаписьКалендаря, НастройкиПовторения) Экспорт
	
	ЗаписьКалендаряОбъект = ЗаписьКалендаря.ПолучитьОбъект();
	Если ТипЗнч(ЗаписьКалендаряОбъект) = Тип("СправочникОбъект.ЦБР_ЗаписиРабочегоКалендаря") Тогда
		Если ЗаписьКалендаряОбъект.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
			ВызватьИсключение НСтр("ru = 'Невозможно настроить повторения для исключения повторения.'");
		КонецЕсли;
		Если ЗаписьКалендаряОбъект.ПометкаУдаления Тогда
			ВызватьИсключение НСтр("ru = 'Невозможно настроить повторения для помеченного на удаление события.'");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЗаписьКалендаряОбъект) = Тип("ДокументОбъект.Бронь") Тогда
		Если ЗаписьКалендаряОбъект.ТипЗаписи = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
			ВызватьИсключение НСтр("ru = 'Невозможно настроить повторения для исключения повторения.'");
		КонецЕсли;
		Если ЗаписьКалендаряОбъект.ПометкаУдаления Тогда
			ВызватьИсключение НСтр("ru = 'Невозможно настроить повторения для помеченной на удаление брони.'");
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("НастройкиПовторения", НастройкиПовторения);
	ЗаписьКалендаряОбъект.Записать();
	
КонецПроцедуры

// Формирует HTML представление события
Функция СформироватьHTMLПредставление(Событие, ДатаСобытия) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Событие) Тогда
		Возврат РаботаСРабочимКалендаремКлиентСервер.ПолучитьПустоеHTMLПредставление();
	КонецЕсли;
	
	Если ТипЗнч(Событие) = Тип("СправочникСсылка.ЦБР_ЗаписиРабочегоКалендаря") Тогда
		ПредставлениеHTML = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СформироватьHTMLПредставление(Событие, ДатаСобытия);
	Иначе
		ПредставлениеHTML = РаботаСРабочимКалендаремКлиентСервер.ПолучитьПустоеHTMLПредставление();
	КонецЕсли;
	
	Возврат ПредставлениеHTML;
	
КонецФункции

// Возвращает по цвету рабочего календаря цвет для отображения
Функция ПолучитьЦветТабличногоДокумента(ЦветКалендаря) Экспорт
	
	Если ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Красный") Тогда
		ЦветТабличногоДокумента = ЦветаСтиля.ЦветКалендаряКрасный;
	ИначеЕсли ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий") Тогда
		ЦветТабличногоДокумента = ЦветаСтиля.ЦветКалендаряСиний;
	ИначеЕсли ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Желтый") Тогда
		ЦветТабличногоДокумента = ЦветаСтиля.ЦветКалендаряЖелтый;
	ИначеЕсли ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Зеленый") Тогда
		ЦветТабличногоДокумента = ЦветаСтиля.ЦветКалендаряЗеленый;
	ИначеЕсли ЦветКалендаря = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Оранжевый") Тогда
		ЦветТабличногоДокумента = ЦветаСтиля.ЦветКалендаряОранжевый;
	Иначе
		ЦветТабличногоДокумента = ЦветаСтиля.ЦветКалендаряПоУмолчанию;
	КонецЕсли;
	
	Возврат ЦветТабличногоДокумента;
	
КонецФункции

// Изменяет доступность времени
Функция ИзменитьДоступностьВремени(Сотрудник, ДатаНачала, ДатаОкончания,
	Занят = Неопределено) Экспорт
		
	НоваяЗанятость = РегистрыСведений.ЦБР_ЗанятостьСотрудников.ИзменитьДоступностьВремени(
		Сотрудник, ДатаНачала, ДатаОкончания, Занят);
	
	Возврат НоваяЗанятость;
	
КонецФункции

// Возвращает настройку доступности по умолчанию для сотрудника
// 
// Параметры:
//  Сотрудник - СправочникСсылка.Сотрудники
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ДоступностьСотрудника
//
Функция ПолучитьНастройкуДоступностиСотрудника(Сотрудник) Экспорт
	
	ДоступностьПоУмолчанию = РегистрыСведений.ЦБР_НастройкиРабочегоКалендаря.ПолучитьНастройку(
		Сотрудник, Перечисления.ЦБР_НастройкиРабочегоКалендаря.Доступность);
	
	Если НЕ ЗначениеЗаполнено(ДоступностьПоУмолчанию) Тогда
		ДоступностьПоУмолчанию = Перечисления.ЦБР_ДоступноеВремяСотрудников.ДоступенВсегда;
	КонецЕсли;
	
	Возврат ДоступностьПоУмолчанию;
	
КонецФункции

// Возвращает значения настроек доступности для сотрудников.
// 
// Параметры:
//  Сотрудники - Массив
//   * СправочникСсылка.Сотрудники
//  Настройка - ПеречислениеСсылка.ЦБР_НастройкиРабочегоКалендаря
// 
// Возвращаемое значение:
//  ТаблицаЗначений
//   * Сотрудник - СправочникСсылка.Сотрудники - сотрудник
//   * Доступность - ПеречислениеСсылка.ДоступностьСотрудника - значение настройки для сотрудника.
// 
Функция НастройкиДоступностиСотрудников(Сотрудники) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦБР_НастройкиРабочегоКалендаря.Сотрудник КАК Сотрудник,
		|	ЦБР_НастройкиРабочегоКалендаря.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ЦБР_НастройкиРабочегоКалендаря КАК ЦБР_НастройкиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_НастройкиРабочегоКалендаря.Сотрудник В (&Сотрудники)
		|	И ЦБР_НастройкиРабочегоКалендаря.Настройка = &Настройка";
		
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("Настройка", Перечисления.ЦБР_НастройкиРабочегоКалендаря.Доступность);
	
	ЗначенияНастроек = Новый Соответствие();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗначенияНастроек[Выборка.Сотрудник] = Выборка.Значение;
	КонецЦикла;
	
	НастройкиДоступности = Новый ТаблицаЗначений();
	НастройкиДоступности.Колонки.Добавить("Сотрудник");
	НастройкиДоступности.Колонки.Добавить("Доступность");
	
	Для Каждого Сотрудник Из Сотрудники Цикл
		СтрокаТаблицы = НастройкиДоступности.Добавить();
		СтрокаТаблицы.Сотрудник = Сотрудник;
		
		СтрокаТаблицы.Доступность = ЗначенияНастроек[Сотрудник];
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Доступность) Тогда
			СтрокаТаблицы.Доступность = Перечисления.ЦБР_ДоступноеВремяСотрудников.ДоступенВсегда;
		КонецЕсли;
	КонецЦикла;
		
	Возврат НастройкиДоступности;
	
КонецФункции

// Устанавливает настройку доступности по умолчанию
//
// Параметры:
//  Сотрудник - СправочникСсылка.Сотрудники
//  ДоступностьПоУмолчанию - ПеречислениеСсылка.ДоступностьСотрудника
//  
// Возвращаемое значение:
//  Булево - значение настройки изменено.
//
Функция УстановитьНастройкуДоступностиСотрудника(Сотрудник, ДоступностьПоУмолчанию) Экспорт
	
	ИзмененоЗначениеНастройки = РегистрыСведений.ЦБР_НастройкиРабочегоКалендаря.УстановитьНастройку(
		Сотрудник,
		Перечисления.ЦБР_НастройкиРабочегоКалендаря.Доступность,
		ДоступностьПоУмолчанию);
	
	Возврат ИзмененоЗначениеНастройки;
	
КонецФункции

// Переносит элементы планировщика на указанную дату.
//
Функция ПеренестиЭлементыПланировщикаНаДату(ЗаписиКалендаря, ДатаПереноса, НастройкиОтображения = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СсылкиИзмененныхЗаписейКалендаря", Новый Массив);
	Результат.Вставить("НовыеИсключенияПовторения", Новый Массив);
	Результат.Вставить("ОбновитьПринудительно", Ложь);
	Результат.Вставить("ДанныеПланировщика", Неопределено);
	
	НачатьТранзакцию();
	Попытка
		
		НоваяДатаНачала = НачалоДня(ДатаПереноса);
		Для Каждого ЗаписьКалендаря Из ЗаписиКалендаря Цикл
			
			Если ЗаписьКалендаря.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
				
				ЗаписьКалендаряОбъект = СоздатьИсключениеПовторяющейсяЗаписиКалендаря(ЗаписьКалендаря);
				Длительность = ЗаписьКалендаряОбъект.ДатаОкончания - ЗаписьКалендаряОбъект.ДатаНачала;
				ВремяНачала = ЗаписьКалендаряОбъект.ДатаНачала - НачалоДня(ЗаписьКалендаряОбъект.ДатаНачала);
				ЗаписьКалендаряОбъект.ДатаНачала = НоваяДатаНачала + ВремяНачала;
				ЗаписьКалендаряОбъект.ДатаОкончания = ЗаписьКалендаряОбъект.ДатаНачала + Длительность;
				ЗаписьКалендаряОбъект.Записать();
				
				ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаря.Ссылка);
				ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаряОбъект);
				
				Цвет = РаботаСРабочимКалендаремСервер.ПолучитьЦветСобытияКалендаря(ЗаписьКалендаря.Ссылка);
				УстановитьЦветЗаписиКалендаря(ЗаписьКалендаряОбъект.Ссылка, Цвет);
				
				Результат.СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				Результат.СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);
				
				ИсключениеПовторения = Новый Структура("НоваяСсылка, СтараяСсылка, ДатаНачалаИсходная");
				ИсключениеПовторения.НоваяСсылка = ЗаписьКалендаряОбъект.Ссылка;
				ИсключениеПовторения.СтараяСсылка = ЗаписьКалендаря.Ссылка;
				ИсключениеПовторения.ДатаНачалаИсходная = ЗаписьКалендаря.ДатаНачалаИсходная;
				Результат.НовыеИсключенияПовторения.Добавить(ИсключениеПовторения);
				
			Иначе
				
				ЗаписьКалендаряОбъект = ЗаписьКалендаря.Ссылка.ПолучитьОбъект();
				Длительность = ЗаписьКалендаряОбъект.ДатаОкончания - ЗаписьКалендаряОбъект.ДатаНачала;
				ВремяНачала = ЗаписьКалендаряОбъект.ДатаНачала - НачалоДня(ЗаписьКалендаряОбъект.ДатаНачала);
				ЗаписьКалендаряОбъект.ДатаНачала = НоваяДатаНачала + ВремяНачала;
				ЗаписьКалендаряОбъект.ДатаОкончания = ЗаписьКалендаряОбъект.ДатаНачала + Длительность;
				ЗаписьКалендаряОбъект.Записать();
				
				ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаряОбъект);
				
				Результат.СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Результат.ОбновитьПринудительно = Истина;
	Если НастройкиОтображения <> Неопределено Тогда
		Результат.ДанныеПланировщика = ОбновитьДанныеПланировщика(НастройкиОтображения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает представление предмета
Функция ПолучитьПредставлениеПредмета(Предмет) Экспорт
	
	ПредставлениеПредмета = "";
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат ПредставлениеПредмета;
	КонецЕсли;
	
	//++ Центр
	Права = Новый Структура(
	"Добавление, Изменение, Удаление, УправлениеПравами, Чтение",
	Истина, Истина, Истина, Истина, Истина);
	ПраваПоПредмету = Права;
	//-- Центр
	
	//ПраваПоПредмету = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(
	//	Предмет,
	//	ПользователиКлиентСервер.ТекущийПользователь());
	Если Не ПраваПоПредмету.Чтение Тогда
		ПредставлениеПредмета = Строка(Предмет);
		Возврат ПредставлениеПредмета;
	КонецЕсли;
	
	//Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Предмет) Тогда
	//	РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, "Тема, ДатаОтправки");
	//	Наименование = Строка(РеквизитыПисьма.Тема) + ", " + Строка(РеквизитыПисьма.ДатаОтправки);
	//ИначеЕсли ТипЗнч(Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
	//	Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Наименование");
	//Иначе
		Наименование = Строка(Предмет);
	//КонецЕсли;
	
	//Если ТипЗнч(Предмет) = Тип("ДокументСсылка.ЦБР_Задание") Тогда
	//	ПредставлениеОбъекта = Метаданные.Документы.Задача.Синоним;
	//ИначеЕсли ЗначениеЗаполнено(Предмет.Метаданные().ПредставлениеОбъекта) Тогда 
	//	ПредставлениеОбъекта = Предмет.Метаданные().ПредставлениеОбъекта; 
	//Иначе 	
		ПредставлениеОбъекта = Предмет.Метаданные().Синоним;
	//КонецЕсли;
	
	ПредставлениеПредмета = СтрШаблон("%1 (%2)", Наименование, ПредставлениеОбъекта);
	
	Возврат ПредставлениеПредмета;
	
КонецФункции

// Заполняет список рабочих дней в соответствии с графиком пользователям. Заполняется год
// соответствующий дате заполнения и смежные с ним года. После заполнения год включается в список 
// заполненных. Если дата заполнения присутствует в списке, то заполнение рабочих не производится.
//
// Параметры:
//  РабочиеДни - СписокЗначений - Список дней, среди которых рабочие выделены пометкой.
//  ЗаполненныеГода - СписокЗначений - Список лет, для которых заполнены рабочие дни.
//  ДатаЗаполнения - Дата - Дата, на которую хотим заполнить рабочие дни.
//
Процедура ЗаполнитьРабочиеДни(РабочиеДни, ЗаполненныеГода, ДатаЗаполнения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Если рабочие дни по году уже заполнены, то не требуется заполнять рабочие дни.
	ГодЗаполнения = Год(ДатаЗаполнения);
	Если ЗаполненныеГода.НайтиПоЗначению(ГодЗаполнения) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Отбор лет, для которых требуется заполнить рабочие дни - год заполнения и смежные с ним.
	МассивЛетЗаполнения = Новый Массив;
	Если ЗаполненныеГода.НайтиПоЗначению(ГодЗаполнения) = Неопределено Тогда
		МассивЛетЗаполнения.Добавить(ГодЗаполнения);
		ЗаполненныеГода.Добавить(ГодЗаполнения);
	КонецЕсли;
	
	ПредшествующийГод = ГодЗаполнения - 1;
	Если ЗаполненныеГода.НайтиПоЗначению(ПредшествующийГод) = Неопределено Тогда
		МассивЛетЗаполнения.Добавить(ПредшествующийГод);
		ЗаполненныеГода.Добавить(ПредшествующийГод);
	КонецЕсли;
	
	СледующийГод = ГодЗаполнения + 1;
	Если ЗаполненныеГода.НайтиПоЗначению(СледующийГод) = Неопределено Тогда
		МассивЛетЗаполнения.Добавить(СледующийГод);
		ЗаполненныеГода.Добавить(СледующийГод);
	КонецЕсли;
	
	// Определение графика работы сотрудника. Если график не задан то рабочие дни не заполняем.
	ГрафикРаботы = Константы.ОсновнойКалендарьПредприятия.Получить(); //ГрафикиРаботы.ГрафикРаботыСотрудника(Сотрудники.ОсновнойСотрудник());
	Если Не ЗначениеЗаполнено(ГрафикРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение рабочие дней по данным календаря
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КалендарныеГрафики.ДатаГрафика,
		|	КалендарныеГрафики.ДеньВключенВГрафик
		|ИЗ
		|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
		|ГДЕ
		|	КалендарныеГрафики.Календарь = &Календарь
		|	И КалендарныеГрафики.Год В(&МассивЛетЗаполнения)";
	
	Запрос.УстановитьПараметр("Календарь", ГрафикРаботы);
	Запрос.УстановитьПараметр("МассивЛетЗаполнения", МассивЛетЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РабочиеДни.Добавить(Выборка.ДатаГрафика, , Выборка.ДеньВключенВГрафик);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает события сотрудника физ. лица за определенный промежуток времени.
// 
// Параметры:
//  ДатаНачала - Дата - С какой даты следует получать события.
//  ДатаОкончания - Дата - По какую дату следует получать события.
//  ФизЛицоСотрудника - СправочникСсылка.ФизическиеЛица - физ. лицо, события которого необходимо получить.
//  БезЗаписейОбОтсутствии - Булево - Без записей календаря об отсутствии.
//
Функция ПолучитьСобытияФизЛицаСотрудника(
	ДатаНачала, ДатаОкончания, ФизЛицоСотрудника, БезЗаписейОбОтсутствии = Истина) Экспорт
	
	ЗаписиКалендаряСНапоминаниями = ЗаписиКалендаряСНапоминаниямиТекущегоПользователя();
	
	СотрудникиФизЛица = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователи.ТекущийПользователь(), "ФизическоеЛицо");
	
	ТаблицаСобытий = ПолучитьТаблицуСобытий(
		ДатаНачала,
		ДатаОкончания,
		Ложь,
		СотрудникиФизЛица,
		Ложь,
		ЗаписиКалендаряСНапоминаниями);
	
	Если БезЗаписейОбОтсутствии Тогда
		КоличествоЭлементов = ТаблицаСобытий.Количество();
		Для Индекс = 1 По КоличествоЭлементов Цикл
			Строка = ТаблицаСобытий[КоличествоЭлементов - Индекс];
			Если Строка.Связанная И ТипЗнч(Строка.ЗаданиеСсылка) = Тип("ДокументСсылка.Отсутствие") Тогда
				ТаблицаСобытий.Удалить(Строка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаСобытий;
		
КонецФункции

// Формирует таблицу занятости
Функция СформироватьТаблицуЗанятости() Экспорт
	
	ТаблицаЗанятости = Новый ТаблицаЗначений;
	ТаблицаЗанятости.Колонки.Добавить("Сотрудник");
	ТаблицаЗанятости.Колонки.Добавить("ДатаНачала");
	ТаблицаЗанятости.Колонки.Добавить("ДатаОкончания");
	ТаблицаЗанятости.Колонки.Добавить("Занят");
	
	Возврат ТаблицаЗанятости;
	
КонецФункции

// Заполняет таблицу событий данными повторяющегося события.
//
Процедура ВнестиПовторяющеесяСобытиеВТаблицу(ТаблицаСобытий, ДанныеСобытия, ДатаНачала, ДатаОкончания) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверяемаяДата = НачалоДня(ДатаНачала);
	Пока ПроверяемаяДата < ДатаОкончания Цикл
		
		СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
		ЗаполнитьЗначенияСвойств(СтруктураПравилаПовторения, ДанныеСобытия);
		СтруктураПравилаПовторения.ИсключенияПовторения = ДанныеСобытия.ИсключенияПовторения.Выгрузить();
		СтруктураПравилаПовторения.ПовторениеПоДням = ДанныеСобытия.ПовторениеПоДням.Выгрузить();
		
		Если ДатаУдовлетворяетПравилуПовторения(ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
			
			ДатаНачалаЭлементаПовторения = НачалоДня(ПроверяемаяДата)
				+ (ДанныеСобытия.ДатаНачала - НачалоДня(ДанныеСобытия.ДатаНачала));
			ДатаОкончанияЭлементаПовторения = НачалоДня(ПроверяемаяДата)
				+ (ДанныеСобытия.ДатаОкончания - НачалоДня(ДанныеСобытия.ДатаНачала));
			
			Если ДатаНачала < ДатаОкончанияЭлементаПовторения И ДатаОкончания > ДатаНачалаЭлементаПовторения Тогда
				
				НоваяСтрока = ТаблицаСобытий.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСобытия);
				
				НоваяСтрока.ДатаНачала = МестноеВремя(ДатаНачалаЭлементаПовторения, ЧасовойПоясСеанса());
				НоваяСтрока.ДатаОкончания = МестноеВремя(ДатаОкончанияЭлементаПовторения, ЧасовойПоясСеанса()); 
				
				ПредставлениеМестногоЧасовогоПояса = ЧасовойПоясСеанса().Представление ;
				Если ЗначениеЗаполнено(ПредставлениеМестногоЧасовогоПояса) Тогда
					НоваяСтрока.Наименование = НоваяСтрока.Наименование + ", " + ПредставлениеМестногоЧасовогоПояса;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет таблицу событий данными выборки.
//
Процедура ЗаполнитьТаблицуПовторяющимисяСобытиями(ТаблицаСобытий, Выборка, ДатаНачала, ДатаОкончания) Экспорт
	
	Пока Выборка.Следующий() Цикл
		ВнестиПовторяющеесяСобытиеВТаблицу(ТаблицаСобытий, Выборка, ДатаНачала, ДатаОкончания);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру правила повторения.
//
Функция ПолучитьСтруктуруПравилаПовторения() Экспорт
	
	СтруктураПравилаПовторения = Новый Структура();
	СтруктураПравилаПовторения.Вставить("ДатаНачалаПовторения", Дата(1, 1, 1));
	СтруктураПравилаПовторения.Вставить("ДатаОкончанияПовторения", Дата(1, 1, 1));
	СтруктураПравилаПовторения.Вставить("ИнтервалПовторения", 0);
	СтруктураПравилаПовторения.Вставить("КоличествоПовторов", 0);
	СтруктураПравилаПовторения.Вставить("ПовторениеПоДнямМесяца", 0);
	СтруктураПравилаПовторения.Вставить("ПовторениеПоМесяцам", 0);
	СтруктураПравилаПовторения.Вставить("ПравилоОкончанияПовторения", Перечисления.ПравилаОкончанияПовторения.ПустаяСсылка());
	СтруктураПравилаПовторения.Вставить("ТипЗаписиКалендаря", Перечисления.ТипЗаписиКалендаря.ПустаяСсылка());
	СтруктураПравилаПовторения.Вставить("ЧастотаПовторения", Перечисления.ЦБР_ЧастотаПовторения.ПустаяСсылка());
	
	СтруктураПравилаПовторения.Вставить("ИсключенияПовторения", Новый ТаблицаЗначений);
	СтруктураПравилаПовторения.ИсключенияПовторения.Колонки.Добавить(
		"ДатаИсключения",
		Новый ОписаниеТипов("Дата"));
	СтруктураПравилаПовторения.ИсключенияПовторения.Колонки.Добавить(
		"ЗаписьИсключения",
		Новый ОписаниеТипов("СправочникСсылка.ЦБР_ЗаписиРабочегоКалендаря"));
	
	СтруктураПравилаПовторения.Вставить("ПовторениеПоДням", Новый ТаблицаЗначений);
	СтруктураПравилаПовторения.ПовторениеПоДням.Колонки.Добавить(
		"ДеньНедели",
		Новый ОписаниеТипов("Число"));
	СтруктураПравилаПовторения.ПовторениеПоДням.Колонки.Добавить(
		"НомерВхождения",
		Новый ОписаниеТипов("Число"));
	
	Возврат СтруктураПравилаПовторения;
	
КонецФункции

// Возвращает правило повторения "Никогда".
// 
// Возвращаемое значение:
//  Структура - Правило повторения "Никогда". См. РаботаСРабочимКалендаремСервер.ПолучитьСтруктуруПравилаПовторения().
//
Функция ПравилоПовторенияНикогда() Экспорт
	
	ПравилоПовторенияНикогда = ПолучитьСтруктуруПравилаПовторения();
	ПравилоПовторенияНикогда.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие;
	
	Возврат ПравилоПовторенияНикогда;
	
КонецФункции

// Получает данные планировщика, при необходимость сохраняя изменную настройку.
//
Функция ПолучитьДанныеПланировщика(Знач НастройкиОтображения, Знач ИзмененнаяНастройка) Экспорт
	
	СохранитьНастройку(НастройкиОтображения, ИзмененнаяНастройка);
	
	ДанныеПланировщика = ПолучитьСтруктуруДанныхПланировщика();
	ДанныеПланировщика.Элементы = ПолучитьСтруктурыЭлементовПланировщика(НастройкиОтображения);
	ДанныеПланировщика.ИнтервалыФона = ПолучитьСтруктурыИнтерваловФонаПланировщика(НастройкиОтображения);
	
	Возврат ДанныеПланировщика;
	
КонецФункции

// Получает данные планировщика, необходимые для обновления данных о записях календаря.
//
Функция ОбновитьДанныеПланировщика(Знач НастройкиОтображения, Знач ЗаписиКалендаря = Неопределено) Экспорт
	
	ДанныеПланировщика = ПолучитьСтруктуруДанныхПланировщика();
	Если ЗаписиКалендаря <> Неопределено Тогда
		ДанныеПланировщика.Элементы = ПолучитьСтруктурыЭлементовПланировщика(НастройкиОтображения, ЗаписиКалендаря);
	КонецЕсли;
	ДанныеПланировщика.ИнтервалыФона = ПолучитьСтруктурыИнтерваловФонаПланировщика(НастройкиОтображения);
	
	Возврат ДанныеПланировщика;
	
КонецФункции

// Сохраняет измененную настройку отображения.
//
Процедура СохранитьНастройку(НастройкиОтображения, ИзмененнаяНастройка) Экспорт
	
	Если Не ЗначениеЗаполнено(ИзмененнаяНастройка) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИзмененнаяНастройка = "ПериодОтображения" Тогда
		УстановитьПерсональнуюНастройку(
			"ПериодОтображенияРабочегоКалендаря",
			НастройкиОтображения.ПериодОтображения);
	КонецЕсли;
	
КонецПроцедуры

// Формирует структуру элемента записи календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СтрокаТаблицыЗначений,
//                     СправочникОбъект.ЗаписиРабочегоКалендаря,
//                     СправочникСсылка.ЗаписиРабочегоКалендаря - Данные записи календаря.
// 
// Возвращаемое значение:
//  Структура - Структура данных записи календаря.
//
Функция СформироватьЭлементЗаписиКалендаря(ЗаписьКалендаря) Экспорт
	
	ЭлементЗаписиКалендаря = Новый Структура;
	
	ЭлементЗаписиКалендаря.Вставить("ДатаНачалаИсходная", ЗаписьКалендаря.ДатаНачала);
	ЭлементЗаписиКалендаря.Вставить("УникальныйИдентификатор", Новый УникальныйИдентификатор);
	
	Возврат ЭлементЗаписиКалендаря;
	
КонецФункции

// Устанавливает условное оформление плана дня на форме.
//
// Параметры:
//  УсловноеОформление	 - УсловноеОформлениеКомпоновкиДанных	 - Содержит настройки условного оформления формы.
//
Процедура УстановитьУсловноеОформлениеПланДня(УсловноеОформление) Экспорт
	
	// Помеченные на удаление элементы.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Поля = Элемент.Поля.Элементы;
	Поля.Добавить().Поле = Новый ПолеКомпоновкиДанных("ПланДня");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПланДня.ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Шрифт", Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , , , , Истина));
	
	// Элемент дня.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Поля = Элемент.Поля.Элементы;
	Поля.Добавить().Поле = Новый ПолеКомпоновкиДанных("ПланДня");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПланДня.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветФона", ЦветаСтиля.ЦветКалендаряФонШапки);
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ЦветКалендаряПодпись);
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Шрифт", Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , Истина));
	
	// Элемент текущего дня.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Поля = Элемент.Поля.Элементы;
	Поля.Добавить().Поле = Новый ПолеКомпоновкиДанных("ПланДня");
	
	ГруппаОтбораИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбораИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПланДня.ДатаДень");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДатаСегодня");
	
	ОтборЭлемента = ГруппаОтбораИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПланДня.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветФона", ЦветаСтиля.ЦветКалендаряФонТекущегоДня);
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ЦветКалендаряПодписьТекущийДень);
	
КонецПроцедуры

// Подключает напоминание автоматически для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря - СправочникСсылка.ЗаписиРабочегоКалендаря - Запись календаря.
//
// Возвращаемое значение:
//  Структура - Напоминание текущего пользователя.
//
Функция ПодключитьНапоминаниеАвтоматически(ЗаписьКалендаря) Экспорт
	
	Напоминание = Неопределено;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя") Тогда
		Возврат Напоминание;
	КонецЕсли;
	
	Пользователь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьКалендаря, "Пользователь");
	Пользователь = Пользователи.ТекущийПользователь();
	ИмяПользователяИБ =Пользователь.Имя;
	
	УстанавливатьНапоминаниеАвтоматически =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний",
			"УстанавливатьНапоминаниеАвтоматически",
			Истина,
			,
			ИмяПользователяИБ);
	Если Не УстанавливатьНапоминаниеАвтоматически Тогда
		Возврат Напоминание;
	КонецЕсли;
	
	ДатаНачала = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьКалендаря, "ДатаНачала");
	Если ДатаНачала < ТекущаяДатаСеанса() Тогда
		Возврат Напоминание;
	КонецЕсли;
	
	СрокНапоминанияПоУмолчанию =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний",
			"СрокНапоминанияПоУмолчанию",
			15,
			,
			ИмяПользователяИБ);
	
	//@skip-check many-actual-parameters
	Напоминание = НапоминанияПользователяСлужебный.ПодключитьНапоминаниеДоВремениПредмета(
		Строка(ЗаписьКалендаря),
		СрокНапоминанияПоУмолчанию * 60,
		ЗаписьКалендаря,
		"ДатаНачала",,
		Пользователь);
	
	Если Пользователь <> Пользователи.ТекущийПользователь() Тогда
		Напоминание = Неопределено;
	КонецЕсли;
	
	Возврат Напоминание;
	
КонецФункции


// Возвращает признак отработана для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СправочикСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
// 
// Возвращаемое значение:
//  Булево - Признак того что запись календаря отработана.
//
Функция ЗаписьКалендаряОтработана(ЗаписьКалендаря) Экспорт
	
	Если ЗаписьКалендаря.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Завершен Тогда   
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает признак приступил для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СправочикСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
// 
// Возвращаемое значение:
//  Булево - Признак того что запись календаря отработана.
//
Функция ЗаписьКалендаряПриступил(ЗаписьКалендаря) Экспорт
	
	Если ЗаписьКалендаря.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал Тогда   
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Отмечает записи календаря как отработанные.
//
Функция ОтработатьВыделенныеЗаписиКалендаря(Знач ЗаписиКалендаря) Экспорт
	
	СсылкиИзмененныхЗаписейКалендаря = Новый Массив;
	ОтключенныеНапоминания = Новый Массив;
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ЗаписьКалендаря Из ЗаписиКалендаря Цикл
			
			Если ЭтоПовторяющеесяСобытие(ЗаписьКалендаря) Тогда
				
				ЗаписьКалендаряОбъект = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
				ЗаписьКалендаряОбъект.Заполнить(ЗаписьКалендаря.Ссылка);
				ЗаполнитьЗначенияСвойств(ЗаписьКалендаряОбъект, ЗаписьКалендаря, "ДатаНачала, ДатаОкончания");
				ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("ПовторяющеесяСобытие", ЗаписьКалендаря.Ссылка);
				ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("ДатаИсключения", ЗаписьКалендаря.ДатаНачалаИсходная);
				ЗаписьКалендаряОбъект.Записать();
				
				ОтработатьЗаписьКалендаря(ЗаписьКалендаряОбъект.Ссылка, ОтключенныеНапоминания);
				
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);
				
			Иначе
				
				ОтработатьЗаписьКалендаря(ЗаписьКалендаря.Ссылка, ОтключенныеНапоминания);
				
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Результат = Новый Структура;
	Результат.Вставить("СсылкиИзмененныхЗаписейКалендаря", СсылкиИзмененныхЗаписейКалендаря);
	Результат.Вставить("ОтключенныеНапоминания", ОтключенныеНапоминания);
	
	Возврат Результат;
	
КонецФункции

// Отмечает записи календаря как приступил.
//
Функция ПриступитьВыделенныеЗаписиКалендаря(Знач ЗаписиКалендаря) Экспорт
	
	СсылкиИзмененныхЗаписейКалендаря = Новый Массив;
	ОтключенныеНапоминания = Новый Массив;

	НачатьТранзакцию();
	Попытка

		Если ЗаписиКалендаря.Количество() И ЕстьДействиеВРаботе(ЗаписиКалендаря[0].ЗаданиеСсылка) Тогда
			ОбщегоНазначения.СообщитьПользователю("По этому задание уже есть стартованное действие!");
			Результат = Новый Структура;
			Результат.Вставить("СсылкиИзмененныхЗаписейКалендаря", СсылкиИзмененныхЗаписейКалендаря);
			Результат.Вставить("ОтключенныеНапоминания", ОтключенныеНапоминания);
			Возврат Результат;
		КонецЕсли;

		Для Каждого ЗаписьКалендаря Из ЗаписиКалендаря Цикл

			Если ЭтоПовторяющеесяСобытие(ЗаписьКалендаря) Тогда

				ЗаписьКалендаряОбъект = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
				ЗаписьКалендаряОбъект.Заполнить(ЗаписьКалендаря.Ссылка);
				ЗаполнитьЗначенияСвойств(ЗаписьКалендаряОбъект, ЗаписьКалендаря, "ДатаНачала, ДатаОкончания");
				ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("ПовторяющеесяСобытие", ЗаписьКалендаря.Ссылка);
				ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("ДатаИсключения",
					ЗаписьКалендаря.ДатаНачалаИсходная);
				ЗаписьКалендаряОбъект.Записать();

				ПриступитьЗаписьКалендаря(ЗаписьКалендаряОбъект.Ссылка, ОтключенныеНапоминания);

				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаряОбъект.Ссылка);
				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);

			Иначе

				ПриступитьЗаписьКалендаря(ЗаписьКалендаря.Ссылка, ОтключенныеНапоминания);

				СсылкиИзмененныхЗаписейКалендаря.Добавить(ЗаписьКалендаря.Ссылка);

			КонецЕсли;

		КонецЦикла;

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

	Результат = Новый Структура;
	Результат.Вставить("СсылкиИзмененныхЗаписейКалендаря", СсылкиИзмененныхЗаписейКалендаря);
	Результат.Вставить("ОтключенныеНапоминания", ОтключенныеНапоминания);

	Возврат Результат;
	
КонецФункции


// Устанавливает признак отработана для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СправочникСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
//  Отработана		 - Булево									 - Новый признак отработки.
//
Процедура УстановитьОтработанаЗаписьКалендаря(ЗаписьКалендаря) Экспорт

	ЗаписьКалендаря_Объект = ЗаписьКалендаря.ПолучитьОбъект();
	ЗаписьКалендаря_Объект.Статус =  Перечисления.ЦБР_СтатусЗаписиКалендаря.Завершен;
	ЗаписьКалендаря_Объект.Записать(); 
		
КонецПроцедуры

// Устанавливает признак отработана для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СправочникСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
//  Отработана		 - Булево									 - Новый признак отработки.
//
Процедура УстановитьОтмененаЗаписьКалендаря(ЗаписьКалендаря) Экспорт
	
	ЗаписьКалендаря_Объект = ЗаписьКалендаря.ПолучитьОбъект();
	ЗаписьКалендаря_Объект.Статус =  Перечисления.ЦБР_СтатусЗаписиКалендаря.Отмена;
	ЗаписьКалендаря_Объект.Записать(); 
		
КонецПроцедуры

// Устанавливает признак приступил для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СправочникСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
//  Приступил		 - Булево									 - Новый признак отработки.
//
Процедура УстановитьПриступилЗаписьКалендаря(ЗаписьКалендаря, Приступил = Истина) Экспорт
	
	ЗаписьКалендаря_Объект = ЗаписьКалендаря.ПолучитьОбъект();   
	Длительность = ЗаписьКалендаря_Объект.ДатаОкончания - ЗаписьКалендаря_Объект.ДатаНачала;
	ЗаписьКалендаря_Объект.ДатаНачала 	= ТекущаяДата();
	ЗаписьКалендаря_Объект.Цвет 		= Перечисления.ЦБР_ЦветаРабочегоКалендаря.Зеленый;
	ЗаписьКалендаря_Объект.Статус 		=  Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал;
	ЗаписьКалендаря_Объект.ДатаОкончания = ЗаписьКалендаря_Объект.ДатаНачала + Длительность;
	ЗаписьКалендаря_Объект.Записать(); 
		
КонецПроцедуры      



// Проверяет подходит ли дата под правило повторения
Функция ДатаУдовлетворяетПравилуПовторения(ПроверяемаяДата, СтруктураПравилаПовторения, УчитыватьИсключения = Истина) Экспорт
	
	Если ПроверяемаяДата < СтруктураПравилаПовторения.ДатаНачалаПовторения Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураПравилаПовторения.ДатаОкончанияПовторения)
		И ПроверяемаяДата > СтруктураПравилаПовторения.ДатаОкончанияПовторения
		И УчитыватьИсключения Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если УчитыватьИсключения И ЯвляетсяИсключениемПовторения(ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПроверяемаяДата = СтруктураПравилаПовторения.ДатаНачалаПовторения Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не УдовлетворяетПравилуИнтервалаПовторения(ПроверяемаяДата, СтруктураПравилаПовторения)
		Или Не УдовлетворяетПравилуПовторенияПоДням(ПроверяемаяДата, СтруктураПравилаПовторения)
		Или Не УдовлетворяетПравилуПовторенияПоДнямМесяца(ПроверяемаяДата, СтруктураПравилаПовторения)
		Или Не УдовлетворяетПравилуПовторенияПоМесяцам(ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Правила повторения записей календаря.
//
// Параметры:
//  ЗаписиКалендаря - Массив из СправочникСсылка.ЗаписиКалендаря - Записи календаря.
// 
// Возвращаемое значение:
//  Соответствие - Правила повторения записей календаря.
//
Функция ПравилаПовторения(ЗаписиКалендаря) Экспорт
	
	ПравилаПовторения = Новый Соответствие;
	Если ЗаписиКалендаря.Количество() = 0 Тогда
		Возврат ПравилаПовторения;
	КонецЕсли;
	
	Для Каждого ЗаписьКалендаря Из ЗаписиКалендаря Цикл
		ПравилаПовторения.Вставить(ЗаписьКалендаря, ПравилоПовторенияНикогда());
	КонецЦикла;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК ЗаписьКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения КАК ДатаНачалаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения КАК ДатаОкончанияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИнтервалПовторения КАК ИнтервалПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.КоличествоПовторов КАК КоличествоПовторов,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДнямМесяца КАК ПовторениеПоДнямМесяца,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоМесяцам КАК ПовторениеПоМесяцам,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПравилоОкончанияПовторения КАК ПравилоОкончанияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря КАК ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЧастотаПовторения КАК ЧастотаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИсключенияПовторения.(
		|		ДатаИсключения КАК ДатаИсключения,
		|		ЗаписьИсключения КАК ЗаписьИсключения
		|	) КАК ИсключенияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДням.(
		|		ДеньНедели КАК ДеньНедели,
		|		НомерВхождения КАК НомерВхождения
		|	) КАК ПовторениеПоДням
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка В(&ЗаписиКалендаря)");
	
	Запрос.УстановитьПараметр("ЗаписиКалендаря", ЗаписиКалендаря);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПравилоПовторения = ПравилаПовторения[Выборка.ЗаписьКалендаря];
		
		ЗаполнитьЗначенияСвойств(
			ПравилоПовторения,
			Выборка,
			"ДатаНачалаПовторения, ДатаОкончанияПовторения, ИнтервалПовторения, КоличествоПовторов,
			|ПовторениеПоДнямМесяца, ПовторениеПоМесяцам, ПравилоОкончанияПовторения, ТипЗаписиКалендаря,
			|ЧастотаПовторения");
		
		Для Каждого СтрокаИсключенияПовторения Из Выборка.ИсключенияПовторения.Выгрузить() Цикл
			ЗаполнитьЗначенияСвойств(
				ПравилоПовторения.ИсключенияПовторения.Добавить(),
				СтрокаИсключенияПовторения,
				"ДатаИсключения, ЗаписьИсключения");
		КонецЦикла;
		
		Для Каждого СтрокаПовторенияПоДня Из Выборка.ПовторениеПоДням.Выгрузить() Цикл
			ЗаполнитьЗначенияСвойств(
				ПравилоПовторения.ПовторениеПоДням.Добавить(),
				СтрокаПовторенияПоДня,
				"ДеньНедели, НомерВхождения");
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПравилаПовторения;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// Формирует новую структуру данных записи календаря по предмету.
// 
// Возвращаемое значение:
//  Структура - Данные записи календаря по предмету.
//   * Предмет - ДокументССылка.ЦБР_Задание.
//   * Описание - Строка.
//   * ДатаНачала - Дата.
//   * ДатаОкончания - Дата.
//   * ВесьДень - Булево.
//   * Календарь - СправочникСсылка.Пользователи, СправочникСсылка.Сотрудники.
//   * Состояние - ПеречислениеСсылка.СостоянияЗаписейРабочегоКалендаря.
//   * Связанная - Булево.
//   * ПодключатьНапоминаниеАвтоматически - Булево.
//   * ОповещатьОбИзменении - Булево.
//  
Функция НовыйДанныеЗаписиКалендаряПоПредмету() Экспорт
	
	ДанныеЗаписиКалендаряПоПредмету = Новый Структура;
	ДанныеЗаписиКалендаряПоПредмету.Вставить("Предмет", Неопределено);
	ДанныеЗаписиКалендаряПоПредмету.Вставить("Описание", "");
	ДанныеЗаписиКалендаряПоПредмету.Вставить("ДатаНачала", Дата(1, 1, 1));
	ДанныеЗаписиКалендаряПоПредмету.Вставить("ДатаОкончания", Дата(1, 1, 1));
	ДанныеЗаписиКалендаряПоПредмету.Вставить("ВесьДень", Ложь);
	ДанныеЗаписиКалендаряПоПредмету.Вставить("Пользователь", Неопределено);
	ДанныеЗаписиКалендаряПоПредмету.Вставить("Состояние",
		Перечисления.СостоянияЗаписейРабочегоКалендаря.ПустаяСсылка());
	ДанныеЗаписиКалендаряПоПредмету.Вставить("Связанная", Истина);
	ДанныеЗаписиКалендаряПоПредмету.Вставить("ПодключатьНапоминаниеАвтоматически", Ложь);
	ДанныеЗаписиКалендаряПоПредмету.Вставить("ОповещатьОбИзменении", Ложь);
	
	Возврат ДанныеЗаписиКалендаряПоПредмету;
	
КонецФункции

Процедура СоздатьЗаписьКалендаряМультиОС(ДанныеЗаполнения) Экспорт

	ОС = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(ОС, ДанныеЗаполнения);
	ОС.МультиОС = Истина;
	ОС.Записать();  
	
КонецПроцедуры
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет является ли запись календаря повторяющейся.
//
Функция ЭтоПовторяющеесяСобытие(ЗаписьКалендаря)
	
	Возврат ЗаписьКалендаря.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие;
	
КонецФункции

// Возвращает факт того что установлено напоминание.
//
Функция УстановленоНапоминание(ЗаписьКалендаря)
	
	Если ТипЗнч(ЗаписьКалендаря) = Тип("СправочникСсылка.ЦБР_ЗаписиРабочегоКалендаря") Тогда
		МассивЗаписейКалендаря = Новый Массив;
		МассивЗаписейКалендаря.Добавить(ЗаписьКалендаря);
	Иначе
		МассивЗаписейКалендаря = ЗаписьКалендаря;
	КонецЕсли;
	
	УстановленоНапоминание = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НЕ НапоминанияПользователя.Пользователь ЕСТЬ NULL КАК УстановленоНапоминание
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
		|		ПО ЦБР_ЗаписиРабочегоКалендаря.Ссылка = НапоминанияПользователя.Источник
		|			И (НапоминанияПользователя.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка В (&МассивЗаписейКалендаря)";
	
	Запрос.УстановитьПараметр("МассивЗаписейКалендаря", МассивЗаписейКалендаря);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.УстановленоНапоминание Тогда
			УстановленоНапоминание = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат УстановленоНапоминание;
	
КонецФункции

// Формирует пустую таблицу событий.
//
Функция СформироватьТаблицуСобытий()
	
	ТаблицаСобытий = Новый ТаблицаЗначений;
	ТаблицаСобытий.Колонки.Добавить("Ссылка");
	ТаблицаСобытий.Колонки.Добавить("Наименование");
	ТаблицаСобытий.Колонки.Добавить("ДатаНачала");
	ТаблицаСобытий.Колонки.Добавить("ДатаОкончания");
	ТаблицаСобытий.Колонки.Добавить("ВесьДень");
	ТаблицаСобытий.Колонки.Добавить("Цвет");
	ТаблицаСобытий.Колонки.Добавить("ПометкаУдаления");
	ТаблицаСобытий.Колонки.Добавить("ТипЗаписиКалендаря");
	ТаблицаСобытий.Колонки.Добавить("Состояние");
	ТаблицаСобытий.Колонки.Добавить("УстановленоНапоминание", Новый ОписаниеТипов("Булево"));
	ТаблицаСобытий.Колонки.Добавить("Пользователь");
	ТаблицаСобытий.Колонки.Добавить("ЗаданиеСсылка");
	ТаблицаСобытий.Колонки.Добавить("Связанная");
	ТаблицаСобытий.Колонки.Добавить("Отработана", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаСобытий;
	
КонецФункции

// Получает настройку отображения времени элементов.
//
// Возвращаемое значение:
//  ОтображениеВремениЭлементовПланировщика - Настройка отображения времени элементов
//
Функция ПолучитьНастройкуОтображениеВремениЭлементов()
	
	ОтображениеВремениЭлементов =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображениеВремениЭлементов");
	
	Если ОтображениеВремениЭлементов = Перечисления.ЦБР_ОтображениеВремениЭлементов.НеОтображать Тогда
		НастройкаОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.НеОтображать;
	ИначеЕсли ОтображениеВремениЭлементов = Перечисления.ЦБР_ОтображениеВремениЭлементов.ВремяНачала Тогда
		НастройкаОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.ВремяНачала;
	ИначеЕсли ОтображениеВремениЭлементов = Перечисления.ЦБР_ОтображениеВремениЭлементов.ВремяНачалаИКонца Тогда
		НастройкаОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.ВремяНачалаИКонца;
	Иначе
		НастройкаОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.ВремяНачалаИКонца;
	КонецЕсли;
	
	Возврат НастройкаОтображениеВремениЭлементов;
	
КонецФункции

// Загружает данные планировщика.
//
// Параметры:
//  Планировщик - Планировщик - Планировщик.
//  НастройкиОтображения - Структура - Структура настроек отображения календаря.
//
Процедура ЗагрузитьДанныеПланировщика(Планировщик, НастройкиОтображения)
	
	Планировщик.Элементы.Очистить();
	Планировщик.ИнтервалыФона.Очистить();
	Планировщик.НачалоПериодаОтображения = Неопределено;
	Планировщик.КонецПериодаОтображения = Неопределено;
	
	ПериодОтображения = РаботаСРабочимКалендаремКлиентСервер.ПолучитьПериодОтображения(НастройкиОтображения);
	ВыделенныеДаты = РаботаСРабочимКалендаремКлиентСервер.ПолучитьВыделенныеДаты(НастройкиОтображения);
	
	// События
	Если НастройкиОтображения.ОтображатьСобытия Тогда
		
		// Получение отображаемых данных
		ЗаписиКалендаряСНапоминаниями = ЗаписиКалендаряСНапоминаниямиТекущегоПользователя();
		СобытияПоЧасам = СформироватьТаблицуСобытий();
		Для Каждого ВыделеннаяДата Из ВыделенныеДаты Цикл
			ОтображаемаяДатаНачала =
				РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(
					ПериодОтображения,
					ВыделеннаяДата);
			Если ЗначениеЗаполнено(Планировщик.НачалоПериодаОтображения) Тогда
				Планировщик.НачалоПериодаОтображения = Мин(
					Планировщик.НачалоПериодаОтображения,
					ОтображаемаяДатаНачала);
			Иначе
				Планировщик.НачалоПериодаОтображения = ОтображаемаяДатаНачала;
			КонецЕсли;
			
			ОтображаемаяДатаОкончания =
				РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(
					ПериодОтображения,
					ВыделеннаяДата);
			Если ЗначениеЗаполнено(Планировщик.КонецПериодаОтображения) Тогда
				Планировщик.КонецПериодаОтображения = Мин(
					Планировщик.КонецПериодаОтображения,
					ОтображаемаяДатаОкончания);
			Иначе
				Планировщик.КонецПериодаОтображения = ОтображаемаяДатаОкончания;
			КонецЕсли;
			
			СобытияНаДату = ПолучитьТаблицуСобытий(
				ОтображаемаяДатаНачала,
				ОтображаемаяДатаОкончания,
				НастройкиОтображения.ОтображатьПомеченныеНаУдаление,
				НастройкиОтображения.Пользователи,
				НастройкиОтображения.ОтображатьОтклоненные,
				ЗаписиКалендаряСНапоминаниями);
			Для Каждого Событие Из СобытияНаДату Цикл
				НоваяСтрока = СобытияПоЧасам.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Событие);
			КонецЦикла;
		КонецЦикла;
		
		// Заполнение элементов планировщика - События
		Для Каждого Событие Из СобытияПоЧасам Цикл
			Элемент = Планировщик.Элементы.Добавить(Событие.ДатаНачала, Событие.ДатаОкончания);
			СтруктураЭлемента = ПолучитьСтруктуруЭлементаПланировщика(
				Событие, НастройкиОтображения.ИспользоватьНапоминания,
				НастройкиОтображения.ОтключеноОтображениеВремени);
			Элемент.Загрузить(СтруктураЭлемента);
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

// Формирует таблицу событий
Функция ПолучитьТаблицуСобытий(
	ОтображаемаяДатаНачала,
	ОтображаемаяДатаОкончания,
	ОтображатьПомеченныеНаУдаление,
	МассивПользователей,
	ОтображатьОтклоненные,
	ЗаписиКалендаряСНапоминаниями,
	ЗаписиКалендаря = Неопределено)
	
	// Для возможности добавления в дальнейшем ссылок на мероприятия не используется Выгрузить.
	ТаблицаСобытий = СформироватьТаблицуСобытий();
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Наименование КАК Наименование,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала КАК ДатаНачала,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания КАК ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.ВесьДень КАК ВесьДень,
		|	ЕСТЬNULL(ЦБР_ЗаписиРабочегоКалендаря.Цвет, ЗНАЧЕНИЕ(Перечисление.ЦБР_ЦветаРабочегоКалендаря.Нет)) КАК Цвет,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления КАК ПометкаУдаления,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря КАК ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.Состояние КАК Состояние,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка КАК ЗаданиеСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Связанная КАК Связанная,
		|	ВЫБОР
		|		КОГДА ЦБР_ЗаписиРабочегоКалендаря.Статус = ЗНАЧЕНИЕ(Перечисление.ЦБР_СтатусЗаписиКалендаря.Завершен)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Отработана
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания > &ДатаНачала
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала < &ДатаОкончания
		|	И ЦБР_ЗаписиРабочегоКалендаря.Пользователь В(&МассивПользователей)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря <> ЗНАЧЕНИЕ(Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Ссылка В(&ЗаписиКалендаря)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Статус <> ЗНАЧЕНИЕ(Перечисление.ЦБР_СтатусЗаписиКалендаря.Отмена)";
	
	Если Не ОтображатьПомеченныеНаУдаление Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ";
	КонецЕсли;
	
	Если ОтображатьОтклоненные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "	И ЦБР_ЗаписиРабочегоКалендаря.Статус <> ЗНАЧЕНИЕ(Перечисление.ЦБР_СтатусЗаписиКалендаря.Отмена)", "");
	КонецЕсли;
	
	Если ЗаписиКалендаря <> Неопределено Тогда
		Запрос.УстановитьПараметр("ЗаписиКалендаря", ЗаписиКалендаря);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "	И ЦБР_ЗаписиРабочегоКалендаря.Ссылка В(&ЗаписиКалендаря)", "");
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаНачала";
	
	Запрос.УстановитьПараметр("ДатаНачала", ОтображаемаяДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ОтображаемаяДатаОкончания);
	Запрос.УстановитьПараметр("МассивПользователей", МассивПользователей);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаСобытий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Выборка.Связанная
			И Не Выборка.ВесьДень Тогда
			
			НоваяСтрока.ДатаНачала = Выборка.ДатаНачала;
			НоваяСтрока.ДатаОкончания = Выборка.ДатаОкончания;
						
		ИначеЕсли Не Выборка.Связанная И Не Выборка.ВесьДень Тогда
			
			НоваяСтрока.ДатаНачала = Выборка.ДатаНачала;
			НоваяСтрока.ДатаОкончания = Выборка.ДатаОкончания;
						
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьВТаблицуСобытийПовторяющиесяСобытия(
		ТаблицаСобытий,
		ОтображаемаяДатаНачала,
		ОтображаемаяДатаОкончания,
		МассивПользователей,
		ОтображатьОтклоненные,
		ЗаписиКалендаря);
	
	Для Каждого СтрокаСобытия Из ТаблицаСобытий Цикл
		СтрокаСобытия.УстановленоНапоминание = ЗаписиКалендаряСНапоминаниями[СтрокаСобытия.Ссылка] = Истина;
	КонецЦикла;
	
	ТаблицаСобытий.Сортировать("ДатаНачала");
		
	Возврат ТаблицаСобытий;
	
КонецФункции

// Включает в таблицу событий повторяющиеся события
Процедура ДобавитьВТаблицуСобытийПовторяющиесяСобытия(
	ТаблицаСобытий,
	ОтображаемаяДатаНачала,
	ОтображаемаяДатаОкончания,
	МассивПользователей,
	ОтображатьОтклоненные,
	ЗаписиКалендаря)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Наименование КАК Наименование,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала КАК ДатаНачала,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания КАК ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.ВесьДень КАК ВесьДень,
		|	ЕСТЬNULL(ЦБР_ЗаписиРабочегоКалендаря.Цвет, ЗНАЧЕНИЕ(Перечисление.ЦБР_ЦветаРабочегоКалендаря.Нет)) КАК Цвет,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления КАК ПометкаУдаления,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря КАК ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИсключенияПовторения.(
		|		ДатаИсключения КАК ДатаИсключения,
		|		ЗаписьИсключения КАК ЗаписьИсключения
		|	) КАК ИсключенияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДням.(
		|		ДеньНедели КАК ДеньНедели,
		|		НомерВхождения КАК НомерВхождения
		|	) КАК ПовторениеПоДням,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения КАК ДатаНачалаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения КАК ДатаОкончанияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИнтервалПовторения КАК ИнтервалПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.КоличествоПовторов КАК КоличествоПовторов,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДнямМесяца КАК ПовторениеПоДнямМесяца,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоМесяцам КАК ПовторениеПоМесяцам,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЧастотаПовторения КАК ЧастотаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.Состояние КАК Состояние,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка КАК ЗаданиеСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Связанная КАК Связанная
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения >= &ДатаНачала
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения < &ДатаОкончания
		|	И ЦБР_ЗаписиРабочегоКалендаря.Пользователь В(&МассивПользователей)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря = ЗНАЧЕНИЕ(Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Ссылка В(&ЗаписиКалендаря)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.Отклонено)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Статус = ЗНАЧЕНИЕ(Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Наименование,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.ВесьДень,
		|	ЕСТЬNULL(ЦБР_ЗаписиРабочегоКалендаря.Цвет, ЗНАЧЕНИЕ(Перечисление.ЦБР_ЦветаРабочегоКалендаря.Нет)),
		|	ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИсключенияПовторения.(
		|		ДатаИсключения,
		|		ЗаписьИсключения
		|	),
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДням.(
		|		ДеньНедели,
		|		НомерВхождения
		|	),
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИнтервалПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.КоличествоПовторов,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДнямМесяца,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоМесяцам,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЧастотаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.Состояние,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Связанная
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения = ДАТАВРЕМЯ(1, 1, 1)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения < &ДатаОкончания
		|	И ЦБР_ЗаписиРабочегоКалендаря.Пользователь В(&МассивПользователей)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря = ЗНАЧЕНИЕ(Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Ссылка В(&ЗаписиКалендаря)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.Отклонено)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Статус = ЗНАЧЕНИЕ(Перечисление.ЦБР_СтатусЗаписиКалендаря.Завершен)";
	
	Если ОтображатьОтклоненные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "	И ЦБР_ЗаписиРабочегоКалендаря.Статус <> ЗНАЧЕНИЕ(Перечисление.ЦБР_СтатусЗаписиКалендаря.Отмена)", "");
	КонецЕсли;
	
	Если ЗаписиКалендаря <> Неопределено Тогда
		Запрос.УстановитьПараметр("ЗаписиКалендаря", ЗаписиКалендаря);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ЦБР_ЗаписиРабочегоКалендаря.Ссылка В(&ЗаписиКалендаря)", "");   
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаНачала", ОтображаемаяДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ОтображаемаяДатаОкончания);
	Запрос.УстановитьПараметр("МассивПользователей", МассивПользователей);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	ЗаполнитьТаблицуПовторяющимисяСобытиями(
		ТаблицаСобытий,
		Выборка,
		ОтображаемаяДатаНачала,
		ОтображаемаяДатаОкончания);
	
КонецПроцедуры


// Проверяет подходит ли дата под правило повторения по интервалам
Функция УдовлетворяетПравилуИнтервалаПовторения(ПроверяемаяДата, СтруктураПравилаПовторения)
	
	Если СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежедневно Тогда
		
		ПроверяемыйИнтервалВремя =
			НачалоДня(ПроверяемаяДата) - НачалоДня(СтруктураПравилаПовторения.ДатаНачалаПовторения);
		НомерПериодаИнтервала = ПроверяемыйИнтервалВремя / 86400; // 86400 - число секунд в сутках
		
	ИначеЕсли СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Еженедельно Тогда
		
		ПроверяемыйИнтервалВремя =
			НачалоНедели(ПроверяемаяДата) - НачалоНедели(СтруктураПравилаПовторения.ДатаНачалаПовторения);
		НомерПериодаИнтервала = ПроверяемыйИнтервалВремя / 604800; // 604800 - число секунд в неделе
		
	ИначеЕсли СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежемесячно Тогда
		
		КоличествоЛетИнтервала =
			Год(ПроверяемаяДата) - Год(СтруктураПравилаПовторения.ДатаНачалаПовторения);
		НомерПериодаИнтервала =
			Месяц(ПроверяемаяДата) - Месяц(СтруктураПравилаПовторения.ДатаНачалаПовторения) + КоличествоЛетИнтервала * 12;
		
	ИначеЕсли СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежегодно Тогда
		
		НомерПериодаИнтервала =
			Год(ПроверяемаяДата) - Год(СтруктураПравилаПовторения.ДатаНачалаПовторения);
		
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Возврат НомерПериодаИнтервала % СтруктураПравилаПовторения.ИнтервалПовторения = 0;
	
КонецФункции

// Проверяет подходит ли дата под правило повторения по дням
Функция УдовлетворяетПравилуПовторенияПоДням(ПроверяемаяДата, СтруктураПравилаПовторения)
	
	Если СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Еженедельно Тогда
		
		Для Каждого ПовторениеПоДнямСтрока Из СтруктураПравилаПовторения.ПовторениеПоДням Цикл
			
			Если ПовторениеПоДнямСтрока.НомерВхождения <> 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПовторениеПоДнямСтрока.ДеньНедели = ДеньНедели(ПроверяемаяДата) Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Ложь;
		
	ИначеЕсли СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежемесячно Тогда
		
		Если ЗначениеЗаполнено(СтруктураПравилаПовторения.ПовторениеПоДнямМесяца) Тогда
			Возврат Истина;
		Иначе
			
			Если СтруктураПравилаПовторения.ПовторениеПоДням.Количество() = 0 Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Для Каждого ПовторениеПоДнямСтрока Из СтруктураПравилаПовторения.ПовторениеПоДням Цикл
				
				Если ПовторениеПоДнямСтрока.НомерВхождения = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ПроверяемаяДатаДеньНеделиВМесяце = 
					РаботаСРабочимКалендаремКлиентСервер.ПолучитьДеньНеделиВМесяце(ПроверяемаяДата);
				ПроверяемаяДатаДеньНеделиВМесяцеБезУчетаПоследнего = 
					РаботаСРабочимКалендаремКлиентСервер.ПолучитьДеньНеделиВМесяце(ПроверяемаяДата, Ложь);
				
				Если (ПовторениеПоДнямСтрока.ДеньНедели = ПроверяемаяДатаДеньНеделиВМесяце.ДеньНедели
						И ПовторениеПоДнямСтрока.НомерВхождения = ПроверяемаяДатаДеньНеделиВМесяце.НомерВхождения)
					ИЛИ (ПовторениеПоДнямСтрока.ДеньНедели = ПроверяемаяДатаДеньНеделиВМесяцеБезУчетаПоследнего.ДеньНедели
						И ПовторениеПоДнямСтрока.НомерВхождения = ПроверяемаяДатаДеньНеделиВМесяцеБезУчетаПоследнего.НомерВхождения) Тогда
					Возврат Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежегодно Тогда
		
		Если ЗначениеЗаполнено(СтруктураПравилаПовторения.ПовторениеПоДнямМесяца) Тогда
			Возврат Истина;
		Иначе
			
			Если СтруктураПравилаПовторения.ПовторениеПоДням.Количество() = 0 Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Для Каждого ПовторениеПоДнямСтрока Из СтруктураПравилаПовторения.ПовторениеПоДням Цикл
				
				Если ПовторениеПоДнямСтрока.НомерВхождения = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ПроверяемаяДатаДеньНеделиВМесяце = 
					РаботаСРабочимКалендаремКлиентСервер.ПолучитьДеньНеделиВМесяце(ПроверяемаяДата);
				ПроверяемаяДатаДеньНеделиВМесяцеБезУчетаПоследнего = 
					РаботаСРабочимКалендаремКлиентСервер.ПолучитьДеньНеделиВМесяце(ПроверяемаяДата, Ложь);
				
				Если (ПовторениеПоДнямСтрока.ДеньНедели = ПроверяемаяДатаДеньНеделиВМесяце.ДеньНедели
						И ПовторениеПоДнямСтрока.НомерВхождения = ПроверяемаяДатаДеньНеделиВМесяце.НомерВхождения)
					Или (ПовторениеПоДнямСтрока.ДеньНедели = ПроверяемаяДатаДеньНеделиВМесяцеБезУчетаПоследнего.ДеньНедели
						И ПовторениеПоДнямСтрока.НомерВхождения = ПроверяемаяДатаДеньНеделиВМесяцеБезУчетаПоследнего.НомерВхождения) Тогда
					Возврат Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет подходит ли дата под правило повторения по дням месяца
Функция УдовлетворяетПравилуПовторенияПоДнямМесяца(ПроверяемаяДата, СтруктураПравилаПовторения)
	
	Если СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежемесячно Тогда
		
		Если Не ЗначениеЗаполнено(СтруктураПравилаПовторения.ПовторениеПоДнямМесяца) Тогда
			Возврат Истина;
		КонецЕсли;
		
		МассивПовторениеПоДнямМесяца =
			РаботаСРабочимКалендаремКлиентСервер.ЧислоПовторениеПоДнямМесяцаВМассив(
				СтруктураПравилаПовторения.ПовторениеПоДнямМесяца);
		Если МассивПовторениеПоДнямМесяца.Найти(День(ПроверяемаяДата)) <> Неопределено  Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	ИначеЕсли СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежегодно Тогда
		
		Если Не ЗначениеЗаполнено(СтруктураПравилаПовторения.ПовторениеПоДнямМесяца) Тогда
			Возврат Истина;
		КонецЕсли;
		
		МассивПовторениеПоДнямМесяца =
			РаботаСРабочимКалендаремКлиентСервер.ЧислоПовторениеПоДнямМесяцаВМассив(
				СтруктураПравилаПовторения.ПовторениеПоДнямМесяца);
		Если МассивПовторениеПоДнямМесяца.Найти(День(ПроверяемаяДата)) <> Неопределено  Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет подходит ли дата под правило повторения по месяцам
Функция УдовлетворяетПравилуПовторенияПоМесяцам(ПроверяемаяДата, СтруктураПравилаПовторения)
	
	Если СтруктураПравилаПовторения.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежегодно Тогда
		
		МассивПовторениеПоМесяцам =
			РаботаСРабочимКалендаремКлиентСервер.ЧислоПовторениеПоМесяцамВМассив(
				СтруктураПравилаПовторения.ПовторениеПоМесяцам);
		Если МассивПовторениеПоМесяцам.Найти(Месяц(ПроверяемаяДата)) <> Неопределено  Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет является ли дата исключением
Функция ЯвляетсяИсключениемПовторения(ПроверяемаяДата, СтруктураПравилаПовторения)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ДатаИсключения", НачалоДня(ПроверяемаяДата));
	НайденныеСтроки = СтруктураПравилаПовторения.ИсключенияПовторения.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеСтроки.Количество() <> 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Формирует текстовое описание события
Функция СформироватьТекстовоеОписаниеСобытия(Событие, ВключатьВремяВОписание, ОтображатьПользователя)
	
	Описание = Новый Массив;
	
	Если Событие.Состояние = Перечисления.СостоянияЗаписейРабочегоКалендаря.ПодВопросом Тогда
		Описание.Добавить("?");
	ИначеЕсли Событие.Состояние = Перечисления.СостоянияЗаписейРабочегоКалендаря.Отклонено Тогда
		Описание.Добавить("-");
	КонецЕсли;
	
	Если ВключатьВремяВОписание Тогда
		Описание.Добавить(СтрШаблон("%1 - %2",
			Формат(Событие.ДатаНачала, "ДФ=ЧЧ:мм"),
			Формат(Событие.ДатаОкончания, "ДФ=ЧЧ:мм")));
	КонецЕсли;
	
	Если ОтображатьПользователя Тогда
		Описание.Добавить(СтрШаблон("%1,", Событие.Пользователь));
	КонецЕсли;
	
	Описание.Добавить(Событие.Наименование);
	
	Возврат СтрСоединить(Описание, " ");
	
КонецФункции

// Вспомогательная функция расчета занятости сотрудника по краткому алгоритму
Функция РассчитатьЗанятостьСотрудникаПоКраткомуАлгоритму(ТаблицаЗанятости, Сотрудник,
	ЗанятостьСотрудника)
	
	РасчетЗанятостиЗавершен = Ложь;
	
	// Учитывается что отсутствует занятость может быть установлена только у отсутствий.
	ИтоговаяЗанятостьСотрудника = ПолучитьИтоговуюЗанятостьСотрудника(
		ТаблицаЗанятости, Сотрудник);
	Если ИтоговаяЗанятостьСотрудника = Перечисления.ЦБР_СостоянияЗанятости.Отсутствует Тогда
		ЗанятостьСотрудника = Перечисления.ЦБР_СостоянияЗанятости.Отсутствует;
		РасчетЗанятостиЗавершен = Истина;
	ИначеЕсли ИтоговаяЗанятостьСотрудника = Перечисления.ЦБР_СостоянияЗанятости.Занят Тогда
		ЗанятостьСотрудника = Перечисления.ЦБР_СостоянияЗанятости.Занят;
		РасчетЗанятостиЗавершен = Истина;
	ИначеЕсли ИтоговаяЗанятостьСотрудника = Перечисления.ЦБР_СостоянияЗанятости.ПодВопросом Тогда
		ЗанятостьСотрудника = Перечисления.ЦБР_СостоянияЗанятости.ПодВопросом;
	КонецЕсли;
	
	Возврат РасчетЗанятостиЗавершен;
	
КонецФункции

// Возвращает итоговую занятость сотрудника по таблице занятости
Функция ПолучитьИтоговуюЗанятостьСотрудника(ТаблицаЗанятости, Сотрудник)
	
	ИтоговаяЗанятость = Перечисления.ЦБР_СостоянияЗанятости.Доступен;
	
	// Учитывается что отсутствует занятость может быть установлена только у отсутствий.
	ПараметрыОтбора = Новый Структура("Сотрудник");
	ПараметрыОтбора.Сотрудник = Сотрудник;
	МассивЗанятостиСотрудников = ТаблицаЗанятости.НайтиСтроки(ПараметрыОтбора);
	Для Каждого СтрокаЗанятостиСотрудника Из МассивЗанятостиСотрудников Цикл
		Если СтрокаЗанятостиСотрудника.Занят = Перечисления.ЦБР_СостоянияЗанятости.Отсутствует Тогда
			ИтоговаяЗанятость = Перечисления.ЦБР_СостоянияЗанятости.Отсутствует;
			Прервать;
		ИначеЕсли СтрокаЗанятостиСотрудника.Занят = Перечисления.ЦБР_СостоянияЗанятости.Занят Тогда
			ИтоговаяЗанятость = Перечисления.ЦБР_СостоянияЗанятости.Занят;
			Прервать;
		ИначеЕсли СтрокаЗанятостиСотрудника.Занят = Перечисления.ЦБР_СостоянияЗанятости.ПодВопросом Тогда
			ИтоговаяЗанятость = Перечисления.ЦБР_СостоянияЗанятости.ПодВопросом;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИтоговаяЗанятость;
	
КонецФункции

// Вносит занятости из таблицы в таблицу занятости
Процедура ВнестиЗанятостиВТаблицуЗанятости(ТаблицаЗанятости, ТаблицаЗанятостиИсточник,
	Сотрудник, ДатаНачала, ДатаОкончания, Занят = Неопределено)
	
	Если Занят <> Неопределено Тогда
		ПараметрыОтбора = Новый Структура("Сотрудник, Занят");
		ПараметрыОтбора.Сотрудник = Сотрудник;
		ПараметрыОтбора.Занят = Занят;
	Иначе
		ПараметрыОтбора = Новый Структура("Сотрудник");
		ПараметрыОтбора.Сотрудник = Сотрудник;
	КонецЕсли;
	
	МассивЗанятостиСотрудника = ТаблицаЗанятостиИсточник.НайтиСтроки(ПараметрыОтбора);
	Для Каждого СтрокаЗанятостиСотрудника Из МассивЗанятостиСотрудника Цикл
		
		ВнестиЗанятостьВТаблицуЗанятости(ТаблицаЗанятости, Сотрудник,
			СтрокаЗанятостиСотрудника.ДатаНачала, СтрокаЗанятостиСотрудника.ДатаОкончания,
			СтрокаЗанятостиСотрудника.Занят, ДатаНачала, ДатаОкончания);
		
	КонецЦикла;
	
КонецПроцедуры

// Вносит занятость в таблицу занятости
Процедура ВнестиЗанятостьВТаблицуЗанятости(ТаблицаЗанятости,
	Сотрудник, Знач ДатаНачала, Знач ДатаОкончания, Занят, ДатаНачалаТаблицы, ДатаОкончанияТаблицы)
	
	Если ДатаОкончания <= ДатаНачалаТаблицы Тогда
		Возврат;
	КонецЕсли;
	
	Если ДатаНачала >= ДатаОкончанияТаблицы Тогда
		Возврат;
	КонецЕсли;
	
	Если ДатаНачала < ДатаНачалаТаблицы Тогда
		ДатаНачала = ДатаНачалаТаблицы;
	КонецЕсли;
	
	Если ДатаОкончания > ДатаОкончанияТаблицы Тогда
		ДатаОкончания = ДатаОкончанияТаблицы;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("Сотрудник");
	ПараметрыОтбора.Сотрудник = Сотрудник;
	МассивЗанятости = ТаблицаЗанятости.НайтиСтроки(ПараметрыОтбора);
	СтрокиКУдалению = Новый Массив;
	
	Для Каждого СтрокаЗанятости Из МассивЗанятости Цикл
		
		// Разбиваем существующие строки занятости
		Если СтрокаЗанятости.ДатаНачала < ДатаОкончания
			И СтрокаЗанятости.ДатаОкончания > ДатаНачала
			И СтрокаЗанятости.Занят <> Занят Тогда
			
			Если СтрокаЗанятости.ДатаНачала < ДатаНачала Тогда
				СтрокаЗанятостиПерваяЧасть = ТаблицаЗанятости.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗанятостиПерваяЧасть, СтрокаЗанятости);
				СтрокаЗанятостиПерваяЧасть.ДатаОкончания = ДатаНачала;
			КонецЕсли;
			
			Если СтрокаЗанятости.ДатаОкончания > ДатаОкончания Тогда
				СтрокаЗанятостиВтораяЧасть = ТаблицаЗанятости.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗанятостиВтораяЧасть, СтрокаЗанятости);
				СтрокаЗанятостиВтораяЧасть.ДатаНачала = ДатаОкончания;
			КонецЕсли;
			
			СтрокиКУдалению.Добавить(СтрокаЗанятости);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаЗанятости.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	НоваяСтрока = ТаблицаЗанятости.Добавить();
	НоваяСтрока.Сотрудник = Сотрудник;
	НоваяСтрока.ДатаНачала = ДатаНачала;
	НоваяСтрока.ДатаОкончания = ДатаОкончания;
	НоваяСтрока.Занят = Занят;
	
	СкорректироватьТаблицуЗанятости(ТаблицаЗанятости);
	
КонецПроцедуры


// Корректирует дубли и пересечение в таблице занятости
Процедура СкорректироватьТаблицуЗанятости(ТаблицаЗанятости)
	
	ТребуетсяВнестиКоррективы = Истина;
	
	Пока ТребуетсяВнестиКоррективы Цикл
		
		ТребуетсяВнестиКоррективы = Ложь;
		
		ТаблицаЗанятостиКопия = ТаблицаЗанятости.Скопировать();
		
		ТаблицаЗанятости.Очистить();
		
		Для Каждого СтрокаЗанятости Из ТаблицаЗанятостиКопия Цикл
			
			ВнестиЗаписьВТаблицуЗанятости = Истина;
			
			Для Каждого ВнесеннаяСтрокаЗанятости Из ТаблицаЗанятости Цикл
				
				Если ЗанятостиСовпадают(СтрокаЗанятости, ВнесеннаяСтрокаЗанятости) Тогда
					ВнестиЗаписьВТаблицуЗанятости = Ложь;
					Прервать;
				КонецЕсли;
				
				Если СтрокаЗанятости.Сотрудник = ВнесеннаяСтрокаЗанятости.Сотрудник
					И СтрокаЗанятости.ДатаНачала <= ВнесеннаяСтрокаЗанятости.ДатаОкончания
					И СтрокаЗанятости.ДатаОкончания >= ВнесеннаяСтрокаЗанятости.ДатаНачала
					И СтрокаЗанятости.Занят = ВнесеннаяСтрокаЗанятости.Занят Тогда
					
					ВнесеннаяСтрокаЗанятости.ДатаНачала = Мин(ВнесеннаяСтрокаЗанятости.ДатаНачала, СтрокаЗанятости.ДатаНачала);
					ВнесеннаяСтрокаЗанятости.ДатаОкончания = Макс(ВнесеннаяСтрокаЗанятости.ДатаОкончания, СтрокаЗанятости.ДатаОкончания);
					
					ТребуетсяВнестиКоррективы = Истина;
					ВнестиЗаписьВТаблицуЗанятости = Ложь;
					Прервать;
					
				КонецЕсли;			
			КонецЦикла;
			
			Если НЕ ВнестиЗаписьВТаблицуЗанятости Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаЗанятости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗанятости);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаЗанятости.Сортировать("Сотрудник, ДатаНачала");
	
КонецПроцедуры

// Проверяет совпадение строк занятости
Функция ЗанятостиСовпадают(Занятость1, Занятость2)
	
	Возврат Занятость1.Сотрудник = Занятость2.Сотрудник
		И Занятость1.ДатаНачала = Занятость2.ДатаНачала
		И Занятость1.ДатаОкончания = Занятость2.ДатаОкончания
		И Занятость1.Занят = Занятость2.Занят;
	
КонецФункции


// Добавляет ссылку в историю работы пользователя.
//
Процедура ДобавитьВИсториюРаботыПользователя(ЗаписьКалендаря)
	
	ТипПараметра = ТипЗнч(ЗаписьКалендаря);
	Если ТипПараметра = Тип("СправочникОбъект.ЦБР_ЗаписиРабочегоКалендаря")
		Или ТипПараметра = Тип("ДокументОбъект.Отсутствие") Тогда
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ЗаписьКалендаря.Ссылка);
	ИначеЕсли ТипПараметра = Тип("СправочникСсылка.ЦБР_ЗаписиРабочегоКалендаря")
		Или ТипПараметра = Тип("ДокументСсылка.Отсутствие") Тогда
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ЗаписьКалендаря);
	КонецЕсли;
	ИсторияРаботыПользователя.Добавить(НавигационнаяСсылка);
	
КонецПроцедуры

// Устанавливает значение измерения элемента планировщика.
//
// Параметры:
//  Элемент - ЭлементПланировщика - Элемент планировщика.
//  Измерение - Любой - Измерение планировщика.
//  ЗначениеИзмерения - Любой - Значение измерения планировщика.
//
Процедура УстановитьЗначениеИзмерения(Элемент, Измерение, ЗначениеИзмерения)
	
	СтруктураЭлемента = Новый Структура("ЗначенияИзмерений");
	
	СоответсвиеИзмерений = Новый Соответствие();
	СоответсвиеИзмерений.Вставить(Измерение, ЗначениеИзмерения);
	СтруктураЭлемента.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответсвиеИзмерений);
	
	Элемент.Загрузить(СтруктураЭлемента);
	
КонецПроцедуры

// Возвращает цвет занятости.
//
// Параметры:
//  Занятость - Структура - Занятость.
//
// Возвращаемое значение:
//  Цвет - Цвет занятости.
//
Функция ПолучитьЦветЗанятости(Занятость)
	
	Если Занятость.Занят = Перечисления.ЦБР_СостоянияЗанятости.Занят Тогда
		ЦветЗанятости = ЦветаСтиля.ЦветКалендаряЗанятоеВремя;
	ИначеЕсли Занятость.Занят = Перечисления.ЦБР_СостоянияЗанятости.ПодВопросом Тогда
		ЦветЗанятости = ЦветаСтиля.ЦветКалендаряПодВопросомВремя;
	ИначеЕсли Занятость.Занят = Перечисления.ЦБР_СостоянияЗанятости.Отсутствует Тогда
		ЦветЗанятости = ЦветаСтиля.ЦветКалендаряОтсутствует;
	Иначе
		ЦветЗанятости = ЦветаСтиля.ЦветКалендаряДоступноеВремя;
	КонецЕсли;
	
	Возврат ЦветЗанятости;
	
КонецФункции

// Возвращает пустую структуру данных планировщика.
//
// Возвращаемое значение:
//  Структура - Структура данных планировщика.
//
Функция ПолучитьСтруктуруДанныхПланировщика()
	
	ДанныеПланировщика = Новый Структура;
	ДанныеПланировщика.Вставить("Элементы", Новый Массив);
	ДанныеПланировщика.Вставить("ИнтервалыФона", Новый Массив);
	
	Возврат ДанныеПланировщика;
	
КонецФункции

// Создает и заполняет объект повторяющейся записи календаря на основании элемента планировщика.
//
// Параметры:
//  ЗаписьКалендаря - Структура - Структура записи календаря планировщика.
//
// Возвращаемое значение:
//  СправочникОбъект.ЗаписиРабочегоКалендаря - Не записанный в базу объект исключения повторяющегося события.
//
Функция СоздатьИсключениеПовторяющейсяЗаписиКалендаря(ЗаписьКалендаря)
	
	СтруктураОснование = Новый Структура("ПовторяющеесяСобытие, ДатаИсключения");
	СтруктураОснование.ПовторяющеесяСобытие = ЗаписьКалендаря.Ссылка;
	СтруктураОснование.ДатаИсключения = ЗаписьКалендаря.ДатаНачалаИсходная;
	
	ЗаписьКалендаряОбъект = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
	ЗаписьКалендаряОбъект.Заполнить(СтруктураОснование);
	ЗаполнитьЗначенияСвойств(ЗаписьКалендаряОбъект, ЗаписьКалендаря, "ДатаНачала, ДатаОкончания, ВесьДень, Пользователь");
	ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("ПовторяющеесяСобытие", ЗаписьКалендаря.Ссылка);
	ЗаписьКалендаряОбъект.ДополнительныеСвойства.Вставить("ДатаИсключения", ЗаписьКалендаря.ДатаНачалаИсходная);
	
	Возврат ЗаписьКалендаряОбъект;
	
КонецФункции

// Возвращает параметры напоминания по записе календаря.
//
// Параметры:
//  ЗаписьКалендаря	 - СправочникСсылка.ЗаписиРабочегоКалендар	 - Запись календаря.
// 
// Возвращаемое значение:
//  Структура - Структура напоминания.
//
Функция ПолучитьПараметрыНапоминанияПоИсточнику(ЗаписьКалендаря)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НапоминанияПользователя.Пользователь,
	|	НапоминанияПользователя.ВремяСобытия,
	|	НапоминанияПользователя.Источник,
	|	НапоминанияПользователя.СрокНапоминания КАК СрокНапоминания,
	|	НапоминанияПользователя.Описание КАК Описание,
	|	2 КАК ИндексКартинки,
	|	НапоминанияПользователя.СпособУстановкиВремениНапоминания,
	|	НапоминанияПользователя.ИнтервалВремениНапоминания,
	|	НапоминанияПользователя.ИмяРеквизитаИсточника
	|ИЗ
	|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
	|ГДЕ
	|	НапоминанияПользователя.Пользователь = &Пользователь
	|	И НапоминанияПользователя.Источник = &Источник";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("Источник", ЗаписьКалендаря);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПараметрыНапоминания = Новый Структура("Пользователь,Источник,ВремяСобытия,СрокНапоминания,
		|Описание,СпособУстановкиВремениНапоминания,ИнтервалВремениНапоминания,ИмяРеквизитаИсточника");
	
	Результат = Неопределено;
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыНапоминания, Выборка);
		Результат = ПараметрыНапоминания;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

// Отключает напоминание для записи календаря.
//
// Параметры:
//  ЗаписьКалендаря			 - СправочнкиСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
//  ОтключенныеНапоминания	 - Массив									 - Отключенные напоминания пользователя.
//
Процедура ОтключитьНапоминание(ЗаписьКалендаря, ОтключенныеНапоминания)
	
	Напоминание = ПолучитьПараметрыНапоминанияПоИсточнику(ЗаписьКалендаря);
	Если Напоминание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НапоминанияПользователяСлужебный.ОтключитьНапоминание(Напоминание, Ложь);
	ОтключенныеНапоминания.Добавить(Напоминание);
	
КонецПроцедуры

// Возвращает цвет рамки элемента календаря.
//
// Параметры:
//  ЦветФона	 - Цвет	 - Цвет фона элемента планировщика.
// 
// Возвращаемое значение:
//  Цвет - Цвет рамки записи календаря.
//
Функция ПолучитьЦветРамкиЭлементаПланировщика(ЦветФона)
	
	Если ЦветФона = ЦветаСтиля.ЦветКалендаряОтработано Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряОтработана;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряКрасный Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряКрасная;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряСиний Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряСиняя;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряЖелтый Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряЖелтая;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряЗеленый Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряЗеленая;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряОранжевый Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряОранжевая;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряПоУмолчанию Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряПоУмолчанию;
	ИначеЕсли ЦветФона = ЦветаСтиля.ЦветКалендаряТекущаяЗапись Тогда
		ЦветРамки = ЦветаСтиля.РамкаКалендаряТекущаяЗапись;
	Иначе
		ЦветРамки = ЦветаСтиля.ЦветРамки;
	КонецЕсли;
	
	Возврат ЦветРамки;
	
КонецФункции

// Отмечает запись календаря как отработанную.
//
// Параметры:
//  ЗаписьКалендаря			 - СправочникСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
//  ОтключенныеНапоминания	 - Массив									 - Отключенные напоминания пользователя.
//
Процедура ОтработатьЗаписьКалендаря(ЗаписьКалендаря, ОтключенныеНапоминания)
	
	УстановитьОтработанаЗаписьКалендаря(ЗаписьКалендаря);
	УстановитьЦветЗаписиКалендаря(ЗаписьКалендаря, Перечисления.ЦБР_ЦветаРабочегоКалендаря.Нет);
	ОтключитьНапоминание(ЗаписьКалендаря, ОтключенныеНапоминания);

	Действие = ЗаписьКалендаря.ПолучитьОбъект();
	Если Не Действие.ВидРаботы.ПоПлану Тогда
	Действие.ДатаОкончания = ТекущаяДатаСеанса();
	Действие.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Отмечает запись календаря как Приступившую.
//
// Параметры:
//  ЗаписьКалендаря			 - СправочникСсылка.ЗаписиРабочегоКалендаря	 - Запись календаря.
//  ОтключенныеНапоминания	 - Массив									 - Отключенные напоминания пользователя.
//
Процедура ПриступитьЗаписьКалендаря(ЗаписьКалендаря, ОтключенныеНапоминания)
	
	УстановитьПриступилЗаписьКалендаря(ЗаписьКалендаря);
	УстановитьЦветЗаписиКалендаря(ЗаписьКалендаря, Перечисления.ЦБР_ЦветаРабочегоКалендаря.Зеленый);
	ОтключитьНапоминание(ЗаписьКалендаря, ОтключенныеНапоминания);

	Действие = ЗаписьКалендаря.ПолучитьОбъект();
	Если Не Действие.ВидРаботы.ПоПлану Тогда
		Длительность = Действие.ДатаОкончания - Действие.ДатаНачала;
		Действие.ДатаНачала = ТекущаяДатаСеанса();
		Действие.ДатаОкончания = Действие.ДатаНачала + Длительность;
		Действие.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Определяет, по каким записям календаря у текущего пользователя установлены напоминания.
// 
// Возвращаемое значение:
//  Соответствие - Записи календаря с напоминания текущего пользователя.
//   * Ключ     - СправочникСсылка.ЗаписиРабочегоКалендаря - Запись календаря.
//   * Значение - Булево                                   - Наличие напоминания текущему пользователю.
//
Функция ЗаписиКалендаряСНапоминаниямиТекущегоПользователя()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НапоминанияПользователя.Источник КАК Источник
		|ИЗ
		|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
		|ГДЕ
		|	НапоминанияПользователя.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	ЗаписиКалендаряСНапоминаниями = Новый Соответствие;
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписиКалендаряСНапоминаниями[Выборка.Источник] = Истина;
	КонецЦикла;
	
	Возврат ЗаписиКалендаряСНапоминаниями;
	
КонецФункции

Функция ЕстьДействиеВРаботе(ЗаданиеСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаданиеСсылка", ЗаданиеСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.Статус = Значение(Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал)";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти