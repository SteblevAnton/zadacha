#Область ОбработчикиФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СсылкаНаЗадачу") Тогда
		СсылкаНаЗадачу = Параметры.СсылкаНаЗадачу;
	КонецЕсли;
	
	Если Параметры.Свойство("СсылкаНаЗадание") Тогда
		СсылкаНаЗадание = Параметры.СсылкаНаЗадание;
	КонецЕсли;
	
	ЯМенеджер = ЦБР_ЗадачиЗаданияВызовСервера.ЯМенеджер(СсылкаНаЗадачу); 
	ЯКуратор  = ЦБР_ЗадачиЗаданияВызовСервера.ЯКуратор(СсылкаНаЗадачу); 
	
	Если Не ЯКуратор И Не ЯМенеджер Тогда
		Исполнитель = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;	
	
	ЗаполнитьЧасы();
	
	УстановитьДоступностьЭлементов();

	ЗаполнитьМесяцПоДатеВТабличнойЧасти(ТаблицаЧасов, "ПериодЗП", "ПериодЗПСтрокой");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыЧасов 

&НаКлиенте
Процедура ТаблицаЧасовЗачтеноПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаЧасов.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда 
		
		ДобавитьЗаданияДляИзменения(ТекущаяСтрока.Задание);   
		
		Модифицированность = Истина;
		
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЧасовЗаСчетКомпанииПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаЧасов.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда 
		
		ДобавитьЗаданияДляИзменения(ТекущаяСтрока.Задание);   
		
		Модифицированность = Истина;
		
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЧасовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЧасовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикКомандФормы
 
&НаКлиенте
Процедура РазбитьСтроку(Команда)
	
	ТекущаяСтрока = Элементы.ТаблицаЧасов.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда 
		
		Оповещение = Новый ОписаниеОповещения("ПослеВводаКоличестваЧасов", ЭтотОбъект);
		
		ПоказатьВводЧисла(Оповещение, ТекущаяСтрока.ЭффективныеЧасы, "Введите часы для новой строки");    
		
	КонецЕсли;  

КонецПроцедуры   

&НаКлиенте
Процедура ПослеВводаКоличестваЧасов(Результат, Параметры) Экспорт
 
	Если Не Результат = Неопределено Тогда
		
		ТекущаяСтрока = Элементы.ТаблицаЧасов.ТекущиеДанные;
		
		Если ТекущаяСтрока <> Неопределено Тогда 
			
			Если Результат > ТекущаяСтрока.ЭффективныеЧасы Тогда
		        ОбщегоНазначенияКлиент.СообщитьПользователю("Отделяемые часы превышают остаток!!!");
				Возврат;
			КонецЕсли;	
			
			ТекущаяСтрока.ЭффективныеЧасы     = ТекущаяСтрока.ЭффективныеЧасы - Результат; 
			
			НоваяСтрокаЧасов                  = ТаблицаЧасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЧасов, ТекущаяСтрока);  
			НоваяСтрокаЧасов.ЭффективныеЧасы = Результат;	
			
			ДобавитьЗаданияДляИзменения(ТекущаяСтрока.Задание);   
			
			Модифицированность = Истина;

		КонецЕсли;  
	КонецЕсли;
 
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	СохранитьЧасыНаСервере(); 
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРеализацию(Команда)
	
	Для Каждого Строка Из Элементы.ТаблицаЧасов.ВыделенныеСтроки Цикл
		ОчиститьРеализациюПоСтроке(Строка);    
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМесяц(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", НачалоМесяца(ТекущаяДата()), КонецМесяца(ТекущаяДата()));
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьМесяцЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборПериода", ПараметрыВыбора, , , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМесяцЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Для Каждого Строка Из Элементы.ТаблицаЧасов.ВыделенныеСтроки Цикл
			УстановитьМесяцПоСтроке(Строка, ВыбранноеЗначение); 
		КонецЦикла;     
		
		Модифицированность = Истина;

	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЧасовОчиститьРеализацию", "Видимость", ЯМенеджер); 
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЧасовУстановитьМесяц", "Видимость", Не ЯМенеджер);  
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЧасовПериодЗПСтрокой", "ТолькоПросмотр", ЯМенеджер);  
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЧасовПериодЗПСтрокой", "ТолькоПросмотр", ЯМенеджер);  
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЧасовЗаСчетКомпании", "ТолькоПросмотр", НЕ РольДоступна("ЦБР_ЧасыЗаСчетКомпании"));  
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТаблицаЧасовЗачтено", "ТолькоПросмотр", Не ЯКуратор);  
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЧасы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦБР_ЗаданиеЧасыЗП.Ссылка КАК Задание,
		|	ЦБР_ЗаданиеЧасыЗП.ПериодЗП КАК ПериодЗП,
		|	ЦБР_ЗаданиеЧасыЗП.Исполнитель КАК Исполнитель,
		|	ЦБР_ЗаданиеЧасыЗП.ЭффективныеЧасы КАК ЭффективныеЧасы,
		|	ЦБР_ЗаданиеЧасыЗП.ВидЧаса КАК ВидЧаса,
		|	ЦБР_ЗаданиеЧасыЗП.Реализация КАК Реализация,
		|	ЦБР_ЗаданиеЧасыЗП.ЗаСчетКомпании КАК ЗаСчетКомпании,
		|	ЦБР_ЗаданиеЧасыЗП.Зачтено КАК Зачтено,
		|	ЦБР_ЗаданиеЧасыЗП.Ссылка.Наименование КАК НаименованиеЭтапа
		|ИЗ
		|	Документ.ЦБР_Задание.ЧасыЗП КАК ЦБР_ЗаданиеЧасыЗП
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &СсылкаНаЗадачу = ЗНАЧЕНИЕ(Справочник.ЦБР_Задача.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЦБР_ЗаданиеЧасыЗП.Ссылка.СсылкаНаЗадачу = &СсылкаНаЗадачу
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &СсылкаНаЗадание = ЗНАЧЕНИЕ(Документ.ЦБР_Задание.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЦБР_ЗаданиеЧасыЗП.Ссылка = &СсылкаНаЗадание
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Исполнитель = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЦБР_ЗаданиеЧасыЗП.Исполнитель = &Исполнитель
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодЗП";
	
	Запрос.УстановитьПараметр("СсылкаНаЗадание", СсылкаНаЗадание);
	Запрос.УстановитьПараметр("СсылкаНаЗадачу", СсылкаНаЗадачу);
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЧасов.Загрузить(РезультатЗапроса.Выгрузить());
		
КонецПроцедуры

&НаСервере
Процедура СохранитьЧасыНаСервере()
	
	ТаблицаЧасовСвернутая = ТаблицаЧасов.Выгрузить();
	ТаблицаЧасовСвернутая.Свернуть("Задание,НаименованиеЭтапа,Исполнитель,ВидЧаса,Реализация,ЗаСчетКомпании,Зачтено,ПериодЗП,ПериодЗПСтрокой", "ЭффективныеЧасы");
	
	Для Каждого Стр Из ЗаданияДляИзменения Цикл
		
		ЗаданиеОбъект = Стр.Задание.ПолучитьОбъект();
		ЗаданиеОбъект.ЧасыЗП.Очистить();
		
		СтрокиЗадания = ТаблицаЧасовСвернутая.НайтиСтроки(Новый Структура("Задание", Стр.Задание));
		Для Каждого СтрокаЗадание Из СтрокиЗадания Цикл
			НоваяСтр = ЗаданиеОбъект.ЧасыЗП.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр, СтрокаЗадание); 
		КонецЦикла;	 
		
		ЗаданиеОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеализациюПоСтроке(Строка)
	
	СтрокаЧасов = ТаблицаЧасов.НайтиПоИдентификатору(Строка);    
	СтрокаЧасов.Реализация = Документы.cbr_РеализацияТоваровУслугУПР.ПустаяСсылка();
	
	ДобавитьЗаданияДляИзменения(СтрокаЧасов.Задание);

КонецПроцедуры

&НаСервере
Процедура УстановитьМесяцПоСтроке(Строка, ПериодЗП)
	
	СтрокаЧасов = ТаблицаЧасов.НайтиПоИдентификатору(Строка);    
	СтрокаЧасов.ПериодЗП        = ПериодЗП;    
	СтрокаЧасов.ПериодЗПСтрокой = ПолучитьПредставлениеМесяца(ПериодЗП);
	
	ДобавитьЗаданияДляИзменения(СтрокаЧасов.Задание);

КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаданияДляИзменения(Задание)
	
	СтрокиЗадание = ЗаданияДляИзменения.НайтиСтроки(Новый Структура("Задание", Задание)); 
	
	Если СтрокиЗадание.Количество() = 0 Тогда 
		НоваяСтрокаЗадание = ЗаданияДляИзменения.Добавить();  
		НоваяСтрокаЗадание.Задание = Задание;  
	КонецЕсли;	
			
КонецПроцедуры

#КонецОбласти  

#Область ЗаполнениеМесяца

&НаКлиенте
Процедура ЭффективныеЧасыПериодЗПСтрокойПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ТаблицаЧасов.ТекущиеДанные;
	
	ДобавитьЗаданияДляИзменения(ТекСтрока.Задание);

	ВводМесяцаПриИзменении(ТекСтрока, "ПериодЗП", "ПериодЗПСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыПериодЗПСтрокойНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.ТаблицаЧасов.ТекущиеДанные, "ПериодЗП", "ПериодЗПСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыПериодЗПСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.ТаблицаЧасов.ТекущиеДанные;
	
	ДобавитьЗаданияДляИзменения(ТекСтрока.Задание);
	
	ВводМесяцаРегулирование(ТекСтрока, "ПериодЗП", "ПериодЗПСтрокой", Направление, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыПериодЗПСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыПериодЗПСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДатаСеанса()
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ТекущаяДатаСеанса();
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли
	
КонецФункции

&НаСервере
Процедура ЗаполнитьМесяцПоДатеВТабличнойЧасти(ДанныеТабличнойЧасти, ПутьРеквизита, ПутьРеквизитаПредставления)
	
	Для Каждого СтрокаТабличнойЧасти Из ДанныеТабличнойЧасти Цикл
		Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(СтрокаТабличнойЧасти, ПутьРеквизита);
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(СтрокаТабличнойЧасти,
																					ПутьРеквизитаПредставления,
																					ПолучитьПредставлениеМесяца(Значение));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеМесяца(Знач ДатаНачалаМесяца)
	
	Возврат Формат(ДатаНачалаМесяца, "ДФ='ММММ гггг'");
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьМесяцПоДате(РедактируемыйОбъект, ПутьРеквизита, ПутьРеквизитаПредставления)
	
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления, ПолучитьПредставлениеМесяца(Значение));
	
КонецПроцедуры

&НаКлиенте
Процедура ВводМесяцаПриИзменении(РедактируемыйОбъект, ПутьРеквизита, ПутьРеквизитаПредставления, Модифицированность = Ложь)
	
	ЗначениеПредставления = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления);
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита);
	
	ДатаКакМесяцПодобратьДатуПоТексту(ЗначениеПредставления, Значение);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект,
																				ПутьРеквизитаПредставления,
																				ПолучитьПредставлениеМесяца(Значение));
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	
	Модифицированность = Истина;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВводМесяцаНачалоВыбора(	Форма,
											РедактируемыйОбъект,
											ПутьРеквизита,
											ПутьРеквизитаПредставления,
											ИзменитьМодифицированность = Истина,
											ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("РедактируемыйОбъект", РедактируемыйОбъект);
	ДополнительныеПараметры.Вставить("ПутьРеквизита", ПутьРеквизита);
	ДополнительныеПараметры.Вставить("ПутьРеквизитаПредставления", ПутьРеквизитаПредставления);
	ДополнительныеПараметры.Вставить("ИзменитьМодифицированность", ИзменитьМодифицированность);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита);
	
	Оповещение = Новый ОписаниеОповещения("ВводМесяцаНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ВыборПериода", 
		Новый Структура("Значение,РежимВыбораПериода,ЗапрашиватьРежимВыбораПериодаУВладельца", Значение, "Месяц", Ложь),
		Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводМесяцаРегулирование(	РедактируемыйОбъект,
											ПутьРеквизита,
											ПутьРеквизитаПредставления,
											Направление,
											Модифицированность = Ложь)
	
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита);
	Значение = ДобавитьМесяц(Значение, Направление);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект,
																				ПутьРеквизита,
																				Значение);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект,
																				ПутьРеквизитаПредставления,
																				ПолучитьПредставлениеМесяца(Значение));
	
	Модифицированность = Истина;
 	
КонецПроцедуры 

&НаКлиенте
Процедура ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если Не ПустаяСтрока(Текст) Тогда
		ДанныеВыбора = ДатаКакМесяцПодобратьДатуПоТексту(Текст);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если Текст <> "" Тогда
		ДанныеВыбора = ДатаКакМесяцПодобратьДатуПоТексту(Текст);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДатаКакМесяцПодобратьДатуПоТексту(Текст, ДатаПоТексту = НеОпределено)
	
	СписокВозврата = Новый СписокЗначений;
	ТекущийГод = Год(ДатаСеанса());
	
	Если ПустаяСтрока(Текст) Тогда
		ДатаПоТексту = Дата(1, 1, 1);
		Возврат СписокВозврата;
	КонецЕсли;
	
	Если СтрНайти(Текст, ".") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ".");
	ИначеЕсли СтрНайти(Текст, ",") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ",");
	ИначеЕсли СтрНайти(Текст, "-") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "-");
	ИначеЕсли СтрНайти(Текст, "/") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "/");
	ИначеЕсли СтрНайти(Текст, "\") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "\");
	Иначе
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, " ");
	КонецЕсли;
	
	Если Подстроки.Количество() = 1 Тогда
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Текст) Тогда
			МесяцЧислом = Число(Текст);
			Если МесяцЧислом >= 1 И МесяцЧислом <=12 Тогда
				ДатаПоТексту = Дата(ТекущийГод, МесяцЧислом, 1);
				Если СтрДлина(Текст) = 1 Тогда
					СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='М/гг'"));
				Иначе
					СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММ/гг'"));
				КонецЕсли;
			Иначе
				Возврат СписокВозврата;
			КонецЕсли;                
		Иначе
			СписокМесяцев = СписокМесяцевПоСтроке(Текст);
			Для Каждого Месяц Из СписокМесяцев Цикл
				ДатаПоТексту = Дата(ТекущийГод, Месяц, 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Подстроки.Количество() = 2 Тогда
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстроки[1]) Тогда
			
			Если ПустаяСтрока(Подстроки[1]) Тогда
				ГодЧислом = 0;
				Подстроки[1] = "0";
				ТекстВозврата = Текст + "0";
			Иначе
				ГодЧислом = Число(Подстроки[1]);
				ТекстВозврата = "";
			КонецЕсли;
			
			Если ГодЧислом > 3000 Тогда
				Возврат СписокВозврата;
			КонецЕсли;
			
			Если СтрДлина(Подстроки[1]) <= 1 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 3) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 2 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 2) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 3 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 1) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 4 Тогда
				ГодЧислом = Число(Подстроки[1]);
			КонецЕсли;                    
			
		Иначе
			
			Возврат СписокВозврата;
			
		КонецЕсли;                
		Если ЗначениеЗаполнено(Подстроки[0]) И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстроки[0]) Тогда
			
			МесяцЧислом = Число(Подстроки[0]);
			Если МесяцЧислом >= 1 И МесяцЧислом <= 12 Тогда
				ДатаПоТексту = Дата(ГодЧислом, МесяцЧислом, 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			Иначе
				Возврат СписокВозврата;
			КонецЕсли;                
			
		Иначе
			
			СписокМесяцев = СписокМесяцевПоСтроке(Подстроки[0]);
			
			Если СписокМесяцев.Количество() = 1 Тогда
				ДатаПоТексту = Дата(ГодЧислом, СписокМесяцев[0], 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			Иначе
				Для Каждого Месяц Из СписокМесяцев Цикл
					ДатаПоТексту = Дата(ГодЧислом, Месяц, 1);
					СписокВозврата.Добавить(Формат(Дата(ГодЧислом, Месяц, 1), "ДФ='ММММ гггг'"));
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат СписокВозврата;
	
КонецФункции

&НаКлиенте
Функция СписокМесяцевПоСтроке(Текст)
	
	СписокМесяцев  = Новый СписокЗначений;
	Месяцы         = Новый Соответствие;
	МесяцыВозврата = Новый Массив;
	
	Для Счетчик = 1 По 12 Цикл
		Представление = Формат(Дата(2000, Счетчик, 1), "ДФ='ММММ'");
		СписокМесяцев.Добавить(Счетчик, Представление);
		Представление = Формат(Дата(2000, Счетчик, 1), "ДФ='МММ'");
		СписокМесяцев.Добавить(Счетчик, Представление);
	КонецЦикла;
	
	Для Каждого ЭлементСписка Из СписокМесяцев Цикл
		Если ВРег(Текст) = ВРег(Лев(ЭлементСписка.Представление, СтрДлина(Текст))) Тогда
			Месяцы[ЭлементСписка.Значение] = 0;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из Месяцы Цикл
		МесяцыВозврата.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Возврат МесяцыВозврата;
	
КонецФункции

&НаКлиенте
Процедура ВводМесяцаНачалоВыбораЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	РедактируемыйОбъект = ДополнительныеПараметры.РедактируемыйОбъект;
	ПутьРеквизита = ДополнительныеПараметры.ПутьРеквизита;
	ПутьРеквизитаПредставления = ДополнительныеПараметры.ПутьРеквизитаПредставления;
	ИзменитьМодифицированность = ДополнительныеПараметры.ИзменитьМодифицированность;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Значение = ВыбранноеЗначение;
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	Представление = ПолучитьПредставлениеМесяца(Значение);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления, Представление);
	
	Если ИзменитьМодифицированность Тогда 
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

