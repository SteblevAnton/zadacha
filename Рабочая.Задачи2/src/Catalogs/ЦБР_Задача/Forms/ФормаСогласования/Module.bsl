#Область ОбработчикиФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СсылкаНаЗадание") Тогда
		СсылкаНаЗадание = Параметры.СсылкаНаЗадание;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаЗадание) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("Не заполнено задание для согласования");
		Возврат;
	КонецЕсли;	
	
	ЗначенияРеквизитов     = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаЗадание, "Описание,ТрудозатратыПлан,ТрудозатратыПланСтарый,КомментарийОценки,СсылкаНаЗадачу,ВидЧаса,ОплатаПоФакту,ТипОценкиЗадачи");
	ОписаниеЗадания        = ЗначенияРеквизитов.Описание.Получить(); 
	ТрудозатратыПлан       = ЗначенияРеквизитов.ТрудозатратыПлан;
	ТрудозатратыПланСтарый = ЗначенияРеквизитов.ТрудозатратыПланСтарый;  
	КомментарийОценки      = ЗначенияРеквизитов.КомментарийОценки; 
	ВидЧаса                = ЗначенияРеквизитов.ВидЧаса; 
	ОплатаПоФакту          = ЗначенияРеквизитов.ОплатаПоФакту; 
    ТипОценкиЗадачи        = ЗначенияРеквизитов.ТипОценкиЗадачи;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланСтарый", "Видимость", Не ОплатаПоФакту);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПлан", "Видимость", Не ОплатаПоФакту);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОплатаПоФакту", "Видимость", ОплатаПоФакту);
	
	Если ТипОценкиЗадачи = Перечисления.ЦБР_ТипОценкиЗадачи.Предсогласованные Тогда
		ТрудозатратыПланСтарый = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПредсогласованныеЧасы(ЗначенияРеквизитов.СсылкаНаЗадачу);  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланСтарый", "Заголовок", "Уже согласовано часов"); 		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПлан", "Заголовок", "Доп. часы к согласованию"); 
	ИначеЕсли ТрудозатратыПлан = ТрудозатратыПланСтарый Или ТрудозатратыПланСтарый = 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТрудозатратыПланСтарый", "Видимость", Ложь);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикКомандФормы

&НаКлиенте
Процедура Отказать(Команда)
	
	Если СокрЛП(КомментарийОтказа) = "" Тогда  
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажи причину отказа"); 
		Возврат;
	КонецЕсли;   
	
	СтруктураОтказа = Новый Структура();
	СтруктураОтказа.Вставить("Согласование", Ложь);                    
	СтруктураОтказа.Вставить("КомментарийОтказа", КомментарийОтказа);     
	СтруктураОтказа.Вставить("СсылкаНаЗадание", СсылкаНаЗадание);
	Закрыть(СтруктураОтказа);
	
КонецПроцедуры

&НаКлиенте
Процедура Согласовать(Команда) 
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса",
      ЭтотОбъект);	
 
    ПоказатьВопрос(Оповещение,
        "Согласовываем оценку?",
        РежимДиалогаВопрос.ОКОтмена,
        0, 
        КодВозвратаДиалога.ОК,
        "Согласование");   
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
 
    Если Результат = КодВозвратаДиалога.ОК Тогда
		СтруктураСогласования = Новый Структура();
		СтруктураСогласования.Вставить("Согласование", Истина);                    
		СтруктураСогласования.Вставить("СсылкаНаЗадание", СсылкаНаЗадание);
		Закрыть(СтруктураСогласования)
    КонецЕсли;	
 
КонецПроцедуры

#КонецОбласти
