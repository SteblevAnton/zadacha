#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокИсполнителей.Параметры.УстановитьЗначениеПараметра("Руководитель",
		Пользователи.ТекущийПользователь().Подразделение.ТекущийРуководитель);
	
	Если Параметры.Свойство("ЭтоУдаление") Тогда
		ЭтоУдаление = Параметры.ЭтоУдаление;
		Заголовок = "Удаление сотрудников из задачи";
	КонецЕсли;
	
	Если Параметры.Свойство("МассивИсполнителей") Тогда
		
		ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(СписокИсполнителей, "Пользователь",
			Параметры.МассивИсполнителей, ВидСравненияКомпоновкиДанных.ВСписке, "Пользователь");
		
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(СписокИсполнителей, "Пользователь",
		Пользователи.АвторизованныйПользователь(), ВидСравненияКомпоновкиДанных.НеРавно, "Пользователь");
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокИсполнителей

&НаКлиенте
Процедура СписокИсполнителейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыбранных();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ОбработатьВыбранных();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ОбработатьВыбранных()
	МассивИсполнителей = Новый Массив;

	Для Каждого Строка Из Элементы.СписокИсполнителей.ВыделенныеСтроки Цикл
		Элементы.СписокИсполнителей.ТекущаяСтрока = Строка;
		Данные = Элементы.СписокИсполнителей.ТекущиеДанные;
		Если ЭтоУдаление Тогда
			МассивИсполнителей.Добавить(Данные.Пользователь);
		Иначе
			МассивИсполнителей.Добавить(Данные);
		КонецЕсли;
	КонецЦикла;

	ПараметрыПрименения = Новый Структура;
	ПараметрыПрименения.Вставить("МассивИсполнителей", МассивИсполнителей);
	Закрыть(ПараметрыПрименения);
КонецПроцедуры

#КонецОбласти