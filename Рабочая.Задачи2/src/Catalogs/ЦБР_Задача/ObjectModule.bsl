#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Постановщик = Пользователи.ТекущийПользователь();
			
	НоваяСтрока = Команда.Добавить();
	НоваяСтрока.Исполнитель = Постановщик;
	НоваяСтрока.Роль = Перечисления.ЦБР_РольВЗадаче.Постановщик;
	
	РуководительПользователя = ЦБР_ЗадачиЗаданияВызовСервера.ВернутьРуководителя(Постановщик);
	
	Если РуководительПользователя <> Неопределено Тогда
		НоваяСтрока = Команда.Добавить();
		НоваяСтрока.Исполнитель = РуководительПользователя;
		НоваяСтрока.Роль = Перечисления.ЦБР_РольВЗадаче.КураторПостановщика;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидЗадачи = Перечисления.ЦБР_ВидЗадачи.Стандартная Тогда
		Если Конфигурации.Количество() = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю("Добавьте хотя бы одну конфигурацию!",Ссылка,,,Отказ);
		КонецЕсли;
		Если НетВыбранныхКонфигураций() Тогда
			ОбщегоНазначения.СообщитьПользователю("Выберите хотя бы одну конфигурацию!",Ссылка,,,Отказ);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ТелефонКонтактногоЛица =  УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			КонтактноеЛицо,
			Справочники.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛица,,
			ТекущаяДатаСеанса());
			
	ПоискПользователяВКоманде(Куратор, Перечисления.ЦБР_РольВЗадаче.КураторИсполнителя);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ЦБР_ЗадачиЗаданияВызовСервера.ДобавитьПредсогласованныеЧасы(ЭтотОбъект, Ссылка);

	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусыЗадачи(Ссылка);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НетВыбранныхКонфигураций()
	
	МассивСтрок = Конфигурации.НайтиСтроки(Новый Структура("Выбрана", Истина));
	Если МассивСтрок.Количество() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьДанныеТребуютВнимания()
		
	СостояниеЗадачи = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадачиПользователя(Ссылка, Пользователи.АвторизованныйПользователь());
	
	Если СостояниеЗадачи = Перечисления.ЦБР_Состояние.Черновик Тогда
		НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Задача.Установить(Ссылка);			
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
		
		Возврат;
		
	КонецЕсли;
	
	Длительность = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСуммуОценокПоЗадаче(Ссылка);
	
	МассивРезультатов = МассивВыборокДанныхПоСотрудникамИРуководителям(Куратор);
	
	ВыборкаКураторов    = МассивРезультатов[5].Выбрать();
	ВыборкаИсполнителей = МассивРезультатов[6].Выбрать();
	
	НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Задача.Установить(Ссылка);			
	НаборЗаписей.Отбор.ПричинаВнимания.Установить(Перечисления.ЦБР_ПричиныВнимания.ЭтоНоваяЗадача);
	
	//Отбор для формирования требующих внимания по кураторам		
	Пока ВыборкаКураторов.Следующий() Цикл
		
		НаборЗаписей.Отбор.УделяющийВнимание.Установить(ВыборкаКураторов.Куратор);		
		НаборЗаписей.Прочитать();
		
		Если НЕ ВыборкаКураторов.ЭтоАктивныйКуратор Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
			Продолжить;	
		КонецЕсли;
		
		Если ВыборкаКураторов.КоличествоПодчиненных = 0 И НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Ответственный              = ВыборкаКураторов.Куратор;
			НоваяЗапись.Задача                     = Ссылка;
			НоваяЗапись.НаименованиеДокументаЭтапа = Наименование;
			НоваяЗапись.Основание                  = Перечисления.ЦБР_ПричиныВнимания.ЭтоНоваяЗадача;
			НоваяЗапись.ДатаДобавления             = ТекущаяДатаСеанса();
			НоваяЗапись.ТрудозатратыПлан           = Длительность;
			НаборЗаписей.Записать();
			
		ИначеЕсли ВыборкаКураторов.КоличествоПодчиненных > 0 Тогда 
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;
		
	//Отбор для формирования требующих внимания по исполнителям
	Если СостояниеЗадачи = Перечисления.ЦБР_Состояние.ЗапросИнформации Тогда 
		
		НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Задача.Установить(Ссылка);
		НаборЗаписей.Прочитать();
		
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
		
	ИначеЕсли Не СостояниеЗадачи = Перечисления.ЦБР_Состояние.Выполнен Тогда
		
		Пока ВыборкаИсполнителей.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.УделяющийВнимание.Установить(ВыборкаИсполнителей.Исполнитель);
			НаборЗаписей.Отбор.Задача.Установить(Ссылка);
			НаборЗаписей.Отбор.Задание.Установить(Документы.ЦБР_Задание.ПустаяСсылка());
			НаборЗаписей.Отбор.ПричинаВнимания.Установить(Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий);
			НаборЗаписей.Прочитать();
			НаборЗаписей.Очистить();
			Если НаборЗаписей.Количество() Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Ответственный = ВыборкаИсполнителей.Исполнитель;
				НоваяЗапись.Задача = Ссылка;
				НоваяЗапись.НаименованиеДокументаЭтапа = Наименование;
				НоваяЗапись.Основание = Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий;
				НоваяЗапись.ДатаДобавления = ТекущаяДатаСеанса();
				НоваяЗапись.ТрудозатратыПлан = Длительность;
				НаборЗаписей.Записать();
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
			
			НаборЗаписей.Отбор.УделяющийВнимание.Установить(ВыборкаИсполнителей.Исполнитель);
			НаборЗаписей.Отбор.Задача.Установить(Ссылка);
			НаборЗаписей.Отбор.ПричинаВнимания.Установить(Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 И ВыборкаИсполнителей.КоличествоЗапланированныхДействий = 0 И (ВыборкаИсполнителей.КоличествоРабочихЭтаповБезЗаданий > 0 ИЛИ ВыборкаИсполнителей.КоличествоЭтаповИсполнителя = 0) Тогда
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Ответственный = ВыборкаИсполнителей.Исполнитель;
				НоваяЗапись.Задача = Ссылка;
				НоваяЗапись.НаименованиеДокументаЭтапа = Наименование;
				НоваяЗапись.Основание = Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий;
				НоваяЗапись.ДатаДобавления = ТекущаяДатаСеанса();
				НоваяЗапись.ТрудозатратыПлан = Длительность;
				НаборЗаписей.Записать(Истина);
				
			ИначеЕсли ВыборкаИсполнителей.КоличествоЗапланированныхДействий > 0 ИЛИ (ВыборкаИсполнителей.КоличествоРабочихЭтаповБезЗаданий = 0 И ВыборкаИсполнителей.КоличествоЭтаповИсполнителя > 0) Тогда 
				НаборЗаписей.Записать();
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьТребуютВниманияЗадания()
	
	МассивЗаданий = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьЗаданияПоЗадаче(Ссылка);
	
	Для Каждого Задание Из МассивЗаданий Цикл
		Длительность = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСуммуОценокПоЗадаче(Ссылка);
		Исполнитель = Задание.Исполнитель;
		Состояние = Задание.Состояние;
		КоличествоЗапланированныхДействий = Задание.КоличествоЗапланированныхДействий;
		
		////Для заданий делать: 
		//1. Для одного исполнителя
		//2. Потребуется только количество запланированных действий в календаре
		//3. Возможно, действовать только при записи в зависимости от статуса (+ в календаре при записи, старте и окончании ОС)	 
		
		Если Состояние = Перечисления.ЦБР_Состояние.ЗапросИнформации Или 
			Состояние = Перечисления.ЦБР_Состояние.НаСогласовании Тогда 
			
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Задание.Установить(Задание.Ссылка);			
			НаборЗаписей.Отбор.ПричинаВнимания.Установить(Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий);	
			
			НаборЗаписей.Отбор.УделяющийВнимание.Установить(Исполнитель);		
			НаборЗаписей.Прочитать();
			
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
			
		ИначеЕсли Состояние = Перечисления.ЦБР_Состояние.Выполнен Тогда
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Задание.Установить(Задание.Ссылка);			
			
			НаборЗаписей.Прочитать();
			
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
			
			ЦБР_ЗадачиЗаданияВызовСервера.УбратьБлокТребуетВнимания(Ссылка);
		ИначеЕсли Состояние = Перечисления.ЦБР_Состояние.Отменен Тогда
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Задача.Установить(Ссылка);
			НаборЗаписей.Отбор.Задание.Установить(Задание.Ссылка);
			НаборЗаписей.Записать(); 
		Иначе
			НаборЗаписей = РегистрыСведений.ЦБР_ТребуютВнимания.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Задание.Установить(Задание.Ссылка);			
			НаборЗаписей.Отбор.ПричинаВнимания.Установить(Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий);	
			НаборЗаписей.Отбор.УделяющийВнимание.Установить(Исполнитель);		
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 И КоличествоЗапланированныхДействий = 0 Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Ответственный              = Исполнитель;
				НоваяЗапись.Задача                     = Ссылка;
				НоваяЗапись.Задание                    = Задание.Ссылка;
				НоваяЗапись.НаименованиеДокументаЭтапа = Наименование;
				НоваяЗапись.Основание                  = Перечисления.ЦБР_ПричиныВнимания.НетЗапланированныхДействий;
				НоваяЗапись.ДатаДобавления             = ТекущаяДатаСеанса();
				НоваяЗапись.ТрудозатратыПлан           = Длительность;
				НаборЗаписей.Записать();
			ИначеЕсли КоличествоЗапланированныхДействий > 0 Тогда
				НаборЗаписей.Очистить();
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция МассивВыборокДанныхПоСотрудникамИРуководителям(АктивныйКуратор)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Исполнитель КАК Исполнитель,
	|	ЦБР_ЗадачаКоманда.Роль КАК Роль,
	|	ВЫБОР
	|		КОГДА ЦБР_ЗадачаКоманда.Исполнитель = &АктивныйКуратор
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоАктивныйКуратор
	|ПОМЕСТИТЬ ВТ_Кураторы
	|ИЗ
	|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|ГДЕ
	|	ЦБР_ЗадачаКоманда.Роль = ЗНАЧЕНИЕ(Перечисление.ЦБР_РольВЗадаче.КураторИсполнителя)
	|	И ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Исполнитель КАК Исполнитель,
	|	ЦБР_ЗадачаКоманда.Роль КАК Роль,
	|	ЦБР_ЗадачаКоманда.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Исполнители
	|ИЗ
	|	Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|ГДЕ
	|	(ЦБР_ЗадачаКоманда.Роль = ЗНАЧЕНИЕ(Перечисление.ЦБР_РольВЗадаче.Исполнитель)
	|			ИЛИ ЦБР_ЗадачаКоманда.Роль = ЗНАЧЕНИЕ(Перечисление.ЦБР_РольВЗадаче.ИсполнительРазработчик)
	|			ИЛИ ЦБР_ЗадачаКоманда.Роль = ЗНАЧЕНИЕ(Перечисление.ЦБР_РольВЗадаче.ИсполнительАналитик))
	|	И ЦБР_ЗадачаКоманда.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЦБР_ЗадачаКоманда.Исполнитель,
	|	ЦБР_ЗадачаКоманда.Роль,
	|	ЦБР_ЗадачаКоманда.Ссылка
	|ИЗ
	|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_СтруктураЗадач
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЦБР_Задача.Команда КАК ЦБР_ЗадачаКоманда
	|		ПО ЦБР_СтруктураЗадач.Исполнитель = ЦБР_ЗадачаКоманда.Исполнитель
	|ГДЕ
	|	ЦБР_СтруктураЗадач.Владелец = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь,
	|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Событие
	|ПОМЕСТИТЬ ВТ_ЗапланированныеСобытия
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.Статус В(&Статус)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦБР_ЗадачаДеревоЗаданий.Наименование КАК НаименованиеЭтапа,
	|	ЦБР_ЗадачаДеревоЗаданий.Исполнитель КАК Исполнитель,
	|	ЦБР_ЗадачаДеревоЗаданий.СсылкаНаЗадание.СсылкаНаЗадачу КАК Ссылка
	|ПОМЕСТИТЬ ВТ_РабочиеЭтапыБезЗаданий
	|ИЗ
	|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_ЗадачаДеревоЗаданий
	|ГДЕ
	|	ЦБР_ЗадачаДеревоЗаданий.СсылкаНаЗадание.СсылкаНаЗадачу = &Ссылка
	|	И (ЦБР_ЗадачаДеревоЗаданий.Состояние = ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.ВРаботе)
	|			ИЛИ ЦБР_ЗадачаДеревоЗаданий.Состояние = ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.ОценкаНеСогласована)
	|			ИЛИ ЦБР_ЗадачаДеревоЗаданий.Состояние = ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.ОценкаСогласована)
	|			ИЛИ ЦБР_ЗадачаДеревоЗаданий.Состояние = ЗНАЧЕНИЕ(Перечисление.ЦБР_Состояние.Черновик))
	|	И ЦБР_ЗадачаДеревоЗаданий.СсылкаНаЗадание = ЗНАЧЕНИЕ(Документ.ЦБР_Задание.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦБР_ЗадачаДеревоЗаданий.Наименование КАК НаименованиеЭтапа,
	|	ЦБР_ЗадачаДеревоЗаданий.Исполнитель КАК Исполнитель,
	|	ЦБР_ЗадачаДеревоЗаданий.СсылкаНаЗадание.СсылкаНаЗадачу КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ЭтапыИсполнителя
	|ИЗ
	|	Справочник.ЦБР_СтруктураЗадач КАК ЦБР_ЗадачаДеревоЗаданий
	|ГДЕ
	|	ЦБР_ЗадачаДеревоЗаданий.СсылкаНаЗадание.СсылкаНаЗадачу = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Кураторы.Исполнитель КАК Куратор,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Исполнители.Исполнитель) КАК КоличествоПодчиненных,
	|	ВТ_Кураторы.ЭтоАктивныйКуратор КАК ЭтоАктивныйКуратор
	|ИЗ
	|	ВТ_Кураторы КАК ВТ_Кураторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Исполнители КАК ВТ_Исполнители
	|		ПО (ВТ_Исполнители.Исполнитель.Подразделение = ВТ_Кураторы.Исполнитель.Подразделение)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Кураторы.Исполнитель,
	|	ВТ_Кураторы.ЭтоАктивныйКуратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Исполнители.Исполнитель КАК Исполнитель,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ЗапланированныеСобытия.Событие) КАК КоличествоЗапланированныхДействий,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_РабочиеЭтапыБезЗаданий.НаименованиеЭтапа) КАК КоличествоРабочихЭтаповБезЗаданий,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ЭтапыИсполнителя.НаименованиеЭтапа) КАК КоличествоЭтаповИсполнителя
	|ИЗ
	|	ВТ_Исполнители КАК ВТ_Исполнители
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗапланированныеСобытия КАК ВТ_ЗапланированныеСобытия
	|		ПО (ВТ_ЗапланированныеСобытия.Событие.ЗаданиеСсылка.СсылкаНаЗадачу = ВТ_Исполнители.Ссылка)
	|			И (ВТ_ЗапланированныеСобытия.Событие.Автор = ВТ_Исполнители.Исполнитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РабочиеЭтапыБезЗаданий КАК ВТ_РабочиеЭтапыБезЗаданий
	|		ПО (ВТ_РабочиеЭтапыБезЗаданий.Исполнитель = ВТ_Исполнители.Исполнитель)
	|			И (ВТ_РабочиеЭтапыБезЗаданий.Ссылка = ВТ_Исполнители.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЭтапыИсполнителя КАК ВТ_ЭтапыИсполнителя
	|		ПО ВТ_Исполнители.Ссылка = ВТ_ЭтапыИсполнителя.Ссылка
	|			И ВТ_Исполнители.Исполнитель = ВТ_ЭтапыИсполнителя.Исполнитель
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Исполнители.Исполнитель";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("АктивныйКуратор", АктивныйКуратор);

	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый);
	МассивСтатусов.Добавить(Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);
	Запрос.УстановитьПараметр("Статус", МассивСтатусов);
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();

	Возврат МассивРезультатовЗапроса;

КонецФункции

Процедура ПоискПользователяВКоманде(Исполнитель, Роль)
	
	НайденаяСтрока = Команда.НайтиСтроки(Новый Структура("Исполнитель", Исполнитель));
	Если НайденаяСтрока.Количество() Тогда
		Для Каждого Строка Из НайденаяСтрока Цикл
			Если Строка.Роль = Роль Тогда
				Возврат;
			КонецЕсли;   
		КонецЦикла;	
	КонецЕсли;
		
	НоваяСтрока = Команда.Добавить();
	НоваяСтрока.Исполнитель = Исполнитель;
	НоваяСтрока.Роль = Роль;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли