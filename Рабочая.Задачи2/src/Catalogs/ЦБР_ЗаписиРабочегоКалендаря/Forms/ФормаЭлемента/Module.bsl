#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущаяЗаписьКалендаря;

&НаКлиенте
Перем ТекущаяДатаНачала;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьУсловноеОформление();
	ЗаполнитьСписокВидаРабот(Элементы.ВидРаботы.СписокВыбора); 
	
	Если Параметры.Свойство("ПовторяющеесяСобытие") И ЗначениеЗаполнено(Параметры.ПовторяющеесяСобытие)
		И Параметры.Свойство("ДатаИсключения") И ЗначениеЗаполнено(Параметры.ДатаИсключения) Тогда
		
		ПовторяющеесяСобытие = Параметры.ПовторяющеесяСобытие;
		ПовторяющеесяСобытиеДатаИсключения = Параметры.ДатаИсключения;
		
	КонецЕсли;
	
	ИспользоватьНапоминанияПользователя = ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя");
	Если ИспользоватьНапоминанияПользователя Тогда
		СрокНапоминанияПоУмолчанию = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиНапоминаний", "СрокНапоминанияПоУмолчанию", 15);
		УстанавливатьНапоминаниеАвтоматически = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиНапоминаний", "УстанавливатьНапоминаниеАвтоматически", Истина);
		ПрочитатьНапоминание();
	КонецЕсли;
	
	НачальноеЗначениеДатаНачала = Объект.ДатаНачала;
	НачальноеЗначениеДатаОкончания = Объект.ДатаОкончания;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	УстановитьПодписьПовторять();
	УстановитьВидимостьЭлементовФормы();
		
	ОтображатьВремяС = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьВремяС");
	
	ИнициализироватьКалендарь();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Пользователь = ТекущийПользователь;
		Объект.Автор = ТекущийПользователь; 
	    Объект.ВидРаботы = Элементы.ВидРаботы.СписокВыбора[0].Значение;
	КонецЕсли;
	
	ОбновитьПредставлениеМестногоЧасовогоПояса();
	
	ОбновитьФактическиеЧасы();
	
	Если ЗначениеЗаполнено(Объект.ЗадачаСсылка) И Не ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда
		Объект.ЗаданиеСсылка = СоздатьДокументЗадача("Уточнение требований",,Перечисления.ЦБР_Состояние.Новый);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементовФормы();
	ВывестиДлительностьСобытия();
	ЗаполнитьСпискиВыбораДат();
	ЗаполнитьИнтервалыНапоминания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И ((УстановитьНапоминание И (ИзмененоНапоминание Или ИзмененоВремя
		Или Не УстановленоНапоминание)) Или (Не УстановитьНапоминание И УстановленоНапоминание)) Тогда
		ОбработатьИзменениеНапоминанияНаСервере();
		ОбработатьИзменениеНапоминанияНаКлиенте();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НапоминанияПользователя_Документооборот" И Источник = Объект.Ссылка
		И ПараметрыНапоминанияДокументооборота(Параметр) И ИспользоватьНапоминанияПользователя Тогда
		
		Напоминание = Параметр;
		УстановитьНадписьНапоминания(
		Параметр, УстановитьНапоминание, УстановленоНапоминание, СрокНапоминанияПоУмолчанию, ИнтервалВремениСтрокой,
		СпособУстановкиВремениНапоминания, ВремяНапоминания);
		УстановитьДоступностьЭлементовФормы();
		ЗаполнитьИнтервалыНапоминания();
		
	ИначеЕсли ИмяСобытия = "Удаление_НапоминанияПользователя_Документооборот" И Источник = Объект.Ссылка
		И ИспользоватьНапоминанияПользователя Тогда
		
		Напоминание = Неопределено;
		УстановитьНадписьНапоминания(
		Неопределено, УстановитьНапоминание, УстановленоНапоминание, СрокНапоминанияПоУмолчанию,
		ИнтервалВремениСтрокой, СпособУстановкиВремениНапоминания, ВремяНапоминания);
		УстановитьДоступностьЭлементовФормы();
		ЗаполнитьИнтервалыНапоминания();
		
	ИначеЕсли ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда
		
		Если (ТипЗнч(Параметр) = Тип("СправочникСсылка.ЦБР_ЗаписиРабочегоКалендаря") И Параметр = Объект.Ссылка И Источник
			<> УникальныйИдентификатор) Или (ТипЗнч(Параметр) = Тип("Массив") И Параметр.Найти(Объект.Ссылка) <> Неопределено
			И Источник <> УникальныйИдентификатор) Тогда
			
			Прочитать();
			УстановитьДоступностьЭлементовФормы();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда
		
		Если Источник = УникальныйИдентификатор Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Если Параметр.Количество() <> 0 Тогда
				ТекущаяЗаписьКалендаря = Параметр[0];
				ТекущаяДатаНачала = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьОтображениеКалендарьКлиент(Параметр);
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДоступноеВремяПользователя" Или ИмяСобытия = "Запись_ЗанятостьСотрудника" Или ИмяСобытия
		= "Запись_Мероприятие" Или ИмяСобытия = "Запись_ДоступноеВремяПользователя" Или ИмяСобытия = "Запись_Отсутствие"
		Или ИмяСобытия = "Запись_НастройкиКалендаря" Тогда
		
		ОбновитьОтображениеКалендарьКлиент();
		
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеДатыТекущейЗаписи" И Источник = УникальныйИдентификатор Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметр);
		ПриИзмененииДаты(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПрочитатьНапоминание();
	УстановитьПодписьПовторять();
	УстановитьВидимостьЭлементовФормы();
	ОбновитьОтображениеКалендарь();
	ОбновитьПредставлениеМестногоЧасовогоПояса();
	
	ОписаниеВыполненнойРаботы = ТекущийОбъект.Результат.Получить();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
    
  Если Объект.Ссылка.Пустая() Тогда
	  КоличествоДней = (НачалоДня(Объект.ДатаОкончания) - НачалоДня(Объект.ДатаНачала)) / (60 * 60 * 24) + 1;
	  
	  Если КоличествоДней > 1 Тогда
		  ДатыПоДням = РазбитьЗаписиНаНесколькоДней(Объект.ДатаНачала, Объект.ДатаОкончания, КоличествоДней);
		  Для Каждого Строка Из ДатыПоДням Цикл
			  Если Строка = ДатыПоДням[0] Тогда
				  Объект.ДатаНачала = Строка.Начало;
				  Объект.ДатаОкончания = Строка.Конец;
			  Иначе
				  СоздатьНовуюЗаписьОС(Строка.Начало, Строка.Конец);
			  КонецЕсли;
		  КонецЦикла;
		  Оповестить("Запись_Мероприятие");
	  КонецЕсли; 
	  
	  Если Не ЗначениеЗаполнено(Объект.Описание) Тогда
		  ОбщегоНазначенияКлиент.СообщитьПользователю(
		  НСтр("ru = 'Заполните описание!'"), Объект.Ссылка, "Описание", "Объект.Описание", );
		  Отказ = Истина;
	  КонецЕсли;
	  
	  //Если ЗначениеЗаполнено(Объект.ЗадачаСсылка) И НЕ Значениезаполнено(Объект.ЗаданиеССылка) Тогда
	  //    Задание = СоздатьЗаданиеУточнениеТребований();	
	  //    Объект.ЗаданиеССылка = Задание;	
	  //КонецЕсли; 
	  Если ЗначениеЗаполнено(Объект.ЗаданиеССылка) Тогда
		  ТекстУведомления = "Действие: " + Объект.Описание + Символы.ПС + "Дата начала: " + Объект.ДатаНачала + Символы.ПС + "Дата окончания: " + Объект.ДатаОкончания;
		  ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.ЗаданиеССылка, "НовоеДействиеОС",ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеССылка, "Постановщик"), ТекстУведомления);
		  ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеССылка, "СсылкаНаЗадачу"), "НовоеДействиеОС",ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеССылка, "Постановщик"), ТекстУведомления);
	  КонецЕсли;
	  
  КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ПовторяющеесяСобытие) И ЗначениеЗаполнено(ПовторяющеесяСобытиеДатаИсключения) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ПовторяющеесяСобытие", ПовторяющеесяСобытие);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ДатаИсключения", ПовторяющеесяСобытиеДатаИсключения);
	КонецЕсли;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("УстановитьНапоминание", УстановитьНапоминание);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);
	
    ЭффективныеЧасыПоЗаданию = ПолучитьФактическиеЧасыПоЗаданию();
	Если ЭффективныеЧасыПоЗаданию <> ЭффективныеЧасы Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ЭффективныеЧасы", ЭффективныеЧасы);
	КонецЕсли;
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	ТекущийОбъект.Результат = Новый ХранилищеЗначения(ОписаниеВыполненнойРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Объект.Статус <> Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал Тогда
		ЦветЗаписи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.ВидРаботы, "ЦветЗаписи");
		Объект.Цвет = ЦветЗаписи.Получить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ВыполнитьОповещениеОбИзменении();
	ОбработатьИзменениеНапоминанияНаКлиенте();
	УстановитьДоступностьЭлементовФормы();
	
	ТекстОповещения = ?(ПараметрыЗаписи.ЭтоНовыйОбъект, НСтр("ru = 'Создание:'"), НСтр("ru = 'Изменение:'"));
	ПоказатьОповещениеПользователя(
	ТекстОповещения, ПолучитьНавигационнуюСсылку(Объект.Ссылка), Строка(Объект.Ссылка),
	БиблиотекаКартинок.Информация32);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриступилПриИзменении(Элемент)
	
	Если Объект.Ссылка.Пустая() Тогда
		Записать();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Описание) Тогда 
	   Приступил = Ложь;
	   Возврат        
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда                   
	    Состояние = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(Объект.ЗаданиеСсылка);
		Если Состояние = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе") Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю("Задание уже в работе", , "Приступил");
			Приступил = Ложь;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Объект.ДатаНачала = ТекущаяДата();
	ПриИзмененииДаты();

	УстановитьНапоминание = Ложь;
	РаботаСРабочимКалендаремСервер.УстановитьОтработанаЗаписьКалендаря(Объект.Ссылка);
	
	
	ВыполнитьОповещениеОбИзменении();
	
	Объект.Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Зеленый");
	
	Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) И ТипЗнч(Объект.ЗаданиеСсылка) = Тип("ДокументСсылка.ЦБР_Задание") Тогда 		
		
		Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
		Структура.Задание = Объект.ЗаданиеСсылка;
		Структура.ДатаНачалаДействия = ТекущаяДата();
		Структура.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.ВРаботе");
		
		ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.Ссылка);
		Если ПоследнееДействие = Неопределено Или ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
			Структура.ЗаписьКалендаря = Объект.Ссылка;
			ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю("Есть не заврешенное действие по этой задаче!");
			Возврат;
		КонецЕсли;
		
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.ЗаданиеСсылка, 
			ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_Состояние.ВРаботе"));
		
	КонецЕсли;
	
	Записать();
	УстановитьДоступностьЭлементовФормы();
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяПриИзменении(Элемент)
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораЭлемента(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломРедактированиемЭлемента(Элемент, НовыйЭлемент,
	СтандартнаяОбработка, Объект.Пользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередСозданием(
	Элемент, Объект.ЗаданиеСсылка, Объект.ЗадачаСсылка,Начало, Конец, Значения, Текст, СтандартнаяОбработка, Ложь, Объект.Пользователь,
	НастройкиОтображения, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемЭлемента(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)
	
	Если Элемент.ВыделенныеЭлементы.Количество() Тогда
		Если Объект.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал") Тогда
			Элемент.ВыделенныеЭлементы[0].РежимРазрешенияРедактирования = РежимРазрешенияРедактированияЭлементаПланировщика.ЗапретитьРедактирование; 
		Иначе
			Элемент.ВыделенныеЭлементы[0].РежимРазрешенияРедактирования = РежимРазрешенияРедактированияЭлементаПланировщика.РазрешитьРедактирование;
		КонецЕсли;
		
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПриАктивизации(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаОкончанияРедактированияЭлемента(
	Элемент, НовыйЭлемент, ОтменаРедактирования, Планировщик, НастройкиОтображения, УникальныйИдентификатор,
	Объект.Пользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРаботыПриИзменении(Элемент)
	
	РеквизитыВидаРабот = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(Объект.ВидРаботы, "Отсутствие, ПоПлану, ВесьДень");
	
	Если РеквизитыВидаРабот.Отсутствие Тогда 
		Объект.Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий");
	КонецЕсли;
	
	Если РеквизитыВидаРабот.ПоПлану Тогда
		Объект.Описание = Строка(Объект.ВидРаботы);
	КонецЕсли;
	
	Если Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Болезнь") Тогда
		Объект.Описание = "Больничный";
	ИначеЕсли Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Отпуск") Тогда 
		Объект.Описание = "Отпуск";
	ИначеЕсли Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Административный") Тогда 
		Объект.Описание = "Административный"; 
	Иначе
		Объект.Описание = ""; 
	КонецЕсли;
	
	Объект.ВесьДень = РеквизитыВидаРабот.ВесьДень; 
	
	УстановитьДоступностьЭлементовФормы(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторятьПриИзменении(Элемент)
	
	Повторять = (Объект.ТипЗаписиКалендаря = ПредопределенноеЗначение(
	"Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие"));
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиент.ОткрытьФормуНастройкиПовторения(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанаПриИзменении(Элемент)
	
	//Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда
	//	Если ТипЗнч(Объект.ЗаданиеСсылка) = Тип("ДокументСсылка.ЦБР_Задание") Тогда 
	//		РеквизитыЗадания = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(Объект.ЗаданиеСсылка, "ТрудозатратыПлан, ТрудозатратыФакт, ОплатаПоФакту");     
	//		ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.ЗаданиеСсылка);
	//		Если Не ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
	//			ДопПараметр = Новый Структура("Выполнение", Ложь);
	//			Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект, ДопПараметр);
	//			ПараметрыФормы = Новый Структура;  
	//			ПараметрыФормы.Вставить("ЧасыДоступны", РеквизитыЗадания.ТрудозатратыПлан - РеквизитыЗадания.ТрудозатратыФакт);
	//			ПараметрыФормы.Вставить("ПоФакту", РеквизитыЗадания.ОплатаПоФакту);
	//			ПараметрыФормы.Вставить("Задание", Объект.ЗаданиеСсылка);
	//			ПараметрыФормы.Вставить("ДействиеКалендаря", Объект.Ссылка);
	//			ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	//		КонецЕсли;	
	//	Иначе
	//		Подсказка = "Результат:";
	//		Оповещение  = Новый ОписаниеОповещения("ПослеВводаРезультата", ЭтотОбъект);
	//		ПоказатьВводСтроки(Оповещение, , Подсказка, 0, Истина);		
	//	КонецЕсли;
	//Иначе
	//	УстановитьОтработана();	
	//КонецЕсли;
	//
	//Объект.ДатаОкончания = ТекущаяДата();
	//
	//УстановитьДоступностьЭлементовФормы();  
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(Текст, ДанныеВыбора, Объект.ДатаНачала);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(Текст, ДанныеВыбора, Объект.ДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяПриИзменении(Элемент)
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);
	
	ПриИзмененииДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(Текст, ДанныеВыбора, Объект.ДатаОкончания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(Текст, ДанныеВыбора, Объект.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВесьДеньПриИзменении(Элемент)
	
	РаботаСРабочимКалендаремКлиент.ПриИзмененииВесьДень(
	Объект, НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, ОтображатьВремяС);
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
	ЗаполнитьСпискиВыбораДат();
	ВывестиДлительностьСобытия();
	УстановитьДоступностьЭлементовФормы();
	ОбновитьОтображениеТекущейЗаписиВКалендаре();
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторятьНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиент.ОткрытьФормуНастройкиПовторения(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение( , Объект.ЗаданиеСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНапомнитьНажатие(Элемент)
	
	УстановитьНапоминание = Не УстановитьНапоминание;
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Задача = ПолучитьЗадачу();
	ПоказатьЗначение( , Задача);
КонецПроцедуры  

&НаКлиенте
Процедура УстановитьНапоминаниеПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы();
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойПриИзменении(Элемент)
	
	ЗаполнитьИнтервалыНапоминания();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = НСтр("ru = 'Другое...'") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("СпособУстановкиВремениНапоминания", СпособУстановкиВремениНапоминания);
		ПараметрыФормы.Вставить("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);
		ПараметрыФормы.Вставить("ВремяНапоминания", ?(СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя"), ВремяНапоминания, Объект.ДатаНачала));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ИнтервалВремениСтрокойОбработкаВыбораЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("РегистрСведений.НапоминанияПользователя.Форма.МоиНапоминания", ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	ИначеЕсли ИнтервалВремениСтрокой <> ВыбранноеЗначение Тогда
		
		ИзмененоНапоминание = Истина;
		СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПланДня

&НаКлиенте
Процедура ПланДняВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораПланДня(
	Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаНачалаПеретаскиванияПланДня(
	Элемент, ПараметрыПеретаскивания, Выполнение, НастройкиОтображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломДобавленияПланДня(
	Элемент, Отказ, Копирование, Родитель, Группа, НастройкиОтображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломИзменения(Элемент, Отказ)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломИзмененияПланДня(Элементы.ПланДня, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередУдалением(Элемент, Отказ)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемПланДня(Элементы.ПланДня, ПланДня, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПеретаскиванияПланДня(
	Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПриАктивизацииСтроки(Элемент)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаАктивизацииОбластиПланДня(
	Элемент, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПроверкиПеретаскиванияПланДня(
	Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЭтоСозвон(Команда)             
	Если ТипЗнч(Объект.ЗаданиеСсылка) = Тип("ДокументСсылка.ЦБР_Задание") Тогда
		Объект.Описание = СтрШаблон("Созвон по заданию: %1", Объект.ЗаданиеСсылка);
	Иначе
		Объект.Описание = СтрШаблон("Созвон по задаче: %1", ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеСсылка, "НаименованиеЗадачи"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеСсылка(Команда)
	
	Если Объект.ЗаданиеСсылка.Пустая() ТОгда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Объект.ЗадачаСсылка) <> Тип("СправочникСсылка.ЦБР_Задача") Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЭтап();
	ОбщегоНазначенияКлиент.СообщитьПользователю("Этап создан!");
	Объект.СобытиеДобавленно = Истина;
	УстановитьДоступностьЭлементовФормы();
	Записать();      
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКалендарь(Команда)
	
	ПоказатьКалендарьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", Ложь);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкаУдаления(Команда)
	
	Если Модифицированность = Истина Или (Не ЗначениеЗаполнено(Объект.Ссылка)) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПометкаУдаленияЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр(
		"ru = 'Для установки отметки удаления необходимо записать внесенные вами изменения. Записать данные?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиент.УстановитьПометкуУдаления(Объект.Ссылка, Не Объект.ПометкаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработатьПланДня(Команда)
	
	РаботаСРабочимКалендаремКлиент.ОтработатьВыделенныеЗаписиКалендаряПланДня(Элементы.ПланДня, ПланДня);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработатьПланировщик(Команда)
	
	РаботаСРабочимКалендаремКлиент.ОтработатьВыделенныеЗаписиКалендаря(Элементы.Планировщик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоПлану(Команда)

	Если НачалоДня(Объект.ДатаНачала) < НачалоДня(ТекущаяДата() - 7 * 24 * 3600) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нельзя создать ОС по плану позже " + Формат(НачалоДня(ТекущаяДата() - 7 * 24 * 3600),"ДФ=dd.MM.yyyy"));  
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Описание) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполните описание!");  
		Возврат;
	КонецЕсли;	
	
	Записать();
	
	КоличествоДней = (НачалоДня(Объект.ДатаОкончания) - НачалоДня(Объект.ДатаНачала)) / (60 * 60 * 24) + 1;

	Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;   
	ПараметрыФормы.Вставить("Задание", Объект.ЗаданиеСсылка);
	ПараметрыФормы.Вставить("ЗаписьОС", Объект.Ссылка);
	ПараметрыФормы.Вставить("Описание", Объект.Описание);
	ПараметрыФормы.Вставить("ПоПлану", Истина);       
	ПараметрыФормы.Вставить("КоличествоДней", КоличествоДней); 
	Если КоличествоДней > 1 Тогда
		ПараметрыФормы.Вставить("ДатыПоДням", РазбитьЗаписиНаНесколькоДней(Объект.ДатаНачала, Объект.ДатаОкончания, КоличествоДней));
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПредставлениеВремениДокументооборот(Время)
	
	Если Время = 0 Тогда
		Возврат НСтр("ru = 'при наступлении события'");
	Иначе
		ПредставлениеВремени = ПредставлениеВремени(Время);
		ПредставлениеПредлога = НСтр("ru = 'за'");
		Возврат СтрШаблон(НСтр("ru = '%1 %2'"), ПредставлениеПредлога, ПредставлениеВремени);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПредставлениеВремени(Знач Время, ПолноеПредставление = Истина, ВыводитьСекунды = Истина)
	Результат = "";
	
	// Представление единиц измерения времени в винительном падеже для количеств: 1, 2-4, 5-20.
	ПредставлениеНедель = НСтр("ru = ';%1 неделю;;%1 недели;%1 недель;%1 недели'");
	ПредставлениеДней   = НСтр("ru = ';%1 день;;%1 дня;%1 дней;%1 дня'");
	ПредставлениеЧасов  = НСтр("ru = ';%1 час;;%1 часа;%1 часов;%1 часа'");
	ПредставлениеМинут  = НСтр("ru = ';%1 минуту;;%1 минуты;%1 минут;%1 минуты'");
	ПредставлениеСекунд = НСтр("ru = ';%1 секунду;;%1 секунды;%1 секунд;%1 секунды'");
	
	Время = Число(Время);
	
	Если Время < 0 Тогда
		Время = -Время;
	КонецЕсли;
	
	КоличествоНедель = Цел(Время / 60/60/24/7);
	КоличествоДней   = Цел(Время / 60/60/24);
	КоличествоЧасов  = Цел(Время / 60/60);
	КоличествоМинут  = Цел(Время / 60);
	КоличествоСекунд = Цел(Время);
	
	КоличествоСекунд = КоличествоСекунд - КоличествоМинут * 60;
	КоличествоМинут  = КоличествоМинут - КоличествоЧасов * 60;
	КоличествоЧасов  = КоличествоЧасов - КоличествоДней * 24;
	КоличествоДней   = КоличествоДней - КоличествоНедель * 7;
	
	Если Не ВыводитьСекунды Тогда
		КоличествоСекунд = 0;
	КонецЕсли;
	
	Если КоличествоНедель > 0 И КоличествоДней+КоличествоЧасов+КоличествоМинут+КоличествоСекунд=0 Тогда
		Результат = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеНедель, КоличествоНедель);
	Иначе
		КоличествоДней = КоличествоДней + КоличествоНедель * 7;
		
		Счетчик = 0;
		Если КоличествоДней > 0 Тогда
			Результат = Результат + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеДней, КоличествоДней) + " ";
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если КоличествоЧасов > 0 Тогда
			Результат = Результат + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеЧасов, КоличествоЧасов) + " ";
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если (ПолноеПредставление Или Счетчик < 2) И КоличествоМинут > 0 Тогда
			Результат = Результат + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеМинут, КоличествоМинут) + " ";
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если (ПолноеПредставление Или Счетчик < 2) И (КоличествоСекунд > 0 Или КоличествоНедель+КоличествоДней+КоличествоЧасов+КоличествоМинут = 0) Тогда
			Результат = Результат + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеСекунд, КоличествоСекунд);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СокрП(Результат);
	
КонецФункции

&НаСервере
Процедура СоздатьНовуюЗаписьОС(Начало, Конец)
	
	Запись_ОС = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(Запись_ОС, Объект);
	Запись_ОС.ДатаНачала = Начало;
	Запись_ОС.ДатаОкончания = Конец;
	Запись_ОС.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазбитьЗаписиНаНесколькоДней(Начало, Конец, КоличествоДней)
	
	ОсПоДням = Новый Массив;
	День = 1;
	ДатаОкончания = Конец;
	Пока День <= КоличествоДней Цикл
		Если ДеньНедели(Начало) = 6 Или ДеньНедели(Начало) = 7 Тогда
			День = День + 1;
			Начало = НачалоДня(НачалоДня(Начало) + 25 * 60 * 60) + 9 * 60 * 60;
			Продолжить;
		КонецЕсли;
		СтруктураДат = Новый Структура("Начало, Конец, Часов");
		Если День = КоличествоДней Тогда
			Конец = ДатаОкончания;
		Иначе  
			Конец = НачалоДня(Начало) + 64800;
		КонецЕсли;
		СтруктураДат.Начало = Начало;
		СтруктураДат.Конец = Конец; 
		
		КолВоЧасов = (Конец-Начало)/3600;
		
		СтруктураДат.Часов = ЦенообразованиеКлиентСервер.ОкруглитьЦену(КолВоЧасов, 1, Перечисления.ВариантыОкругления.ВсегдаВПользуПредприятия);    
		ОсПоДням.Добавить(СтруктураДат);
		День = День + 1;
		Начало = НачалоДня(НачалоДня(Начало) + 25 * 60 * 60) + 9 * 60 * 60;
	КонецЦикла;   
	
	Возврат ОсПоДням; 
	
КонецФункции

&НаКлиенте
Процедура ПередЗаписьюПослеВопросаОВозможностиОтсутствия(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьОтсутствие = Ложь;
	Если Записать(ПараметрыЗаписи) Тогда
		
		Если ПараметрыЗаписи.Свойство("ЗакрытьПослеЗаписи") И Не ПараметрыЗаписи.ЗакрытьПослеЗаписи Тогда
			Возврат;
		КонецЕсли;
		
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтработана(ПоПлану = Ложь)

	Если Не ПоПлану Тогда
		Объект.ДатаОкончания = ТекущаяДата();
	КонецЕсли;
	
	ПриИзмененииДаты();
	УстановитьДоступностьЭлементовФормы();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВыполнитьОповещениеОбИзменении();
	КонецЕсли;

	Объект.Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Нет");
	
КонецПроцедуры 

&НаСервере
Процедура ОбновитьПредставлениеМестногоЧасовогоПояса()
	
	//НастройкаРежимОтображенияМестногоВремени = Константы.РежимОтображенияМестногоВремени.Получить();
	//Если НастройкаРежимОтображенияМестногоВремени <> Перечисления.РежимыОтображенияМестногоВремени.ВремяСеанса Тогда
	//	
	//	ЧасовойПоясПользователя = ЧасовойПоясСеанса();
	//	ЧасовойПоясПоУмолчанию = ЧасовойПоясПользователя;
	//	Если ЧасовойПоясПользователя <> ЧасовойПоясПоУмолчанию Тогда
	//		ПредставлениеМестногоЧасовогоПояса = ЧасовойПоясСеанса().Представление;
	//	Иначе
	//		ПредставлениеМестногоЧасовогоПояса = "";
	//	КонецЕсли;
	//	
	//Иначе
		
		ПредставлениеМестногоЧасовогоПояса = "";
		
	//КонецЕсли;
	
	Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость = ЗначениеЗаполнено(ПредставлениеМестногоЧасовогоПояса);
	Элементы.ДлительностьСтр.Видимость = Не Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	РаботаСРабочимКалендаремСервер.УстановитьУсловноеОформлениеПланДня(УсловноеОформление);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДаты(ИзменятьДатуНачалаИОкончания = Истина)
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
	Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень, НачальноеЗначениеДатаНачала,
	НачальноеЗначениеДатаОкончания, , , , , ИзменятьДатуНачалаИОкончания);
	
	ЗаполнитьСпискиВыбораДат();
	ВывестиДлительностьСобытия();
	
	ОбновитьОтображениеТекущейЗаписиВКалендаре();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьКалендарьНаСервере()
	
	ПоказыватьКалендарь = Не ПоказыватьКалендарь;
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьКалендарьВЗаписиКалендаря", ПоказыватьКалендарь);
	ИнициализироватьКалендарь();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВидРаботыОписание", "Видимость",
	ЗначениеЗаполнено(ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидРаботы, "Описание")));
	
	Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда
		
		РеквизитыЗадания = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(Объект.ЗаданиеСсылка, "ЭтоУточнениеТребований, УдалосьРешитьПриПодключении, ТрудозатратыФакт, ТрудозатратыПлан, ТипОценкиЗадачи, ОплатаПоФакту");     
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УдалосьРешитьПриПодключении", "Пометка", РеквизитыЗадания.УдалосьРешитьПриПодключении);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УдалосьРешитьПриПодключении", "Видимость", РеквизитыЗадания.ЭтоУточнениеТребований И Объект.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Завершен") И Не Объект.УдалосьРешитьПриПодключенииПометка);
		
		ТипОценкиЗадачи = РеквизитыЗадания.ТипОценкиЗадачи;     
		
		Если РеквизитыЗадания.ОплатаПоФакту Тогда
			ОстатокЧасов = "По Факту"; 
		ИначеЕсли ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.Предсогласованные") Тогда
			ОстатокЧасов = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьОстатокПредсогласованныхЧасов(Объект.ЗадачаСсылка);
		Иначе
			ОстатокЧасов = РеквизитыЗадания.ТрудозатратыПлан - РеквизитыЗадания.ТрудозатратыФакт;
		КонецЕсли;   
		
		СписаноЧасов = РеквизитыЗадания.ТрудозатратыФакт;
		Если СписаноЧасов > 0 Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "СписаноЧасов", "Видимость", Истина);
		КонецЕсли;
		
		Если ТипОценкиЗадачи = ПредопределенноеЗначение("Перечисление.ЦБР_ТипОценкиЗадачи.ПоФакту") Или ЗначениеЗаполнено(ОстатокЧасов) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "ОстатокЧасов", "Видимость", Истина);
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "ОстатокЧасов", "Видимость", Ложь);
		КонецЕсли;
		
		Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Эффективный");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "ВидРаботы", "Доступность", Ложь);

	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредметСтрокой) ИЛИ ЗначениеЗаполнено(ЗадачаСтрокой) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЭтоСозвон", "Видимость", Объект.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый"));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЭтоФулл", 	"Видимость", Объект.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый"));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоПлану", "Видимость", Объект.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый"));

	Если ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидРаботы, "ПоПлану") Тогда
		//ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Приступил", "Видимость", Ложь);
		//ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отработана", "Видимость", Ложь);
		//ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Описание", "Доступность", Ложь);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Приступил", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отработана", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Описание", "Доступность", Истина);
	КонецЕсли;
	
	Элементы.ДатаНачалаВремя.Видимость = Не Объект.ВесьДень;
	Элементы.ДатаОкончанияВремя.Видимость = Не Объект.ВесьДень;
	Элементы.Повторять.Видимость = Не Объект.ПометкаУдаления И Объект.ТипЗаписиКалендаря <> ПредопределенноеЗначение(
	"Перечисление.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия") И Не ТолькоПросмотр;
	
	Если ИспользоватьНапоминанияПользователя Тогда
		Элементы.ИнтервалВремениСтрокой.Доступность = УстановитьНапоминание;
		Элементы.ГруппаНапоминаниеОтносительноДатыНачала.Видимость = Не Объект.ПометкаУдаления;
	КонецЕсли;
	
	Если ОписаниеВыполненнойРаботы.Элементы.Количество() > 0 Тогда  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Результат", "Видимость", Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Результат", "Видимость", Ложь);
	КонецЕсли;
	
	Если Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Болезнь") ИЛИ
		Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Административный") ИЛИ
		Объект.ВидРаботы = ПредопределенноеЗначение("Справочник.ЦБР_ВидыРаботВКалендаре.Отпуск") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы( Элементы, "Описание", "Доступность", Ложь);
	КонецЕсли;         
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Если Объект.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал Тогда
		ТолькоПросмотр = Истина;
	Иначе
		ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Элементы.ЗаданиеССылка.Видимость = ЗначениеЗаполнено(Объект.ЗаданиеССылка);
	Элементы.ПовторениеСтрокой.Видимость = Повторять;
	
	Элементы.Автор.Видимость = Объект.Пользователь <> Объект.Автор;
	
	Элементы.ФормаЗаписатьИЗакрыть.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = Не ТолькоПросмотр;
	Элементы.ФормаЗаписать.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаУстановитьПометкаУдаления.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаЗакрыть.Видимость = ТолькоПросмотр;
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = ТолькоПросмотр;
	
	Элементы.Повторять.Видимость = Не Объект.ПометкаУдаления 
			И Объект.ТипЗаписиКалендаря <> Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия 
			И Не ТолькоПросмотр;
	
	Элементы.ИнтервалВремениСтрокой.Видимость = УстановитьНапоминание;
	
	Если ТипЗнч(Объект.ЗаданиеССылка) = Тип("ДокументСсылка.ЦБР_Задание") И ЗначениеЗаполнено(Объект.ЗаданиеССылка) Тогда
		ПредметСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеССылка, "Наименование");
		ЗадачаСтрокой = РаботаСРабочимКалендаремСервер.ПолучитьПредставлениеПредмета(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеССылка, "СсылкаНаЗадачу"));
		Элементы.ГруппаПредмет.Видимость = Истина;
	ИначеЕсли ЗначениеЗаполнено(Объект.ЗаданиеССылка) Тогда
		ПредметСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеССылка, "Наименование");
		ЗадачаСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗадачаСсылка, "Наименование");
		Элементы.ГруппаПредмет.Видимость = Истина;
	ИначеЕсли ЗначениеЗаполнено(Объект.ЗадачаСсылка) Тогда
		ЗадачаСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗадачаСсылка, "Наименование");
		Элементы.ГруппаПредмет.Видимость = Истина;
	Иначе
		Элементы.ГруппаПредмет.Видимость = Ложь;
	КонецЕсли;

	Если Объект.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Новый Тогда   
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Начать", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отменить", "Видимость", Истина);
	ИначеЕсли Объект.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал Тогда      
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Начать", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Истина); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отменить", "Видимость", Истина);
	ИначеЕсли Объект.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Завершен Тогда        
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Начать", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отменить", "Видимость", Ложь);    
		ТолькоПросмотр = Истина;                
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНапоминаниеОтносительноДатыНачала", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНастройкиВремени", "Видимость", Ложь);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Начать", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Завершить", "Видимость", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отменить", "Видимость", Ложь);       
		ТолькоПросмотр = Истина;  
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНапоминаниеОтносительноДатыНачала", "Видимость", Ложь); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНастройкиВремени", "Видимость", Ложь);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УдалосьРешитьПриПодключенииПометка", "Видимость", Объект.УдалосьРешитьПриПодключенииПометка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПодписьПовторять()
	
	Повторять = (Объект.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие);
	Если Повторять Тогда
		НастройкиПовторения = ПолучитьНастройкиПовторения();
		ПовторениеСтрокой = РаботаСРабочимКалендаремКлиентСервер.ПолучитьТекстовоеПредставлениеПовторения(
		НастройкиПовторения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиДлительностьСобытия()
	
	ДлительностьСтр = "";
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Возврат;
	КонецЕсли;
	
	ДлительностьСек = Объект.ДатаОкончания - Объект.ДатаНачала;
	
	Если Объект.ВесьДень Тогда
		ДлительностьСек = ДлительностьСек + 1;
	КонецЕсли;
	
	Дней = Цел(ДлительностьСек / 86400); // 86400 - число секунд в сутках
	ПодписьДней = ДелопроизводствоКлиентСервер.ПолучитьПодписьДней(Дней);
	
	Часов = Цел((ДлительностьСек - Дней * 86400) / 3600); // 86400 - число секунд в сутках
	ПодписьЧасов = ДелопроизводствоКлиентСервер.ПолучитьПодписьЧасов(Часов);
	
	Минут = Цел((ДлительностьСек - Дней * 86400 - Часов * 3600) / 60); // 86400 - число секунд в сутках
	ПодписьМинут = ДелопроизводствоКлиентСервер.ПолучитьПодписьМинут(Минут);
	
	Если Дней > 0 Тогда
		ДлительностьСтр = ДлительностьСтр + Строка(Дней) + " " + ПодписьДней;
	КонецЕсли;
	
	Если Часов > 0 Тогда
		
		Если Дней > 0 Тогда
			ДлительностьСтр = ДлительностьСтр + " ";
		КонецЕсли;
		
		ДлительностьСтр = ДлительностьСтр + Строка(Часов) + " " + ПодписьЧасов;
	КонецЕсли;
	
	Если Минут > 0 Тогда
		
		Если Дней > 0 Или Часов > 0 Тогда
			ДлительностьСтр = ДлительностьСтр + " ";
		КонецЕсли;
		
		ДлительностьСтр = ДлительностьСтр + Строка(Минут) + " " + ПодписьМинут;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпискиВыбораДат()
	
	Элементы.ДатаНачалаВремя.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		ТекДата = НачалоДня(Объект.ДатаНачала);
	Иначе
		ТекДата = НачалоДня(ТекущаяДата());
	КонецЕсли; 
	
	ТекДата = ТекДата + 60 * 60 * 8;
	
	Для Инд = 1 По 23 Цикл
		
		Элементы.ДатаНачалаВремя.СписокВыбора.Добавить(ТекДата, Формат(ТекДата, "ДФ=ЧЧ:мм"));
		Если Объект.ДатаНачала > ТекДата И Объект.ДатаНачала < ТекДата + 1800 Тогда
			Элементы.ДатаНачалаВремя.СписокВыбора.Добавить(Объект.ДатаНачала, Формат(Объект.ДатаНачала, "ДФ=ЧЧ:мм"));
		КонецЕсли;
		
		ТекДата = ТекДата + 1800;
		
	КонецЦикла;
	
	Элементы.ДатаОкончанияВремя.СписокВыбора.Очистить();
	
	СобытиеВПределахОдногоДня = Ложь;
	
	Если СобытиеВПределахОдногоДня Тогда
		
		ТекДата = РаботаСРабочимКалендаремКлиентСервер.КонецПолучаса(Объект.ДатаНачала);
		Если Объект.ДатаОкончания > ТекДата - 1800 И Объект.ДатаОкончания < ТекДата Тогда
			Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(Объект.ДатаОкончания, Формат(Объект.ДатаОкончания,
			"ДФ=ЧЧ:мм"));
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		ТекДата = НачалоДня(Объект.ДатаОкончания);
	Иначе
		ТекДата = НачалоДня(ТекущаяДата());
	КонецЕсли;
	
	ТекДата = ТекДата + 60 * 60 * 8;
	
	Для Инд = 1 По 23 Цикл
		
		Если СобытиеВПределахОдногоДня И ТекДата > КонецДня(Объект.ДатаНачала) Тогда
			Прервать;
		КонецЕсли;
		
		Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(ТекДата, Формат(ТекДата, "ДФ=ЧЧ:мм"));
		Если Объект.ДатаОкончания > ТекДата И Объект.ДатаОкончания < ТекДата + 1800 Тогда
			Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(Объект.ДатаОкончания, Формат(Объект.ДатаОкончания,
			"ДФ=ЧЧ:мм"));
		КонецЕсли;
		ТекДата = ТекДата + 1800;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыНапоминанияПоИсточнику(Источник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НапоминанияПользователя.Пользователь,
	|	НапоминанияПользователя.ВремяСобытия,
	|	НапоминанияПользователя.Источник,
	|	НапоминанияПользователя.СрокНапоминания КАК СрокНапоминания,
	|	НапоминанияПользователя.Описание КАК Описание,
	|	2 КАК ИндексКартинки,
	|	НапоминанияПользователя.СпособУстановкиВремениНапоминания,
	|	НапоминанияПользователя.ИнтервалВремениНапоминания,
	|	НапоминанияПользователя.ИмяРеквизитаИсточника
	|ИЗ
	|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
	|ГДЕ
	|	НапоминанияПользователя.Пользователь = &Пользователь
	|	И НапоминанияПользователя.Источник = &Источник";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("Источник", Источник);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	//@skip-check structure-consructor-too-many-keys
	ПараметрыНапоминания = Новый Структура("Пользователь,Источник,ВремяСобытия,СрокНапоминания,
	|Описание,СпособУстановкиВремениНапоминания,ИнтервалВремениНапоминания,ИмяРеквизитаИсточника");
	
	Результат = Неопределено;
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыНапоминания, Выборка);
		Результат = ПараметрыНапоминания;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьНадписьНапоминания(Напоминание, УстановитьНапоминание, УстановленоНапоминание,
	СрокНапоминанияПоУмолчанию, ИнтервалВремениСтрокой, СпособУстановкиВремениНапоминания, ВремяНапоминания)
	
	Если Напоминание <> Неопределено Тогда
		
		УстановитьНапоминание = Истина;
		УстановленоНапоминание = Истина;
		
		Если Напоминание.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя") Тогда
			
			ИнтервалВремениСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'в %2 %1'"), Формат(Напоминание.ВремяСобытия, "ДЛФ=D"), Формат(Напоминание.ВремяСобытия,
			"ДФ=ЧЧ:мм"));
			СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя");
			ВремяНапоминания = Напоминание.ВремяСобытия;
			
		ИначеЕсли Напоминание.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета") Тогда
			
			Если Напоминание.ИнтервалВремениНапоминания >= 0 И Напоминание.ИмяРеквизитаИсточника = "ДатаНачала" Тогда
				
				ИнтервалВремениСтрокой = ПредставлениеВремениДокументооборот(
				Напоминание.ИнтервалВремениНапоминания);
				
			ИначеЕсли Напоминание.ИнтервалВремениНапоминания <> 0 И Напоминание.ИмяРеквизитаИсточника = "ДатаОкончания" Тогда
				
				ИнтервалВремениСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'за %1 до окончания'"), ПредставлениеВремени(Напоминание.ИнтервалВремениНапоминания));
				
			ИначеЕсли Напоминание.ИнтервалВремениНапоминания = 0 И Напоминание.ИмяРеквизитаИсточника = "ДатаОкончания" Тогда
				
				ИнтервалВремениСтрокой = НСтр("ru = 'при окончании события'");
				
			КонецЕсли;
			
			СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");
			ВремяНапоминания = Дата(1, 1, 1);
			
		ИначеЕсли Напоминание.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.Периодически") Тогда
			
			ИнтервалВремениСтрокой = НСтр("ru = 'по заданному расписанию'");
			СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.Периодически");
			ВремяНапоминания = Дата(1, 1, 1);
			
		КонецЕсли;
		
	Иначе
		
		УстановитьНапоминание = Ложь;
		УстановленоНапоминание = Ложь;
		ИнтервалВремениСтрокой = ПредставлениеВремениДокументооборот(СрокНапоминанияПоУмолчанию * 60);
		СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");
		ВремяНапоминания = Дата(1, 1, 1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыНапоминанияДокументооборота(ПараметрыНапоминания)
	
	Если ПараметрыНапоминания.Свойство("Пользователь") И ПараметрыНапоминания.Свойство("Источник")
		И ПараметрыНапоминания.Свойство("ВремяСобытия") И ПараметрыНапоминания.Свойство(
		"СпособУстановкиВремениНапоминания") И ПараметрыНапоминания.Свойство("ИнтервалВремениНапоминания")
		И ПараметрыНапоминания.Свойство("ИмяРеквизитаИсточника") Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьИнтервалыНапоминания()
	
	Если ИспользоватьНапоминанияПользователя Тогда
		
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Очистить();
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'при наступлении события'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 5 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 10 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 15 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 30 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 1 час'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 2 часа'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 3 часа'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'Другое...'"));
		
		Если Элементы.ИнтервалВремениСтрокой.СписокВыбора.НайтиПоЗначению(ИнтервалВремениСтрокой) = Неопределено Тогда
			Элементы.ИнтервалВремениСтрокой.СписокВыбора.Вставить(0, ИнтервалВремениСтрокой);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНапоминание()
	
	Если ИспользоватьНапоминанияПользователя Тогда
		
		ИзмененоНапоминание = Ложь;
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			
			Напоминание = ПолучитьПараметрыНапоминанияПоИсточнику(Объект.Ссылка);
			УстановитьНадписьНапоминания(
			Напоминание, УстановитьНапоминание, УстановленоНапоминание, СрокНапоминанияПоУмолчанию,
			ИнтервалВремениСтрокой, СпособУстановкиВремениНапоминания, ВремяНапоминания);
			
		Иначе
			
			УстановитьНапоминание = УстанавливатьНапоминаниеАвтоматически;
			УстановленоНапоминание = Ложь;
			ИнтервалВремениСтрокой = ПредставлениеВремениДокументооборот(СрокНапоминанияПоУмолчанию * 60);
			СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета;
			ВремяНапоминания = Дата(1, 1, 1);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкиПовторения()
	
	ПовторениеПоДнямНедели = ПолучитьПовторениеПоДнямНедели();
	ПовторениеПоДнямНеделиВМесяце = Неопределено;
	
	Если Объект.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежемесячно И Не ЗначениеЗаполнено(
		Объект.ПовторениеПоДнямМесяца) И Объект.ПовторениеПоДням.Количество() = 1 Тогда
		
		ПовторениеПоДнямНеделиВМесяце = Новый Структура("НомерВхождения, ДеньНедели");
		ЗаполнитьЗначенияСвойств(ПовторениеПоДнямНеделиВМесяце, Объект.ПовторениеПоДням[0]);
		
	ИначеЕсли Объект.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежегодно И Не ЗначениеЗаполнено(
		Объект.ПовторениеПоДнямМесяца) И Объект.ПовторениеПоДням.Количество() = 1 Тогда
		
		ПовторениеПоДнямНеделиВМесяце = Новый Структура("НомерВхождения, ДеньНедели");
		ЗаполнитьЗначенияСвойств(ПовторениеПоДнямНеделиВМесяце, Объект.ПовторениеПоДням[0]);
		
	КонецЕсли;
	
	НастройкиПовторения = РаботаСРабочимКалендаремКлиентСервер.ПолучитьСтруктуруНастройкиПовторения(
	Объект.ЧастотаПовторения, Объект.ИнтервалПовторения, Объект.ПравилоОкончанияПовторения,
	Объект.КоличествоПовторов, Объект.ДатаОкончанияПовторения, ПовторениеПоДнямНедели,
	Объект.ПовторениеПоДнямМесяца, ПовторениеПоДнямНеделиВМесяце, Объект.ПовторениеПоМесяцам);
	
	Возврат НастройкиПовторения;
	
КонецФункции

&НаСервере
Функция ПолучитьПовторениеПоДнямНедели()
	
	ПовторениеПоДням = Новый Соответствие;
	Для ИндексПовторениеПоДням = 1 По 7 Цикл
		
		Если Объект.ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Еженедельно Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДеньНедели", ИндексПовторениеПоДням);
			ПараметрыОтбора.Вставить("НомерВхождения", 0);
			НайденныеСтроки = Объект.ПовторениеПоДням.НайтиСтроки(ПараметрыОтбора);
			
			ПовторениеПоДням.Вставить(ИндексПовторениеПоДням, НайденныеСтроки.Количество() <> 0);
			
		Иначе
			ПовторениеПоДням.Вставить(ИндексПовторениеПоДням, Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПовторениеПоДням;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьОповещениеОбИзменении()
	
	ИзмененныеЗаписиКалендаря = Новый Массив;
	ИзмененныеЗаписиКалендаря.Добавить(Объект.Ссылка);
	Если ЗначениеЗаполнено(ПовторяющеесяСобытие) И ЗначениеЗаполнено(ПовторяющеесяСобытиеДатаИсключения) Тогда
		ИзмененныеЗаписиКалендаря.Добавить(ПовторяющеесяСобытие);
		ПовторяющеесяСобытие = Неопределено;
		ПовторяющеесяСобытиеДатаИсключения = Неопределено;
	КонецЕсли;
	Оповестить("Запись_ЗаписьКалендаря", ИзмененныеЗаписиКалендаря, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеНапоминанияНаСервере()
	
	Если Не ИспользоватьНапоминанияПользователя Тогда
		Возврат;
	КонецЕсли;
	
	Если УстановитьНапоминание И (ИзмененоНапоминание Или ИзмененоВремя Или Не УстановленоНапоминание) Тогда
		
		// Отключение старого напоминания, если оно было установлено ранее
		Если УстановленоНапоминание И (ИзмененоНапоминание Или ИзмененоВремя) Тогда
			НапоминанияПользователяСлужебный.ОтключитьНапоминание(Напоминание, Ложь);
			НапоминаниеСтарое = Напоминание;
			Напоминание = Неопределено;
		КонецЕсли;
		
		ИзмененоНапоминание = Ложь;
		ИзмененоВремя = Ложь;
		
		Если Объект.Статус <> Перечисления.ЦБР_СтатусЗаписиКалендаря.Отмена И 
			Объект.Статус <> Перечисления.ЦБР_СтатусЗаписиКалендаря.Завершен Тогда
			Если СпособУстановкиВремениНапоминания
				= Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета Тогда
				// Подключение напоминания относительно времени предмета
				ИнтервалВремениНапоминания = 300;
				Напоминание = НапоминанияПользователяСлужебный.ПодключитьНапоминаниеДоВремениПредмета(
				Строка(Объект.Ссылка), ИнтервалВремениНапоминания, Объект.Ссылка, "ДатаНачала");
				ОбновитьЗаписьВКешеНапоминаний = Истина;
				УстановленоНапоминание = Истина;
				
			ИначеЕсли СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ВУказанноеВремя Тогда
				// Подключение напоминания в указанное время
				Напоминание = НапоминанияПользователяСлужебный.ПодключитьПроизвольноеНапоминание(
				Строка(Объект.Ссылка), ВремяНапоминания, 0, Объект.Ссылка);
				ОбновитьЗаписьВКешеНапоминаний = Истина;
				УстановленоНапоминание = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Не УстановитьНапоминание И УстановленоНапоминание Тогда
		
		// Отключение старого напоминания
		НапоминанияПользователяСлужебный.ОтключитьНапоминание(Напоминание);
		НапоминаниеСтарое = Напоминание;
		Напоминание = Неопределено;
		УстановленоНапоминание = Ложь;
		
	КонецЕсли;
	
	УстановитьНадписьНапоминания(Напоминание, УстановитьНапоминание, УстановленоНапоминание,
	СрокНапоминанияПоУмолчанию, ИнтервалВремениСтрокой, СпособУстановкиВремениНапоминания, ВремяНапоминания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеНапоминанияНаКлиенте()
	
	Если Не ИспользоватьНапоминанияПользователя Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбновитьЗаписьВКешеНапоминаний Тогда
		НапоминанияПользователяКлиент.ОбновитьЗаписьВКэшеОповещений(Напоминание);
		Оповестить("Запись_НапоминанияПользователя_Документооборот", Напоминание, Напоминание.Источник);
		ОбновитьЗаписьВКешеНапоминаний = Ложь;
	ИначеЕсли НапоминаниеСтарое <> Неопределено Тогда
		НапоминанияПользователяКлиент.УдалитьЗаписьИзКэшаОповещений(НапоминаниеСтарое);
		Оповестить("Удаление_НапоминанияПользователя_Документооборот", , НапоминаниеСтарое.Источник);
		НапоминаниеСтарое = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

 &НаКлиенте
Процедура ДобавитьОписаниеЧасыДляЗапиейКалендаря(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		СоздатьЗаписьКалендаряНаСервере(); 
		ЭтаФорма.Прочитать();  
		УстановитьВидимостьЭлементовФормы();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаписьКалендаряНаСервере() 

	НачатьТранзакцию();
	
	Попытка
		
		МассивЗаписейКалендаря = Новый Массив;
		
		Если Объект.Ссылка.Пустая() Тогда
			КоличествоДней = (НачалоДня(Объект.ДатаОкончания) - НачалоДня(Объект.ДатаНачала)) / (60 * 60 * 24) + 1;
			
			Если КоличествоДней > 1 Тогда
				ДатыПоДням = РазбитьЗаписиНаНесколькоДней(Объект.ДатаНачала, Объект.ДатаОкончания, КоличествоДней);
				Для Каждого Строка Из ДатыПоДням Цикл
					Если Строка = ДатыПоДням[0] Тогда
						Объект.ДатаНачала = Строка.Начало;
						Объект.ДатаОкончания = Строка.Конец;
					Иначе
						МассивЗаписейКалендаря.Добавить(СоздатьЗаписьКалендаря(Строка.Начало, Строка.Конец));
					КонецЕсли;
				КонецЦикла;  
			Иначе
				Записать(); 
				МассивЗаписейКалендаря.Добавить(Объект.Ссылка);
			КонецЕсли; 
		КонецЕсли;

		Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
		Структура.Задание = Объект.ЗаданиеСсылка;
		Структура.ДатаНачалаДействия = ТекущаяДатаСеанса();
		Структура.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.ВРаботе");
		Для Каждого ЭлементКалендаря Из	МассивЗаписейКалендаря Цикл
			Структура.ЗаписьКалендаря = Объект.Ссылка;
			ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();             
		
		ЗаписьЖурналаРегистрации("Ошибка ЦБР", УровеньЖурналаРегистрации.Ошибка, , ,
		"При создании Записей календаря произошла ошибка." + Символы.ПС +
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
		
КонецПроцедуры

&НаСервере
Функция СоздатьЗаписьКалендаря(Начало, Конец)
	
	Запись_ОС = Справочники.ЦБР_ЗаписиРабочегоКалендаря.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(Запись_ОС, Объект);
	Запись_ОС.ДатаНачала = Начало;
	Запись_ОС.ДатаОкончания = Конец;
	Запись_ОС.Записать();
	
	Возврат Запись_ОС.Ссылка;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьЭффективныеЧасыОкончание(Результат, ДопПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		//ЦБР_ЗадачиЗаданияВызовСервера.ЗаписатьЧасыВЗадачу(Результат, ДопПараметры);

		//ОписаниеВыполненнойРаботы = Результат.ОписаниеВыполненнойРаботы;
		//УстановитьОтработана(Результат.ПоПлану);
		//
		//Получатели = Новый Массив;     		
		//Задача = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеСсылка, "СсылкаНаЗадачу");
		//ТекстРезультата = "Действие: " + Объект.Описание + Символы.ПС + "Результат: " + ОписаниеВыполненнойРаботы.ПолучитьТекст();
		//Получатели.Добавить(ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Задача, "Постановщик"));
		//ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Задача, "РезультатОС", Получатели, ТекстРезультата);
		//
		//ЦБР_ЗадачиЗаданияВызовСервера.ПаузаНаСервере(ОписаниеВыполненнойРаботы.ПолучитьТекст(),, Объект.ЗаданиеСсылка);   
		//Объект.УдалосьРешитьПриПодключенииПометка = Результат.РешеноПриПервомПодключении;
		//Объект.Статус = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_СтатусЗаписиКалендаря.Завершен");
		//
		//Записать();
		ВыполнитьОповещениеОбИзменении();	
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры
 
&НаСервере
Процедура ИнициализироватьКалендарь()
	
	ЧасовойПоясПользователя = ЧасовойПоясСеанса();
	ЧасовойПоясТекущегоПользователя = ЧасовойПоясПользователя;
	СкрытьПринудительно = ЧасовойПоясПользователя <> ЧасовойПоясТекущегоПользователя;
	
	Если СкрытьПринудительно Тогда
		ПоказыватьКалендарь = Ложь;
	Иначе
		ПоказыватьКалендарь = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
		"ОтображатьКалендарьВЗаписиКалендаря");
	КонецЕсли;
	Если ПоказыватьКалендарь Тогда
		КлючСохраненияПоложенияОкна = "СКалендарем";
	Иначе
		КлючСохраненияПоложенияОкна = "БезКалендаря";
	КонецЕсли;
	
	Элементы.ГруппаКалендарь.Видимость = ПоказыватьКалендарь;
	Элементы.ФормаПоказатьКалендарь.Видимость = Не СкрытьПринудительно;
	Элементы.ФормаПоказатьКалендарь.Пометка = ПоказыватьКалендарь;
	Элементы.ГруппаЗаписьКалендаря.РастягиватьПоГоризонтали = Не ПоказыватьКалендарь;
	
	НастройкиОтображения = РаботаСРабочимКалендаремСервер.ПолучитьНастройкиОтображения();
	ОбновитьОтображениеКалендарь();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройкиОтображения()
	
	НастройкиОтображения.Пользователи.Очистить();
	НастройкиОтображения.Пользователи.Добавить(Объект.Пользователь);
	НастройкиОтображения.ОтобразитьТекущуюЗапись = Истина;
	НастройкиОтображения.ЗапретСоздания = Истина;
	НастройкиОтображения.ЗапретИзменения = ТолькоПросмотр;
	НастройкиОтображения.ПланДняСкрытьПустые = Истина;
	НастройкиОтображения.ТекущаяЗаписьСсылка = Объект.Ссылка;
	НастройкиОтображения.ТекущаяЗаписьДатаНачала = Объект.ДатаНачала;
	НастройкиОтображения.ТекущаяЗаписьДатаОкончания = Объект.ДатаОкончания;
	НастройкиОтображения.ТекущаяЗаписьВесьДень = Объект.ВесьДень;
	
	НачалоОтображаемогоПериода = НачалоДня(Объект.ДатаНачала);
	ОтображаемаяДата = НачалоОтображаемогоПериода;
	КонецОтображаемогоПериода = НачалоДня(Объект.ДатаОкончания);
	ОтображаемыйПериод = (КонецОтображаемогоПериода - НачалоОтображаемогоПериода) / 86400 + 1;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты.Очистить();
	КоличествоДат = 0;
	Пока ОтображаемаяДата <= КонецОтображаемогоПериода Цикл
		НастройкиОтображения.ВыделенныеДаты.Добавить(ОтображаемаяДата);
		ОтображаемаяДата = ОтображаемаяДата + 86400;
		КоличествоДат = КоличествоДат + 1;
		Если КоличествоДат > 31 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОтображаемыйПериод <= 3 Тогда
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.День");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланировщик;
	Иначе
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланДня;
	КонецЕсли;
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;
	
	Если Не ЗначениеЗаполнено(НастройкиОтображения.ТекущаяЗаписьСсылка) И ЗначениеЗаполнено(ПовторяющеесяСобытие) Тогда
		НастройкиОтображения.ТекущаяЗаписьСсылка = ПовторяющеесяСобытие;
	КонецЕсли;
	НастройкиОтображения.ИсключенияЗанятости.Добавить(НастройкиОтображения.ТекущаяЗаписьСсылка);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОтображенияКлиент()
	
	НастройкиОтображения.Пользователи.Очистить();
	НастройкиОтображения.Пользователи.Добавить(Объект.Пользователь);
	НастройкиОтображения.ОтобразитьТекущуюЗапись = Истина;
	НастройкиОтображения.ЗапретСоздания = Истина;
	НастройкиОтображения.ПланДняСкрытьПустые = Истина;
	НастройкиОтображения.ТекущаяЗаписьСсылка = Объект.Ссылка;
	НастройкиОтображения.ТекущаяЗаписьДатаНачала = Объект.ДатаНачала;
	НастройкиОтображения.ТекущаяЗаписьДатаОкончания = Объект.ДатаОкончания;
	НастройкиОтображения.ТекущаяЗаписьВесьДень = Объект.ВесьДень;
	
	НачалоОтображаемогоПериода = НачалоДня(Объект.ДатаНачала);
	ОтображаемаяДата = НачалоОтображаемогоПериода;
	КонецОтображаемогоПериода = НачалоДня(Объект.ДатаОкончания);
	ОтображаемыйПериод = (КонецОтображаемогоПериода - НачалоОтображаемогоПериода) / 86400 + 1;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты.Очистить();
	КоличествоДат = 0;
	Пока ОтображаемаяДата <= КонецОтображаемогоПериода Цикл
		НастройкиОтображения.ВыделенныеДаты.Добавить(ОтображаемаяДата);
		ОтображаемаяДата = ОтображаемаяДата + 86400;
		КоличествоДат = КоличествоДат + 1;
		Если КоличествоДат > 31 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОтображаемыйПериод <= 3 Тогда
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.День");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланировщик;
	Иначе
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланДня;
	КонецЕсли;
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;
	
	Если Не ЗначениеЗаполнено(НастройкиОтображения.ТекущаяЗаписьСсылка) И ЗначениеЗаполнено(ПовторяющеесяСобытие) Тогда
		НастройкиОтображения.ТекущаяЗаписьСсылка = ПовторяющеесяСобытие;
	КонецЕсли;
	НастройкиОтображения.ИсключенияЗанятости.Добавить(НастройкиОтображения.ТекущаяЗаписьСсылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеКалендарь()
	
	Если Не ПоказыватьКалендарь Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Элементы.Планировщик.Доступность = Ложь;
		Элементы.ПланДня.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.Планировщик.Доступность = Истина;
	Элементы.ПланДня.Доступность = Истина;
	
	ОбновитьНастройкиОтображения();
	
	Если ПериодОтображения = Перечисления.ПериодОтображенияРабочегоКалендаря.ПланДня Тогда
		ПланДняЗначение = РеквизитФормыВЗначение("ПланДня");
		РаботаСРабочимКалендаремСервер.ОтобразитьПланДня(ПланДняЗначение, НастройкиОтображения);
		ЗначениеВРеквизитФормы(ПланДняЗначение, "ПланДня");
	Иначе
		РаботаСРабочимКалендаремСервер.ОтобразитьКалендарь(Планировщик, НастройкиОтображения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеКалендарьКлиент(Параметр = Неопределено)
	
	Если Не ПоказыватьКалендарь Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметр <> Неопределено И ПериодОтображения <> ПредопределенноеЗначение(
		"Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		РаботаСРабочимКалендаремКлиент.ОбновитьЗаписиКалендаряВПланировщике(
		Планировщик, НастройкиОтображения, Параметр);
		ОбновитьОтображениеТекущейЗаписиВКалендаре();
		Возврат;
	КонецЕсли;
	
	СвернутыеЭлементы = РаботаСРабочимКалендаремКлиент.ПолучитьСвернутыеЭлементыПланаДня(
	Элементы.ПланДня, ПланДня);
	ОбновитьОтображениеКалендарь();
	РаботаСРабочимКалендаремКлиент.ВосстановитьСостояниеПланаДня(
	Элементы.ПланДня, ПланДня, СвернутыеЭлементы, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеТекущейЗаписиВКалендаре()
	
	Если Не ПоказыватьКалендарь Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Элементы.Планировщик.Доступность = Ложь;
		Элементы.ПланДня.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.Планировщик.Доступность = Истина;
	Элементы.ПланДня.Доступность = Истина;
	
	СтарыйПериодОтображения = ПериодОтображения;
	ОбновитьНастройкиОтображенияКлиент();
	Если ПериодОтображения <> СтарыйПериодОтображения Тогда
		ОбновитьОтображениеКалендарьКлиент();
	ИначеЕсли ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.День") Тогда
		РаботаСРабочимКалендаремКлиентСервер.ОбновитьТекущуюЗапись(Планировщик, НастройкиОтображения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаРезультата(Ответ, ДопПараметры) Экспорт
	
	Если Не Ответ = Неопределено И ЗначениеЗаполнено(Ответ) Тогда
		Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда 
			Получатели = Новый Массив;
			Получатели.Добавить(ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеСсылка, "Постановщик"));
			ТекстРезультата = "Действие: " + Объект.Описание + Символы.ПС + "Результат: " + Ответ; 
			ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.ЗаданиеСсылка, "РезультатОС", Получатели, ТекстРезультата);
			//ЦБР_ЗадачиЗаданияВызовСервера.ПоменятьСтатусЗадачи(Объект.ЗаданиеСсылка); 
			ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусыЗадачи(Объект.ЗадачаСсылка);	
		КонецЕсли;
		Объект.Результат = Ответ;
		Если ДопПараметры.Свойство("ПоПлану") Тогда 
			ПоПлану = ДопПараметры.ПоПлану;
			Приступил = Истина;
			Отработана = Истина;
		Иначе
			ПоПлану = Ложь;
		КонецЕсли;
		УстановитьОтработана(ПоПлану);
		УстановитьДоступностьЭлементовФормы();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нет результата!");
		Отработана = Ложь;
		Элементы.Отработана.Доступность = Истина;
		УстановитьДоступностьЭлементовФормы();
	КонецЕсли;  
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВидаРабот(Список)
	
 	Список.Очистить();
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(Объект.ЗадачаСсылка) Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦБР_ВидыРаботВКалендаре.Ссылка КАК Ссылка,
		|	ЦБР_ВидыРаботВКалендаре.Представление КАК Представление
		|ИЗ
		|	Справочник.ЦБР_ВидыРаботВКалендаре КАК ЦБР_ВидыРаботВКалендаре
		|ГДЕ
		|	ЦБР_ВидыРаботВКалендаре.ПометкаУдаления = ЛОЖЬ
		|	И ЦБР_ВидыРаботВКалендаре.ЭффективныеЧасы";
	Иначе   
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦБР_ВидыРаботВКалендаре.Ссылка КАК Ссылка,
		|	ЦБР_ВидыРаботВКалендаре.Представление КАК Представление
		|ИЗ
		|	Справочник.ЦБР_ВидыРаботВКалендаре КАК ЦБР_ВидыРаботВКалендаре
		|ГДЕ
		|	ЦБР_ВидыРаботВКалендаре.ПометкаУдаления = ЛОЖЬ
		|	И НЕ ЦБР_ВидыРаботВКалендаре.ЭффективныеЧасы";
	КонецЕсли;
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		Список.Добавить(Результат.Ссылка, Результат.Представление);
	КонецЦикла;  
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭтап()
	
	Задача = Объект.ЗаданиеСсылка.ПолучитьОбъект();
	НовыйЭтап = Задача.ДеревоЗаданий.Добавить();
	НовыйЭтап.ВремяНачалаПлан = Объект.ДатаНачала;
	НовыйЭтап.ВремяОкончанияПлан = Объект.ДатаОкончания;
	НовыйЭтап.ДатаНачалоПлан = Объект.ДатаНачала;
	НовыйЭтап.ДатаОкончаниеПлан = Объект.ДатаОкончания;
	НовыйЭтап.Исполнитель = Объект.Пользователь;
	НовыйЭтап.Наименование = Объект.Описание;
	
	СтруктураДат = Новый Структура;
	
	СтруктураДат.Вставить("ДатаНачала", НачалоДня(Объект.ДатаНачала));
	СтруктураДат.Вставить("ДатаОкончания", НачалоДня(Объект.ДатаОкончания));
	СтруктураДат.Вставить("ВремяНачала", Объект.ДатаНачала);
	СтруктураДат.Вставить("ВремяОкончания", Объект.ДатаОкончания);
	Длительность = ЦБР_ЗадачиЗаданияВызовСервера.РасчитатьДлительностьФакт(СтруктураДат, 0);
	НовыйЭтап.ДлительностьПлан = Длительность;
	НовыйЭтап.ТрудозатратыПлан = Длительность;
	НовыйЭтап.ТрудозатратыПланПредставление = Длительность;
		
	Задача.Записать();    
	
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Задача, Перечисления.ЦБР_Состояние.Черновик);

КонецПроцедуры

//&НаСервере
//Функция СоздатьЗаданиеУточнениеТребований()

//    НачатьТранзакцию();

//	Попытка
//		Задача = Объект.ЗадачаСсылка; 
//		НовыйЭтап                               = Справочники.ЦБР_СтруктураЗадач.СоздатьЭлемент();
//		НовыйЭтап.Исполнитель                   = Пользователи.ТекущийПользователь();
//		НовыйЭтап.Наименование                  = "Уточнение требований";
//		НовыйЭтап.ТипЗадания                    = Перечисления.ЦБР_ТипЗадания.Взаимодействие;
//		НовыйЭтап.ТрудозатратыПлан              = 0;
//		НовыйЭтап.ТрудозатратыПланПредставление = 0;  
//		НовыйЭтап.ОценкаСогласована             = Истина;     
//		НовыйЭтап.Владелец                      = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Ссылка");
//		НовыйЭтап.Записать();

//		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(НовыйЭтап.Ссылка, Перечисления.ЦБР_Состояние.ВРаботе);	
//		
//		ДанныеЗаполнения = Новый Структура; 
//		ДанныеЗаполнения.Вставить("ЭтоУточнениеТребований", Истина);
//		СсылкаНаЗадание = ЦБР_ЗадачиЗаданияВызовСервера.СоздатьНовоеЗадание(НовыйЭтап.Ссылка, ДанныеЗаполнения);
//		
//		ЗафиксироватьТранзакцию();
//	Исключение
//		ОтменитьТранзакцию();             
//		
//		ЗаписьЖурналаРегистрации("Ошибка ЦБР", УровеньЖурналаРегистрации.Ошибка, , ,
//		"При создании Задания Уточнение требований произошла ошибка." + Символы.ПС +
//		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
//		
//		СсылкаНаЗадание = Документы.ЦБР_Задание.ПустаяСсылка();
//	КонецПопытки;
//	
//	Возврат СсылкаНаЗадание;
//	
//КонецФункции // СоздатьЗаданиеУточнениеТребований() 

&НаСервере
Функция ПолучитьЗадачу() 
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗадачаСсылка, "Ссылка"); 
	
КонецФункции // ПолучитьЗадачу()

&НаКлиенте
Процедура УстановитьПометкаУдаленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	
	РаботаСРабочимКалендаремКлиент.УстановитьПометкуУдаления(Объект.Ссылка, Не Объект.ПометкаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета") Тогда
		
		Если УстановитьНапоминание = Ложь Или ИнтервалВремениСтрокой <> Результат.ИнтервалВремениСтрокой Тогда
			УстановитьНапоминание = Истина;
			ИзмененоНапоминание = Истина;
		КонецЕсли;
		СпособУстановкиВремениНапоминания = Результат.СпособУстановкиВремениНапоминания;
		ИнтервалВремениСтрокой = Результат.ИнтервалВремениСтрокой;
		ВремяНапоминания = Дата(1, 1, 1);
		
	ИначеЕсли Результат.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя") Тогда
		
		Если УстановитьНапоминание = Ложь Или ВремяНапоминания <> Результат.ВремяНапоминания Тогда
			УстановитьНапоминание = Истина;
			ИзмененоНапоминание = Истина;
		КонецЕсли;
		СпособУстановкиВремениНапоминания = Результат.СпособУстановкиВремениНапоминания;
		ВремяНапоминания = Результат.ВремяНапоминания;
		ИнтервалВремениСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'в %2 %1'"), Формат(ВремяНапоминания, "ДЛФ=D"), Формат(ВремяНапоминания, "ДФ=ЧЧ:мм"));
		
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();
	ЗаполнитьИнтервалыНапоминания();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОС(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаПричиныОтменыОС", ЭтотОбъект);
	ПоказатьВводСтроки(Оповещение,,"Укажите причину отмены ОС.", 0, Истина);
	
	//Объект.ДатаНачала = ТекущаяДата();
	//Объект.ДатаОкончания = Объект.ДатаНачала;
	//Объект.Статус = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_СтатусЗаписиКалендаря.Отмена");
	//Объект.Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий");
	//Записать(); 
	//
	//МассивЗаписейДляУдаления = Новый Массив;
	//МассивЗаписейДляУдаления.Добавить(объект.Ссылка);
	//
	//МассивПовторяющихсяЗаписей = Новый Массив; 
	//
	//РаботаСРабочимКалендаремКлиент.УстановитьПометкиУдаления(МассивЗаписейДляУдаления,МассивПовторяющихсяЗаписей,Истина);
	//
	//Закрыть();

	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаПричиныОтменыОС(Результат, Параметры) Экспорт
 
	Если Не Результат = Неопределено Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
			Объект.ДатаНачала = ТекущаяДата();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
			Объект.ДатаОкончания = ТекущаяДата();
		КонецЕсли;
		
		Объект.Статус = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ЦБР_СтатусЗаписиКалендаря.Отмена");
		Объект.Цвет = ПредопределенноеЗначение("Перечисление.ЦБР_ЦветаРабочегоКалендаря.Синий");
		Записать(); 
		
		Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда
			Если ЗначениеЗаполнено(Результат) Тогда
				ТекстУведомления = "Задание: " + Объект.ЗаданиеСсылка + " Действие: " + Объект.Ссылка + " Причина отмены:" + Результат + "";
				ЦБР_ОбсужденияСервер.УведомлениеВОбсуждение(Объект.ЗадачаСсылка, "ПричинаИзмененияОС",ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗадачаСсылка, "Постановщик"), ТекстУведомления);
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана причина отмены!");
				Возврат;
			КонецЕсли; 
		КонецЕсли;
		
		УстановленаПометкаУдаления = РаботаСРабочимКалендаремСервер.УстановитьПометкуУдаления(
		Объект.Ссылка,
		Истина,
		Результат);
		
		Если УстановленаПометкаУдаления Тогда
			Оповестить("Запись_ЗаписьКалендаря", Объект.Ссылка);
		КонецЕсли;

		//
		//МассивЗаписейДляУдаления = Новый Массив;
		//МассивЗаписейДляУдаления.Добавить(объект.Ссылка);
		//
		//МассивПовторяющихсяЗаписей = Новый Массив; 
		//
		//РаботаСРабочимКалендаремКлиент.УстановитьПометкиУдаления(МассивЗаписейДляУдаления,МассивПовторяющихсяЗаписей,Истина);
		//
		Закрыть();
	КонецЕсли;
 
КонецПроцедуры

&НаКлиенте
Процедура Старт(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Записать();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Описание) Тогда 
	   Возврат;        
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда 		
		
		Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
		Структура.Задание = Объект.ЗаданиеСсылка;
		Структура.ДатаНачалаДействия = ТекущаяДата();
		Структура.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.ВРаботе");
		
		ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.Ссылка);
		Если ПоследнееДействие = Неопределено Или ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
			Структура.ЗаписьКалендаря = Объект.Ссылка;
			ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю("Есть незавершенное действие по этой задаче!");
			Возврат;
		КонецЕсли;
		
		ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(Объект.ЗаданиеСсылка, ОбщегоНазначенияКлиент.ПредопределенныйЭлемент(
		"Перечисление.ЦБР_Состояние.ВРаботе"));
		
	КонецЕсли;

	РаботаСРабочимКалендаремСервер.УстановитьПриступилЗаписьКалендаря(Объект.Ссылка);	
	ВыполнитьОповещениеОбИзменении();	
	ЭтаФорма.Прочитать();  
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	
	Если ЗначениеЗаполнено(Объект.ЗаданиеСсылка) Тогда
		Если ТипЗнч(Объект.ЗаданиеСсылка) = Тип("ДокументСсылка.ЦБР_Задание") Тогда			
			ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Объект.ЗаданиеСсылка);
			Если Не ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
				
				Записать();
				
				Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект);
				
				ПараметрыФормы = Новый Структура;       
				ПараметрыФормы.Вставить("Задание", Объект.ЗаданиеСсылка);
				ПараметрыФормы.Вставить("ЗаписьОС", Объект.Ссылка);
				ПараметрыФормы.Вставить("Описание", Объект.Описание);
				
				ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
			КонецЕсли;	
		Иначе
			Подсказка = "Результат:";
			Оповещение  = Новый ОписаниеОповещения("ПослеВводаРезультата", ЭтотОбъект);
			ПоказатьВводСтроки(Оповещение, , Подсказка, 0, Истина);		
		КонецЕсли;
	Иначе
		УстановитьОтработана();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоФулл(Команда)
	
	Объект.Описание = "Работа на Аутстафф.";

КонецПроцедуры

&НаСервере
Функция СоздатьДокументЗадача(НазваниеЗадания, ПредыдущееЗадание = Неопределено, Состояние);
	
	НовоеЗадание                   = Документы.ЦБР_Задание.СоздатьДокумент();
	НовоеЗадание.Дата              = ТекущаяДатаСеанса();   
	НовоеЗадание.ВидЧаса           = Перечисления.ЦБР_ВидыЧасовПоЗадачам.Платные;
	НовоеЗадание.Наименование      = НазваниеЗадания;
	НовоеЗадание.Постановщик       = Объект.ЗадачаСсылка.Постановщик;
	НовоеЗадание.ПредыдущееЗадание = ПредыдущееЗадание; 
	НовоеЗадание.СсылкаНаЗадачу    = Объект.ЗадачаСсылка;  
	НовоеЗадание.ТипОценкиЗадачи   = Перечисления.ЦБР_ТипОценкиЗадачи.Стандартная;  
	НовоеЗадание.Исполнитель       = ПараметрыСеанса.ТекущийПользователь;  
	НовоеЗадание.Записать(РежимЗаписиДокумента.Проведение);
	
	ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(НовоеЗадание.Ссылка, Состояние);
	
	Возврат НовоеЗадание.Ссылка;
	
КонецФункции

#КонецОбласти 

#Область ЭффективныеЧасы

&НаСервере
Процедура ОбновитьФактическиеЧасы()
		
	ЭффективныеЧасы            = ПолучитьФактическиеЧасыПоЗаданию();
    ЭффективныеЧасыДоИзменения = ЭффективныеЧасы;	
	
	Если Объект.ВидРаботы = Справочники.ЦБР_ВидыРаботВКалендаре.Эффективный И (ЭффективныеЧасы > 0 Или Объект.Статус = Перечисления.ЦБР_СтатусЗаписиКалендаря.Завершен)
		И ЗначениеЗаполнено(Объект.Пользователь) И ЦБР_ЗадачиЗаданияВызовСервера.ЕстьПравоСписанияЧасовПоЗаданию(Объект.Пользователь, Объект.ЗадачаСсылка) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЭффективныеЧасы", "Видимость", Истина); 
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЭффективныеЧасы", "Видимость", Ложь); 
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьФактическиеЧасыПоЗаданию()
	
	ЭффективныеЧасыПоЗаданию = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ЦБР_ЗаданиеЭффективныеЧасы.ЭффективныеЧасы) КАК ЭффективныеЧасы,
		|	ЦБР_ЗаданиеЭффективныеЧасы.ДействиеКалендаря КАК ДействиеКалендаря
		|ИЗ
		|	Документ.ЦБР_Задание.ЭффективныеЧасы КАК ЦБР_ЗаданиеЭффективныеЧасы
		|ГДЕ
		|	ЦБР_ЗаданиеЭффективныеЧасы.Ссылка = &Ссылка
		|	И ЦБР_ЗаданиеЭффективныеЧасы.ДействиеКалендаря = &ДействиеКалендаря
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦБР_ЗаданиеЭффективныеЧасы.ДействиеКалендаря";
	
	Запрос.УстановитьПараметр("ДействиеКалендаря", Объект.Ссылка);
	Запрос.УстановитьПараметр("Ссылка", Объект.ЗаданиеСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		ЭффективныеЧасыПоЗаданию = ВыборкаДетальныеЗаписи.ЭффективныеЧасы;
	КонецЕсли;
	
	Возврат ЭффективныеЧасыПоЗаданию;
	
КонецФункции

&НаСервере
Процедура СохранитьЭффективныеЧасыНаСервере()
	
	Если ЭффективныеЧасы <> ЭффективныеЧасыДоИзменения Тогда
		Попытка
			ЗаданиеОбъект = Объект.ЗаданиеСсылка.ПолучитьОбъект();  
			СтрокаЧасов   = ЗаданиеОбъект.ЭффективныеЧасы.Найти(Объект.Ссылка, "ДействиеКалендаря"); 
			Если СтрокаЧасов <> Неопределено Тогда
				
				ЦБР_ЗадачиЗаданияВызовСервера.ДобавитьЧасыЗП(ЗаданиеОбъект, Неопределено, ЭффективныеЧасы - СтрокаЧасов.ЭффективныеЧасы);
								
				СтрокаЧасов.ЭффективныеЧасы   = ЭффективныеЧасы; 
			    СтрокаЧасов.ВидЧаса           = ЗаданиеОбъект.ВидЧаса; 
				
			Иначе
			    СтрокаЧасов                   = ЗаданиеОбъект.ЭффективныеЧасы.Добавить();
				СтрокаЧасов.ВидЧаса           = ЗаданиеОбъект.ВидЧаса;
				СтрокаЧасов.Дата              = Объект.ДатаОкончания; 
				СтрокаЧасов.Исполнитель       = Объект.Пользователь;
				СтрокаЧасов.ДействиеКалендаря = Объект.Ссылка;
				СтрокаЧасов.ЭффективныеЧасы   = ЭффективныеЧасы;  
				
				ЦБР_ЗадачиЗаданияВызовСервера.ДобавитьЧасыЗП(ЗаданиеОбъект, НачалоМесяца(Объект.ДатаОкончания), ЭффективныеЧасы);
				
			КонецЕсли;	
			ЗаданиеОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ЗаписьЖурналаРегистрации("ЦБР_Задание.ЭффективныеЧасы", УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ЦБР_Задание, ЗаданиеОбъект, ОписаниеОшибки());
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Не удалось записать документ." + Символы.ПС
			+ ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + Символы.ПС
			+ "Подробности см. в журнале регистрации");
			
		КонецПопытки;
	КонецЕсли;	

КонецПроцедуры 

&НаКлиенте
Процедура СохранитьЭффективныеЧасы(Команда)
	
	СохранитьЭффективныеЧасыНаСервере();
	Оповестить("Изменение_ЭффективныеЧасы");
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭффективныеЧасыПриИзменении(Элемент)
	
	Если ОстатокЧасов = "По Факту" Тогда
		Возврат;
	ИначеЕсли ЭффективныеЧасы > ЭффективныеЧасыДоИзменения + ОстатокЧасов Тогда
		 ЭффективныеЧасы = ЭффективныеЧасыДоИзменения;
		 ТекстОшибки = СтрШаблон("Максимальное количество часов для списания: %1",ЭффективныеЧасыДоИзменения + ОстатокЧасов); 
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки); 
	КонецЕсли;	
	
КонецПроцедуры

 #КонецОбласти
