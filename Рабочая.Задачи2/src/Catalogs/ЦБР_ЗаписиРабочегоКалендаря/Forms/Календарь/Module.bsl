#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПользовательКалендаря  = Пользователи.АвторизованныйПользователь();
	ПользователиКалендаря.Добавить(ПользовательКалендаря);
	
	УстановитьУсловноеОформление();

	ОтображаемаяДата = НачалоДня(ТекущаяДатаСеанса());
	ПериодОтображения = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
		"ПериодОтображенияРабочегоКалендаря");

	УстановитьГрафикРаботы();
	УстановитьАвтообновление();
	УстановитьВариантРаботыФормы();

	ОбновитьОтображениеСервер();
	
	Если Параметры.Свойство("ЗадачаСсылка") Тогда
		ЗадачаСсылка = Параметры.ЗадачаСсылка;
	КонецЕсли;

	Если Параметры.Свойство("ЗаданиеСсылка") Тогда
		ЗаданиеССылка = Параметры.ЗаданиеСсылка; 
	КонецЕсли;	
		
	Если Параметры.Свойство("Задания") Тогда
		Для Каждого Строка Из Параметры.Задания Цикл
			МультиОС.Добавить(Строка);
		КонецЦикла;
	КонецЕсли;

	ЗаполнитьТаблицуТребуетВнимания(Пользователи.ТекущийПользователь());

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ДатаСегодня = НачалоДня(ТекущаяДата());

	УстановитьОтображаемуюДату(ОтображаемаяДата);

#Если Не ВебКлиент И Не МобильныйКлиент Тогда
	УстановитьАвтообновлениеФормы();
#КонецЕсли

	ПланировщикПриАктивизации(Элементы.Планировщик, Истина);

#Если МобильныйКлиент Тогда
	ОбновитьНадписьЗаголовок();
#КонецЕсли

#Если Не ВебКлиент И Не МобильныйКлиент Тогда
	ПодключитьОбработчикОжидания("ЗаполнитьТаблицуТребуетВниманияКлиент", 60);
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуТребуетВниманияКлиент() Экспорт

	ЗаполнитьТаблицуТребуетВнимания(ПользователиКлиент.ТекущийПользователь());

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТребуетВнимания(Сотрудник)

	ТребуютВнимания.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦБР_ТребуютВнимания.НаименованиеДокументаЭтапа КАК НаименованиеДокументаЭтапа,
		|	ЦБР_ТребуютВнимания.Задача КАК Задача,
		|	ЦБР_ТребуютВнимания.Задание КАК Задание,
		|	ВЫБОР
		|		КОГДА ЦБР_ТребуютВнимания.Задание = ЗНАЧЕНИЕ(Документ.ЦБР_Задание.ПустаяСсылка)
		|			ТОГДА ""#"" + ЦБР_ТребуютВнимания.Задача.Код
		|		ИНАЧЕ ""#"" + ЦБР_ТребуютВнимания.Задание.Номер
		|	КОНЕЦ КАК НомерДокументаЭтапа
		|ИЗ
		|	РегистрСведений.ЦБР_ТребуютВнимания КАК ЦБР_ТребуютВнимания
		|ГДЕ
		|	ЦБР_ТребуютВнимания.Ответственный = &Ответственный";
	
	Запрос.УстановитьПараметр("Ответственный", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТребуютВнимания.Загрузить(РезультатЗапроса.Выгрузить());

КонецПроцедуры

&НаКлиенте
Процедура ТребуютВниманияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекСтрока = Элемент.ТекущиеДанные;
	Если Не ТекСтрока = Неопределено Тогда
		Отправка = Новый Структура;
		Если ТекСтрока.Задание = ПредопределенноеЗначение("Документ.ЦБР_Задание.ПустаяСсылка") Тогда
			Отправка.Вставить("Ключ", ТекСтрока.Задача);
			ОткрытьФорму("Документ.ЦБР_Задача.Форма.ФормаДокумента", Отправка);
		Иначе
			Отправка.Вставить("Ключ", ТекСтрока.Задание);
			ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДокумента", Отправка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда

		Оповестить("Запись_ВзаимодействияКалендаря");
		Закрыть();
	КонецЕсли;

	Если ИмяСобытия = "Запись_ДоступноеВремяПользователя" Или ИмяСобытия = "Запись_ЗанятостьСотрудника" Или ИмяСобытия
		= "Запись_Мероприятие" Или ИмяСобытия = "Запись_ДоступноеВремяПользователя" Или ИмяСобытия = "Запись_Отсутствие"
		Или ИмяСобытия = "Закрытие_НастройкиСинхронизацииКалендаря" Тогда

		ОбновитьОтображениеКлиент();
		 
	КонецЕсли;

	Если ИмяСобытия = "Запись_НастройкиКалендаря" Тогда
		ОбновитьОтображениеКлиент( , , Истина);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодОтображенияПриИзменении(Элемент)

	УстановитьОтображаемуюДату(ОтображаемаяДата);

	ОбновитьДанныеКалендаря("ПериодОтображения");

КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПриИзменении(Элемент)

	ВыделенныеДаты.ЗагрузитьЗначения(Элемент.ВыделенныеДаты);
	ОбновитьНастройкиОтображения();

	ОбновитьДанныеКалендаря();

#Если МобильныйКлиент Тогда
	ИзменитьВидимостьВыбораДаты();
	//ОбновитьНадписьЗаголовок();
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПриАктивизацииДаты(Элемент)

	Если Элемент.ВыделенныеДаты.Количество() = ВыделенныеДаты.Количество() Тогда
		Возврат;
	КонецЕсли;

	ВыделенныеДаты.ЗагрузитьЗначения(Элемент.ВыделенныеДаты);
	Если ВыделенныеДаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ВыделенныеДаты.НайтиПоЗначению(ОтображаемаяДата) = Неопределено Тогда
		ОтображаемаяДата = ВыделенныеДаты[ВыделенныеДаты.Количество() - 1].Значение;
	КонецЕсли;
	ОбновитьНастройкиОтображения();

	ОбновитьДанныеКалендаря("ПериодОтображения");

КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПриВыводеПериода(Элемент, ОформлениеПериода)

	Если Не ЗаданГрафикРаботы Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаОформленияПериода Из ОформлениеПериода.Даты Цикл

		Если ЗаполненныеГода.НайтиПоЗначению(Год(СтрокаОформленияПериода.Дата)) = Неопределено Тогда
			РаботаСРабочимКалендаремСервер.ЗаполнитьРабочиеДни(
				РабочиеДни, ЗаполненныеГода, СтрокаОформленияПериода.Дата);
		КонецЕсли;

		СтрокаРабочиеДни = РабочиеДни.НайтиПоЗначению(СтрокаОформленияПериода.Дата);
		Если СтрокаРабочиеДни = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если СтрокаРабочиеДни.Пометка Тогда
			СтрокаОформленияПериода.ЦветТекста = WebЦвета.Черный;
		Иначе
			СтрокаОформленияПериода.ЦветТекста = WebЦвета.Красный;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, ВыбраннаяДата)

	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;

	Если ПараметрыПеретаскивания.Значение.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("ЭлементПланировщика") Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Структура") Тогда
		//@skip-check event-heandler-boolean-param
		СтандартнаяОбработка = Не РаботаСРабочимКалендаремКлиентСервер.ЭтоЭлементЗаписиКалендаря(
				ПараметрыПеретаскивания.Значение[0]);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, ВыбраннаяДата)

	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;

	Если ПараметрыПеретаскивания.Значение.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("ЭлементПланировщика") Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.ПеренестиЭлементыПланировщикаНаДату(
			Элементы.Планировщик, Планировщик, НастройкиОтображения, ПараметрыПеретаскивания.Значение, ВыбраннаяДата,
			УникальныйИдентификатор);
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Структура") Тогда

		Если Не РаботаСРабочимКалендаремКлиентСервер.ЭтоЭлементЗаписиКалендаря(
				ПараметрыПеретаскивания.Значение[0]) Тогда
			Возврат;
		КонецЕсли;

		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.ПеренестиЭлементыЗаписейКалендаряНаДату(
			ПараметрыПеретаскивания.Значение, ВыбраннаяДата);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПользователиКалендаряНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ВыбратьКалендарьПользователя();

КонецПроцедуры

&НаКлиенте
Процедура ПользователиКалендаряПриИзменении(Элемент)

	ОбновитьНастройкиОтображения();
	ЗавершениеВыбратьКалендарьПользователяСервер();

КонецПроцедуры

&НаКлиенте
Процедура НадписьПустоНажатие(Элемент)

	ВыбратьКалендарьПользователя();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКалендаря

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ОбработкаПриАктивизации(Элемент, СтандартнаяОбработка);  
	
	ЕстьВыделенныеЭлементы = Элемент.ВыделенныеЭлементы.Количество() <> 0;
		
	Если ЕстьВыделенныеЭлементы Тогда 
		ЗаписьВКалендаре = Элемент.ВыделенныеЭлементы[0].Значение.Ссылка;
		
		СтатусНовый = ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый"));
		СтатусНачал = ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_Приступить", "Видимость", СтатусНовый);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_Закончить", "Видимость", СтатусНачал);   
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_ПоПлану", "Видимость", СтатусНовый);   
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланировщикКонтекстноеМенюОС_Отменить", "Видимость", СтатусНовый);   
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередСозданием(
		Элемент,
		?(МультиОС.Количество(), МультиОС, ЭтотОбъект.ЗаданиеССылка), ЭтотОбъект.ЗадачаСсылка,
		Начало, Конец, Значения, Текст, СтандартнаяОбработка, ИспользоватьБыстроеРедактирование, ПользовательКалендаря,
		НастройкиОтображения, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораЭлемента(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)

	РаботаСРабочимКалендаремКлиент.ОбработкаОкончанияРедактированияЭлемента(
		Элемент, НовыйЭлемент, ОтменаРедактирования, Планировщик, НастройкиОтображения, УникальныйИдентификатор,
		ПользовательКалендаря);
		
      
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломРедактированиемЭлемента(Элемент, НовыйЭлемент,
		СтандартнаяОбработка, ПользовательКалендаря);
         
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемЭлемента(Элемент, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПланДня

&НаКлиенте
Процедура ПланДняПриАктивизацииСтроки(Элемент)

	РаботаСРабочимКалендаремКлиент.ОбработкаАктивизацииОбластиПланДня(
		Элемент, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораПланДня(
		Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломДобавленияПланДня(
		Элемент, Отказ, Копирование, Родитель, Группа, НастройкиОтображения, ПользовательКалендаря);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломИзменения(Элемент, Отказ)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломИзмененияПланДня(Элементы.ПланДня, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередУдалением(Элемент, Отказ)

	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемПланДня(Элементы.ПланДня, ПланДня, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)

	РаботаСРабочимКалендаремКлиент.ОбработкаНачалаПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, Выполнение, НастройкиОтображения);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	РаботаСРабочимКалендаремКлиент.ОбработкаПроверкиПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	РаботаСРабочимКалендаремКлиент.ОбработкаПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтработатьПланДня(Команда)

	Если Приступил() Тогда
		РаботаСРабочимКалендаремКлиент.ОтработатьВыделенныеЗаписиКалендаряПланДня(Элементы.ПланДня, ПланДня);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Невозможно завершить действие т.к. оно еще не начато!");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Отработать(Команда)
	Если Приступил() Тогда
		ЗаписьВКалендаре = Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.Ссылка;
		ЗаданиеССылка = ПолучитьЗначениеРеквизита(ЗаписьВКалендаре, "СсылкаНаЗадание");

		РаботаСРабочимКалендаремКлиент.ОтработатьВыделенныеЗаписиКалендаря(Элементы.Планировщик);

		Если ЗначениеЗаполнено(ЗаданиеССылка) Тогда
			Если Не ЗначениеЗаполнено(ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(
				ЗаданиеССылка).ДатаОкончаниеДействия) Тогда
				Подсказка = "Результат:";
				Оповещение  = Новый ОписаниеОповещения("ПослеВводаОписаниеПричиныПаузы", ЭтотОбъект,
					Новый Структура("СсылкаНаЗадание", ЗаданиеССылка));
				ПоказатьВводСтроки(Оповещение, , Подсказка, 0, Истина);
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю("Нет действия в работе!");
			КонецЕсли;
			ЦБР_ЗадачиЗаданияВызовСервера.ПоменятьСтатусЗадания(ЗаданиеССылка, ОбщегоНазначенияКлиент.ПредопределенныйЭлемент(
				"Перечисление.ЦБР_Состояние.Приостановлен"));
		КонецЕсли;

	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Невозможно завершить действие т.к. оно еще не начато!");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Приступить(Команда)
	Если Не Приступил() Тогда
		Если ЕстьБольшеДвухАктивныхДействий() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нельзя начать больше 2-ух действий!");
		Возврат;
		КонецЕсли;
		РаботаСРабочимКалендаремКлиент.ПриступитьВыделеныеЗаписиКалендаря(Элементы.Планировщик);
		ДобавитьЗаписьВДействияПоЗаданию();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаписьВДействияПоЗаданию()
	
	Если Элементы.Планировщик.ВыделенныеЭлементы.Количество() Тогда
		ЗаписьВКалендаре = Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.Ссылка;
		ЗаданиеССылка = ПолучитьЗначениеРеквизита(ЗаписьВКалендаре, "СсылкаНаЗадание");
		Если ЗначениеЗаполнено(ЗаданиеССылка) И ТипЗнч(ЗаданиеССылка) = Тип("ДокументСсылка.ЦБР_Задание") Тогда
			Структура = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьСтруктуруПоследнегоДействия();
			Структура.Задание = ЗаданиеССылка;
			Структура.ДатаНачалаДействия =  ТекущаяДатаСеанса();
			Структура.Статус = ПредопределенноеЗначение("Перечисление.ЦБР_Состояние.ВРаботе");

			ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(ЗаданиеССылка);
			Если ПоследнееДействие = Неопределено Или ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
				Структура.ЗаписьКалендаря = ЗаписьВКалендаре;
				ЦБР_ЗадачиЗаданияВызовСервера.СоздатьЗаписьДействияПоЗадаче(Структура);
			Иначе
				ОбщегоНазначения.СообщитьПользователю("Есть не заврешенное действие по этой задаче!");
				Возврат;
			КонецЕсли;
			ЦБР_ЗадачиЗаданияВызовСервера.ПоменятьСтатусЗадания(ЗаданиеССылка, Перечисления.ЦБР_Состояние.ВРаботе);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКалендарь(Команда)

	ОбновитьОтображениеКлиент();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиКалендаря(Команда)

	ОткрытьФорму("Справочник.ЦБР_ЗаписиРабочегоКалендаря.Форма.НастройкиКалендаря");

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПредыдущийПериод(Команда)

	УстановитьОтображаемуюДату(
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаПредыдущегоПериода(
			ПериодОтображения, ОтображаемаяДата));

	ОбновитьДанныеКалендаря();

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСегодня(Команда)
	
	//@skip-check use-non-recommended-method	
	УстановитьОтображаемуюДату(ТекущаяДата());

	ОбновитьДанныеКалендаря();

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСледующийПериод(Команда)

	УстановитьОтображаемуюДату(
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаСледующегоПериода(
			ПериодОтображения, ОтображаемаяДата));

	ОбновитьДанныеКалендаря();

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗанятость(Команда)
	
	//@skip-check use-non-recommended-method	
	Если ОтображаемаяДата = НачалоДня(ТекущаяДата()) Тогда
		РаботаСРабочимКалендаремКлиент.СоздатьЗанятость();
		Возврат;
	КонецЕсли;

	ДатаНачалаНовойЗаписи = ОтображаемаяДата + НастройкиОтображения.ОтображатьВремяС * 3600;
	ДатаОкончанияНовойЗаписи = ДатаНачалаНовойЗаписи + 3600;
	РаботаСРабочимКалендаремКлиент.СоздатьЗанятость(ДатаНачалаНовойЗаписи, ДатаОкончанияНовойЗаписи, Ложь);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьКалендаря(Команда)

	Если ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		РаботаСРабочимКалендаремКлиент.СоздатьЗаписьКалендаряПланДня(
			Элементы.ПланДня, НастройкиОтображения, ПользовательКалендаря);
	Иначе
		РаботаСРабочимКалендаремКлиент.СоздатьЗаписьКалендаряПланировщик(
			НастройкиОтображения, ПользовательКалендаря);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьЗаписьКалендаря(Команда)

	Если Элементы.Планировщик.ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	РаботаСРабочимКалендаремКлиент.СкопироватьЗаписьКалендаря(
		Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.Ссылка,
		Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.ДатаНачала,
		Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.ДатаОкончания);

КонецПроцедуры

&НаКлиенте
Процедура НадписьПериодНажатие(Элемент)

	ИзменитьВидимостьВыбораДаты();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗаписьКалендаря(Команда)
	ПараметрыФормы = Новый Структура("Ключ", Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.Ссылка);
	ОткрытьФорму("Справочник.ЦБР_ЗаписиРабочегоКалендаря.Форма.ФормаЭлемента", ПараметрыФормы);
КонецПроцедуры

#КонецОбласти

#Область ОС_Календаря 

&НаКлиенте
Процедура ОС_Закончить(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	Если Не ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Начал")) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Команда доступна только для записей календаря в статусе ""Начал""");
		Возврат;
	КонецЕсли;
	
	РеквизитыЗаписи = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ЗаписьВКалендаре, "ЗаданиеСсылка,ЗадачаСсылка,Описание");     
	
	Если ЗначениеЗаполнено(РеквизитыЗаписи.ЗаданиеСсылка) Тогда
		
		ПоследнееДействие = ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(РеквизитыЗаписи.ЗаданиеСсылка);
		Если Не ЗначениеЗаполнено(ПоследнееДействие.ДатаОкончаниеДействия) Тогда
			
			Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект);
			
			ПараметрыФормы = Новый Структура;       
			ПараметрыФормы.Вставить("Задание", РеквизитыЗаписи.ЗаданиеСсылка);
			ПараметрыФормы.Вставить("Описание", РеквизитыЗаписи.Описание);
			ПараметрыФормы.Вставить("ЗаписьОС", ЗаписьВКалендаре);
			
			ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
		КонецЕсли;	
	Иначе
		ЦБР_ЗадачиЗаданияВызовСервера.УстановитьОтработана(Новый Структура("ЗаписьОС,ПоПлану,РешеноПриПервомПодключении,ОписаниеВыполненнойРаботы", ЗаписьВКалендаре, Ложь, Ложь, Неопределено));	
	    Оповестить("Запись_ЗаписьКалендаря");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОС_Приступить(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	Если Не ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый")) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Команда доступна только для записей календаря в статусе ""Новый""");
		Возврат;
	КонецЕсли;	
	
	ЦБР_ЗадачиЗаданияВызовСервера.УстановитьСтарт(ЗаписьВКалендаре);
	
	Оповестить("Запись_ЗаписьКалендаря");

КонецПроцедуры

&НаКлиенте
Процедура ОС_ПоПлану(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	Если Не ЦБР_ЗадачиЗаданияВызовСервера.ОС_ЕстьСтатус(ЗаписьВКалендаре, ПредопределенноеЗначение("Перечисление.ЦБР_СтатусЗаписиКалендаря.Новый")) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Команда доступна только для записей календаря в статусе ""Новый""");
		Возврат;
	КонецЕсли;
	
	РеквизитыЗаписи = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ЗаписьВКалендаре, "ЗаданиеСсылка,ЗадачаСсылка,Описание,ДатаНачала,ДатаОкончания");     
			
	Если НачалоДня(РеквизитыЗаписи.ДатаНачала) < НачалоДня(ТекущаяДата() - 7 * 24 * 3600) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нельзя создать ОС по плану позже " + Формат(НачалоДня(ТекущаяДата() - 7 * 24 * 3600),"ДФ=dd.MM.yyyy"));  
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитыЗаписи.Описание) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполните описание!");  
		Возврат;
	КонецЕсли;	
		
	КоличествоДней = (НачалоДня(РеквизитыЗаписи.ДатаОкончания) - НачалоДня(РеквизитыЗаписи.ДатаНачала)) / (60 * 60 * 24) + 1;
 
	Оповещение = Новый ОписаниеОповещения("ДобавитьЭффективныеЧасыОкончание", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;   	
	ПараметрыФормы.Вставить("Задание", РеквизитыЗаписи.ЗаданиеСсылка); 
	ПараметрыФормы.Вставить("ЗаписьОС", ЗаписьВКалендаре);
	ПараметрыФормы.Вставить("Описание", РеквизитыЗаписи.Описание);  	
	ПараметрыФормы.Вставить("ПоказатьЗакрытьЗадание", Ложь);      
	ПараметрыФормы.Вставить("ПоПлану", Истина);       
	ПараметрыФормы.Вставить("КоличествоДней", КоличествоДней);
	Если КоличествоДней > 1 Тогда
		ПараметрыФормы.Вставить("ДатыПоДням", РазбитьЗаписиНаНесколькоДней(РеквизитыЗаписи.ДатаНачала, РеквизитыЗаписи.ДатаОкончания, КоличествоДней));
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЦБР_Задание.Форма.ФормаДобавленияЭффективныхЧасов", ПараметрыФормы, ЭтотОбъект, , , ,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры 

&НаКлиенте
Процедура ОС_Отменить(Команда)
	
	ВыделенныеЭлементыПланировщика = Элементы.Планировщик.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементыПланировщика.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запись календаря не найдена. Попробуйте обновить календарь");
		Возврат;
	КонецЕсли;	
	
	ЗаписьВКалендаре = ВыделенныеЭлементыПланировщика[0].Значение.Ссылка;
	
	ПараметрыОповещения = Новый Структура("ЗаписьВКалендаре", ЗаписьВКалендаре);
	
	Оповещение = Новый ОписаниеОповещения("ОС_ОтменитьОкончание", ЭтотОбъект, ПараметрыОповещения);
	ПоказатьВводСтроки(Оповещение,,"Укажите причину отмены ОС.", 0, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭффективныеЧасыОкончание(Результат, ДопПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		Оповестить("Запись_ЗаписьКалендаря");
	КонецЕсли;
		
КонецПроцедуры  

&НаКлиенте
Процедура ОС_ОтменитьОкончание(Результат, Параметры) Экспорт
 
	Если Не Результат = Неопределено И ЗначениеЗаполнено(Результат) Тогда
		
		ЗаписьВКалендаре = Параметры.ЗаписьВКалендаре;
		
		ЦБР_ЗадачиЗаданияВызовСервера.УстановитьОтменена(ЗаписьВКалендаре, Результат);
			
		УстановленаПометкаУдаления = РаботаСРабочимКалендаремСервер.УстановитьПометкуУдаления(
		ЗаписьВКалендаре,
		Истина,
		Результат);
		
		Если УстановленаПометкаУдаления Тогда
			Оповестить("Запись_ЗаписьКалендаря", ЗаписьВКалендаре);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана причина отмены!");
	КонецЕсли;
 
КонецПроцедуры 

&НаСервереБезКонтекста
Функция РазбитьЗаписиНаНесколькоДней(Начало, Конец, КоличествоДней)
	
	ОсПоДням = Новый Массив;
	День = 1;
	ДатаОкончания = Конец;
	Пока День <= КоличествоДней Цикл
		Если ДеньНедели(Начало) = 6 Или ДеньНедели(Начало) = 7 Тогда
			День = День + 1;
			Начало = НачалоДня(НачалоДня(Начало) + 25 * 60 * 60) + 9 * 60 * 60;
			Продолжить;
		КонецЕсли;
		СтруктураДат = Новый Структура("Начало, Конец, Часов");
		Если День = КоличествоДней Тогда
			Конец = ДатаОкончания;
		Иначе  
			Конец = НачалоДня(Начало) + 64800;
		КонецЕсли;
		СтруктураДат.Начало = Начало;
		СтруктураДат.Конец = Конец; 
		
		КолВоЧасов = (Конец-Начало)/3600;
		
		СтруктураДат.Часов = ЦенообразованиеКлиентСервер.ОкруглитьЦену(КолВоЧасов, 1, Перечисления.ВариантыОкругления.ВсегдаВПользуПредприятия);    
		ОсПоДням.Добавить(СтруктураДат);
		День = День + 1;
		Начало = НачалоДня(НачалоДня(Начало) + 25 * 60 * 60) + 9 * 60 * 60;
	КонецЦикла;   
	
	Возврат ОсПоДням; 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция Приступил()
	
	МенеджерЗаписи = РегистрыСведений.НастройкиОтображенияЗаписейРабочегоКалендаря.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Событие = Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.Ссылка;
	МенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.Настройка = Перечисления.НастройкиЗаписейРабочегоКалендаря.Приступил;

	МенеджерЗаписи.Прочитать();

	Возврат МенеджерЗаписи.ЗначениеНастройки;
КонецФункции

&НаСервере
Процедура ЗавершениеВыбратьКалендарьПользователяСервер()

	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ТекущиеКалендариВсеКалендари", ПользователиКалендаря.ВыгрузитьЗначения());
	ОбновитьОтображениеСервер();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()


КонецПроцедуры

&НаСервере
Процедура УстановитьАвтообновление()

	НастройкиАвтообновления = Автообновление.ПолучитьНастройкиАвтообновленияФормы(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьАвтообновлениеФормы()

	Если ТипЗнч(НастройкиАвтообновления) = Тип("Структура") И НастройкиАвтообновления.Автообновление Тогда
		ПодключитьОбработчикОжидания(
			"ОбработкаАвтообновления", НастройкиАвтообновления.ПериодАвтоОбновления, Ложь);
	Иначе
		ОтключитьОбработчикОжидания("ОбработкаАвтообновления");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаАвтообновления()

	Если ТипЗнч(НастройкиАвтообновления) <> Тип("Структура") Или (ТипЗнч(НастройкиАвтообновления) = Тип("Структура")
		И Не НастройкиАвтообновления.Автообновление) Тогда
		ОтключитьОбработчикОжидания("ОбработкаАвтообновления");
	Иначе

		Если ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда

			ОбновитьОтображениеКлиент();

		Иначе

			ОбновитьДанныеКалендаря();

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьГрафикРаботы()

	ГрафикРаботы = Константы.ОсновнойКалендарьПредприятия.Получить();

	Если ЗаданГрафикРаботы Тогда
		РаботаСРабочимКалендаремСервер.ЗаполнитьРабочиеДни(
			РабочиеДни, ЗаполненныеГода, ОтображаемаяДата);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтображаемуюДату(НоваяОтображаемаяДата)

	Элементы.ОтображаемаяДата.ВыделенныеДаты.Очистить();
	ОтображаемаяДата = НоваяОтображаемаяДата;
	Элементы.ОтображаемаяДата.ВыделенныеДаты.Добавить(ОтображаемаяДата);
	ВыделенныеДаты.ЗагрузитьЗначения(Элементы.ОтображаемаяДата.ВыделенныеДаты);

	ОбновитьНастройкиОтображения();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСобытияРабочегоКалендаря(Цвет)

	Если ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		РаботаСРабочимКалендаремКлиент.ОбработкаИзмененияЦветаПланДня(Элементы.ПланДня, ПланДня, Цвет);
	Иначе
		РаботаСРабочимКалендаремКлиент.ОбработкаИзмененияЦвета(Элементы.Планировщик, Цвет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеКлиент(Параметр = Неопределено, ИзмененнаяНастройка = Неопределено,
	ОбновитьНастройки = Ложь)
	
	//@skip-check use-non-recommended-method	
	ДатаСегодня = НачалоДня(ТекущаяДата());

	Если Параметр <> Неопределено И ПериодОтображения <> ПредопределенноеЗначение(
		"Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда

		РаботаСРабочимКалендаремКлиент.ОбновитьЗаписиКалендаряВПланировщике(
			Планировщик, НастройкиОтображения, Параметр);
		Возврат;

	КонецЕсли;

	СвернутыеЭлементы = РаботаСРабочимКалендаремКлиент.ПолучитьСвернутыеЭлементыПланаДня(
		Элементы.ПланДня, ПланДня);
	ОбновитьОтображениеСервер(Параметр, ИзмененнаяНастройка, ОбновитьНастройки);
	РаботаСРабочимКалендаремКлиент.ВосстановитьСостояниеПланаДня(
		Элементы.ПланДня, ПланДня, СвернутыеЭлементы, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);

#Если Не ВебКлиент Тогда
	Если ОбновитьНастройки Тогда
		УстановитьАвтообновлениеФормы();
	КонецЕсли;
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеКалендаря(ИзмененнаяНастройка = Неопределено)

	Если ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда

		ОбновитьОтображениеКлиент( , ИзмененнаяНастройка);

	Иначе

		РаботаСРабочимКалендаремКлиент.ОбновитьОтображениеПланировщика(
			Планировщик, НастройкиОтображения, ИзмененнаяНастройка);

	КонецЕсли;

#Если МобильныйКлиент Тогда
	ОбновитьНадписьЗаголовок();
#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеСервер(Параметр = Неопределено, ИзмененнаяНастройка = Неопределено,
	ОбновитьНастройки = Ложь)

	РаботаСРабочимКалендаремСервер.СохранитьНастройку(НастройкиОтображения, ИзмененнаяНастройка);

	НастройкиОтображения = РаботаСРабочимКалендаремСервер.ПолучитьНастройкиОтображения();
	НастройкиОтображения.Пользователи.Очистить();
	Для Каждого ОчереднойПользователь Из ПользователиКалендаря Цикл
		НастройкиОтображения.Пользователи.Добавить(ОчереднойПользователь.Значение);
	КонецЦикла;
	Если ПользователиКалендаря.Количество() = 1 Тогда
		ПользовательКалендаря = ПользователиКалендаря[0].Значение;
	Иначе
		ПользовательКалендаря = Неопределено;
	КонецЕсли;
	Если ПользователиКалендаря.Количество() > 1 Тогда
		НастройкиОтображения.ОтображатьЗанятость = Ложь;
	КонецЕсли;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты = ВыделенныеДаты.ВыгрузитьЗначения();
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;

	УстановитьТекущуюСтраницу(Элементы, ПользователиКалендаря, ПериодОтображения);

	Если ПериодОтображения = Перечисления.ПериодОтображенияРабочегоКалендаря.ПланДня Тогда

		ПланДняЗначение = РеквизитФормыВЗначение("ПланДня");
		РаботаСРабочимКалендаремСервер.ОтобразитьПланДня(ПланДняЗначение, НастройкиОтображения);
		ЗначениеВРеквизитФормы(ПланДняЗначение, "ПланДня");

	Иначе

		РаботаСРабочимКалендаремСервер.ОтобразитьКалендарь(Планировщик, НастройкиОтображения);

		Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
			ИспользоватьБыстроеРедактирование = Ложь;
		Иначе
			ИспользоватьБыстроеРедактирование = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
					"ИспользоватьБыстроеРедактирование");
		КонецЕсли;

		УстановитьВидимостьЭлементов();

	КонецЕсли;

	Если ОбновитьНастройки Тогда
		УстановитьАвтообновление();
		УстановитьВидимостьЭлементов();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьВариантРаботыФормы()

	Если Не ЗначениеЗаполнено(Параметры.ВариантРаботыФормы) Тогда
		Параметры.ВариантРаботыФормы = "МойКалендарь";
	КонецЕсли;

	Если Параметры.ВариантРаботыФормы = "МойКалендарь" Тогда
		КлючНазначенияИспользования = "МойКалендарь";
		Заголовок = НСтр("ru = 'Мой календарь'");

		ПользовательКалендаря = Пользователи.АвторизованныйПользователь();

		Элементы.ПользователиКалендаря.Видимость = Ложь;
		Элементы.ФормаСоздатьЗанятость.Видимость = Истина;
	ИначеЕсли Параметры.ВариантРаботыФормы = "ВсеКалендари" Тогда
		КлючНазначенияИспользования = "ВсеКалендари";
		Заголовок = НСтр("ru = 'Все календари'");
		ПользователиКалендаря.ЗагрузитьЗначения(РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
			"ТекущиеКалендариВсеКалендари"));
		Элементы.ПользователиКалендаря.Видимость = Истина;
		Элементы.ФормаСоздатьЗанятость.Видимость = Ложь;
	ИначеЕсли Параметры.ВариантРаботыФормы = "КалендарьСотрудника" Тогда

		КлючНазначенияИспользования = "КалендарьСотрудника";
		Заголовок = СтрШаблон(НСтр("ru = 'Календарь сотрудника %1'"), Параметры.ФизЛицоСотрудника);

		ПользовательКалендаря = Пользователи.АвторизованныйПользователь();

		Элементы.ПользователиКалендаря.Видимость = Ложь;
		Элементы.ФормаСоздатьЗанятость.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОтображения()

	НастройкиОтображения.Пользователи.Очистить();
	Для Каждого ОчереднойПользователь Из ПользователиКалендаря Цикл
		НастройкиОтображения.Пользователи.Добавить(ОчереднойПользователь.Значение);
	КонецЦикла;
	Если ПользователиКалендаря.Количество() = 1 Тогда
		ПользовательКалендаря = ПользователиКалендаря[0].Значение;
	Иначе
		ПользовательКалендаря = Неопределено;
	КонецЕсли;
	Если ПользователиКалендаря.Количество() > 1 Тогда
		НастройкиОтображения.ОтображатьЗанятость = Ложь;
	КонецЕсли;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты = ВыделенныеДаты.ВыгрузитьЗначения();
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;

	УстановитьТекущуюСтраницу(Элементы, ПользователиКалендаря, ПериодОтображения);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКалендарьПользователя()

	ВыбранныеПользователи = Новый Массив;
	Для Каждого СтрПользовательКалендаря Из ПользователиКалендаря Цикл
		СтруктураВыбранногоАдресата = РаботаСАдреснойКнигойКлиент.СтруктураВыбранногоАдресата();
		СтруктураВыбранногоАдресата.Контакт = СтрПользовательКалендаря.Значение;
		ВыбранныеПользователи.Добавить(СтруктураВыбранногоАдресата);
	КонецЦикла;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 2);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор пользователей календаря'"));
	ПараметрыФормы.Вставить("ВыбранныеПользователи", ВыбранныеПользователи);
	ПараметрыФормы.Вставить("ПараметрыРасширеннойФормыПодбора", ПараметрыРасширеннойФормыПодбора());
	

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбратьКалендарьПользователя", ЭтотОбъект);

	РаботаСАдреснойКнигойКлиент.ВыбратьАдресатов(ПараметрыФормы, ЭтаФорма, ОписаниеОповещения);

КонецПроцедуры

&НаСервере
Функция ПараметрыРасширеннойФормыПодбора()
	
	ВыбранныеПользователи = Новый ТаблицаЗначений;
	ВыбранныеПользователи.Колонки.Добавить("Пользователь");
	ВыбранныеПользователи.Колонки.Добавить("НомерКартинки");
	
	//УчастникиГруппы = Объект.Состав.Выгрузить(, "Пользователь");
	
	Для каждого Элемент Из ПользователиКалендаря Цикл
		
		СтрокаВыбранныеПользователи = ВыбранныеПользователи.Добавить();
		СтрокаВыбранныеПользователи.Пользователь = Элемент.Значение;
		
	КонецЦикла;
	
	ЗаголовокФормыПодбора = НСтр("ru = 'Подбор участников группы пользователей'");
	ПараметрыРасширеннойФормыПодбора = 
		Новый Структура("ЗаголовокФормыПодбора, ВыбранныеПользователи, ПодборГруппНевозможен",
		                 ЗаголовокФормыПодбора, ВыбранныеПользователи, Истина);
	АдресХранилища = ПоместитьВоВременноеХранилище(ПараметрыРасширеннойФормыПодбора);
	Возврат АдресХранилища;
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеВыбратьКалендарьПользователя(ВыбранныеПользователи, ДопПараметры) Экспорт

	Если ВыбранныеПользователи = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПользователиКалендаря.Очистить();
	ПолучитьПользователей(ВыбранныеПользователи);

	ОбновитьНастройкиОтображения();
	ЗавершениеВыбратьКалендарьПользователяСервер();

КонецПроцедуры

&НаСервере
Процедура ПолучитьПользователей(ВыбранныеПользователи)
	//Состав = ВыбранныеПользователи.ПолучитьОбъект().Состав;
	Для Каждого ВыбранныйПользователь Из ВыбранныеПользователи Цикл
		ПользователиКалендаря.Добавить(ВыбранныйПользователь);
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Элементы, ПользователиКалендаря, ПериодОтображения)

	УказаныПользователи = ПользователиКалендаря.Количество() <> 0;

	Если УказаныПользователи И ПериодОтображения = ПредопределенноеЗначение(
		"Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		Элементы.СтраницыПланировщика.ТекущаяСтраница = Элементы.СтраницаПланДня;
	ИначеЕсли УказаныПользователи Тогда
		Элементы.СтраницыПланировщика.ТекущаяСтраница = Элементы.СтраницаПланировщик;
	Иначе
		Элементы.СтраницыПланировщика.ТекущаяСтраница = Элементы.СтраницаПусто;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	РаботаСРабочимКалендаремСервер.УстановитьУсловноеОформлениеПланДня(УсловноеОформление);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьВыбораДаты()

	Элементы.ГруппаДата.Видимость = Не Элементы.ГруппаДата.Видимость;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьЗаголовок()

	ПредставленияПериода = "";
	Если ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.Месяц") Тогда
		ПредставленияПериода = Формат(ОтображаемаяДата, "ДФ='MMMM yy'");

	ИначеЕсли ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.Неделя")
		Или ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ТриДня")
		Или ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда

		НачалоПериода = РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(
						ПериодОтображения, ОтображаемаяДата);
		КонецПериода = РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(
						ПериодОтображения, ОтображаемаяДата);
		ПредставленияПериода = СтрШаблон("%1 - %2", Формат(НачалоПериода, "ДФ=dd.MM"), Формат(КонецПериода, "ДФ=dd.MM"));

	ИначеЕсли ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.День") Тогда
		ПредставленияПериода= Формат(ОтображаемаяДата, "ДФ='dd MMM yyyy'");

	КонецЕсли;

	Элементы.НадписьПериод.Заголовок = ПредставленияПериода;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаОписаниеПричиныПаузы(Ответ, Параметры) Экспорт
	Если НЕ Ответ = Неопределено И ЗначениеЗаполнено(Ответ)Тогда 
			
		ПаузаНаСервере(Ответ, Параметры.СсылкаНаЗадание);
		
	Иначе
		
		Ответ = "Завершение действия в календаре";
		ПаузаНаСервере(Ответ, Параметры.СсылкаНаЗадание);
	КонецЕсли;   
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПаузаНаСервере(ПричинаПереноса, Предмет)

	МенеджерЗаписи = РегистрыСведений.ЦБР_ДействияПоЗаданиям.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьПоследнееДействие(Предмет));
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.ДатаОкончаниеДействия = ТекущаяДатаСеанса();
	МенеджерЗаписи.Статус = Перечисления.ЦБР_Состояние.Приостановлен;
	МенеджерЗаписи.Комментарий = ПричинаПереноса;

	МенеджерЗаписи.Записать(Истина);

	СтруктураДат = Новый Структура;
	СтруктураДат.Вставить("ДатаНачала", НачалоДня(МенеджерЗаписи.ДатаНачалаДействия));
	СтруктураДат.Вставить("ДатаОкончания", НачалоДня(МенеджерЗаписи.ДатаОкончаниеДействия));
	СтруктураДат.Вставить("ВремяНачала", ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьВремя(МенеджерЗаписи.ДатаНачалаДействия));
	СтруктураДат.Вставить("ВремяОкончания", ЦБР_ЗадачиЗаданияВызовСервера.ПолучитьВремя(
		МенеджерЗаписи.ДатаОкончаниеДействия));
	ДлительностьФакт = Предмет.ДлительностьФакт;
	Длительность = ЦБР_ЗадачиЗаданияВызовСервера.РасчитатьДлительностьФакт(СтруктураДат, ДлительностьФакт);
	Задание = Предмет.ПолучитьОбъект();
	Задание.ДлительностьФакт = Длительность;
	Задание.Состояние = Перечисления.ЦБР_Состояние.Приостановлен;
	Задание.Записать();
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеРеквизита(Ссылка, Реквизит)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, Реквизит);
КонецФункции

&НаСервере
Функция ЕстьБольшеДвухАктивныхДействий()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Выполнено.Событие
	|ИЗ
	|	РегистрСведений.НастройкиОтображенияЗаписейРабочегоКалендаря КАК Выполнено
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтображенияЗаписейРабочегоКалендаря КАК Приступил
	|		ПО Выполнено.Пользователь = Приступил.Пользователь
	|		И Выполнено.Событие = Приступил.Событие
	|ГДЕ
	|	Выполнено.Пользователь = &Пользователь
	|	И Выполнено.Настройка = ЗНАЧЕНИЕ(Перечисление.НастройкиЗаписейРабочегоКалендаря.Отработана)
	|	И Выполнено.ЗначениеНастройки = ЛОЖЬ
	|	И Приступил.Настройка = ЗНАЧЕНИЕ(Перечисление.НастройкиЗаписейРабочегоКалендаря.Приступил)
	|	И Приступил.ЗначениеНастройки = ИСТИНА";

	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Если РезультатЗапроса.Количество() > 1 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ПриПовторномОткрытии()
	 //ЗаданиеССылка = Параметры.СсылкаНаЗадание;
КонецПроцедуры
#КонецОбласти