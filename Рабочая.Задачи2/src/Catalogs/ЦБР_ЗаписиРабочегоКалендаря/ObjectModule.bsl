#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьИсключениеПовторения(ДатаИсключения, ЗаписьИсключения = Неопределено) Экспорт
	
	Если ТипЗаписиКалендаря <> Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		ВызватьИсключение НСтр("ru = 'Некорректный тип повторяющегося события.'");
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ДатаИсключения", ДатаИсключения);
	НайденныеСтроки = ИсключенияПовторения.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеСтроки.Количество() <> 0 Тогда
		ВызватьИсключение НСтр("ru = 'На данную дату уже указано исключение повторения.'");
	КонецЕсли;
	
	НоваяСтрокаИсключенияПовторения = ИсключенияПовторения.Добавить();
	НоваяСтрокаИсключенияПовторения.ЗаписьИсключения = ЗаписьИсключения;
	НоваяСтрокаИсключенияПовторения.ДатаИсключения = ДатаИсключения;
	
	Если ЗначениеЗаполнено(ЗаписьИсключения) Тогда
		ДополнительныеСвойства.Вставить("ИзмененоВхождение");
	Иначе
		ДополнительныеСвойства.Вставить("УдаленоВхождение");
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСледующуюДатуНачала(ДатаОтсчета) Экспорт
	
	Если ЗначениеЗаполнено(ДатаОкончанияПовторения) И ДатаОтсчета >= ДатаОкончанияПовторения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	
	НомерИтерации = 0;
	ПроверяемаяДата = НачалоДня(ДатаОтсчета);
	
	Пока НомерИтерации < 10000 Цикл // Ограничение на слишком следующую дату начала
		
		Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
			
			ВозможнаяДата = ПроверяемаяДата + (ДатаНачала - НачалоДня(ДатаНачала));
			Если ДатаОтсчета < ВозможнаяДата Тогда
				Возврат ВозможнаяДата;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		НомерИтерации = НомерИтерации + 1;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьРеквизитыЗаписиКалендаря(ДатаЗаписи = Неопределено) Экспорт
	
	РеквизитыЗаписиКалендаря = Справочники.ЦБР_ЗаписиРабочегоКалендаря.ПолучитьСтруктуруРеквизитовЗаписиКалендаря();
	ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ЭтотОбъект);
	
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		
		Если ЗначениеЗаполнено(ДатаЗаписи) Тогда
			ДатаНачалаИОкончания = ПолучитьДатуНачалаИДатуОкончания(ДатаЗаписи);
			Если НЕ ЗначениеЗаполнено(ДатаНачалаИОкончания) Тогда
				Возврат Неопределено;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ДатаНачалаИОкончания);
		Иначе
			РеквизитыЗаписиКалендаря.Повторять = ПолучитьТекстовоеПредставлениеПовторения();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РеквизитыЗаписиКалендаря;
	
КонецФункции

Функция Реквизиты(Дата = Неопределено) Экспорт
	
	РеквизитыЗаписиКалендаря = Справочники.ЦБР_ЗаписиРабочегоКалендаря.ПолучитьСтруктуруРеквизитовЗаписиКалендаря();
	ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ЭтотОбъект);
	
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		
		Если ЗначениеЗаполнено(Дата) Тогда
			ДатаНачалаИОкончания = ПолучитьДатуНачалаИДатуОкончания(Дата);
			Если Не ЗначениеЗаполнено(ДатаНачалаИОкончания) Тогда
				Возврат Неопределено;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ДатаНачалаИОкончания);
		Иначе
			РеквизитыЗаписиКалендаря.Повторять = ПолучитьТекстовоеПредставлениеПовторения();
		КонецЕсли;
		
	КонецЕсли;
	
	РеквизитыЗаписиКалендаря.Описание =
		РаботаСРабочимКалендаремКлиентСервер.ВыделитьОписаниеБезПервойСтроки(РеквизитыЗаписиКалендаря.Описание);
	
	Возврат РеквизитыЗаписиКалендаря;
	
КонецФункции

Функция НомерВхождения(Дата) Экспорт
	
	НомерВхождения = Неопределено;
	ПроверяемаяДата = НачалоДня(ДатаНачалаПовторения);
	ИскомаяДата = НачалоДня(Дата);
	Если ТипЗаписиКалендаря <> Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие
		Или Не ЗначениеЗаполнено(ИскомаяДата)
		Или ИскомаяДата < ПроверяемаяДата Тогда
		Возврат НомерВхождения;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	
	НомерИтерации = 0;
	НомерВхождения = 0;
	Пока НомерИтерации < 10000 Цикл // Ограничение на слишком большую дату окончания повторения
		
		Если ИскомаяДата < ПроверяемаяДата Тогда
			НомерВхождения = Неопределено;
			Прервать;
		КонецЕсли;
		
		ДатаУдовлетворяетПравилуПовторения = РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
			ПроверяемаяДата,
			СтруктураПравилаПовторения,
			Ложь);
		Если ДатаУдовлетворяетПравилуПовторения Тогда
			
			НомерВхождения = НомерВхождения + 1;
			Если ПроверяемаяДата = ИскомаяДата Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		НомерИтерации = НомерИтерации + 1;
		
	КонецЦикла;
	
	Возврат НомерВхождения;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		Если ДанныеЗаполнения <> Неопределено И ДанныеЗаполнения.Свойство("Пользователь") Тогда
			Пользователь = ДанныеЗаполнения.Пользователь
		Иначе
			Автор = Пользователи.АвторизованныйПользователь();
			Пользователь = Автор;
		КонецЕсли;
		Состояние = Перечисления.СостоянияЗаписейРабочегоКалендаря.Принято;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ЗаписьКалендаря") И ЗначениеЗаполнено(ДанныеЗаполнения.ЗаписьКалендаря) Тогда
			СкопироватьЗаписьКалендаря(ДанныеЗаполнения.ЗаписьКалендаря);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ЗаданиеССылка") И ЗначениеЗаполнено(ДанныеЗаполнения.ЗаданиеССылка) Тогда
			
			Если ТипЗнч(ДанныеЗаполнения.ЗаданиеССылка) = Тип("ДокументСсылка.ЦБР_Задание")  Тогда
				
				Мероприятие = ДанныеЗаполнения.ЗаданиеССылка;
				                                
				ОписаниеЗаписиКалендаря = Новый Массив;
				Если ЗначениеЗаполнено(Мероприятие) Тогда
					ОписаниеЗаписиКалендаря.Добавить(Символы.ПС);
					ОписаниеЗаписиКалендаря.Добавить(НСтр("ru = 'Задание:'"));
					ОписаниеЗаписиКалендаря.Добавить(" ");
					ОписаниеЗаписиКалендаря.Добавить(Мероприятие.НомерЗаданияВУровне);
					ОписаниеЗаписиКалендаря.Добавить(" ");
					ОписаниеЗаписиКалендаря.Добавить(Мероприятие.Наименование);
				КонецЕсли;

				Описание = СтрСоединить(ОписаниеЗаписиКалендаря);
				
				ДатаНачала = ДанныеЗаполнения.ДатаНачала;
				ДатаОкончания = ДанныеЗаполнения.ДатаОкончания;
				ВесьДень = Ложь;
				
		
			Иначе
				
				Описание = СокрЛП(ДанныеЗаполнения.ЗаданиеССылка.Наименование);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("ПовторяющеесяСобытие") И ЗначениеЗаполнено(ДанныеЗаполнения.ПовторяющеесяСобытие)
			И ДанныеЗаполнения.Свойство("ДатаИсключения") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаИсключения) Тогда
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.ПовторяющеесяСобытие,
				"ВесьДень, ДатаНачала, ДатаОкончания, Описание, Пользователь, ЗаданиеССылка, Состояние");
			
			ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия;
			
			ИсходнаяДатаНачала = ДатаНачала;
			ДатаНачала = НачалоДня(ДанныеЗаполнения.ДатаИсключения) + (ИсходнаяДатаНачала - НачалоДня(ИсходнаяДатаНачала));
			ДатаОкончания = НачалоДня(ДанныеЗаполнения.ДатаИсключения) + (ДатаОкончания - НачалоДня(ИсходнаяДатаНачала));
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ЦБР_ЗаписиРабочегоКалендаря") Тогда
		
		СкопироватьЗаписьКалендаря(ДанныеЗаполнения);
		
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Если Не ЗначениеЗаполнено(ДатаНачала) И Не ЗначениеЗаполнено(ДатаОкончания) Тогда
			ДатаНачала = РаботаСРабочимКалендаремКлиентСервер.КонецПолучаса(ТекущаяДатаСеанса());
			ДатаОкончания = ДатаНачала + 3600;
		КонецЕсли;
				
				
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(ДатаНачала, ДатаОкончания, ВесьДень);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	Ошибки = Неопределено;

	Если ДатаНачала >= ДатаОкончания Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Дата окончания должна быть больше даты начала.'"), ЭтотОбъект, "ДатаОкончания", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЗаданиеСсылка) и ВидРаботы = Справочники.ЦБР_ВидыРаботВКалендаре.Эффективный Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ВидРаботы", "Эффективные часы могут быть только у задач."); 
		Отказ = Истина;
	КонецЕсли;         
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если ВидРаботы = Справочники.ЦБР_ВидыРаботВКалендаре.Болезнь ИЛИ
		ВидРаботы = Справочники.ЦБР_ВидыРаботВКалендаре.Административный ИЛИ
		ВидРаботы = Справочники.ЦБР_ВидыРаботВКалендаре.Отпуск Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Описание");
	КонецЕсли;         
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", Не ЗначениеЗаполнено(Ссылка));
	
	Если ДатаНачала >= ДатаОкончания Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Дата начала  не может быть меньше даты окончания.'"));
		Возврат;
	КонецЕсли;
	
	Если ВесьДень Тогда
		РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
			ДатаНачала, ДатаОкончания, ВесьДень);
	КонецЕсли;
	
	ЗаполнитьНаименованиеИКраткоеОписание();
	
	// Установка настроек повторения
	Если ДополнительныеСвойства.Свойство("НастройкиПовторения") Тогда
		
		// Отказ в установке повторения для помеченной на удаление записи
		Если ПометкаУдаления = Истина Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Невозможно настроить повторение для записи, помеченной на удаление.'"));
			Возврат;
		КонецЕсли;
		
		// Отказ в установке повторения для записи, уже являющейся исключением повторения
		Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Невозможно настроить повторение для записи, являющейся исключением повторения.'"));
			Возврат;
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ИзмененыНастройкиПовторения");
		// При смене частоты повторения очищаются все настройки повторения, в том числе и исключения.
		Если ЧастотаПовторения <> ДополнительныеСвойства.НастройкиПовторения.ЧастотаПовторения Тогда
			ОчиститьРеквизитыПовторения();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДополнительныеСвойства.НастройкиПовторения);
		
		// Проверка заполненности частоты повторения
		Если ЗначениеЗаполнено(ЧастотаПовторения) Тогда
			ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие;
		Иначе
			ОтменитьПовторение();
		КонецЕсли;
		
		// Установка настроек и проверка заполненности, особенная для каждой частоты повторения.
		Если ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Еженедельно Тогда
			
			ПовторениеПоДням.Очистить();
			
			Для Каждого ДеньПовтора Из ДополнительныеСвойства.НастройкиПовторения.ПовторениеПоДнямНедели Цикл
				Если ДеньПовтора.Значение Тогда
					НоваяСтрока = ПовторениеПоДням.Добавить();
					НоваяСтрока.ДеньНедели = ДеньПовтора.Ключ;
					НоваяСтрока.НомерВхождения = 0;
				КонецЕсли;
			КонецЦикла;
			
			Если ПовторениеПоДням.Количество() = 0 Тогда
				ОтменитьПовторение();
			КонецЕсли;
			
		ИначеЕсли ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежемесячно Тогда
			
			ПовторениеПоДням.Очистить();
			
			Если ЗначениеЗаполнено(ДополнительныеСвойства.НастройкиПовторения.ПовторениеПоДнямНеделиВМесяце) Тогда
				НоваяСтрока = ПовторениеПоДням.Добавить();
				ЗаполнитьЗначенияСвойств(
					НоваяСтрока,
					ДополнительныеСвойства.НастройкиПовторения.ПовторениеПоДнямНеделиВМесяце);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ПовторениеПоДнямМесяца) И ПовторениеПоДням.Количество() = 0 Тогда
				ОтменитьПовторение();
			КонецЕсли;
			
		ИначеЕсли ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежегодно Тогда
			
			ПовторениеПоДням.Очистить();
			
			Если НЕ ЗначениеЗаполнено(ПовторениеПоДнямМесяца) ИЛИ НЕ ЗначениеЗаполнено(ПовторениеПоМесяцам) Тогда
				ОтменитьПовторение();
			КонецЕсли;
			
		КонецЕсли;
		
		// Удаление исключений повторения, не подходящих под новое правило повторения
		СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
		ИсключенияПовторенияКоличество = ИсключенияПовторения.Количество();
		Для ИндексИсключения = 1 По ИсключенияПовторенияКоличество Цикл
			
			ИсключениеПовторения = ИсключенияПовторения[ИсключенияПовторенияКоличество - ИндексИсключения];
			Если НЕ РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ИсключениеПовторения.ДатаИсключения, СтруктураПравилаПовторения, Ложь) Тогда
				
				ПометитьНаУдалениеИсключениеПовторения(ИсключениеПовторения);
				ИсключенияПовторения.Удалить(ИсключениеПовторения);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Корректировка даты начала повторения и даты окончания повторения
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие
		ИЛИ ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
		
		ОчиститьРеквизитыПовторения();
		
	ИначеЕсли ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		
		ДатаНачалаПовторения = ДатаНачала;
		
		Если ПравилоОкончанияПовторения = Перечисления.ЦБР_ПравилаОкончанияПовторения.Никогда Тогда
			ДатаОкончанияПовторения = Дата(1,1,1);
		ИначеЕсли ПравилоОкончанияПовторения = Перечисления.ЦБР_ПравилаОкончанияПовторения.ПослеЧислаПовторов Тогда
			РассчитатьДатуОкончанияПовторения();
		ИначеЕсли ПравилоОкончанияПовторения = Перечисления.ЦБР_ПравилаОкончанияПовторения.ДоДаты Тогда
			Если НЕ ЗначениеЗаполнено(ДатаОкончанияПовторения) Тогда
				ПравилоОкончанияПовторения = Перечисления.ЦБР_ПравилаОкончанияПовторения.Никогда;
			ИначеЕсли ДатаОкончанияПовторения < ДатаНачалаПовторения Тогда
				ОтменитьПовторение();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Очистка настроек повторения при удалении повторяющегося события
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие И ИзмениласьПометкаУдаления() Тогда
		
		Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
			ОтменитьПовторение();
		КонецЕсли;
		
	КонецЕсли;
	
	// Обработка рабочей группы
	СсылкаОбъекта = Ссылка;
	
	// Установка ссылки нового
	Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
		СсылкаОбъекта = ПолучитьСсылкуНового();
		Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
			СсылкаНового = Справочники.ЦБР_ЗаписиРабочегоКалендаря.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
			СсылкаОбъекта = СсылкаНового;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаданиеСсылка) Тогда       
		ЗадачаСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаданиеСсылка, "СсылкаНаЗадачу");	
	КонецЕсли;                                  
	
	Если ДополнительныеСвойства.Свойство("Цвет") Тогда
		Цвет = ДополнительныеСвойства.Цвет;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ПометкаУдаления Тогда
		
		ТекстОписания = Наименование;
		Если Не ЗначениеЗаполнено(ТекстОписания) Тогда
			ТекстОписания = Описание;
		КонецЕсли;
		
		// Получаем параметры
		УстановитьНапоминание = Неопределено;
		ИнтервалВремениСтрокой = Неопределено;
		ДополнительныеСвойства.Свойство("УстановитьНапоминание", УстановитьНапоминание);
		ДополнительныеСвойства.Свойство("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);
		
		// Если доп свойства получены, значит запись выполняется из формы.
		// Если не получены - нужно проверить есть ли напоминания в регистре.
		// Если есть напоминания - переустановить на новые значения.
		Если УстановитьНапоминание = Неопределено Тогда
			
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Пользователь", Пользователь);
			Запрос.УстановитьПараметр("Источник", Ссылка);
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	НапоминанияПользователя.СрокНапоминания,
				|	НапоминанияПользователя.ВремяСобытия,
				|	НапоминанияПользователя.СпособУстановкиВремениНапоминания,
				|	НапоминанияПользователя.ИнтервалВремениНапоминания,
				|	НапоминанияПользователя.ИмяРеквизитаИсточника,
				|	НапоминанияПользователя.Расписание,
				|	НапоминанияПользователя.ПредставлениеИсточника
				|ИЗ
				|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
				|ГДЕ
				|	НапоминанияПользователя.Пользователь = &Пользователь
				|	И НапоминанияПользователя.Источник = &Источник";
				
			Напоминания = Запрос.Выполнить().Выгрузить();
			Если Напоминания.Количество() = 0 Тогда
				УстановитьНапоминание = Ложь;
			Иначе
				УстановитьНапоминание = Истина;
				ИнтервалВремениСтрокой = ПредставлениеВремени(Напоминания[0].ИнтервалВремениНапоминания);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДополнительныеСвойства.Свойство("ПовторяющеесяСобытие") И ЗначениеЗаполнено(ДополнительныеСвойства.ПовторяющеесяСобытие)
		И ДополнительныеСвойства.Свойство("ДатаИсключения") И ЗначениеЗаполнено(ДополнительныеСвойства.ДатаИсключения) Тогда
		
		ПовторяющеесяСобытиеОбъект = ДополнительныеСвойства.ПовторяющеесяСобытие.ПолучитьОбъект();
		ПовторяющеесяСобытиеОбъект.Заблокировать();
		ПовторяющеесяСобытиеОбъект.ДобавитьИсключениеПовторения(ДополнительныеСвойства.ДатаИсключения, Ссылка);
		ПовторяющеесяСобытиеОбъект.Записать();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаданиеСсылка) Тогда
		
		СостояниеЗадания  = ЦБР_СтатусыОбъектовПолныеПрава.ПолучитьАктуальныйСтатусЗадания(ЗаданиеСсылка);
		
		Если СостояниеЗадания <> Перечисления.ЦБР_Состояние.ВРаботе Тогда
			Если Не ЕстьСтартованныеДействия(ЗаданиеСсылка, Пользователь) Тогда
				Попытка
					ПредметОбъект = ЗаданиеСсылка.ПолучитьОбъект();  
					ЦБР_СтатусыОбъектовПолныеПрава.УстановитьСтатусЗадания(ЗаданиеСсылка, Перечисления.ЦБР_Состояние.ВРаботе);
					Если Не ЗначениеЗаполнено(ПредметОбъект.ДатаНачалоФакт) и ЭтоПервоеДействие() Тогда
						ПредметОбъект.ДатаНачалоФакт = ТекущаяДатаСеанса();	
					КонецЕсли;    
					ПредметОбъект.Записать(РежимЗаписиДокумента.Проведение); 
				Исключение
					ЗаписьЖурналаРегистрации("Смена статуса в В Работе", УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Документы.ЦБР_Задание, ПредметОбъект, ОписаниеОшибки());
					Отказ = Истина;
					ОбщегоНазначения.СообщитьПользователю("Не удалось записать документ." + Символы.ПС
					+ ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + Символы.ПС
					+ "Подробности см. в журнале регистрации");
					
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;  
		
	КонецЕсли;

КонецПроцедуры

Функция ПредставлениеВремени(Знач Время, ПолноеПредставление = Истина, ВыводитьСекунды = Истина)
	
	ВремяСтрокой = "";
	
	// Представление единиц измерения времени в винительном падеже для количеств: 1, 2-4, 5-20.
	ПредставлениеНедель = НСтр("ru = ';%1 неделю;;%1 недели;%1 недель;%1 недели'");
	ПредставлениеДней   = НСтр("ru = ';%1 день;;%1 дня;%1 дней;%1 дня'");
	ПредставлениеЧасов  = НСтр("ru = ';%1 час;;%1 часа;%1 часов;%1 часа'");
	ПредставлениеМинут  = НСтр("ru = ';%1 минуту;;%1 минуты;%1 минут;%1 минуты'");
	ПредставлениеСекунд = НСтр("ru = ';%1 секунду;;%1 секунды;%1 секунд;%1 секунды'");
	
	Время = Число(Время);
	
	Если Время < 0 Тогда
		Время = -Время;
	КонецЕсли;
	
	КоличествоНедель = Цел(Время / 60/60/24/7);
	КоличествоДней   = Цел(Время / 60/60/24);
	КоличествоЧасов  = Цел(Время / 60/60);
	КоличествоМинут  = Цел(Время / 60);
	КоличествоСекунд = Цел(Время);
	
	КоличествоСекунд = КоличествоСекунд - КоличествоМинут * 60;
	КоличествоМинут  = КоличествоМинут - КоличествоЧасов * 60;
	КоличествоЧасов  = КоличествоЧасов - КоличествоДней * 24;
	КоличествоДней   = КоличествоДней - КоличествоНедель * 7;
	
	Если Не ВыводитьСекунды Тогда
		КоличествоСекунд = 0;
	КонецЕсли;
	
	Если КоличествоНедель > 0 И КоличествоДней+КоличествоЧасов+КоличествоМинут+КоличествоСекунд=0 Тогда
		ВремяСтрокой = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеНедель, КоличествоНедель);
	Иначе
		КоличествоДней = КоличествоДней + КоличествоНедель * 7;
		
		Счетчик = 0;
		Если КоличествоДней > 0 Тогда
			ВремяСтрокой = ВремяСтрокой + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеДней, КоличествоДней) + " ";
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если КоличествоЧасов > 0 Тогда
			ВремяСтрокой = ВремяСтрокой + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеЧасов, КоличествоЧасов) + " ";
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если (ПолноеПредставление Или Счетчик < 2) И КоличествоМинут > 0 Тогда
			ВремяСтрокой = ВремяСтрокой + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеМинут, КоличествоМинут) + " ";
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если (ПолноеПредставление Или Счетчик < 2) И (КоличествоСекунд > 0 Или КоличествоНедель+КоличествоДней+КоличествоЧасов+КоличествоМинут = 0) Тогда
			ВремяСтрокой = ВремяСтрокой + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ПредставлениеСекунд, КоличествоСекунд);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СокрП(ВремяСтрокой);
	
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = Пользователи.АвторизованныйПользователь();
	Пользователь = Автор;
	
	СкопироватьЗаписьКалендаря(ОбъектКопирования);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьСтартованныеДействия(ЗаданиеСсылка, Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЦБР_ЗаписиРабочегоКалендаря.Ссылка) КАК КоличествоСтартованныхДействий
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь = &Пользователь
	|	И ЦБР_ЗаписиРабочегоКалендаря.Статус = &Статус
	|	И НЕ ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления
	|	И ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка = &ЗаданиеСсылка";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ЗаданиеСсылка", ЗаданиеСсылка);
	Запрос.УстановитьПараметр("Статус", Перечисления.ЦБР_СтатусЗаписиКалендаря.Начал);  
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.КоличествоСтартованныхДействий > 0;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура СкопироватьЗаписьКалендаря(ЗаписьКалендаря)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗаписьКалендаря,
		"ВесьДень, ДатаНачала, ДатаОкончания, Описание, Состояние, ЗаданиеССылка");
	
	ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие;
	Связанная = Ложь;
	
	ДатаНачалаПовторения = Неопределено;
	ДатаОкончанияПовторения = Неопределено;
	ИнтервалПовторения = Неопределено;
	КоличествоПовторов = Неопределено;
	ПовторениеПоДнямМесяца = Неопределено;
	ПовторениеПоМесяцам = Неопределено;
	ПравилоОкончанияПовторения = Неопределено;
	ЧастотаПовторения = Неопределено;
	
	ИсключенияПовторения.Очистить();
	ПовторениеПоДням.Очистить();
	
КонецПроцедуры

Процедура ЗаполнитьНаименованиеИКраткоеОписание()
	
	Наименование = СтрПолучитьСтроку(СокрЛ(Описание), 1);
	
КонецПроцедуры

Процедура ОчиститьРеквизитыПовторения()
	
	ДатаНачалаПовторения = Дата(1,1,1);
	ДатаОкончанияПовторения = Дата(1,1,1);
	ИнтервалПовторения = 0;
	ПовторениеПоДнямМесяца = 0;
	ПовторениеПоМесяцам = 0;
	ПравилоОкончанияПовторения = Перечисления.ЦБР_ПравилаОкончанияПовторения.ПустаяСсылка();
	ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.ПустаяСсылка();
	КоличествоПовторов = 0;
	ПовторениеПоДням.Очистить();
	
	ОчиститьИсключенияПовторения();
	
КонецПроцедуры

Процедура ОчиститьИсключенияПовторения()
	
	Для Каждого ИсключениеПовторения Из ИсключенияПовторения Цикл
		
		ПометитьНаУдалениеИсключениеПовторения(ИсключениеПовторения);
		
	КонецЦикла;
	
	ИсключенияПовторения.Очистить();
	
КонецПроцедуры

Процедура ПометитьНаУдалениеИсключениеПовторения(ИсключениеПовторения)
	
	Если ЗначениеЗаполнено(ИсключениеПовторения.ЗаписьИсключения) Тогда
		
		ЗаписьИсключенияОбъект = ИсключениеПовторения.ЗаписьИсключения.ПолучитьОбъект();
		
		Если ЗаписьИсключенияОбъект = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ЗаписьИсключенияОбъект.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
			ЗаписьИсключенияОбъект.Заблокировать();
			ЗаписьИсключенияОбъект.ПометкаУдаления = Истина;
			ЗаписьИсключенияОбъект.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьДатуОкончанияПовторения() Экспорт
	
	Если ЗначениеЗаполнено(ДатаОкончанияПовторения) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КоличествоПовторов) Тогда
		ПравилоОкончанияПовторения = Перечисления.ЦБР_ПравилаОкончанияПовторения.Никогда;
		Возврат;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	
	НомерИтерации = 0;
	НомерПовтора = 0;
	ПроверяемаяДата = ДатаНачалаПовторения;
	
	Пока НомерИтерации < 10000 Цикл // Ограничение на слишком большую дату окончания повторения
		
		Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
			
			НомерИтерации = 0;
			НомерПовтора = НомерПовтора + 1;
			
			Если НомерПовтора = КоличествоПовторов Тогда
				ДатаОкончанияПовторения = ПроверяемаяДата;
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		НомерИтерации = НомерИтерации + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИзмениласьПометкаУдаления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат ПометкаУдаления И НЕ Выборка.ПометкаУдаления;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьСтруктуруПравилаПовторения()
	
	СтруктураПравилаПовторения = РаботаСРабочимКалендаремСервер.ПолучитьСтруктуруПравилаПовторения();
	ЗаполнитьЗначенияСвойств(СтруктураПравилаПовторения, ЭтотОбъект);
	СтруктураПравилаПовторения.ИсключенияПовторения = ИсключенияПовторения.Выгрузить();
	СтруктураПравилаПовторения.ПовторениеПоДням = ПовторениеПоДням.Выгрузить();
	
	Возврат СтруктураПравилаПовторения;
	
КонецФункции

Процедура ОтменитьПовторение()
	
	ОчиститьРеквизитыПовторения();
	ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие;
	
КонецПроцедуры

Функция ПолучитьДатуНачалаИДатуОкончания(ДатаЗаписи)
	
	Если ДатаЗаписи < ДатаНачалаПовторения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончанияПовторения) И ДатаЗаписи > ДатаОкончанияПовторения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	ПроверяемаяДата = НачалоДня(ДатаЗаписи);
	
	Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
			ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
		
		ДатаНачалаИОкончания = Новый Структура("ДатаНачала, ДатаОкончания");
		ДатаНачалаИОкончания.ДатаНачала = ПроверяемаяДата + (ДатаНачала - НачалоДня(ДатаНачала));
		ДатаНачалаИОкончания.ДатаОкончания = ПроверяемаяДата + (ДатаОкончания - НачалоДня(ДатаНачала));
		
		Возврат ДатаНачалаИОкончания;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьТекстовоеПредставлениеПовторения()
	
	ТекстовоеПредставлениеПовторения = "";
	
	НастройкиПовторения = ПолучитьНастройкиПовторения();
	ТекстовоеПредставлениеПовторения =
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьТекстовоеПредставлениеПовторения(НастройкиПовторения);
	
	Возврат ТекстовоеПредставлениеПовторения;
	
КонецФункции

Функция ПолучитьНастройкиПовторения()
	
	ПовторениеПоДнямНедели = Новый Соответствие;
	Для ИндексПовторениеПоДням = 1 По 7 Цикл
		
		Если ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Еженедельно Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДеньНедели", ИндексПовторениеПоДням);
			ПараметрыОтбора.Вставить("НомерВхождения", 0);
			НайденныеСтроки = ПовторениеПоДням.НайтиСтроки(ПараметрыОтбора); 
			
			ПовторениеПоДнямНедели.Вставить(ИндексПовторениеПоДням, НайденныеСтроки.Количество() <> 0);
			
		Иначе
			ПовторениеПоДнямНедели.Вставить(ИндексПовторениеПоДням, Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
	ПовторениеПоДнямНеделиВМесяце = Неопределено;
	
	Если ЧастотаПовторения = Перечисления.ЦБР_ЧастотаПовторения.Ежемесячно
		И НЕ ЗначениеЗаполнено(ПовторениеПоДнямМесяца) И ПовторениеПоДням.Количество() = 1 Тогда
		
		ПовторениеПоДнямНеделиВМесяце = Новый Структура("НомерВхождения, ДеньНедели");
		ЗаполнитьЗначенияСвойств(ПовторениеПоДнямНеделиВМесяце, ПовторениеПоДням[0]);
		
	КонецЕсли;
	
	НастройкиПовторения = РаботаСРабочимКалендаремКлиентСервер.ПолучитьСтруктуруНастройкиПовторения(
		ЧастотаПовторения, ИнтервалПовторения, ПравилоОкончанияПовторения,
		КоличествоПовторов, ДатаОкончанияПовторения, ПовторениеПоДнямНедели,
		ПовторениеПоДнямМесяца, ПовторениеПоДнямНеделиВМесяце, ПовторениеПоМесяцам);
	
	Возврат НастройкиПовторения;
	
КонецФункции

Функция ЭтоПервоеДействие()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
	|ГДЕ
	|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеССылка = &ЗаданиеССылка
	|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ
	|	И ЦБР_ЗаписиРабочегоКалендаря.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("ЗаданиеССылка", ЗаданиеСсылка);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли