#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	// Переопределение для целей избранного - вместо карточки с настройками размещения отчета будет открываться его основная форма.
	Если ВидФормы = "ФормаОбъекта" Тогда
		
		СсылкаЗаписиКалендаря = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ключ");
		Если Не ЗначениеЗаполнено(СсылкаЗаписиКалендаря) Тогда
			Возврат;
		КонецЕсли;
		
		РеквизитыЗаписиКалендаря = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаЗаписиКалендаря, "Связанная, ЗаданиеСсылка");
		Если Не РеквизитыЗаписиКалендаря.Связанная Тогда
			Возврат;
		КонецЕсли;
		
		ТипПредмета = ТипЗнч(РеквизитыЗаписиКалендаря.ЗаданиеССылка);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Возвращает таблицу занятости сотрудников.
//
Функция ПолучитьТаблицуЗанятости(Знач МассивСотрудников, ДатаНачала, ДатаОкончания, ИсключенияЗанятости) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(МассивСотрудников) = Тип("СправочникСсылка.Пользователи") Тогда
		//Или ТипЗнч(МассивСотрудников) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = МассивСотрудников;
		МассивСотрудников = Новый Массив;
		МассивСотрудников.Добавить(Сотрудник);
	КонецЕсли;
	
	Если ИсключенияЗанятости = Неопределено Тогда
		МассивИсключенийЗанятости = Новый Массив;
	ИначеЕсли ТипЗнч(ИсключенияЗанятости) = Тип("Массив") Тогда
		СвязанныеЗаписиКалендаря =
			Справочники.ЦБР_ЗаписиРабочегоКалендаря.СвязанныеЗаписиКалендаря(ИсключенияЗанятости);
		МассивИсключенийЗанятости = СвязанныеЗаписиКалендаря.ВыгрузитьКолонку("Ссылка");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивИсключенийЗанятости, ИсключенияЗанятости, Истина);
	Иначе
		СвязанныеЗаписиКалендаря =
			Справочники.ЦБР_ЗаписиРабочегоКалендаря.СвязанныеЗаписиКалендаря(ИсключенияЗанятости);
		МассивИсключенийЗанятости = СвязанныеЗаписиКалендаря.ВыгрузитьКолонку("Ссылка");
		МассивИсключенийЗанятости.Добавить(ИсключенияЗанятости);
	КонецЕсли;
	
	ТаблицаЗанятости = Новый ТаблицаЗначений;
	ТаблицаЗанятости.Колонки.Добавить("Сотрудник");
	ТаблицаЗанятости.Колонки.Добавить("ДатаНачала");
	ТаблицаЗанятости.Колонки.Добавить("ДатаОкончания");
	ТаблицаЗанятости.Колонки.Добавить("Занят");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Сотрудник,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала КАК ДатаНачала,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания КАК ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.Состояние КАК Состояние,
		|	ЦБР_ЗаписиРабочегоКалендаря.Связанная КАК Связанная,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка КАК ЗаданиеСсылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.ВесьДень КАК ВесьДень
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь В(&МассивСотрудников)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала < &ДатаОкончания
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания > &ДатаНачала
		|	И (ЦБР_ЗаписиРабочегоКалендаря.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.Принято)
		|			ИЛИ ЦБР_ЗаписиРабочегоКалендаря.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.ПодВопросом))
		|	И ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря <> ЗНАЧЕНИЕ(Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ
		|	И НЕ ЦБР_ЗаписиРабочегоКалендаря.Ссылка В (&ИсключенияЗанятости)";
	
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ИсключенияЗанятости", МассивИсключенийЗанятости);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаЗанятости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Выборка.Связанная
			И Не Выборка.ВесьДень
			И (ТипЗнч(Выборка.ЗаданиеСсылка) = Тип("СправочникСсылка.Мероприятия")
				Или ТипЗнч(Выборка.ЗаданиеСсылка) = Тип("ДокументСсылка.Отсутствие")) Тогда
			
			НоваяСтрока.ДатаНачала = Выборка.ДатаНачала;
			НоваяСтрока.ДатаОкончания = Выборка.ДатаОкончания;
			
		ИначеЕсли Не Выборка.Связанная И Не Выборка.ВесьДень Тогда
			
			НоваяСтрока.ДатаНачала = Выборка.ДатаНачала;
			НоваяСтрока.ДатаОкончания = Выборка.ДатаОкончания;
			
		
		КонецЕсли;
		
		НоваяСтрока.Занят = ПолучитьСоответствующееСостояниеЗанятости(
			Выборка.Состояние,
			Выборка.Связанная,
			Выборка.ЗаданиеСсылка);
		
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала КАК ДатаНачала,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания КАК ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря КАК ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИсключенияПовторения.(
		|		ДатаИсключения КАК ДатаИсключения,
		|		ЗаписьИсключения КАК ЗаписьИсключения
		|	) КАК ИсключенияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДням.(
		|		ДеньНедели КАК ДеньНедели,
		|		НомерВхождения КАК НомерВхождения
		|	) КАК ПовторениеПоДням,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения КАК ДатаНачалаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения КАК ДатаОкончанияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИнтервалПовторения КАК ИнтервалПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.КоличествоПовторов КАК КоличествоПовторов,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДнямМесяца КАК ПовторениеПоДнямМесяца,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоМесяцам КАК ПовторениеПоМесяцам,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря КАК ТипЗаписиКалендаря1,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЧастотаПовторения КАК ЧастотаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.Состояние КАК Состояние,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Сотрудник,
		|	ЦБР_ЗаписиРабочегоКалендаря.Связанная КАК Связанная,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка КАК ЗаданиеСсылка
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь В(&МассивСотрудников)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения >= &ДатаНачала
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения < &ДатаОкончания
		|	И (ЦБР_ЗаписиРабочегоКалендаря.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.Принято)
		|			ИЛИ ЦБР_ЗаписиРабочегоКалендаря.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.ПодВопросом))
		|	И ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря = ЗНАЧЕНИЕ(Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ
		|	И НЕ ЦБР_ЗаписиРабочегоКалендаря.Ссылка В (&ИсключенияЗанятости)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачала,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончания,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИсключенияПовторения.(
		|		ДатаИсключения,
		|		ЗаписьИсключения
		|	),
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДням.(
		|		ДеньНедели,
		|		НомерВхождения
		|	),
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.ИнтервалПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.КоличествоПовторов,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоДнямМесяца,
		|	ЦБР_ЗаписиРабочегоКалендаря.ПовторениеПоМесяцам,
		|	ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЧастотаПовторения,
		|	ЦБР_ЗаписиРабочегоКалендаря.Состояние,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь,
		|	ЦБР_ЗаписиРабочегоКалендаря.Связанная,
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь В(&МассивСотрудников)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения = ДАТАВРЕМЯ(1, 1, 1)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ДатаНачалаПовторения < &ДатаОкончания
		|	И (ЦБР_ЗаписиРабочегоКалендаря.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.Принято)
		|			ИЛИ ЦБР_ЗаписиРабочегоКалендаря.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаписейРабочегоКалендаря.ПодВопросом))
		|	И ЦБР_ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря = ЗНАЧЕНИЕ(Перечисление.ТипЗаписиКалендаря.ПовторяющеесяСобытие)
		|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ
		|	И НЕ ЦБР_ЗаписиРабочегоКалендаря.Ссылка В (&ИсключенияЗанятости)";
	
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ИсключенияЗанятости", МассивИсключенийЗанятости);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПроверяемаяДата = НачалоДня(ДатаНачала);
		Пока ПроверяемаяДата < ДатаОкончания Цикл
			
			СтруктураПравилаПовторения = РаботаСРабочимКалендаремСервер.ПолучитьСтруктуруПравилаПовторения();
			ЗаполнитьЗначенияСвойств(СтруктураПравилаПовторения, Выборка);
			СтруктураПравилаПовторения.ИсключенияПовторения = Выборка.ИсключенияПовторения.Выгрузить();
			СтруктураПравилаПовторения.ПовторениеПоДням = Выборка.ПовторениеПоДням.Выгрузить();
			
			Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
				
				ДатаНачалаПоПравилуПовторения = НачалоДня(ПроверяемаяДата) + (Выборка.ДатаНачала - НачалоДня(Выборка.ДатаНачала));
				ДатаОкончанияПоПравилуПовторения = НачалоДня(ПроверяемаяДата) + (Выборка.ДатаОкончания - НачалоДня(Выборка.ДатаНачала));
				Если ДатаНачалаПоПравилуПовторения < ДатаОкончания
					И ДатаОкончанияПоПравилуПовторения > ДатаНачала Тогда
					
					НоваяСтрока = ТаблицаЗанятости.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					
					НоваяСтрока.ДатаНачала = МестноеВремя(ДатаНачалаПоПравилуПовторения, ЧасовойПоясСеанса());
					НоваяСтрока.ДатаОкончания = МестноеВремя(ДатаОкончанияПоПравилуПовторения, ЧасовойПоясСеанса()); 
					
					НоваяСтрока.Занят = ПолучитьСоответствующееСостояниеЗанятости(
					Выборка.Состояние,
					Выборка.Связанная,
					Выборка.ЗаданиеСсылка);
					
				КонецЕсли;
			КонецЕсли;
			
			ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаЗанятости;
	
КонецФункции

Функция ПолучитьСтруктуруРеквизитовЗаписиКалендаря() Экспорт
	
	Возврат РеквизитыЗаписиКалендаря();
	
КонецФункции

Функция РеквизитыЗаписиКалендаря(ЗаписьКалендаря = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ЗаписьКалендаря) Тогда
		ЗаписьКалендаряОбъект = ЗаписьКалендаря.ПолучитьОбъект();
		РеквизитыЗаписиКалендаря = ЗаписьКалендаряОбъект.Реквизиты();
	Иначе
		РеквизитыЗаписиКалендаря = Новый Структура(
			"Ссылка, Описание, Пользователь, ДатаНачала, ДатаОкончания,
			|ВесьДень, Повторять, Предмет, Наименование, Автор");
		РеквизитыЗаписиКалендаря.Повторять = "";
	КонецЕсли;
	
	Возврат РеквизитыЗаписиКалендаря;
	
КонецФункции

Функция СвязанныеЗаписиКалендаря(ЗаданиеСсылка) Экспорт
	
	Если ТипЗнч(ЗаданиеСсылка) = Тип("Массив") Тогда
		ЗаданияМассив = ЗаданиеСсылка;
	ИначеЕсли ЗначениеЗаполнено(ЗаданиеСсылка) Тогда
		ЗаданияМассив = Новый Массив;
		ЗаданияМассив.Добавить(ЗаданиеСсылка);
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка,
		|	ЦБР_ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.ЗаданиеСсылка В(&ЗаданияСсылка)
		|	И ЦБР_ЗаписиРабочегоКалендаря.Связанная = ИСТИНА
		|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("ЗаданияСсылка", ЗаданияМассив);
	
	СвязанныеЗаписиКалендаря = Запрос.Выполнить().Выгрузить();
	
	Возврат СвязанныеЗаписиКалендаря;
	
КонецФункции

Функция СвязаннаяЗаписьКалендаря(Предмет, Знач Пользователь) Экспорт
	
	СвязаннаяЗаписьКалендаря = Справочники.ЦБР_ЗаписиРабочегоКалендаря.ПустаяСсылка();
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат СвязаннаяЗаписьКалендаря;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЦБР_ЗаписиРабочегоКалендаря КАК ЦБР_ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЦБР_ЗаписиРабочегоКалендаря.Предмет = &Предмет
		|	И ЦБР_ЗаписиРабочегоКалендаря.Пользователь = &Пользователь
		|	И ЦБР_ЗаписиРабочегоКалендаря.Связанная = ИСТИНА
		|	И ЦБР_ЗаписиРабочегоКалендаря.ПометкаУдаления = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		СвязаннаяЗаписьКалендаря = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат СвязаннаяЗаписьКалендаря;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает соответствующее состояние занятости
//
// Параметры:
//  СостояниеЗаписиРабочегоКалендаря - ПеречислениеСсылка.СостоянияЗаписейРабочегоКалендаря - Состояние записи рабочего календаря.
//  Связанная - Булево - Запись календаря является связанной.
//  Предмет - СправочникСсылка, ДокументСсылка - Предмет записи календаря.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ЦБР_СостоянияЗанятости - Состояние занятости.
//
Функция ПолучитьСоответствующееСостояниеЗанятости(СостояниеЗаписиРабочегоКалендаря, Связанная, Предмет) Экспорт
	
	ЦБР_СостоянияЗанятости = Неопределено;
	
	Если Связанная И ТипЗнч(Предмет) = Тип("ДокументСсылка.Отсутствие")
		И СостояниеЗаписиРабочегоКалендаря = Перечисления.СостоянияЗаписейРабочегоКалендаря.Принято Тогда
		ЦБР_СостоянияЗанятости = ?(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "БудуРазбиратьЗадачи"),
			Перечисления.ЦБР_СостоянияЗанятости.Доступен,
			Перечисления.ЦБР_СостоянияЗанятости.Отсутствует);
	ИначеЕсли СостояниеЗаписиРабочегоКалендаря = Перечисления.СостоянияЗаписейРабочегоКалендаря.Принято Тогда
		ЦБР_СостоянияЗанятости = Перечисления.ЦБР_СостоянияЗанятости.Занят;
	ИначеЕсли СостояниеЗаписиРабочегоКалендаря = Перечисления.СостоянияЗаписейРабочегоКалендаря.Отклонено Тогда
		ЦБР_СостоянияЗанятости = Перечисления.ЦБР_СостоянияЗанятости.Доступен;
	ИначеЕсли СостояниеЗаписиРабочегоКалендаря = Перечисления.СостоянияЗаписейРабочегоКалендаря.ПодВопросом
		Или Не ЗначениеЗаполнено(СостояниеЗаписиРабочегоКалендаря) Тогда
		ЦБР_СостоянияЗанятости = Перечисления.ЦБР_СостоянияЗанятости.ПодВопросом;
	КонецЕсли;
	
	Возврат ЦБР_СостоянияЗанятости;
	
КонецФункции

#КонецОбласти

#КонецЕсли
